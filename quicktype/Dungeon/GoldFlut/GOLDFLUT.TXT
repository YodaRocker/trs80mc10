10 REM * THE GOLDEN FLUTE * Delton T. Horn *
19 REM * SET UP *
20 CLEAR 100
30 DIM A(100): DIM B(100): DIM C(9)
40 CLS: PRINT: PRINT: PRINT" ","THE GOLDEN FLUTE"
50 PRINT: PRINT" "," ","by Delton T. Horn": PRINT: PRINT 
55 PW=0: GW=0
60 INPUT"WHAT IS YOUR NAME";N$
61 IF N$="" THEN PRINT "TRY AGAIN ...":GOTO 60
70 BR=1:DG=1:GR=1:RU=1:SJ=1:JS=1:AL=1:PM=1 
80 AX=0: LC=1: M=1: SW=0: FL=0: MO=0: GL=0: MZ=0 
90 FOR X =1 TO 100: A(X) = 0: B(X) = 0: NEXT
95 FOR X =1 TO 9: C(X) =1: NEXT
100 PRINT: PRINT"setting variables": PRINT: PRINT
109 REM * WOODS *
110 A(1) = 1: A(2) = 1: A(11) = 1: A(12) = 1
119 REM * ORACLE *
120 A(33)=2
129 REM * PITS *
130 X=RND(7)+2: Y=RND(4)+4: X=X+Y*10: Y=X+1 
140 A(X) = 3: A(Y) = 3: X =X+10: Y =Y+ 1: A(X) = 3: A(Y) = 3
149 REM * WALL *
150 Q=0
160 Q=Q+1: X=(RND(4)+3)*10+RND(3)+3: Y=X+5: Z=0 
170 IF Q>7 GOTO 220
180 FOR V = X TO Y: IF A(V) > 0 THEN Z = 1
190 IF A(V+ 10)>0 THEN Z= 1
200 NEXT: IF Z =1 GOTO 160
210 FOR V = X TO Y: A(V) =13 : A(V + 10) = 14: NEXT
220 FOR X = 1 TO 100: B(X) = A(X): PRINT" % "; : NEXT: PRINT
229 REM * HIDDEN ITEMS *
230 X = RND(100)
232 IF B(X) > 0 GOTO 230
235 B(X) = 29
239 REM * TERAK & GUARDS *
240 X = (RND(4)+4)*10+RND(7)+2: IF B(X) > 0 GOTO 240
250 B(X)=4: Z=9: Y=X-1: GOSUB 5010
260 Y = X+ 10: GOSUB 5010
270 Y = X - 10: GOSUB 5010
280 Y = X+ 1: GOSUB 5010
290 Y = Y + 10: GOSUB 5010
300 Y = Y - 20: GOSUB 5010
309 REM * GARGOYLES *
310 U = RND(10) + 2: FOR X =1 TO U: Y = RND(100): GOSUB 5010: NEXT 
320 Y = RND(70) + 30: PRINT"*";: IF B(Y) > 0 GOTO 320
330 B(Y) = 5
340 Y = RND(100): IF B(Y) > 0 GOTO 340
350 B(Y) = 21: FOR X =1 TO 3
360 Y = RND(100): IF B(Y) > 0 GOTO 360
370 B(Y)=22: NEXT
380 Y = RND(50): IF B(Y) > 0 GOTO 380
390 B(Y) = 23
400 Y = RND(50): IF B(Y) > 0 GOTO 400
410 B(Y) = 24
420 Y = RND(90): IF B(Y) > 0 GOTO 420
430 B(Y) = 25
440 Y = RND(80) + 15: IF B(Y) > 0 GOTO 440
450 B(Y) = 26: FOR X = 1 TO 5
460 Y = RND(80) + 15: IF B(Y) > 0 GOTO 460
470 B(Y) = 27: NEXT
480 Y = RND(100): IF B(Y) > 0 GOTO 480
490 B(Y) = 26
500 Z = 6: FOR X =1 TO 10: Y = RND(80)+20: GOSUB 5010: NEXT
510 Z = 8: FOR X =1 TO 10: Y = RND(100) : GOSUB 5010: NEXT
520 Y = RND(100): IF B(Y)> 0 GOTO 520
530 B(Y) = 7: Z = 10: FOR X = 1 TO 10: Y = RND(95) + 5: GOSUB 5010: NEXT
540 Y = RND(100): IF B(Y) > 0 GOTO 540
550 B(Y) = 11: QQ = 0
560 F = RND(100): IF B(F) > 0 GOTO 560
570 B(F) =15
580 Y = RND(55)+45: IF B(Y)> 0 GOTO 580
590 B(Y) =17: BZ = 0
599 REM * INTRO *
600 CLS: PRINT: PRINT" ", "GREETINGS, ";N$: PRINT
610 PRINT"You have joined the Magical creatures of the Woodlands in their" 
620 PRINT"G1orious Quest to recover the Golden Flute of Love from the"
630 PRINT"clutches of the evil gremlin, Terak, and to return it to the"
640 PRINT"Woodlands. At all times watch out for dragons, Sirens,"
650 PRINT"goblins, gargoyles, and the dreaded Hopeless Pits!" 
660 PRINT"Coramble, the Great Oracle of Purlicon Mountain may be of" 
670 PRINT"assistance.": PRINT
680 INPUT"Do you want directions" ; Q$ : Q$ = LEFT$(Q$,1)
690 IF Q$ = "Y" GOSUB 5030 
695 LC = 1: A(LC) = 12
699 REM * CHARIOT CONTENTS *
700 PRINT: GOSUB 5000
710 PRINT" ", "The Magic Chariot is now carrying:"
720 PRINT N$;" the Human"
730 IF BR = 1 PRINT"BROMBIRAN the Elf"
740 IF DG = 1 PRINT"DAGGLETTE the Elf"
750 IF GR = 1 PRINT"GROMPHLUR the Elf"
760 IF RU = 1 PRINT"RULF the Satyr"
770 IF SJ = 1 PRINT"SEJJAN the Fairy"
780 IF JS = 1 PRINT"JESSAN the Fairy"
790 IF AL = 1 PRINT"ALLEGRECIA the Queen of the Sprites"
800 IF PM = 1 PRINT"PRINCESS MELVA"
810 IF FL = 1 PRINT"The Golden Flute"
820 IF MO = 0 PRINT"A Magic Orb" 
830 IF SW = 1 PRINT"A Magic Sword"
840 IF BZ = 1 PRINT"A Magic Bazooka"
850 IF GL < 1 GOTO 880
860 PRINT GL; "piece"; : IF GL> 1 PRINT"s";
870 PRINT" of gold"
880 IF MZ = 1 PRINT"A Magic Zither"
890 INPUT"Please press 'ENTER' ";Q$
899 REM * MAIN PLAY *
900 ZZ = LC: PRINT"You are at location ";LC, "Move #";M
910 INPUT"Your move? (Enter 'K' for key) ";Q$: Q$ = LEFT$(Q$,1) 
920 IF Q$ = "K" GOTO 4000
925 IF Q$ = "M" GOTO 7000
930 IF Q$ = "B" AND BZ = 1 GOTO 4030
935 M = M + 1: A(LC) = B(LC) 
940 IF Q$ = "D" GOTO 1000 
950 IF Q$ = "U" GOTO 1190 
960 IF Q$ = "R" GOTO 1200 
970 IF Q$ = "L" GOTO 1220
980 PRINT" ", "INVALID MOVE!"
990 GOTO 910
1000 LC = LC+ 10: IF LC > 100 GOTO 1020
1010 GOTO 4250
1020 PRINT: PRINT"The Magic Chariot has left the boundaries of the Magic Kingdom": PRINT
1030 GOSUB 5000
1040 PRINT"Without Magic to hold it up, the Magic Chariot crashes!": PRINT 
1050 GOSUB 5000: M = M - 1: PRINT"You lose this time."
1060 PRINT"IT TOOK YOU ";M;" MOVES."
1070 IF PW = 0 OR PW = M GOTO 1100
1080 PRINT"Previous win record was ";PW
1100 PRINT
1110 GW = GW+ 1: PRINT"This was game #";GW
1120 PRINT" ",: INPUT"PLAY AGAIN";Q$: Q$ = LEFT$(Q$,1)
1130 IF Q$ = "Y" GOTO 8000
1140 END
1150 M = M - 1: PRINT"You win this time": PRINT
1155 PRINT"It took you "; M ; " moves ! "
1160 IF PW = 0 GOTO 1175
1170 PRINT"PREVIOUS BEST SCORE WAS ";PW: IF PW < M GOTO 1100 
1175 PW = M : GOTO 1100
1190 LC = LC - 10: IF LC < 1 GOTO 1020 ELSE GOTO 4250
1200 LC = LC + 1: X = LC/ 10: Y = (X - INT(X))* 10: IF(Y = 0) OR (Y> 1.5) GOTO 4250
1210 GOTO 1020
1220 LC = LC - 1: X = LC/ 10: Y = (X - INT(X))* 10: IF Y = 0 GOTO 1020
1230 GOTO 4250
4000 PRINT"U = Up, D = Down, R = Right, L = Left, M = Map, ";
4010 IF BZ > 0 PRINT"B = Fire Bazooka, "
4020 PRINT"K = Key": GOTO 910
4030 INPUT "Bazooka loaded-Aim? (Enter 'X' to disarm) ";Q$
4040 Q$ = LEFT$(Q$, 1): IF Q$ = "X" GOTO 900
4050 IF Q$ = "U" GOTO 4100
4060 IF Q$ = "D" GOTO 4160
4070 IF Q$ = "R" GOTO 4190
4080 IF Q$ = "L" GOTO 4210
4090 GOTO 4030
4100 BU = 10000: BS = 1: BT = - 10
4110 PRINT" ", "KER-"; : FOR X = 1 TO 55: NEXT: PRINT"POW! !": PRINT: X = LC
4120 X = X + BT: IF X< BS GOTO 900
4130 IF X > BU GOTO 900
4140 IF (X < 1) OR (X> 100) GOTO 900
4150 A(X) = 20: B(X) = 20: GOTO 4120
4160 BU = 10000: BS = 0: BT = 10
4170 GOTO 4110
4180 PRINT" ", "phffft!": PRINT: GOTO 900
4190 G = INT(LC/ 10): IF G = LC/ 10 GOTO 4180
4200 BS=0: BU=G*10+10: BT= 1: GOTO 4110
4210 G = INT(LC/ 10): H=LC/10-G: IFH=0.1 GOTO 4180
4220 BU = 10000: BS = G* 10 + 1: BT = - 1: GOTO 4110
4250 PRINT: PRINT: PRINT: A(LC) = 12
4260 XX = B(LC)
4270 IF(LC = 1) OR (LC = 2) OR (LC = 11) OR (LC = 12) GOTO 10000 
4280 IF LC = 33 GOSUB 5120
4290 IF(LC > 21 AND LC < 25) OR (LC > 41 AND LC <45) GOSUB 5300 
4300 IF LC = 32 OR LC = 34 GOSUB 5300
4310 IFXX=4 GOSUB 5310
4320 P = RND(150): IF (P > 148) AND (FL = 0) GOTO 10030
4330 IF XX = 3 GOTO 10080 
4340 IF XX = 5 GOTO 10200 
4350 X = LC - 1: GOSUB 5580 
4360 X = LC+ 1: GOSUB 5580 
4370 X = LC - 10: GOSUB 5580 
4380 X = X - 1: GOSUB 5580 
4390 X =X+2: GOSUB 5580 
4400 X = LC + 10: GOSUB 5580 
4410 X = X - 1: GOSUB 5580 
4420 X = X + 2: GOSUB 5580 
4430 IF XX = 6 GOTO 10240 
4440 IF XX = 7 GOSUB 5600 
4450 IF XX = 8 GOSUB 5610 
4460 IF XX = 9 GOTO 10250 
4470 IF XX =10 GOTO 10260 
4480 IF XX = 11 GOSUB 5630
4490 IF XX = 13 OR XX = 14 GOTO 11700
4500 IF XX = 15 GOTO 11780 
4510 IF XX = 17 GOSUB 5640
4520 IF XX = 20 PRINT"This area is a smouldering ruin."
4525 IF XX = 21 GOTO 10270 
4527 IF XX = 22 GOTO 10280 
4530 IF XX = 23 GOSUB 5660 
4540 IF XX = 24 GOSUB 5670 
4550 IFXX=25 GOSUB 5750 
4560 IF XX = 26 GOSUB 6000 
4570 IF XX = 27 GOTO 11800 
4580 IF XX = 28 GOSUB 6010 
4590 IF XX = 29 GOSUB 6050 
4900 GOTO 700
4999 STOP
5000 FOR TT =1 TO 234: NEXT: RETURN
5009 REM * BLANK SPACE CHECK *
5010 IF B(Y) = 0 THEN B(Y) = Z
5020 PRINT"#";: RETURN
5029 REM * INSTRUCTIONS *
5030 PRINT"Each move may be Up, Down, Right, or Left. Only the first"
5040 PRINT"letter is needed. Never leave the map boundaries!"
5050 PRINT"If you want to see a map of the Magic Kingdom, enter 'M' as"
5060 PRINT"your move. This will not increment your move counter."
5070 PRINT"If you have the Magic Bazooka, you may enter 'B' to fire."
5080 PRINT"To have Terak's location displayed on the map, you must visit" 
5090 PRINT"the Oracle on Purlicon mountain. If you are carrying a Magic"
5100 PRINT"Orb he may even tell you more. Or he may not."
5110 PRINT"The object of the game is to return the Golden Flute to the"
5115 PRINT"Woodlands in the fewest moves. Please press 'ENTER' ";: INPUT Q$: RETURN
5120 IF XX = 20 GOTO 5290
5130 PRINT"Coramble, the Great Oracle, reveals that Terak's lair is"
5140 PRINT"at location #";
5150 FOR Y = 1 TO 100: IF B(Y) = 4 GOTO 5170 
5160 NEXT: IF MO = 1 GOTO 5180 ELSE RETURN 
5170 PRINT Y: A(Y) = 4: GOTO 5160
5180 IF AX = 3 THEN RETURN
5190 AX=AX+1: A=RND(6)+4: IF(A=5) OR (A=6) OR (A=9) OR (A = 10) GOTO 5210
5200 RETURN
5210 PRINT"He also reveals the location of all "; 
5220 IF A = 5 PRINT"Sirens."
5230 IF A = 6 PRINT"dragons."
5240 IF A = 9 PRINT"gargoyles."
5250 IF A = 10 PRINT"goblins."
5260 PRINT"They will be shown on your next map."
5270 FOR X = 1 TO 100: IF B(X) = A THEN A(X) = B(X) 
5280 NEXT: RETURN
5290 PRINT"The Great Oracle's body lies smoking in the corner.": RETURN
5300 PRINT"You are at the foot of Purlicon Mountain.": RETURN
5310 PRINT"You have infiltrated Terak's lair!": GOSUB 5000 
5320 IF FL = 1 GOTO 5360
5330 FL = 1: PRINT"You recover the Golden Flute!" 
5340 IF MO > 0 GOTO 5 540
5350 RETURN
5360 D = RND(8): IF D =1 AND BR = 0 GOTO 5530 
5370 IFD = 2 AND DG = 0 GOTO 5530
5380 IF D = 3 AND GR = 0 GOTO 5530
5390 IF D = 4 AND RU = 0 GOTO 5530
5400 IFD = 5 AND SJ = 0 GOTO 5530
5410 IFD=6 AND JS=0 GOTO 5530
5420 IF D = 7 AND AL = 0 GOTO 5530
5430 IFD=8 AND PM=0 GOTO 5530
5440 IF D = 1 PRINT"Brombiran";: BR = 0
5450 IF D = 2 PRINT"Dagglette";: DG = 0
5460 IF D = 3 PRINT"Gromphlur";: GR = 0
5470 IF D = 4 PRINT"Rulf";: RU = 0
5480 IFD = 5 PRINT"Sejjan:";: SJ = 0
5490 IF D = 6 PRINT"Jessan"; : JS = 0
5500 IF D = 7 PRINT"Allegrecia";: AL = 0
5510 IF D = 8 PRINT"Princess Melva"; : PM = 0
5520 PRINT" is dead.": PRINT
5530 RETURN
5540 A = RND(3): IF A = 1 GOTO 5360
5550 IF A = 3 GOTO 5570
5560 RETURN
5570 PRINT"The Magic Orb is destroyed!": MO = 0: RETURN
5580 IF (X < 1) OR (X > 100) THEN RETURN
5590 IF B(X) = 5 PRINT"An eerie sweet singing is heard in the distance."
5595 RETURN
5600 PRINT: PRINT"You just found a Magic Sword!": B(LC) = 0: SW = 1: RETURN
5610 X = RND(199)+ 1: PRINT"You just found ";X;" pieces of gold!"
5620 B(LC) = 0: GL = GL+X: PRINT: RETURN
5630 PRINT"You just found a Magic Orb!": PRINT: MO = 1: B(LC) = 0: RETURN
5640 B(LC) = 0: PRINT"You just found a ";: GOSUB 5000
5650 PRINT"***** MAGIC BAZOOKA *****": BZ =1: RETURN
5660 G$ = "KLUFFOOT": GOTO 5680
5670 G$ = "FRIEK"
5680 IF FL = 0 THEN RETURN
5690 PRINT G$;", THE GARGOYLE SLAVE OF TERAK,"
5700 IF SW = 1 GOTO 5720
5710 PRINT" STEALS THE GOLDEN FLUTE AGAIN!": FL = 0: RETURN
5720 PRINT" ATTEMPTS TO STEAL THE GOLDEN FLUTE AGAIN!": PRINT 
5730 PRINT"BUT THE MAGIC SWORD KILLS ";G$; "!": PRINT
5740 B(LC) = 0: RETURN
5750 PRINT"You are in the Enchanted Forest!": PRINT
5760 ZX=BR+DG+GR+RU+SJ+JS+AL+PM
5770 IF ZX > 5 THEN RETURN
5780 R = RND(10): IF R > 8 THEN RETURN
5790 IF (R =1) AND (BR = 0) GOTO 5880
5800 IF (R = 2) AND (DG = 0) GOTO 5890
5810 IF (R = 3) AND (GR = 0) GOTO 5900
5820 IF (R = 4) AND (RU = 0) GOTO 5910
5830 IF (R = 5) AND (SI = 0) GOTO 5920
5840 IF (R = 5) AND (JS = 0) GOTO 5930
5850 IF (R = 7) AND (AL = 0) GOTO 5940
5860 IF (R = 8) AND (PM = 0) GOTO 5950
5870 RETURN
5880 PRINT"BROMBIRAN";: BR = 1: GOTO 5970
5890 PRINT"DAGGLETTE";: DG = 1: GOTO 5970
5900 PRINT"GROMPHLUR";: GR =1: GOTO 5970
5910 PRINT"RULF";: RU =1: GOTO 5970
5920 PRINT"SEJJAN";: SJ = 1: GOTO 5970
5930 PRINT"JESSAN"; : JS =1: GOTO 5970
5940 PRINT"ALLEGRECIA";: AL = 1: GOTO 5970
5950 PRINT"PRINCESS MELVA";: PM = 1
5970 C(R) = 1.5: B(LC) = 0: PRINT" IS MAGICALLY RESTORED TO LIFE!" 
5980 PRINT: RETURN
6000 PRINT"You just found a Magic Zither!": PRINT: MZ = 1: B(LC) = 0: RETURN
6010 PRINT"THE MAGIC CHARIOT JUST FLEW OVER AN ENCHANTED LAND MINE!": PRINT
6020 GOSUB 5000: FOR X = 1 TO 50: XZ = RND(1022): PRINT@XZ, "*"; 
6030 U = XZ*X: NEXT: PRINT: PRINT
6040 GOTO 5 360
6050 IF GL < 2 THEN RETURN
6060 PRINT"A wicked witch steals all of your gold coins!": PRINT
6070 GL = 0: PRINT: PRINT" ", "Hee hee hee!": PRINT: PRINT
6080 RETURN
6999 REM * MAP *
7000 CLS: PRINT: PRINT: Z = 1: FOR X = 1 TO 10: PRINT" ",
7010 FOR Y = 1 TO 10: V = A(Z)
7020 IF(V = 0) OR (V = 7) OR (V = 8) OR (V = 11) OR (V> 15 ANDV<20) OR (V > 22 AND V< 27) PRINT". ";
7030 IF V =1 PRINT"W "; 
7040 IF V = 2 PRINT"M "; 
7050 IF V = 3 PRINT"P "; 
7060 IF V = 4 PRINT"T "; 
7070 IF V = 5 PRINT"S "; 
7080 IF V = 6 PRINT"D "; 
7090 IF V = 9 PRINT"G "; 
7100 IF V = 10 PRINT"g "; 
7110 IF V = 12 PRINT"C ";
7120 IF (V = 13) OR (V = 14) PRINT"X ";
7130 IF V = 15 PRINT"F "; 
7140 IF V = 20 PRINT"* "; 
7150 IF V = 21 PRINT"s "; 
7160 IF V = 22 PRINT"d "; 
7170 IF V = 27 PRINT"R ";
7180 IF V = 28 PRINT"! ";
7190 IF V = 29 PRINT"w ";
7200 Z = Z + 1: NEXT Y: PRINT: NEXT X
7210 PRINT"C The Magic Chariot (YOU), D = Dragon, d = Dwarf, F = Fog," 
7220 PRINT"G=Gargoyle, g=goblin, M=Purlicon Mountain, P=The Hopeless"
7230 PRINT"Pits, R=Rock, S=Sirens, s=Magic Sparrow, T=Terak's lair," 
7240 PRINT"W=The Woodlands, w=Witch, !=Land mine, *=smouldering ruin"
7250 GOTO 900
8000 FOR X = 1 TO 100: A(X) = 0: B(X) = 0: PRINT"&";: NEXT
8010 GOTO 70
10000 PRINT"YOU ARE IN THE WOODLANDS, HOME OF THE ELVES.": PRINT
10010 IF FL =1 PRINT"YOU HAVE RECOVERED THE GOLDEN FLUTE!": GOTO 1150 
10020 GOTO 4280
10030 PRINT"Terak fears your approach and moves his lair!": PRINT
10040 FOR X = 1 TO 100: IF B(X) = 4 THEN B(X) = 0: A(X) = 0
10050 NEXT
10060 X = RND(100): IF B(X) > 1 GOTO 10060
10070 B(X) = 4: GOTO 4310
10080 PRINT"The Magic Chariot is mired in the Hopeless Pits!": PRINT
10090 IF MO = 1 GOTO 10130
10100 A = RND(5): IF A> 3 PRINT"YOU ARE DOOMED!": GOTO 1050
10110 IF A < 3 GOSUB 5 360
10120 PRINT"You manage to get the Magic Chariot free of the muck.": GOTO 700
10130 INPUT"Do you rub your Magic Orb";Q$: Q$ = LEFT$(Q$,1)
10140 IF Q$ = "Y" GOTO 10160
10150 GOTO 10100
10160 GOSUB 5000: CLS: PRINT: PRINT: PRINT: PRINT
10170 PRINT" ", "* * * POOF * * *" : PRINT: PRINT: PRINT
10180 LC = RND(100): PRINT"The Magic Chariot is magically transported to location # ";LC
10190 A(ZZ) = B(ZZ): A(LC) = 12: GOTO 700
10200 PRINT: PRINT"The Sweet Song of the Sirens mesmerizes you!": PRINT 
10210 GOSUB 5000
10220 PRINT"The Magic Chariot crashes!": PRINT: GOSUB 5000
10230 GOTO 1050
10240 PRINT"A dragon ";: FO = 1: GOTO 10300
10250 PRINT"A gargoyle ";: FO = 2 :GOTO 10300
10260 PRINT"A goblin ";: FO = 3 : GOTO 10300
10270 PRINT"A Magic Sparrow ";: FO = 4: GOTO 10300
10280 PRINT"An ancient dwarf ";: GM = RND(150): FO = 5
10300 AM = RND(4): PRINT"is in your path!": PRINT: GOSUB 5000 
10310 A(LC) = B(LC)
10320 PRINT" ", "POSSIBLE ACTIONS": PRINT
10330 PRINT" 1 - Move the Magic Chariot"
10340 PRINT" 2 - Throw some gold coins overboard"
10350 PRINT" 3 - Hand to hand combat"
10360 IF MO = 1 PRINT"4 - Rub Magic Orb"
10370 IF SW = 1 PRINT"5 - Unsheath Magic Sword"
10380 IF MZ = 1 PRINT"6 - Play Magic Zither"
10390 PRINT" ",: INPUT"YOUR CHOICE";P
10400 IF P = 1 GOTO 10490
10410 IF P = 2 GOTO 10660 
10420 IF P = 3 GOTO 11000
10430 IF(MO = 1) AND (P = 4) GOTO 10160
10440 IF (SW =1) AND (P = 5) GOTO 11500
10450 IF (MZ =1) AND (P = 6) GOTO 11630
10460 PRINT"INVALID SELECTION!": PRINT: GOTO 10320 
10490 IF FO > 3 GOTO 910
10500 PRINT: INPUT"DIRECTION";M$: M$ = LEFT$(M$,1)
10520 IF M$ = "U" GOTO 10570 
10530 IF M$ = "D" GOTO 10580 
10540 IF M$ = "R" GOTO 10590 
10550 IF M$ = "L" GOTO 10600 
10560 GOTO 10500
10570 IF AM = 1 GOTO 1190 ELSE GOTO 10610
10580 IF AM = 2 GOTO 1000 ELSE GOTO 10610
10590 IF AM = 3 GOTO 1200 ELSE GOTO 10610
10600 IF AM = 4 GOTO 1220
10610 PRINT: PRINT" The ";: IF FO = 1 PRINT"dragon";
10620 IF FO = 2 PRINT"gargoyle"; 
10630 IF FO = 3 PRINT"goblin";
10640 PRINT" will not let you go that way!"
10650 GOTO 10320
10660 PRINT: INPUT"How much gold do you toss overboard";H 
10670 A = ABS(H): H = INT(H): IF H = 0 GOTO 10660
10680 IF H > GL GOTO 10995 
10690 GL = GL - H
10700 IF FO = 1 GOTO 10900 
10710 IF FO = 2 GOTO 10910 
10720 IF FO = 3 GOTO 10940 
10730 IF FO = 4 GOTO 10980
10740 PRINT"The dwarf thanks you very politely "
10750 GM = GM - H: IF GM > 1 GOTO 10320
10760 PRINT"and he reveals the location of all ";
10770 DF = RND(4): IF DF = 1 GOTO 10840
10780 IF DF = 2 GOTO 10860
10790 IF DF = 3 GOTO 10880
10800 PRINT"Sirens": FOR X = 1 TO 100: IF B(X) = 5 THEN A(X) = 5
10810 NEXT
10820 PRINT"This information will be displayed on your next map."
10830 A(LC) = 12: GOTO 700
10840 PRINT"goblins": FOR X = 1 TO 100: IF B(X) = 10 THEN A(X) = 10 
10850 NEXT: GOTO 10820
10860 PRINT"gargoyles": FOR X = 1 TO 100: IF B(X) = 9 THEN A(X) = 9 
10870 NEXT: GOTO 10820
10880 PRINT"dragons": FOR X = 1 TO 100: IF B(X) = 6 THEN A(X) = 6
10890 NEXT: GOTO 10820
10900 PRINT"Dragons have no interest in gold.": PRINT: GOTO 10320
10910 PRINT"The gargoyle puts the gold into its sack ";
10920 I = RND(200): IF H < I THEN PRINT: GOTO 10320
10930 PRINT"and leaves": B(LC) = 0: A(LC) = 12: GOTO 700
10940 I = RND(200): PRINT"The goblin eats the gold greedily ";
10950 IF H < I THEN PRINT: GOTO 10320
10960 PRINT"and dies of': PRINT"TERMINAL INDIGESTION.": PRINT
10970 B(LC) = 0: A(LC) = 12: GOTO 700
10980 PRINT"What would a bird want with ";H;" pieces of gold?": PRINT 
10990 GOTO 10320
10995 PRINT"YOU DO NOT HAVE ";H;" PIECES OF GOLD!": GOSUB 5360: GOTO 10320
11000 PRINT: PRINT" ", "YOUR CHAMPION?" 
11010 IF BR = 1 PRINT"1- BROMBIRAN" 
11020 IF DG = 1 PRINT"2 - DAGGLETTE" 
11030 IF GR = 1 PRINT"3 - GROMPHLUR" 
11040 IF RU = 1 PRINT "4 - RULF"
11050 IF SJ = 1 PRINT"5 - SEJJAN"
11060 IF JS = 1 PRINT"6 -JESSAN"
11070 IF AL =1 PRINT"7 - ALLEGRECIA" 
11080 IF PM = 1 PRINT"8 - PRINCESS MELVA" 
11090 PRINT"9 - "; N$
11100 PRINT" ",: INPUT"YOUR CHOICE";CH 
11110 IF CH =1 AND BR =1 GOTO 11210 
11120 IF CH = 2 AND DG =1 GOTO 11220 
11130 IF CH = 3 AND GR =1 GOTO 11230 
11140 IF CH = 4 AND RU = 1 GOTO 11240 
11150 IF CH = 5 AND SJ = 1 GOTO 11250 
11160 IF CH = 6 AND JS =1 GOTO 11260 
11170 IF CH = 7 AND AL =1 GOTO 11270 
11180 IF CH = 8 AND PM = 1 GOTO 11280 
11190 IF CH = 9 GOTO 11290
11200 PRINT " ","WHO???": PRINT: GOTO 11010
11210 I = 50: GOTO 11300
11220 I = 55: GOTO 11300 
11230 I = 45: GOTO 11300 
11240 I = 85: GOTO 11300 
11250 I = 25: GOTO 11300 
11260 I= 25: GOTO 11300 
11270 I = 50: GOTO 11300 
11280 I = 40: GOTO 11300 
11290 I = 75
11300 H=I*C(CH): C(CH)=C(CH)- .1: GOSUB 5000
11310 PRINT: PRINT: RS = RND(100)
11320 IF FO = 4 GOTO 11390 
11330 IF FO = 5 GOTO 11490 
11340 IF RS > H GOTO 11390
11350 PRINT "THE ";: IF FO =1 PRINT"DRAGON";
11360 IF FO = 2 PRINT"GARGOYLE";
11370 IF FO = 3 PRINT"GOBLIN";
11380 PRINT" IS SLAIN!": PRINT: B(LC) = 0: A(LC) = 12: GOTO 700
11390 IF CH =1 PRINT"BROMBIRAN";: BR = 0
11400 IF CH = 2 PRINT"DAGGLETTE"; : DG = 0
11410 IF CH = 3 PRINT"GROMPHLUR";: GR = 0
11420 IF CH = 4 PRINT"RULF";: RU = 0
11430 IF CH = 5 PRINT"SEJJAN"; : SJ = 0
11440 IF CH = 6 PRINT"JESSAN";: JS = 0
11450 IF CH = 7 PRINT"ALLEGRECIA";: AL = 0
11460 IF CH = 8 PRINT"PRINCESS MELVA";: PM = 0
11470 IF CH = 9 PRINT N$;" IS SLAIN!": PRINT"GAME OVER!": GOTO 1050 
11480 C(CH) = 0: PRINT" IS SLAIN!": PRINT: GOTO 10320
11490 PRINT"THE DWARF IS SLAIN!": B(LC) = 0: A(LC) = 12: GOTO 700 
11500 Q = RND(100): IF FO = 1 GOTO 11550
11510 IF FO = 2 GOTO 11600 
11520 IF FO = 3 GOTO 11350 
11530 IF FO = 4 GOTO 11390 
11540 IF FO = 5 GOTO 11490 
11550 IF Q<65 GOTO 11350
11560 PRINT"This dragon is immune to your sword!": PRINT: GOSUB 5000 
11570 PRINT"It attacks the Magic Chariot!": PRINT: GOSUB 5000
11580 GOSUB 5360: IF Q< 80 GOTO 10320
11590 PRINT"Your Magic Sword is destroyed!": PRINT: SW = 0: GOTO 10320 
11600 IF Q< 50 GOTO 11350
11610 PRINT"The gargoyle draws its own Magic Sword!": PRINT: GOSUB 5000 
11620 GOTO 11580
11630 IF FO = 1 GOTO 11350 
11640 IF FO = 3 GOTO 11670
11650 PRINT"No one is particularly impressed with your talent.": PRINT
11660 GOTO 10320
11670 Q = RND(10): IF Q> 5 GOTO 11350 
11680 GOSUB 5360: GOTO 10320
11699 REM * WALL *
11700 PRINT: PRINT"You just ran into a brick wall!": PRINT
11710 QQ = RND(3)+QQ: IF QQ< 7 GOSUB 5360 
11720 IF QQ> 10 GOTO 11750
11730 A(LC) = B(LC): IF XX = 13 THEN LC = LC - 20 ELSE LC = LC+20
11740 PRINT"THE MAGIC CHARIOT IS HURLED BACKWARDS!": GOTO 4250
11750 PRINT"The Magic Chariot is reduced to a pile of junk.": PRINT
11760 GOSUB 5000
11770 PRINT"You and your entire party are dead.": PRINT: GOTO 1050
11780 PRINT"The Magic Chariot has flown into a thick, mysterious fog!": PRINT: PRINT
11790 FOR X = 1 TO 150: PRINT"#";: NEXT: A(LC) =15 : LC = RND(50) + 30
11795 PRINT"The fog clears.": ZZ = A(LC): A(LC) = 12: GOTO 700
11800 PRINT: PRINT"A gigantic boulder blocks your path!": PRINT
11810 R = RND(4)
11820 A(LC) = 27: ZZ = 27
11830 INPUT"Which way do you try to go";M$: M$ = LEFT$(M$,1)
11840 M = M + 1
11850 IF M$ = "D" AND R = 1 GOTO 1000 
11860 IF M$ = "U" AND R = 2 GOTO 1190 
11870 IF M$ = "R" AND R = 3 GOTO 1200 
11880 IF M$ = "L" AND R = 4 GOTO 1220
11890 PRINT"THE BOULDER IS IN YOUR WAY!": PRINT
11895 FOR X =1 TO 9: C(X) = C(X) - (C(X)/ 20): NEXT: GOTO 11830
