0 CLEAR384:DIMP(3,3),ST(5),SF(11),I$(10),I(10),P$(36),B(4),FM(20),FL(20),FH(20),N$(10,3),IN$(10,2):MC=16384:GOTO24
5 PRINT@CC/2,"X";:RETURN
6 K=PEEK(17024):K1=PEEK(17025):RETURN
7 PRINTI$;:GOSUB6:CC=((KAND1)*256+K1)-1+MC:FORC1=CC-LEN(I$)+1TOCC:POKEC1,PEEK(C1)-64:NEXT:RETURN
9 PRINT@352,;:FORQQ=1TO5:PRINT"                               ";:NEXT
10 PRINT@352,;:RETURN
12 PRINT"YOU FEEL BETTER!":RETURN
13 PRINT"HIT ANY KEY";:GOSUB17:RETURN
14 I$=STR$(CH):PRINT@148,;:GOSUB7:RETURN
15 QQ=50:FORQ=0TO5STEP2:PRINT@QQ,MID$(S$,Q*3+1,2)ST(Q)TAB(24)MID$(S$,(Q+1)*3+1,2)ST(Q+1):QQ=QQ+32:NEXT:RETURN
16 PRINT"YOU HAVE FOUND "I$:RETURN
17 FORQ=1TO500:C$=INKEY$:ON-(C$<>"")GOTO404:NEXT:C$="@":RETURN
18 PRINT"enter TO "I$":";:GOSUB17:RETURN
19 GOSUB9:GOTO134
21 GOSUB23:GOTO9
22 FORQ=0TO11:PRINT@Q*32,"                 ";:NEXT:RETURN
23 FORQ=1TO2500:NEXT:RETURN
24 M$="GNOLL   KOBOLD  SKELETONHOBBIT  ZOMBIE  ORC     FIGHTER MUMMY   ELF     GHOUL   DWARF   TROLL   WRAITH  OGRE"
27 MO$=M$+"    MINOTAURGIANT   SPECTER VAMPIRE DEMON   DRAGON  ":S$="STRINTWISCONDEXCHR":T$="REFUSESILVERGOLD  GEMS  JEWELS"
30 DATASWORD,ARMOR,SHIELD,ELVEN CLOAK,ELVEN BOOTS,RING OF REGENERATION,RING OF PROTECTION,SCROLL OF RESCUE
31 DATAPOTION OF HEALING,POTION OF STRENGTH
32 B$="RED   YELLOWGREEN BLUE":CM$="WXADSIKJL HQPC."+CHR$(31)+"RUG":CR$=CHR$(13):XO=1.6915:YO=1.4278:ZO=1.2462:WO=4694
33 FORI=1TO10:READI$(I):NEXT:FORI=1TO36:READP$(I):NEXT:FORI=1TO20:FM(I)=0:NEXT:FORI=1TO2:FORK=1TO10:READIN$(K,I):NEXT:NEXT
34 DATAMAGIC MISSILE,SLEEP,CURE LIGHT WOUNDS,LIGHT,TURN UNDEAD,PROT/EVIL,WEB,LEVITATE,CAUSE LIGHT WNDS,DETECT TRAPS,CHARM
35 DATASTRENGTH,LIGHTNING BOLT,CURE SERIOUS WNDS,CONTINUAL LIGHT,INVISIBILITY,HOLD MONSTER,PHANTSML FORCES,PASS WALL,FIREBALL
36 DATACAUSE SERIOUS WND,FLESH TO STONE,FEAR,FINGER OF DEATH,TELEPORT,ASTRAL WALK,POWER WORD KILL,ICE STORM,WALL OF FIRE,PLAGUE
38 DATATIME STOP,RAISE DEAD,HOLY SYMBOL,WORD OF RECALL,RESTORATION,PRISMATIC WALL
39 DATASALTY,BOLD,LOAD,OLD,GOODLY,WORTHY,LOFTY,FINE,ROCKY,AGED,ROAD,EYE,TOOTH,DRAGON,MUG,DEMON,WHARF,BRIDGE,MEADE,ALE
41 CLS:C=0:PRINT"sTART OR lOAD CHARACTER?":GOSUB17:IFC$="@"THEN41
44 IFC$<>"L"THEN46
45 PRINT"READ":STOP
46 CLS:PRINT"enter TO USE STATS":FORI=0TO5:Q=0:FORQ1=1TO3:Q=Q+RND(6):NEXT:ST(I)=Q
48 NEXT:GOSUB15:GOSUB17:IFC$<>CR$THEN46
50 HP=ST(3):CH=HP:GD=0:TG=0:CZ=1:LV=1:FORI=1TO10:I(I)=0:SF(I)=0:NEXT:SF(11)=0:GOSUB492:CX=25:CY=13:EX=0:SU=1:CS=1
56 CLS:L=1:GOSUB508:GOSUB410:SY=1:GOTO62
57 FORI=1TO11:SF(I)=SF(I)-1:NEXT:L=CZ:GOSUB9:IFSY=1THENGOSUB410
59 SY=1:IFI(6)=0ORCH=HPTHEN62
60 CH=CH+I(6):IFCH>HPTHENCH=HP
61 GOSUB508
62 IFRND(0)>.3THEN146
63 IFSF(6)>0ANDRND(0)>.2THEN146
64 PRINT@135,"#";:M=RND(20):IFSF(7)>0ANDM<4THEN64
65 IFRND(20)<=I(4)THEN535
66 ML=INT((RND(0)^1.5)*(CZ*2+1)+1):GOSUB522:IFSF(3)>0ANDUN=1ANDRND(0)>.8THEN64
68 GOSUB10:GOSUB489:IFSF(9)>0ANDM<16ANDM<>11THENGOSUB21:GOTO146
70 PRINT@338,"LV"ML;M$:PRINT@384,;
71 MH=INT((RND(0)^.5)*ML*M+1):L=ML
72 IFRND(0)>.95THENONRND(3)GOTO75,78,84
73 IFM<>9THEN77
74 IFRND(0)>.04*ST(5)THEN77
75 PRINT"THE "M$" LIKES YOU":PRINT"HE HEALS YOU!":CH=HP
76 GOSUB508:GOSUB23:GOSUB520:PRINT@135," ";:GOTO146
77 IFM<>4ORRND(0)<.04*ST(5)THEN83
78 PRINT"THE "M$" MAKES A MOVE":GOSUB23
79 FORI=1TO10:IFI(I)>0THEN81
80 NEXT:PRINT"YOU HAVE NOTHING HE WANTS!":GOTO76
81 PRINT"HE STEALS ";:IFI<8THENPRINT"YOUR "I$(I):I(I)=0:GOTO76
82 PRINT"A "I$(I):I(I)=I(I)-1:GOTO76
83 IFM<>20ORRND(0)*30>ST(3)THEN89
84 I=RND(7):IFI(I)>=MLTHEN89
85 C=ML-I(I):C=RND(C):I(I)=I(I)+C
86 PRINT"THE "M$" LIKES YOU!":GOSUB21:PRINT"HE GIVES YOU A "I$(I)" +"I(I):GOSUB508:GOSUB21
88 PRINT@135," ";:GOSUB520:GOTO146
89 IFRND(0)>.5+ST(4)*.02THEN105
90 PRINT"fIGHT, cAST OR eVADE?":GOSUB17
92 FORI=1TO7:IFMID$("FCE0."+CR$+"@",I,1)=C$THEN94
93 NEXT:PRINT"THE "M$" IS NOT AMUSED":GOSUB21:GOTO90
94 ONIGOTO95,130,131,95,130,131,105
95 I=INT(RND(0)*20)+LV+I(1)+ST(0)/2
96 IFSF(1)>0THENI=I+4
97 IFI<10THENPRINT"YOU MISSED...":GOTO105
98 I=INT(RND(0)*8+RND(0)*LV*2+I(1)+1):IFSF(1)>0THENI=I+5
99 PRINT"YOU DO"I"DAMAGE":MH=MH-I:IFMH>0THEN105
101 PRINT"IT'S DEAD."
102 PRINT@136," ";:E=ML*M*10:EX=EX+E:GOSUB23:GOSUB520:GOSUB9:GOSUB508:GOSUB475:GOSUB21:GOTO145
105 DB=1:PA=0:DR=0:MB=0:GOTO116
106 I=INT(RND(0)*20)+ML-I(2)-I(3)+MB:IFM>16ANDM<20ANDSF(4)>0THENI=I-6
108 IFI<10THENPRINT"IT MISSED...":GOSUB21:GOTO90
109 I=INT((RND(0)*8+RND(0)*ML*2+1)*DB):GOSUB14
110 CH=CH-I:GOSUB507:IFCH<1THEN390
111 GOSUB23:IFRND(0)>DRORSF(4)>0THEN114
112 GOSUB9:PRINT"IT DRAINS A LEVEL!":EX=INT(EX/2):GOSUB475:GOSUB21:GOTO108
114 IFRND(0)>PATHENGOSUB21:GOTO90
115 GOSUB9:PRINT"YOU'RE PARALYZED!":GOSUB21:GOTO105
116 IFM=13THENDR=.1:GOTO106
117 IFM=17THENDR=.2:GOTO106
118 IFM=18THENDR=.3:PA=.3:GOTO106
119 IFM=10THENPA=.5:GOTO106
120 IFM<>19THEN124
121 GOSUB21:IFRND(0)>.6THENPRINT"IT USES IT'S SWORD!":MB=4:DB=3:GOTO106
123 PRINT"IT USES IT'S WHIP!":MB=2:DB=2:GOTO106
124 IFM<>20THEN106
125 IFRND(0)>.3THENMB=5:DB=2:GOTO106
126 PRINT"THE DRAGON BREATHS FIRE!":GOSUB21:I=RND(ML*20):IFRND(0)<.05*ST(3)THEN129
128 PRINT"YOU'RE ONLY SINGED":I=INT(I/2+.5)
129 GOSUB14:GOTO110
130 D=1:GOTO539
131 Q=RND(18):IFQ<ST(4)+I(5)THEN134
133 PRINT"YOU'RE STUCK...":GOSUB21:GOSUB10:GOTO105
134 IFSF(8)>0THENC=RND(4):GOTO144
135 ONRND(4)GOTO136,137,138,139
136 C=1:IF(LAND3)<3THEN144
137 C=2:IF(P(2,1)AND3)<3THEN144
138 C=3:IF(INT(L/4)AND3)<3THEN144
139 C=4:IF(INT(P(1,2)/4)AND3)<3THEN144
140 C=1:IF(LAND3)<3THEN144
141 C=2:IF(P(2,1)AND3)<3THEN144
142 C=3:IF(INT(L/4)AND3)<3THEN144
143 GOTO133
144 GOSUB520:GOSUB9:GOTO357
145 IFRND(0)>.5THEN147
146 IFRND(0)>.2THEN187
147 T=0:IFRND(0)>.85THENT=1
148 PRINT@135,"$";:GOSUB10:C=0:IFRND(0)>.7THEN161
149 I$="":GOSUB16:I=RND(5)-1:PRINTMID$(T$,I*6+1,6):PRINT"enter TO PICK UP:";
151 IFT=1THENIFSF(2)>0ANDRND(0)>.1THENPRINT:PRINT"YOU DETECT TRAPS!"
152 GOSUB17:IFC$=CR$THEN154
153 GOSUB21:GOTO187
154 PRINT"GOT IT!":IFT=0THEN158
155 Q=RND(L*3):GOSUB9:PRINT"IT'S TRAPPED!":GOSUB14:CH=CH-Q:GOSUB507:IFCH<1THENGOSUB23:GOTO390
158 J=INT(RND(0)*I*L*200+1):PRINT"IT'S WORTH"J"GP":GD=GD+J:IFI=0ANDRND(0)>.8THENGOSUB508:GOSUB9:GOTO173
160 GOSUB508:GOTO185
161 IFRND(0)>.5THEN173
162 I$="A CHEST":GOSUB16:PRINT"enter TO OPEN IT:";
164 IFT=1THENIFSF(2)>0ANDRND(0)>.1THENPRINT:PRINT"YOU DETECT TRAPS!";
165 GOSUB17:IFC$<>CR$THENPRINT"IGNORE IT":GOTO187
166 PRINT"OPEN IT":IFT=0THEN169
167 GOSUB21:PRINT"IT EXPLODES!":I=RND(10*CZ):CH=CH-I:GOSUB507:IFCH<1THEN390
169 GOSUB21:I=INT(RND(0)*(1000*LV^2)+1):IFRND(0)>.9THENPRINT"INSIDE... ONLY COBWEBS.":GOTO185
171 PRINT"INSIDE:"I"GP!":GD=GD+I:GOSUB508:IFRND(0)>.5THEN185
172 C=1:GOSUB21
173 I=RND(10):PRINT"YOU SEE A ";:J=INT(RND(0)^.5*(L+1)+1):IFI>7THENPRINTI$(I):GOTO176
175 PRINTI$(I)" +"J
176 I$="PICK IT UP":GOSUB18:IFC$=CR$THEN178
177 PRINT"LEAVE IT":GOTO184
178 IFI<>1ORT<>1THEN182
179 PRINT:PRINT"IT'S A HOSTILE SWORD!":I=RND(I*5)
180 GOSUB14:CH=CH-I:GOSUB508:IFCH<1THEN390
181 GOTO185
182 PRINT"GOT IT!":I(I)=I(I)+1:IFI<8THENI(I)=J
183 GOSUB508
184 IFC=1ANDRND(0)>.5THENGOSUB21:GOTO173
185 PRINT@135," ";:GOSUB21:IFRND(0)>.5THENGOSUB691:IFM<>0THEN68
187 X=CX:Y=CY:Z=CZ-1:GOSUB399:J=INT(H/256):I=INT(L/256):IFCZ=1THENJ=0
190 IFI>9THENI=I-9:GOTO190
191 IFJ>9THENJ=J-9:GOTO191
192 IFCZ=50ANDI=4THENI=0
193 IFI=0ANDJ<>4THEN351
194 IFJ=4THEN238
195 ONIGOTO196,219,228,238:STOP
196 IFCZ<>1THEN217
197 J=4:GOTO238
198 Q1=CX*CY:Q2=CX+CY:Q1=Q1-INT(Q1/10)*10+1:Q2=Q2-INT(Q2/10)*10+1:M$=IN$(Q1,1)+" "+IN$(Q2,2)+" "+"INN "
200 GOSUB9:GOSUB22:PRINT@70,"èèàÑèè";:PRINT@102,"èàÄÄÑè";:PRINT@134,"àÄÄÄÄÑ";:PRINT@166,"äÄÄÄÄÖ";:PRINT@198,"äÄÄÄÄÖ";
204 PRINT@230,IN$(Q1,1):PRINT@262,IN$(Q2,2):PRINT@295,IN$(Q3,3)
205 GOSUB10:I$="THE "+M$:GOSUB16
206 PRINT"THEY CASH IN":EX=EX+GD:TG=TG+GD:GD=0:GOSUB508
207 GOSUB475:GOSUB701:GOSUB9:CH=HP:PRINT"YOU HAVE"TG"IN THE SAFE"
208 FORI=0TO11:SF(I)=0:NEXT
209 CS=SU:PRINT"YOU SPEND THE NIGHT":GOSUB23:GOSUB12:GOSUB508:GOSUB9
211 I$="RETURN * TO SAVE":GOSUB18:IFC$="@"THEN213
214 IFC$="*"THENPRINT:GOTO368
215 IFC$<>CR$THEN211
216 PRINT"REENTER";:GOSUB21:CZ=1:GOTO57
217 PRINT@135,"ELV";:GOSUB10:PRINT"YOU FEEL MOMENTARILY HEAVY"
218 GOSUB23:CZ=CZ-1:GOTO57
219 IFCZ=50THEN217
220 PRINT@135,"PIT";:GOSUB10:PRINT"YOU SEE A PIT":IFSF(5)>0THENPRINT"YOU ARE HOVERING ABOVE IT":GOTO223
222 IFRND(20)-1>ST(4)+I(5)THEN226
223 PRINT"DESCEND?";:GOSUB17:IFC$="Y"THEN227
224 GOTO381
226 PRINT"YOU FALL IN!":L=3:GOSUB493:GOSUB23
227 CZ=CZ+1:GOTO57
228 PRINT@135,"TPT";:GOSUB10
229 PRINT"YOU'VE BEEN TELEPORTED...":GOSUB508
230 IF((CX+CY)AND1)=0THENCZ=CZ-1:IF((CX+CY)AND2)=2THENCZ=CZ+2
231 CX=CZ*8+CY*13:CY=CY+CZ*6+CX*17
232 IFCX>200THENCX=CX-200:GOTO232
233 IFCY>200THENCY=CY-200:GOTO233
234 IFCZ=0THENCZ=1
235 IFCZ>50THENCZ=50
236 IFRND(0)>.8THEN230
237 GOTO57
238 PRINT@135,;:IFJ=4THENPRINT"^";
239 IFI=4THENPRINTCHR$(92);
240 GOSUB9:I$="A STAIRWAY":GOSUB16:IFJ=4ANDCZ=1THENPRINT"YOU SEE LIGHT ABOVE"
242 PRINT"DO YOU WANT TO ";:IFJ=4THENPRINT"GO uP, ";
244 IFI=4THENPRINT"GO dOWN, ";
245 PRINT:PRINT"OR sTAY ON LEVEL?";:GOSUB17
246 IFC$="@"THENC$="S"
247 IF(I<>4AND(C$="D"ORC$="1"))OR(J<>4AND(C$="U"ORC$="7"))THEN252
248 IFC$="U"THENCZ=CZ-1:IFCZ>0THEN57
249 IFCZ<=0THEN198
250 IFC$="D"THENCZ=CZ+1:GOTO57
251 IFC$="S"THENGOSUB9:GOTO351
252 PRINT"???";:GOTO240
351 M=0:GOSUB9:GOSUB10:PRINT"->";
352 GOSUB17:IFC$="@"THENC$="S"
353 Q=1:FORC=QTO20:ON-(MID$(CM$,C,Q)=C$)GOTO355:NEXT
354 PRINT@352,"NO";:FORI=1TO200:NEXT:GOTO351
355 IFC>5THENC=C-5
356 IFSF(11)>0ANDC<6THENPRINT"YOU'RE CONFUSED ->";:C=RND(5)
357 ONCGOTO358,360,362,364,366,373,372,375,389,389,368,376,382,385
358 IF(LAND3)>2ANDSF(8)<1ORCY=1THEN354
359 PRINT"NORTH";:CY=CY-1:GOSUB698:GOTO57
360 IF(P(2,1)AND3)>2ANDSF(8)<1ORCY=200THEN354
361 PRINT"SOUTH";:CY=CY+1:GOSUB698:GOTO57
362 IF(INT(L/4)AND3)>2ANDSF(8)<1ORCX=1THEN354
363 PRINT"WEST";:CX=CX-1:GOSUB698:GOTO57
364 IF(INT(P(1,2)/4)AND3)>2ANDSF(8)<1ORCX=200THEN354
365 PRINT"EAST";:CX=CX+1:GOSUB698:GOTO57
366 PRINT"STAY";:PRINT@135,"   ";:GOSUB691:IFM<>0THEN68
367 SY=0:GOTO57
368 PRINT"SAVING...":GOTO372
370 GOSUB21:IFCZ=0THEN211
371 GOTO351
372 PRINT"QUIT":END
373 GOSUB935:GOSUB9:GOSUB13:GOSUB900:GOTO351
375 CLS:GOSUB508:GOSUB410:GOSUB695:GOTO351
376 PRINT"USE "I$(8):IFI(8)<1THEN380
378 I(8)=I(8)-1:CX=25:CY=13:CZ=1:PRINT"*ZAP!*":GOSUB23:GOSUB695
379 GD=0:GOSUB508:GOTO57
380 PRINT"YOU DON'T HAVE ONE!"
381 GOSUB21:GOTO351
382 PRINT"DRINK HEALING POTION":IFI(9)<1THEN380
383 I(9)=I(9)-1:GOSUB508:CH=CH+RND(20):IFCH>HPTHENCH=HP
384 GOSUB12:GOSUB508:GOTO381
385 PRINT"DRINK GIANT STRENGTH POTION":IFI(10)<1THEN380
386 IFSF(1)<0THENSF(1)=0
387 SF(1)=SF(1)+19+RND(20):PRINT"STRENGTH FLOWS THROUGH YOUR BODY"
388 I(10)=I(10)-1:GOSUB508:GOTO381
389 D=0:GOTO539
390 GOSUB9:PRINT"YOU DIED!":GOSUB21
391 IFSF(10)>0THEN674
392 CLS:PRINT@256,"TRY AGAIN?";:GOSUB17:IFC$="N"THENEND
396 IFC$="@"THEN392
397 RUN
399 Q=X*XO+Y*YO+Z*ZO+(X+XO)*(Y+YO)*(Z+ZO):H=(Q-INT(Q))*WO:IFINT(H/256)>5THENH=HAND255
401 IFINT(H/256)>0THENH=(INT((Q*10-INT(Q*10))*15+1)*256)OR(HAND255)
402 IFX=1ORX=201THENH=HOR12
403 IFY=1ORY=201THENH=HOR3
404 RETURN
405 IFW<2THENRETURN
406 FORQ=0TO10:PRINT@(L+Q)/2,"Ä";:NEXT:IFW>2THENRETURN
407 FORQ=4TO7:PRINT@(L+Q)/2,"-";:NEXT:RETURN
408 ON-(W<2)GOTO404:FORQ=0TO192STEP64:PRINT@(L+Q)/2,"Ä";:NEXT:IFW>2THENRETURN
409 PRINT@(L+64)/2,"!";:PRINT@(L+128)/2,"!";:RETURN
410 Z=CZ:FORQ2=0TO3:Y=CY+Q2-1:FORQ1=0TO3:X=CX+Q1-1:GOSUB399
411 P(Q2,Q1)=H:NEXT:NEXT:GOSUB22:PRINT@320,;:Q=0:FORI=1TO11:IFSF(I)<1THEN417
416 PRINTMID$("STRGDTRPLGHTPROTLEVTINVSFEARASTWTMSTRSEDDRNK",I*4-3,4)":";
417 NEXT:IF(P(1,1)AND3)>1OR(INT(P(0,1)/4)AND3)>1THEN419
418 L=2:W=(P(0,0)AND3):GOSUB405
419 IF(P(1,0)AND3)>1OR((INT(P(1,1))/4)AND3)>1THEN421
420 L=2:W=(INT(P(0,0)/4)AND3):GOSUB408
421 IF(P(1,1)AND3)>1THEN424
422 L=12:W=(P(0,1)AND3):GOSUB405:W=(INT(P(0,1)/4)AND3):GOSUB408:L=22:W=(INT(P(0,2)/4)AND3):GOSUB408
424 IF(P(1,1)AND3)>1OR(INT(P(0,2)/4)AND3)>1THEN426
425 L=22:W=P(0,2)AND3:GOSUB405
426 IF(INT(P(1,2)/4)AND3)>1OR(P(1,2)AND3)>1THEN428
427 L=32:W=INT(P(0,3)/4)AND3:GOSUB408
428 IF(INT(P(1,2)/4)AND3)>1THEN431
429 L=214:W=(P(1,2)AND3):GOSUB405:L=224:W=INT(P(1,3)/4)AND3:GOSUB408:L=406:W=P(2,2)AND3:GOSUB405
431 IF(P(2,1)AND3)>1OR(INT(P(2,2)/4)AND3)>1THEN433
432 L=598:W=P(3,2)AND3:GOSUB405
433 IF(INT(P(1,2)/4)AND3)>1OR(P(2,2)AND3)>1THEN435
434 L=416:W=(INT(P(2,3)/4)AND3):GOSUB408
435 IF(P(2,1)AND3)>1THEN438
436 L=406:W=INT(P(2,2)/4)AND3:GOSUB408:L=588:W=P(3,1)AND3:GOSUB405:L=396:W=INT(P(2,1)/4)AND3:GOSUB408
438 IF(P(2,1)AND3)>1OR(INT(P(2,1)/4)AND3)>1THEN440
439 L=578:W=P(3,0)AND3:GOSUB405
440 IF(INT(P(1,1)/4)AND3)>1OR(P(2,0)AND3)>1THEN442
441 L=386:W=INT(P(2,0)/4)AND3:GOSUB408
442 IF(INT(P(1,1)/4)AND3)>1THEN445
443 L=386:W=P(2,0)AND3:GOSUB405:L=194:W=INT(P(1,0)/4)AND3:GOSUB408:W=P(1,0)AND3:GOSUB405
445 L=204:W=INT(P(1,1)/4)AND3:GOSUB408:W=P(1,1)AND3:GOSUB405:L=214:W=INT(P(1,2)/4)AND3:GOSUB408:L=396:W=P(2,1)AND3:GOSUB405
447 CC=337:GOSUB5:L=P(1,1):IFSF(3)<=0THENRETURN
448 IFINT(P(0,0)/256)=0THEN452
449 IF(P(1,0)AND3)<AAND(INT(P(1,1)/4)AND3)<2THEN451
450 IF(P(1,1)AND3)>1OR(INT(P(0,1)/4)AND3)>1THEN452
451 CC=135:GOSUB5
452 IFINT(P(0,1)/256)<>0AND(P(1,1)AND3)<2THENCC=145:GOSUB5
453 IFINT(P(0,2)/256)=0THEN457
454 IF(P(1,1)AND3)<2AND(INT(P(0,2)/4)AND3)<2THEN456
455 IF(P(1,2)AND3)>1OR(INT(P(1,2)/4)AND3)>1THEN457
456 CC=155:GOSUB5
457 IFINT(P(1,2)/256)<>0AND(INT(P(1,2)/4)AND3)<2THENCC=347:GOSUB5
458 IFINT(P(2,2)/256)=0THEN462
459 IF(P(2,2)AND3)<2AND(INT(P(1,2)/4)AND3)<2THEN461
460 IF(P(2,1)AND3)>1OR(INT(P(2,2)/4)AND3)>1THEN462
461 CC=539:GOSUB5
462 IFINT(P(2,1)/256)<>0AND(P(2,1)AND3)<2THENCC=529:GOSUB5
463 IFINT(P(2,0)/256)=0THEN467
464 IF(P(2,0)AND3)<2AND(INT(P(1,1)/4)AND3)<2THEN466
465 IF(P(2,1)AND3)>1OR(INT(P(2,1)/4)AND3)>1THEN467
466 CC=519:GOSUB5
467 IFINT(P(1,0)/256)<>0AND(INT(P(1,1)/4)AND3)<2THENCC=327:GOSUB5
468 RETURN
475 IFEX<1000*2^LVTHEN480
476 GOSUB9:PRINT"YOU WENT UP A LEVEL!";:LV=LV+1:J=RND(ST(3))
477 CH=CH+J:HP=HP+J:IFEX>1000*2^LVTHENEX=1000*2^LV-1
478 CS=CS+LV:SU=SU+LV
479 GOSUB508:GOSUB21:PRINT"YOU GAIN"J"HP":RETURN
480 IFLV=1ANDEX>=0THENRETURN
481 IFEX>1000*2^(LV-1)THENRETURN
482 CS=CS-LV:SU=SU-LV:IFCS<0THENCS=0
483 PRINT"YOU GO DOWN A LEVEL!";:LV=LV-1:J=RND(ST(3))
484 CH=CH-J:HP=HP-J
485 GOSUB508:GOSUB21:GOSUB14
486 IFCH>0ANDLV>0THENRETURN
487 GOSUB23:GOTO390
488 FORQ=1TO100:NEXT:RETURN
489 M$=MID$(MO$,M*8-7,8)
490 IFRIGHT$(M$,1)=" "THENM$=LEFT$(M$,LEN(M$)-1):GOTO490
491 RETURN
492 FORQ=1TO4:B(Q)=RND(4):NEXT:RETURN
493 D=INT(RND(0)*L*6+1):GOSUB14:CH=CH-D:IFCH>0THENGOSUB507:RETURN
495 GOSUB508:GOSUB21:GOTO390
496 I=INT(RND(0)*500*CZ+1):PRINT"YOU JUST ";:IFRND(0)>.5THENPRINT"LOST";:I=-I:GOTO499
498 PRINT"GAINED";
499 PRINTABS(I)"XP":EX=EX+I:GOSUB475:RETURN
500 I=RND(6)-1:IFRND(0)>.5THEN503
501 IFST(I)=18THEN500
502 PRINT"YOUR "MID$(S$,I*3+1,3)" GOES UP":ST(I)=ST(I)+1:GOTO505
503 IFST(I)=3THEN500
504 PRINT"YOUR "MID$(S$,I*3+1,3)" GOES DOWN":ST(I)=ST(I)-1
505 PRINT" BY 1":GOSUB508:GOSUB23:RETURN
506 GOSUB6:EN=2:GOTO509
507 GOSUB6:EN=3:GOTO509
508 GOSUB6:EN=1
509 IFEN=3OREN=2THEN990
511 GOTO900
520 PRINT@336,"":GOSUB9:PRINT@135," ";:RETURN
522 UN=0:IFM=3ORM=5ORM=8ORM=10ORM=13ORM=17ORM=18THENUN=1
523 RETURN
524 D$=""
525 GOSUB17:IFC$<>CHR$(20)THEN529
526 IFD$=""THEN525
527 PRINT"20815";:IFLEN(D$)=1THEN524
528 D$=LEFT$(D$,LEN(D$)-1):GOTO525
529 IFC$=CR$THENC=VAL(D$):PRINT:RETURN
530 IFC$="@"THENC=0:RETURN
531 IFLEN(D$)>9THEN525
532 IFC$="-"ANDD$=""THEN534
533 IFC$<"0"ORC$>"9"THEN525
534 PRINTC$;:D$=D$+C$:GOTO525
535 GOSUB10:PRINT"YOU HAVE NOT BEEN NOTICED..."
536 I$="APPROACH":GOSUB18:IFC$=CR$THENGOSUB9:GOTO66
538 PRINT@135," ";:GOSUB9:GOTO146
539 PRINT"CAST":PRINT"SPELL LEVEL:";:GOSUB17:C=VAL(C$):PRINTC$
540 IFC>0ANDC<=INT(LV/3)+1ANDC<7THEN543
541 IFC=0THEN560
542 PRINT"SPELL LV NOT HIGH ENOUGH!";:GOSUB23:GOTO560
543 IFC>CSTHENPRINT"NOT ENOUGH SPELL UNITS!";:GOTO558
544 PRINT"SPELL( = TO LIST):";:GOSUB17:IFC$=CR$ORC$="@"THEN560
545 IFC$<"1"ORC$>"6"THENC$="="
546 IFC$="="THEN548
547 CS=CS-C:GOSUB506:GOTO553
548 GOSUB9:FORI=1TO3:PRINTMID$(STR$(I),2)" "LEFT$(P$(I+6*(C-1)),13)TAB(16)MID$(STR$(I+3),2)" "LEFT$(P$(I+3+6*(C-1)),13):NEXT
550 GOSUB13:GOSUB9:IFC$<"1"ORC$>"6"THEN544
552 GOTO547
553 S=VAL(C$):ONCGOTO564,587,604,622,644,671
554 MH=MH-I:PRINT"IT SUFFERS"I"DAMAGE":IFMH>0THEN558
555 GOTO101
556 PRINT"NOT IN MELEE!":GOSUB21:GOTO105
557 PRINT"SPELL WASTED!"
558 GOSUB21:IFD=1THEN105
559 GOTO351
560 GOSUB9:IFD=1THEN90
561 GOTO351
562 GOSUB21:GOSUB520:GOTO146
563 PRINT"UNDEAD ARE ALREADY DEAD!":GOTO558
564 ONSGOTO565,567,574,577,580,586
565 PRINTP$(1):GOSUB21:IFD=0THEN557
566 I=RND(8)+4:GOTO554
567 PRINTP$(2):GOSUB21:IFD=0THEN557
568 IFUN=1THENPRINT"UNDEAD DON'T SLEEP!":GOTO105
569 IFRND(20)>ST(1)THENPRINT"THE "M$" ISN'T SLEEPY!":GOTO105
570 PRINT"THE "M$" IS SLEEPING!":I$="KILL":GOSUB18
571 IFC$<>CR$THEN19
572 IFRND(0)>.2THEN101
573 PRINT"IT WOKE UP!":GOSUB23:GOTO105
574 PRINTP$(3):I=RND(8):GOSUB12:CH=CH+I:IFCH>HPTHENCH=HP
576 GOSUB507:GOTO558
577 S=4:T=3:U=11
578 PRINTP$(S):IFSF(T)<0THENSF(T)=0
579 SF(T)=SF(T)+RND(U)+4:GOTO558
580 PRINTP$(5):GOSUB21:IFD=0THEN557
581 IFUN=1THEN583
582 PRINT"THE "M$" IS INSULTED":PRINT"AT BEING CALLED UNDEAD":GOTO558
583 IFRND(0)<.05*ST(2)+.05*LV-.05*MLTHEN585
584 PRINT"THE "M$" LISTENS WITH DEAF EARS":GOTO558
585 ML=INT((ML/2)+.5):PRINT"IT RUNS IN FEAR!":GOTO102
586 S=6:T=4:U=11:GOTO578
587 ONSGOTO588,594,595,597,598,603
588 PRINTP$(7):GOSUB21:IFD=0THEN557
589 IFINT(RND(0)^2*20+ML)>ST(1)THENPRINT"THE "M$" DODGES ASIDE!":GOTO105
590 PRINT"THE "M$" IS WEBBED!":I$="KILL":GOSUB18
591 IFC$<>CR$THEN19
592 IFRND(0)-ML/20>.2THEN101
593 PRINT"IT BROKE FREE!":GOSUB23:GOTO105
594 S=8:T=5:U=21:GOTO578
595 PRINTP$(9):GOSUB21:IFD=0THEN557
596 I=INT(RND(0)*12+3):GOTO554
597 S=10:T=2:U=21:GOTO578
598 PRINTP$(11):GOSUB9:IFD=0THEN557
599 IFUN=1THENPRINT"THE UNDEAD IGNORE YOU!":GOTO105
600 IFINT((RND(0)^2)*20+1)>ST(5)THENPRINT"THE "M$" RESISTS YOU!":GOTO105
601 PRINT"THE "M$" IS CHARMED":I$="KILL":GOSUB18
602 GOTO591
603 S=12:T=1:U=21:GOTO578
604 ONSGOTO605,607,610,611,612,618
605 PRINTP$(13):GOSUB9:PRINT"ZAP!":IFD=0THEN557
606 GOSUB23:I=INT(RND(0)*6*LV+15):GOTO554
607 PRINTP$(14):I=INT(RND(0)*24+1):GOSUB12:CH=CH+I
608 IFCH>HPTHENCH=HP
609 GOSUB507:GOTO558
610 S=15:T=3:U=31:GOTO578
611 S=16:T=6:U=21:GOTO578
612 PRINTP$(17):GOSUB21:IFD=0THEN557
613 IFINT(RND(0)*20+ML)>ST(1)THENPRINT"THE "M$" IGNORES YOU!":GOTO105
614 PRINT"THE "M$" IS HELD":I$="KILL":GOSUB18
615 IFC$<>CR$THEN19
616 IFRND(0)>.2+ML*.03THEN101
617 PRINT"IT BROKE FREE!":GOSUB23:GOTO105
618 PRINTP$(18):GOSUB21:IFD=0THEN557
619 IFRND(21)+ML<ST(1)THEN621
620 PRINT"THE "M$" DOESN'T BELIEVE!":GOTO105
621 PRINT"IT BELIEVES! ARRGH...":GOTO101
622 ONSGOTO623,632,634,636,639,640
623 PRINTP$(19):GOSUB21:IFD=1THEN556
624 PRINT"DIRECTION ";:GOSUB17
625 FORI=1TO9:IFMID$("WXAD8246@",I,1)=C$THEN627
626 NEXT:PRINT"?":GOSUB9:GOTO624
627 IFI>4THENI=I-4
628 IFI=5THEN558
629 IF(I=1ANDCY>1)OR(I=2ANDCY<200)OR(I=3ANDCX>1)OR(I=4ANDCX<200)THEN631
630 PRINT"ONLY STONE THERE...":GOTO558
631 GOSUB701:PRINT"*POOF*":ONIGOTO359,361,363,365
632 PRINTP$(20):GOSUB9:PRINT"WHOOOOSHH!":IFD=0THEN557
633 PRINT"THE "M$" IS BURNING!":I=INT(RND(0)*12*LV+15):GOSUB23:GOTO554
634 PRINTP$(21):GOSUB21:IFD=0THEN557
635 I=INT(RND(0)*32+10):GOTO554
636 PRINTP$(22):GOSUB21:IFD=0THEN557
637 IFRND(0)>.6THENPRINT"THE "M$" ISN'T AFFECTED":GOTO105
638 PRINT"ONE STONE STATUE...":GOTO101
639 S=23:T=7:U=30:GOTO578
640 PRINTP$(24):GOSUB21:PRINT"DIE!":IFD=0THEN557
641 IFUN=1THEN563
642 GOSUB23:IFRND(0)>.3+ML*.4-LV*.03THEN101
643 PRINT"THE "M$" LAUGHS":GOSUB21:GOTO105
644 ONSGOTO645,654,655,658,660,665
645 PRINTP$(25):GOSUB21:IFD=1THEN556
646 PRINT"+NORTH/-SOUTH:";:GOSUB524:NY=C:PRINT"+EAST/-WEST:";:GOSUB524
647 NX=C:PRINT"+UP/-DOWN:";:GOSUB524:NZ=C:I=SQR(NX^2+NY^2+(NZ*5)^2)-.1
648 IFI>LV*5THENPRINT"TOO FAR...TRY AGAIN":GOSUB21:GOTO646
649 NX=CX+NX:NY=CY-NY:NZ=CZ-NZ
650 IFNX>0ANDNX<201ANDNY>0ANDNY<201ANDNZ<51THEN652
651 PRINT"ONLY STONE THERE... SPELL FAILS":GOTO558
652 IFNZ<1THENPRINT"ONLY THIN AIR... SPELL FAILS":GOTO558
653 CX=NX:CY=NY:CZ=NZ:PRINT"*POOF*":GOSUB701:GOTO57
654 S=26:T=8:U=16:GOTO578
655 PRINTP$(27):GOSUB21:IFD=0THEN557
656 PRINT"QWERTY!":GOSUB23:IFRND(0)<.75THEN101
657 PRINT"THE "M$" DOESN'T HEAR...":GOTO558
658 PRINTP$(28):GOSUB21:IFD=0THEN557
659 PRINT"BRRRR!":I=60:GOTO554
660 PRINTP$(29):GOSUB21:PRINT@136,"*";:GOSUB9:PRINT"WHOOSH!":PRINT"A WALL OF FIRE SURROUNDS YOU":GOSUB23:GOSUB21
662 IFD=0THEN557
663 PRINT@136," ";:GOSUB9:IFRND(0)>.4THENPRINT"THE "M$" IS GONE":GOTO562
664 PRINT"THE "M$" WALKS THROUGH!":I=RND(12)+7:GOTO554
665 PRINTP$(30):GOSUB21:IFD=0THEN557
666 IFUN=1THEN563
667 PRINT"BLACK DEATH FOR THE "M$:GOSUB21:IFRND(0)>ST(2)*.05THENPRINT"IT'S IMMUNE!":GOTO558
669 IFRND(0)*2<ST(3)*.03+1THEN101
670 PRINT"THE SPELL BACKFIRES!":GOSUB21:GOTO390
671 ONSGOTO672,673,678,682,685,687
672 S=31:T=9:U=20:GOTO578
673 S=32:T=10:U=40:GOTO578
674 PRINTP$(32):GOSUB21:ST(3)=ST(3)-1
675 SF(10)=0:IFRND(0)>ST(3)*.06THEN677
676 PRINT"IT WORKS!":CH=HP:GOSUB508:GOTO558
677 PRINT"IT DOESN'T WORK!":GOTO390
678 PRINTP$(33):GOSUB21:IFD=0THEN557
679 FORU=1TO9:PRINT@362,MID$(".,+^O0* ",U,1);:GOSUB488:NEXT
680 PRINT@352,;:GOSUB23:IFRND(0)<.9THEN101
681 PRINT"THE "M$" DOESN'T SEE...":GOTO558
682 PRINTP$(34):GOSUB21:IFD=1THEN556
683 FORI=0TO10:I(I)=0:NEXT:GD=0:CLS:GOSUB508:GOSUB701:CX=25:CY=13:CZ=1:PRINT@131,"*ZAP!*":GOSUB23:GOTO57
685 PRINTP$(35):GOSUB21:GOSUB12:CH=HP:GOSUB507:GOTO558
687 PRINTP$(36):GOSUB21:PRINT"A COLORED WALL APPEARS!":IFD=0THEN557
689 FORJ=1TO16:PRINT@168,"*";:GOSUB488:PRINT@168," ";:GOSUB488:NEXT
690 PRINT@167,"X";:PRINT@136," ";:GOSUB9:PRINT"THE "M$" IS GONE":GOTO562
691 M=0:IFFM(1)=0THEN693
692 M=FM(1):ML=FL(1):MH=FH(1):GOSUB522
693 FORI=1TO19:FM(I)=FM(I+1):FL(I)=FL(I+1):FH(I)=FH(I+1):NEXT
694 FM(20)=0:FL(20)=0:FH(20)=0:IFM<>0THENPRINT@135,"#";
695 X=-15:FORQ=1TO11:X=X+32:IFFM(Q)=0THENPRINT@X,".";:GOTO697
696 PRINT@X,"@";
697 NEXT:RETURN
698 FORQ=19TO1STEP-1:FM(Q+1)=FM(Q):FL(Q+1)=FL(Q):FH(Q+1)=FH(Q):NEXT:IFM=.THENFM(1)=0:FL(1)=0:FH(1)=0:GOTO695
700 FM(1)=M:FL(1)=ML:FH(1)=MH:GOTO695
701 FORQ=1TO20:FM(Q)=0:FL(Q)=0:FH(Q)=0:NEXT:GOTO695
900 PRINT@18,"LV"LV:GOSUB15:PRINT@146,"HP"STR$(CH)HP:IFEN=3THEN990
922 PRINT@178,"SU"STR$(CS)SU:IFEN=2THEN990
930 PRINT@210,"EX"EX:PRINT@242,"GP"GD:GOTO990
935 QQ=0:FORI=1TO10:QQ=QQ+1:PRINT@QQ*32-14,;:IFI<4THENIFI(I)>=0THEN945
940 IFI(I)<1THEN980
945 IFI>7THEN965
950 PRINTI$(I);:IFI(I)>0THENPRINT" +"MID$(STR$(I(I)),2);:GOTO980
955 GOTO980
965 PRINTI(I)I$(I);
980 PRINT:NEXT
990 POKE17024,K:POKE17025,K1:RETURN
