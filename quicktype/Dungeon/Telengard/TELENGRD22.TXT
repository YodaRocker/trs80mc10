0 CLEAR384:DIMP(3,3),I$(10),I(98),P$(36),N$(10,3),H$(10,2):MC=16384:GOTO28
1 DB=1:PA=0:DR=0:MB=0:GOTO116
2 C$=" LIKES YOU!":GOTO24
3 IFT=1THENIFI(18)>0ANDRND(0)>.1THENPRINT"YOU DETECT TRAPS!"
4 RETURN
5 FORR=0TO11:PRINT@R*32+17,"":NEXT:RETURN
6 K=PEEK(17024):K1=PEEK(17025):RETURN
7 PRINT"*ZAP!*":GOTO23
8 GOSUB6:GOTO900
9 GOSUB10:FORR=1TO5:PRINT"                               ";:NEXT:IFI(98)THENPRINT"orb";
10 PRINT@352,;:RETURN
11 I$="KILL":GOTO18
12 PRINT"YOU FEEL BETTER!":GOTO23
13 I$="+"+MID$(STR$(R),2,2):RETURN
14 PRINT"YOU SUFFER"Q"DAMAGE":RETURN
15 R=50:FORV=0TO5STEP2:PRINT@R,MID$(S$,V*3+1,2)I(11+V)TAB(24)MID$(S$,(V+1)*3+1,2)I(12+V):R=R+32:NEXT:RETURN
16 C$="YOU HAVE FOUND "+I$:GOSUB25:GOTO23
17 C$=INKEY$:FORQ=1TO500:C$=INKEY$:ON-(C$<>"")GOTO4:NEXT:C$=A$:RETURN
18 PRINT"enter TO "I$":";:GOTO17
19 GOSUB9:GOTO134
20 GOSUB8
21 GOSUB23:GOTO9
22 FORR=0TO11:PRINT@R*32,"                 ";:NEXT:RETURN
23 FORR=1TO2400:NEXT:RETURN
24 C$="THE "+M$+C$
25 R=1:V=32:FORV=VTORSTEP-1:I$=MID$(C$,V,1):IFI$<"!"THEN?MID$(C$,R,V-R):R=V+1:V=R+32:IFI$=""THENV=.
26 V=V+(V>255)*(V-255):NEXT:RETURN
27 PRINT@135,I$;:RETURN
28 M$="GNOLL   KOBOLD  SKELETONHOBBIT  ZOMBIE  ORC     FIGHTER MUMMY   ELF     GHOUL   DWARF   TROLL   WRAITH  OGRE"
29 MO$=M$+"    MINOTAURGIANT   SPECTER VAMPIRE DEMON   DRAGON  ":S$="STRINTWISCONDEXCHR"
30 DATASWORD,ARMOR,SHIELD,ELVEN CLOAK,ELVEN BOOTS,RING OF REGENERATION,RING OF PROTECTION,SCROLL OF RESCUE
32 E$=CHR$(13):A$="@":XO=1.6915:YO=1.4278:ZO=1.2462:WO=4694:DATAPOTION OF HEALING,POTION OF STRENGTH
33 FORI=1TO10:READI$(I):NEXT:FORI=1TO36:READP$(I):NEXT:FORI=1TO2:FORK=1TO10:READH$(K,I):NEXT:NEXT
34 DATAMAGIC MISSILE,SLEEP,CURE LIGHT WOUNDS,LIGHT,TURN UNDEAD,PROT/EVIL,WEB,LEVITATE,CAUSE LIGHT WNDS,DETECT TRAPS,CHARM
35 DATASTRENGTH,LIGHTNING BOLT,CURE SERIOUS WNDS,CONTINUAL LIGHT,INVISIBILITY,HOLD MONSTER,PHANTSML FORCES,PASS WALL,FIREBALL
36 DATACAUSE SERIOUS WND,FLESH TO STONE,FEAR,FINGER OF DEATH,TELEPORT,ASTRAL WALK,POWER WORD KILL,ICE STORM,WALL OF FIRE,PLAGUE
38 DATATIME STOP,RAISE DEAD,HOLY SYMBOL,WORD OF RECALL,RESTORATION,PRISMATIC WALL
39 DATASALTY,BOLD,LOAD,OLD,GOODLY,WORTHY,LOFTY,FINE,ROCKY,AGED,ROAD,EYE,TOOTH,DRAGON,MUG,DEMON,WHARF,BRIDGE,MEADE,ALE
41 CLS:PRINT"sTART OR lOAD CHARACTER?":GOSUB17:IFC$=A$THEN41
42 IFC$<>"L"THEN46
43 PRINT"PRESS PLAY":CLOAD*I,"TELENDAT":B=I(0):G=I(88):TG=I(89):EX=I(90):N=I(91):HP=I(92)
44 CX=I(93):CY=I(94):CZ=I(95):SU=I(96):CS=I(97):CLS:GOSUB8:GOSUB695:SY=1:GOTO57
46 CLS:PRINT"enter TO USE STATS":FORI=0TO5:Q=0:FORQ1=1TO3:Q=Q+RND(6):NEXT:I(11+I)=Q
48 NEXT:GOSUB15:GOSUB17:IFC$<>E$THEN46
50 HP=I(14):N=HP:CZ=1:B=1:CX=25:CY=13:SU=1:CS=1
56 CLS:L=1:GOSUB8:GOSUB410:SY=1:GOTO62
57 FORI=1TO11:I(16+I)=I(16+I)-1:NEXT:L=CZ:GOSUB9:IFSY=1THENGOSUB410
59 SY=1:IFI(6)=0ORN=HPTHEN62
60 N=N+I(6):IFN>HPTHENN=HP
61 GOSUB8
62 IFRND(0)>.3THEN146
63 IFI(22)>0ANDRND(0)>.2THEN146
64 I$="#":GOSUB27:M=RND(20):IFI(23)>0ANDM<4THEN64
65 IFRND(20)<=I(4)THEN535
66 ML=INT((RND(0)^1.5)*(CZ*2+1)+1):GOSUB522:IFI(19)>0ANDUN=1ANDRND(0)>.8THEN64
68 GOSUB10:GOSUB489:IFI(25)>0ANDM<16ANDM<>11THENGOSUB21:GOTO146
70 PRINT@306,;LEFT$("LV"+STR$(ML)+" "+M$,14):GOSUB10:MH=INT((RND(0)^.5)*ML*M+1):L=ML:IFRND(0)>.95THENONRND(3)GOTO75,78,84
73 IFM<>9THEN77
74 IFRND(0)>.04*I(16)THEN77
75 GOSUB2:PRINT"HE HEALS YOU!":N=HP
76 GOSUB8:GOSUB23:GOSUB520:I$=" ":GOSUB27:GOTO146
77 IFM<>4ORRND(0)<.04*I(16)THEN83
78 C$=" MAKES A MOVE":GOSUB24:GOSUB23
79 FORI=1TO10:IFI(I)>0THEN81
80 NEXT:PRINT"YOU HAVE NOTHING HE WANTS!":GOTO76
81 IFI<8THENC$=" STEALS YOUR "+I$(I):GOSUB24:I(I)=0:GOTO76
82 C$=" STEALS A "+I$(I):GOSUB24:I(I)=I(I)-1:GOTO76
83 IFM<>20ORRND(0)*30>I(14)THEN89
84 I=RND(7):IFI(I)>=MLTHEN89
85 C=ML-I(I):C=RND(C):I(I)=I(I)+C:GOSUB2:R=I(I):GOSUB13:C$=" GIVES YOU A "+I$(I)+I$:GOSUB24:GOSUB20
88 I$=" ":GOSUB27:GOSUB520:GOTO146
89 IFRND(0)>.5+I(15)*.02THEN1
90 PRINT"fIGHT, cAST OR eVADE?":GOSUB17
92 FORI=1TO4:IFMID$("FCE@",I,1)=C$THEN94
93 NEXT:C$=" IS NOT AMUSED":GOSUB24:GOSUB21:GOTO90
94 ONIGOTO95,130,131,1
95 I=INT(RND(0)*20)+B+I(1)+I(11)/2
96 IFI(17)>0THENI=I+4
97 IFI<10THENPRINT"YOU MISSED...":GOTO1
98 I=INT(RND(0)*8+RND(0)*B*2+I(1)+1):IFI(17)>0THENI=I+5
99 PRINT"YOU DO"I"DAMAGE":MH=MH-I:IFMH>0THEN1
101 PRINT"IT'S DEAD."
102 PRINT@136," ";:E=ML*M*10:EX=EX+E:GOSUB23:GOSUB520:GOSUB9:GOSUB8:GOSUB475:GOSUB21:GOTO145
106 I=INT(RND(0)*20)+ML-I(2)-I(3)+MB:IFM>16ANDM<20ANDI(20)>0THENI=I-6
108 IFI<10THENPRINT"IT MISSED...":GOSUB21:GOTO90
109 I=INT((RND(0)*8+RND(0)*ML*2+1)*DB)
110 N=N-I:GOSUB8:Q=I:GOSUB14:IFN<1THEN390
111 GOSUB23:IFRND(0)>DRORI(20)>0THEN114
112 GOSUB9:PRINT"IT DRAINS A LEVEL!":EX=INT(EX/2):GOSUB475:GOSUB21:GOTO108
114 IFRND(0)>PATHENGOSUB21:GOTO90
115 GOSUB9:PRINT"YOU'RE PARALYZED!":GOSUB21:GOTO1
116 IFM=13THENDR=.1:GOTO106
117 IFM=17THENDR=.2:GOTO106
118 IFM=18THENDR=.3:PA=.3:GOTO106
119 IFM=10THENPA=.5:GOTO106
120 IFM<>19THEN124
121 GOSUB21:IFRND(0)>.6THENPRINT"IT USES ITS SWORD!":MB=4:DB=3:GOTO106
123 PRINT"IT USES ITS WHIP!":MB=2:DB=2:GOTO106
124 IFM<>20THEN106
125 IFRND(0)>.3THENMB=5:DB=2:GOTO106
126 PRINT"THE DRAGON BREATHS FIRE!":GOSUB21:I=RND(ML*20):IFRND(0)<.05*I(14)THEN110
128 PRINT"YOU'RE ONLY SINGED":I=INT(I/2+.5):GOTO110
130 D=1:GOTO539
131 Q=RND(18):IFQ<I(15)+I(5)THEN134
133 PRINT"YOU'RE STUCK...":GOSUB21:GOSUB10:GOTO1
134 IFI(24)>0THENC=RND(4):GOTO144
135 ONRND(4)GOTO136,137,138,139
136 C=1:IF(PAND3)<3THEN144
137 C=2:IF(P(2,1)AND3)<3THEN144
138 C=3:IF(INT(P/4)AND3)<3THEN144
139 C=4:IF(INT(P(1,2)/4)AND3)<3THEN144
140 C=1:IF(PAND3)<3THEN144
141 C=2:IF(P(2,1)AND3)<3THEN144
142 C=3:IF(INT(P/4)AND3)<3THEN144
143 GOTO133
144 GOSUB520:GOSUB9:GOTO357
145 IFRND(0)>.5THEN147
146 IFRND(0)>.2THEN187
147 T=0:IFRND(0)>.85THENT=1
148 I$="$":GOSUB27:GOSUB10:C=0:IFRND(0)>.7THEN161
149 I=RND(5)-1:I$=MID$("REFUSESILVERGOLD  GEMS  JEWELS",I*6+1,6):GOSUB16:GOSUB3:I$="PICK UP":GOSUB18:IFC$=E$THEN154
153 PRINT"IGNORE IT":GOSUB21:GOTO187
154 PRINT"GOT IT!":IFT=0THEN158
155 Q=RND(L*3):GOSUB21:PRINT"IT'S TRAPPED!":N=N-Q:GOSUB8:GOSUB14:IFN<1THENGOSUB23:GOTO390
158 J=INT(RND(0)*I*L*200+1):PRINT"IT'S WORTH"J"GP";:G=G+J:IFI=0ANDRND(0)>.8THENGOSUB8:GOSUB9:GOTO173
159 GOSUB8:GOTO185
161 IFRND(0)>.5THEN173
162 I$="A CHEST":GOSUB16:GOSUB3:I$="OPEN IT":GOSUB18:IFC$<>E$THEN153
166 PRINT"OPEN IT":IFT=0THEN169
167 GOSUB21:PRINT"IT EXPLODES!":I=RND(10*CZ):N=N-I:GOSUB8:IFN<1THEN390
169 GOSUB21:I=INT(RND(0)*(1000*B^2)+1):IFRND(0)>.9THENPRINT"INSIDE... ONLY COBWEBS":GOTO185
171 PRINT"INSIDE:"I"GP";:G=G+I:GOSUB8:IFRND(0)>.5THEN185
172 C=1:GOSUB21
173 I=RND(10):C$="YOU SEE A "+I$(I):J=INT(RND(0)^.5*(L+1)+1):IFI>7THEN176
175 R=J:GOSUB13:C$=C$+I$
176 GOSUB25:I$="PICK IT UP":GOSUB18:IFC$=E$THEN178
177 PRINT"LEAVE IT":GOTO184
178 IFI<>1ORT<>1THEN182
179 PRINT:PRINT"IT'S A HOSTILE SWORD!":I=RND(I*5):N=N-I:Q=I:GOSUB14:GOSUB8:IFN<1THEN390
181 GOTO185
182 PRINT"IT'S YOURS!":I(I)=I(I)+1:IFI<8THENI(I)=J
183 GOSUB8
184 IFC=1ANDRND(0)>.5THENGOSUB21:GOTO173
185 I$=" ":GOSUB27:GOSUB21:IFRND(0)>.5THENGOSUB691:IFMTHEN68
187 X=CX:Y=CY:Z=CZ-1:GOSUB399:J=INT(H/256):I=INT(P/256):IFCZ=1THENJ=0
190 IFI>9THENI=I-9:GOTO190
191 IFJ>9THENJ=J-9:GOTO191
192 IFCZ=50ANDI=4THENI=0
193 IFI=0ANDJ<>4THEN351
194 ON-(J=4)GOTO238:ONIGOTO196,219,228,238,253:IFCX=9ANDCY=3ANDCZ=3ANDI(98)=0THEN300
195 GOTO351
196 IFCZ<>1THEN217
197 J=4:GOTO238
198 Q1=CX*CY:Q2=CX+CY:Q1=Q1-INT(Q1/10)*10+1:Q2=Q2-INT(Q2/10)*10+1:M$=H$(Q1,1)+" "+H$(Q2,2)+" "+"INN "
200 GOSUB9:GOSUB22:PRINT@70,"èèàÑèè";:PRINT@102,"èàÄÄÑè";:PRINT@134,"àÄÄÄÄÑ";:PRINT@166,"äÄÄÄÄÖ";:PRINT@198,"äÄïöÄÖ";
204 PRINT@225,H$(Q1,1):PRINT@257,H$(Q2,2):PRINT@290,"INN":GOSUB10:I$="THE "+M$:GOSUB16:EX=EX+G:TG=TG+G:G=0:GOSUB8
207 GOSUB475:GOSUB701:GOSUB9:N=HP:PRINT"YOU HAVE"TG"IN THE SAFE":FORI=0TO11:I(16+I)=0:NEXT
209 CS=SU:PRINT"YOU SPEND THE NIGHT":GOSUB23:GOSUB12:GOSUB8
211 GOSUB9:I$="RETURN * TO SAVE":GOSUB18:IFC$=A$THEN211
214 CZ=1:IFC$="*"THEN368
215 IFC$<>E$THEN211
216 GOTO57
217 I$="ELV":GOSUB27:PRINT@352,"YOU FEEL HEAVY FOR A MOMENT":GOSUB23:CZ=CZ-1:GOTO57
219 IFCZ=50THEN217
220 I$="PIT":GOSUB27:PRINT@352,"YOU SEE A PIT":IFI(21)>0THENPRINT"YOU ARE HOVERING ABOVE IT":GOTO223
222 IFRND(20)-1>I(15)+I(5)THEN226
223 I$="DESCEND":GOSUB18:IFC$=E$THEN227
224 GOTO381
226 PRINT"YOU FALL IN!":L=3:GOSUB493:GOSUB23
227 CZ=CZ+1:GOTO57
228 I$="TPT":GOSUB27:PRINT@352,"YOU'VE BEEN TELEPORTED...":GOSUB23:GOSUB8
230 IF((CX+CY)AND1)=0THENCZ=CZ-1:IF((CX+CY)AND2)=2THENCZ=CZ+2
231 CX=CZ*8+CY*13:CY=CY+CZ*6+CX*17
232 IFCX>200THENCX=CX-200:GOTO232
233 IFCY>200THENCY=CY-200:GOTO233
234 IFCZ=0THENCZ=1
235 IFCZ>50THENCZ=50
236 IFRND(0)>.8THEN230
237 GOTO57
238 I$="/":IFJ=4THENI$="^"
239 GOSUB27
240 GOSUB9:I$="A STAIRWAY":GOSUB16:IFJ=4ANDCZ=1THENPRINT"YOU SEE LIGHT ABOVE"
242 C$="DO YOU WANT TO ":IFJ=4THENC$=C$+"GO uP, "
244 IFI=4THENC$=C$+"GO dOWN, "
245 C$=C$+"OR sTAY ON LEVEL?":GOSUB25:GOSUB17
246 IFC$=A$THENC$="S"
247 IF(I<>4ANDC$="D")OR(J<>4ANDC$="U")THEN240
248 IFC$="U"THENCZ=CZ-1:IFCZ>0THEN57
249 IFCZ<=0THEN198
250 IFC$="D"THEN227
251 GOSUB9:GOTO351
253 I$="ALT":GOSUB27:GOSUB9:I$="A HOLY ALTAR":GOSUB16:I$="WORSHIP":GOSUB18:IFC$=E$THEN259
255 IFRND(0)>.7THEN351
256 PRINT:PRINT"DIRTY PAGAN TRASH!":I$="#  ":GOSUB27
257 M=RND(20):GOSUB522:IFUN=0THEN257
258 GOSUB21:GOTO66
259 PRINT:I$="DONATE":GOSUB18:IFC$<>E$THEN255
261 PRINT:PRINT"HOW MUCH?";:GOSUB524:GOSUB9:IFC>GTHENPRINT"YOU DON'T HAVE THAT MUCH!":GOTO256
263 IFC<50*CZTHEN256
264 G=G-C:GOSUB8
265 IFRND(0)<C/(G+C)THEN267
266 PRINT"THANK YOU":GOTO381
267 I=RND(7):IFI(16+I)<0THENI(16+I)=0
268 I(16+I)=I(16+I)+INT(RND(0)*100*C/(G+C)+1):PRINT"YOU'VE BEEN HEARD":GOTO381
300 GOSUB9:I$="THE ORB OF CHARLIE":GOSUB16:I(98)=1
351 M=0:GOSUB9:PRINT"->";:GOSUB17:IFC$=A$THENC$="S"
353 Q=1:FORC=QTO13:ON-(MID$("WXADSHQPC*RUG",C,Q)=C$)GOTO355:NEXT
354 PRINT@352,"NO":GOSUB23:GOTO351
355 IFI(27)>0ANDC<6THENPRINT"YOU'RE CONFUSED ->";:C=RND(5)
357 ONCGOTO358,360,362,364,366,935,372,375,389,368,376,382,385
358 IF(PAND3)>2ANDI(24)<1ORCY=1THEN354
359 PRINT"NORTH":CY=CY-1:GOSUB698:GOTO57
360 IF(P(2,1)AND3)>2ANDI(24)<1ORCY=200THEN354
361 PRINT"SOUTH";:CY=CY+1:GOSUB698:GOTO57
362 IF(INT(P/4)AND3)>2ANDI(24)<1ORCX=1THEN354
363 PRINT"WEST":CX=CX-1:GOSUB698:GOTO57
364 IF(INT(P(1,2)/4)AND3)>2ANDI(24)<1ORCX=200THEN354
365 PRINT"EAST":CX=CX+1:GOSUB698:GOTO57
366 PRINT"STAY":I$="   ":GOSUB27:GOSUB691:IFMTHEN68
367 SY=0:GOTO57
368 I(0)=B:I(88)=G:I(89)=TG:I(90)=EX:I(91)=N:I(92)=HP:I(93)=CX:I(94)=CY:I(95)=CZ:I(96)=SU:I(97)=CS
369 GOSUB9:I$="SAVE":GOSUB18:IFC$=A$THEN369
370 CSAVE*I,"TELENDAT":END
372 GOSUB9:I$="QUIT":GOSUB18:IFC$=A$THEN372
374 IFC$=E$THENEND
375 CLS:GOSUB8:GOSUB410:GOSUB695:GOTO351
376 PRINT"USE "I$(8):IFI(8)<1THEN380
378 I(8)=I(8)-1:CX=25:CY=13:CZ=1:GOSUB7:GOSUB695
379 G=0:GOSUB8:GOTO57
380 PRINT"YOU DON'T HAVE ONE!"
381 GOSUB21:GOTO351
382 PRINT"DRINK HEALING POTION":IFI(9)<1THEN380
383 I(9)=I(9)-1:GOSUB8:N=N+RND(20):IFN>HPTHENN=HP
384 GOSUB12:GOSUB8:GOTO381
385 PRINT"DRINK GIANT STRENGTH POTION":IFI(10)<1THEN380
386 IFI(17)<0THENI(17)=0
387 I(17)=I(17)+19+RND(20):PRINT"STRENGTH FLOWS THROUGH YOUR BODY"
388 I(10)=I(10)-1:GOSUB8:GOTO381
389 D=0:GOTO539
390 PRINT"YOU DIED!":GOSUB21:IFI(26)>0THEN674
392 GOSUB9:PRINT"TRY AGAIN?";:GOSUB17:IFC$="N"THENEND
396 IFC$<>"Y"THEN392
397 RUN
399 Q=X*XO+Y*YO+Z*ZO+(X+XO)*(Y+YO)*(Z+ZO):H=(Q-INT(Q))*WO:IFINT(H/256)>5THENH=HAND255
401 IFINT(H/256)>0THENH=(INT((Q*10-INT(Q*10))*15+1)*256)OR(HAND255)
402 IFX=1ORX=201THENH=HOR12
403 IFY=1ORY=201THENH=HOR3
404 RETURN
405 IFW<2THEN4
406 FORQ=0TO10:POKE(P+Q)/2+MC,32:NEXT:IFW>2THEN4
407 FORQ=4TO7:PRINT@(P+Q)/2,"-";:NEXT:RETURN
408 ON-(W<2)GOTO4:FORQ=0TO192STEP64:POKE(P+Q)/2+MC,32:NEXT:IFW>2THEN4
409 PRINT@(P+64)/2,"!";:PRINT@(P+128)/2,"!";:RETURN
410 Z=CZ:FORQ2=0TO3:Y=CY+Q2-1:FORQ1=0TO3:X=CX+Q1-1:GOSUB399:P(Q2,Q1)=H:NEXT:NEXT:GOSUB22:PRINT@320,;:Q=0
411 FORI=1TO11:IFI(16+I)>=1THENPRINTMID$("STRGDTRPLGHTPROTLEVTINVSFEARASTWTMSTRSEDDRNK",I*4-3,4)":";
417 NEXT:IF(P(1,1)AND3)>1OR(INT(P(0,1)/4)AND3)>1THEN419
418 P=2:W=(P(0,0)AND3):GOSUB405
419 IF(P(1,0)AND3)>1OR((INT(P(1,1))/4)AND3)>1THEN421
420 P=2:W=(INT(P(0,0)/4)AND3):GOSUB408
421 IF(P(1,1)AND3)>1THEN424
422 P=12:W=(P(0,1)AND3):GOSUB405:W=(INT(P(0,1)/4)AND3):GOSUB408:P=22:W=(INT(P(0,2)/4)AND3):GOSUB408
424 IF(P(1,1)AND3)>1OR(INT(P(0,2)/4)AND3)>1THEN426
425 P=22:W=P(0,2)AND3:GOSUB405
426 IF(INT(P(1,2)/4)AND3)>1OR(P(1,2)AND3)>1THEN428
427 P=32:W=INT(P(0,3)/4)AND3:GOSUB408
428 IF(INT(P(1,2)/4)AND3)>1THEN431
429 P=214:W=(P(1,2)AND3):GOSUB405:P=224:W=INT(P(1,3)/4)AND3:GOSUB408:P=406:W=P(2,2)AND3:GOSUB405
431 IF(P(2,1)AND3)>1OR(INT(P(2,2)/4)AND3)>1THEN433
432 P=598:W=P(3,2)AND3:GOSUB405
433 IF(INT(P(1,2)/4)AND3)>1OR(P(2,2)AND3)>1THEN435
434 P=416:W=(INT(P(2,3)/4)AND3):GOSUB408
435 IF(P(2,1)AND3)>1THEN438
436 P=406:W=INT(P(2,2)/4)AND3:GOSUB408:P=588:W=P(3,1)AND3:GOSUB405:P=396:W=INT(P(2,1)/4)AND3:GOSUB408
438 IF(P(2,1)AND3)>1OR(INT(P(2,1)/4)AND3)>1THEN440
439 P=578:W=P(3,0)AND3:GOSUB405
440 IF(INT(P(1,1)/4)AND3)>1OR(P(2,0)AND3)>1THEN442
441 P=386:W=INT(P(2,0)/4)AND3:GOSUB408
442 IF(INT(P(1,1)/4)AND3)>1THEN445
443 P=386:W=P(2,0)AND3:GOSUB405:P=194:W=INT(P(1,0)/4)AND3:GOSUB408:W=P(1,0)AND3:GOSUB405
445 P=204:W=INT(P(1,1)/4)AND3:GOSUB408:W=P(1,1)AND3:GOSUB405:P=214:W=INT(P(1,2)/4)AND3:GOSUB408:P=396:W=P(2,1)AND3:GOSUB405
447 PRINT@168,"X";:P=P(1,1):IFI(19)<=0THEN4
448 IFINT(P(0,0)/256)=0THEN452
449 IF(P(1,0)AND3)<AAND(INT(P(1,1)/4)AND3)<2THEN451
450 IF(P(1,1)AND3)>1OR(INT(P(0,1)/4)AND3)>1THEN452
451 PRINT@67,"É";
452 IFINT(P(0,1)/256)THENIF(P(1,1)AND3)<2THENPRINT@72,"É";
453 IFINT(P(0,2)/256)=0THEN457
454 IF(P(1,1)AND3)<2AND(INT(P(0,2)/4)AND3)<2THEN456
455 IF(P(1,2)AND3)>1OR(INT(P(1,2)/4)AND3)>1THEN457
456 PRINT@77,"É";
457 IFINT(P(1,2)/256)THENIF(INT(P(1,2)/4)AND3)<2THENPRINT@173,"É";
458 IFINT(P(2,2)/256)=0THEN462
459 IF(P(2,2)AND3)<2AND(INT(P(1,2)/4)AND3)<2THEN461
460 IF(P(2,1)AND3)>1OR(INT(P(2,2)/4)AND3)>1THEN462
461 PRINT@269,"É";
462 IFINT(P(2,1)/256)THENIF(P(2,1)AND3)<2THENPRINT@264,"É";
463 IFINT(P(2,0)/256)=0THEN467
464 IF(P(2,0)AND3)<2AND(INT(P(1,1)/4)AND3)<2THEN466
465 IF(P(2,1)AND3)>1OR(INT(P(2,1)/4)AND3)>1THEN467
466 PRINT@259,"É";
467 IFINT(P(1,0)/256)THENIF(INT(P(1,1)/4)AND3)<2THENPRINT@163,"É";:RETURN
475 IFEX<1000*2^BTHEN480
476 GOSUB9:PRINT"YOU WENT UP A LEVEL!":B=B+1:J=RND(I(14))
477 N=N+J:HP=HP+J:IFEX>1000*2^BTHENEX=1000*2^B-1
478 CS=CS+B:SU=SU+B
479 GOSUB20:PRINT"YOU GAIN"J"HP":RETURN
480 IFB=1ANDEX>=0THEN4
481 IFEX>1000*2^(B-1)THEN4
482 CS=CS-B:SU=SU-B:IFCS<0THENCS=0
483 PRINT"YOU GO DOWN A LEVEL!":B=B-1:J=RND(I(14)):N=N-J:HP=HP-J:GOSUB20:Q=J:GOSUB14
486 IFN>0ANDB>0THEN4
487 GOSUB23:GOTO390
488 FORQ=1TO115:NEXT:RETURN
489 M$=MID$(MO$,M*8-7,8)
490 IFRIGHT$(M$,1)=" "THENM$=LEFT$(M$,LEN(M$)-1):GOTO490
491 RETURN
493 D=INT(RND(0)*L*6+1):N=N-D:GOSUB8:Q=D:GOSUB14:IFN>0THEN4
495 GOSUB20:GOTO390
496 I=INT(RND(0)*500*CZ+1):PRINT"YOU JUST ";:IFRND(0)>.5THENPRINT"LOST";:I=-I:GOTO499
498 PRINT"GAINED";
499 PRINTABS(I)"EX":EX=EX+I:GOSUB475:RETURN
500 I=RND(6)-1:IFRND(0)>.5THEN503
501 IFI(11+I)=18THEN500
502 PRINT"YOUR "MID$(S$,I*3+1,3)" GOES UP":I(11+I)=I(11+I)+1:GOTO505
503 IFI(11+I)=3THEN500
504 PRINT"YOUR "MID$(S$,I*3+1,3)" GOES DOWN":I(11+I)=I(11+I)-1
505 PRINT" BY 1":GOSUB8:GOTO23
520 PRINT@306,"":GOSUB9:I$=" ":GOSUB27:RETURN
522 UN=0:IFM=3ORM=5ORM=8ORM=10ORM=13ORM=17ORM=18THENUN=1
523 RETURN
524 D$=""
525 GOSUB17:IFC$<>CHR$(8)THEN529
526 IFD$=""THEN525
527 PRINTC$;:IFLEN(D$)=1THEN524
528 D$=LEFT$(D$,LEN(D$)-1):GOTO525
529 IFC$=E$THENC=VAL(D$):PRINT:RETURN
530 IFC$=A$THENC=0:RETURN
531 IFLEN(D$)>9THEN525
532 IFC$="-"ANDD$=""THEN534
533 IFC$<"0"ORC$>"9"THEN525
534 PRINTC$;:D$=D$+C$:GOTO525
535 PRINT@352,"YOU HAVE NOT BEEN NOTICED...":I$="APPROACH":GOSUB18:GOSUB9:IFC$=E$THEN66
538 I$=" ":GOSUB27:GOTO146
539 PRINT"CAST":PRINT"SPELL LEVEL:";:GOSUB17:C=VAL(C$):PRINTC$
540 IFC>0ANDC<=INT(B/3)+1ANDC<7THEN543
541 IFC=0THEN560
542 PRINT"SPELL LV NOT HIGH ENOUGH!";:GOSUB23:GOTO560
543 IFC>CSTHENPRINT"NOT ENOUGH SPELL UNITS!";:GOTO558
544 GOSUB9:FORI=1TO3:PRINTMID$(STR$(I),2)" "LEFT$(P$(I+6*(C-1)),13)TAB(16)MID$(STR$(I+3),2)" "LEFT$(P$(I+3+6*(C-1)),13):NEXT
550 GOSUB17:GOSUB9:IFC$<"1"ORC$>"6"THEN560
552 CS=CS-C:GOSUB8:S=VAL(C$):ONCGOTO564,587,604,622,644,671
554 MH=MH-I:PRINT"IT SUFFERS"I"DAMAGE":IFMH>0THEN558
555 GOTO101
556 PRINT"NOT IN MELEE!":GOSUB21:GOTO1
557 PRINT"SPELL WASTED!"
558 GOSUB21:ON-(D=1)GOTO1:GOTO351
560 GOSUB9:ON-(D=1)GOTO90:GOTO351
562 GOSUB21:GOSUB520:GOTO146
563 PRINT"UNDEAD ARE ALREADY DEAD!":GOTO558
564 ONSGOTO565,567,574,577,580,586
565 PRINTP$(1):GOSUB21:IFD=0THEN557
566 I=RND(8)+4:GOTO554
567 PRINTP$(2):GOSUB21:IFD=0THEN557
568 IFUN=1THENPRINT"UNDEAD DON'T SLEEP!":GOTO1
569 IFRND(20)>I(12)THENC$=" ISN'T SLEEPY!":GOSUB24:GOTO1
570 C$=" IS SLEEPING!":GOSUB24:GOSUB11:IFC$<>E$THEN19
572 IFRND(0)>.2THEN101
573 PRINT"IT WOKE UP!":GOSUB23:GOTO1
574 PRINTP$(3):I=RND(8):GOSUB12:N=N+I:IFN>HPTHENN=HP
576 GOSUB8:GOTO558
577 S=4:T=3:U=11
578 PRINTP$(S):IFI(16+T)<0THENI(16+T)=0
579 I(16+T)=I(16+T)+RND(U)+4:GOTO558
580 PRINTP$(5):GOSUB21:IFD=0THEN557
581 IFUN=1THEN583
582 C$=" IS INSULTED AT BEING CALLED UNDEAD":GOSUB24:GOTO558
583 IFRND(0)<.05*I(13)+.05*B-.05*MLTHEN585
584 C$=" LISTENS WITH DEAF EARS":GOSUB24:GOTO558
585 ML=INT((ML/2)+.5):PRINT"IT RUNS IN FEAR!":GOTO102
586 S=6:T=4:U=11:GOTO578
587 ONSGOTO588,594,595,597,598,603
588 PRINTP$(7):GOSUB21:IFD=0THEN557
589 IFINT(RND(0)^2*20+ML)>I(12)THENC$=" DODGES ASIDE!":GOSUB24:GOTO1
590 C$=" IS WEBBED!":GOSUB24:GOSUB11
591 IFC$<>E$THEN19
592 IFRND(0)-ML/20>.2THEN101
593 PRINT"IT BROKE FREE!":GOSUB23:GOTO1
594 S=8:T=5:U=21:GOTO578
595 PRINTP$(9):GOSUB21:IFD=0THEN557
596 I=INT(RND(0)*12+3):GOTO554
597 S=10:T=2:U=21:GOTO578
598 PRINTP$(11):GOSUB21:IFD=0THEN557
599 IFUN=1THENPRINT"THE UNDEAD IGNORE YOU!":GOTO1
600 IFINT((RND(0)^2)*20+1)>I(16)THENC$=" RESISTS YOU!":GOSUB24:GOTO1
601 C$=" IS CHARMED":GOSUB24:GOSUB11:GOTO591
603 S=12:T=1:U=21:GOTO578
604 ONSGOTO605,607,610,611,612,618
605 PRINTP$(13):GOSUB7:IFD=0THEN557
606 I=INT(RND(0)*6*B+15):GOTO554
607 PRINTP$(14):I=RND(24):GOSUB12:N=N+I
608 IFN>HPTHENN=HP
609 GOTO576
610 S=15:T=3:U=31:GOTO578
611 S=16:T=6:U=21:GOTO578
612 PRINTP$(17):GOSUB21:IFD=0THEN557
613 IFINT(RND(0)*20+ML)>I(12)THENC$=" IGNORES YOU!":GOSUB24:GOTO1
614 C$=" IS HELD":GOSUB24:GOSUB11
615 IFC$<>E$THEN19
616 IFRND(0)>.2+ML*.03THEN101
617 PRINT"IT BROKE FREE!":GOSUB23:GOTO1
618 PRINTP$(18):GOSUB21:IFD=0THEN557
619 IFRND(21)+ML<I(12)THENC$=" BELIEVES! ARRGH...":GOSUB24:GOTO101
620 C$=" DOESN'T BELIEVE!":GOSUB24:GOTO1
622 ONSGOTO623,632,634,636,639,640
623 PRINTP$(19):GOSUB21:IFD=1THEN556
624 PRINT"DIRECTION ";:GOSUB17:FORI=1TO5:IFMID$("WXAD@",I,1)=C$THEN627
626 NEXT:GOSUB9:GOTO624
627 IFI=5THEN558
629 IF(I=1ANDCY>1)OR(I=2ANDCY<200)OR(I=3ANDCX>1)OR(I=4ANDCX<200)THEN631
630 PRINT"ONLY STONE THERE...":GOTO558
631 GOSUB701:PRINT"*POOF*":ONIGOTO359,361,363,365
632 PRINTP$(20):GOSUB21:PRINT"WHOOSH!":IFD=0THEN557
633 C$=" IS BURNING!":GOSUB24:I=INT(RND(0)*12*B+15):GOSUB23:GOTO554
634 PRINTP$(21):GOSUB21:IFD=0THEN557
635 I=INT(RND(0)*32+10):GOTO554
636 PRINTP$(22):GOSUB21:IFD=0THEN557
637 IFRND(0)>.6THENC$=" ISN'T AFFECTED":GOSUB24:GOTO1
638 PRINT"ONE STONE STATUE...":GOTO101
639 S=23:T=7:U=30:GOTO578
640 PRINTP$(24):GOSUB21:PRINT"DIE!":IFD=0THEN557
641 IFUN=1THEN563
642 GOSUB23:IFRND(0)>.3+ML*.4-B*.03THEN101
643 C$=" LAUGHS":GOSUB24:GOSUB21:GOTO1
644 ONSGOTO645,654,655,658,660,665
645 PRINTP$(25):GOSUB21:IFD=1THEN556
646 PRINT"+NORTH/-SOUTH:";:GOSUB524:NY=C:PRINT"+EAST/-WEST:";:GOSUB524
647 NX=C:PRINT"+UP/-DOWN:";:GOSUB524:NZ=C:I=SQR(NX^2+NY^2+(NZ*5)^2)-.1
648 IFI>B*5THENPRINT"TOO FAR...TRY AGAIN":GOSUB21:GOTO646
649 NX=CX+NX:NY=CY-NY:NZ=CZ-NZ
650 IFNX>0ANDNX<201ANDNY>0ANDNY<201ANDNZ<51THEN652
651 PRINT"ONLY STONE THERE... SPELL FAILS":GOTO558
652 IFNZ<1THENPRINT"ONLY THIN AIR... SPELL FAILS":GOTO558
653 CX=NX:CY=NY:CZ=NZ:PRINT"*POOF*":GOSUB701:GOTO57
654 S=26:T=8:U=16:GOTO578
655 PRINTP$(27):GOSUB21:IFD=0THEN557
656 PRINT"QWERTY!":GOSUB23:IFRND(0)<.75THEN101
657 C$=" DOESN'T HEAR...":GOSUB24:GOTO558
658 PRINTP$(28):GOSUB21:IFD=0THEN557
659 PRINT"BRRRR!":I=60:GOTO554
660 PRINTP$(29):GOSUB21:PRINT@136,"*";:GOSUB9:PRINT"WHOOSH!":PRINT"A WALL OF FIRE SURROUNDS YOU":GOSUB23:GOSUB21:IFD=0THEN557
663 PRINT@136," ";:GOSUB9:IFRND(0)>.4THENC$=" IS GONE":GOSUB24:GOTO562
664 C$=" WALKS THROUGH!":GOSUB24:I=RND(12)+7:GOTO554
665 PRINTP$(30):GOSUB21:IFD=0THEN557
666 IFUN=1THEN563
667 PRINT"BLACK DEATH FOR THE "M$:GOSUB21:IFRND(0)>I(13)*.05THENPRINT"IT'S IMMUNE!":GOTO558
669 IFRND(0)*2<I(14)*.03+1THEN101
670 PRINT"THE SPELL BACKFIRES!":GOSUB21:GOTO390
671 ONSGOTO672,673,678,682,685,687
672 S=31:T=9:U=20:GOTO578
673 S=32:T=10:U=40:GOTO578
674 PRINTP$(32):GOSUB21:I(14)=I(14)-1
675 I(26)=0:IFRND(0)>I(14)*.06THEN677
676 PRINT"IT WORKS!":N=HP:GOTO576
677 PRINT"IT DOESN'T WORK!":GOTO390
678 PRINTP$(33):GOSUB21:IFD=0THEN557
679 FORU=1TO9:PRINT@364,MID$(".'+0O@* ",U,1);:GOSUB488:NEXT:GOSUB10:GOSUB23:IFRND(0)<.9THEN101
681 C$=" DOESN'T SEE...":GOSUB24:GOTO558
682 PRINTP$(34):GOSUB21:IFD=1THEN556
683 FORI=0TO10:I(I)=0:NEXT:G=0:CLS:GOSUB8:GOSUB701:CX=25:CY=13:CZ=1:PRINT@131,;:GOSUB7:GOTO57
685 PRINTP$(35):GOSUB21:GOSUB12:N=HP:GOTO576
687 PRINTP$(36):GOSUB21:PRINT"A COLORED WALL APPEARS!":IFD=0THEN557
689 FORJ=1TO16:?@168,"*";:GOSUB488:?@168," ";:GOSUB488:NEXT:?@167,"X";:?@136," ";:GOSUB9:C$=" IS GONE":GOSUB24:GOTO562
691 M=0:IFI(28)=0THEN693
692 M=I(28):ML=I(48):MH=I(68):GOSUB522
693 FORI=1TO19:I(27+I)=I(28+I):I(47+I)=I(48+I):I(67+I)=I(68+I):NEXT
694 I(47)=0:I(67)=0:I(87)=0:IFMTHENI$="#":GOSUB27
695 X=-15:FORQ=1TO11:X=X+32:PRINT@X," ";:IFI(27+Q)THENPRINT@X,A$;
697 NEXT:RETURN
698 FORQ=19TO1STEP-1:I(28+Q)=I(27+Q):I(48+Q)=I(47+Q):I(68+Q)=I(67+Q):NEXT:IFM=.THENI(28)=0:I(48)=0:I(68)=0:GOTO695
700 I(28)=M:I(48)=ML:I(68)=MH:GOTO695
701 FORQ=1TO20:I(27+Q)=0:I(47+Q)=0:I(67+Q)=0:NEXT:GOTO695
900 PRINT@18,"LV"B:GOSUB15:PRINT@146,"HP"STR$(N)HP:PRINT@178,"SU"STR$(CS)SU:PRINT@210,"EX"EX:PRINT@242,"GP"G
910 POKE17024,K:POKE17025,K1:RETURN
935 GOSUB5:Q=0:FORI=1TO10:Q=Q+1:PRINT@Q*32-14,;:IFI<4THENIFI(I)>=0THEN945
940 IFI(I)<1THEN980
945 IFI>7THENPRINTMID$(STR$(I(I)),2)" "LEFT$(I$(I),11);:GOTO980
950 PRINTLEFT$(I$(I),11);:IFI(I)>0THENR=I(I):GOSUB13:PRINTI$;
980 NEXT:GOSUB9:GOSUB17:GOSUB5:GOSUB900:GOTO351
