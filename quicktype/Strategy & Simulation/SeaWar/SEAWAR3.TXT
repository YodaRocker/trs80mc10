0 CLS:CLEAR256:DIMC1,CC,M$:C1=RND(-(PEEK(9)*256+PEEK(10))):GOTO5
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
5 REM *** DEBUGGED AND CONVERTED TO BASIC-PLUS (PDP-11) BY DAVE AHL
6 REM *** CONVERTED TO MC BASIC BY JIM AND CHARLIE GERRIE
10 M$="YOU COMMAND A FLEET OF SHIPS OPERATING IN ENEMY TERRITORY!":GOSUB1
20 PRINT"INSTRUCTIONS (0=NO 1=YES)";
40 INPUTQ0
50 IFQ0=1THEN90
60 IFQ0=0THEN190
70 PRINT"INPUT 1='YES' OR 0='NO'"
80 GOTO40
90 CLS:PRINT"YOU TELL YOUR GUN CREWS THE     ELEVATION TO SET THEIR GUNS."
100 PRINT"ELEVATION IS IN DEGREES FROM 0  TO 360. YOUR TASK FORCE"
110 PRINT"CONSISTS OF 3 DESTROYERS,       2 CRUISERS, 2 BATTLESHIPS,"
120 PRINT"AND 2 AIRCRAFT CARRIERS. THE"
130 PRINT"ENEMY HAS 9 SHIPS FOR THEIR     DEFENCE. IF YOU SUCCEED IN"
140 PRINT"SINKING ALL OF THEIR SHIPS      BEFORE THEY SINK YOURS, THEN"
150 PRINT"YOU WIN. HOWEVER, IF THEY SINK  ALL OF YOUR SHIPS BEFORE YOU"
160 PRINT"DEFEAT THEM, THEN YOU LOSE!"
170 INPUT"HIT <enter> TO BEGIN";Q0
190 A=0:O=0:P5=0:S1=0:S2=0:C=0:P2=0:P1=0:S=0:P4=0:G=0:R=0
200 M=0
210 PRINT
220 READZ0
230 A=A+1
240 GOTO320
250 RESTORE 
260 IFR=9ORG=9THEN840
270 FORX=1TOA
280 READZ0
290 NEXTX
300 READZ0
310 A=A+1
320 IFZ0=9THEN390
330 IFZ0=1THEN2000
340 IFZ0=5THEN360
350 GOTO1460
360 IFP5=1THEN840
370 P5=1
380 GOTO400
390 RESTORE:A=1
400 M$="YOUR FLAGSHIP REPORTS THE SIGHTING OF AN ENEMY ":P0=Z0:GOSUB3000:M$=M$+".":GOSUB1
410 T=43000-30000*RND(0)+(RND(0)*10)*.987654+102
420 IFT<10000THEN410
430 S=0:P2=0
440 T=INT(T)
450 IFZ0=1THEN2030
460 M$="YOUR INSTRUMENTS READ THE RANGE TO THE TARGET AS"+STR$(INT(T/2))+" METRES.":GOSUB1
480 IFP=1THEN1480
490 IFS>4THEN510
500 GOTO540
510 M$="** THE TARGET HAS MOVED OUT OF RANGE! LET'S TRY IT AGAIN! **":GOSUB1
530 GOTO250
540 INPUT"WHAT ELEVATION";B
590 PRINT"FIRE!":SOUND1,2
600 S=S+1
610 IFB=999THEN1850
620 IFB>360THEN1410
630 IFB<0THEN750
640 IFB=0THEN770
650 IFB=90THEN980
660 IFB>330THEN770
670 IFB>180THEN1370
680 IFB>150THEN1300
690 IFB>90THEN1020
700 REM E1=INT(T-(46500*SIN(2*B/57.3)))   <---ORIGINAL FORMULA--THE ONE FOLLOWING IS CHARLIE'S
705 E1=INT(T-(SIN((B/180*3.14159*2))*456165/9.81))
710 E2=ABS(E1)
711 IFE2<1000THEN1050
720 IFE1>1000THEN1200
730 IFE1<-1000THEN1250
740 GOTO1050
750 M$="GUN BACKFIRED, KILLING CREW!":GOSUB1
760 GOTO820
770 M$="WHAT ARE YOU TRYING TO DO? KILL SOME FISH? THE SHELL EXPLODED UNDER WATER FIFTY FEET FROM YOUR SHIP!":GOSUB1
790 IFP=1THEN1590
800 IFP3=1THEN830
810 IFP=1THEN830
820 PRINT"    ADMIRAL PLEASE !!!!"
830 GOTO490
840 PRINT"    ******** VICTORY *******"
880 M$="YOU FIRED"+STR$(S1)+" ROUNDS. THE ENEMY FIRED"+STR$(S2)+" ROUNDS.":GOSUB1
900 IFR=9THEN920
910 IFG=9THEN950
920 M$="ALL OF YOUR SHIPS HAVE BEEN SUNK. SORRY, THE BATTLE IS OVER... THE ENEMY WINS.":GOSUB1
940 GOTO2220
950 M$="YOU HAVE DECIMATED THE ENEMY... THE BATTLE IS OVER... YOU WIN!":GOSUB1
970 GOTO2220
980 M$="YOU IDIOT! YOU SHOT STRAIGHT UP AND THE SHELL LANDED ON YOUR OWN GUN POSITION, DESTROYING IT!":GOSUB1
1000 IFP=1THEN1590
1010 GOTO800
1020 M$="HEY STUPID, YOU'RE FIRING ON YOUR OWN SHIPS!":GOSUB1
1030 IFP=1THEN1590
1040 GOTO800
1050 IFZ0=1THEN1070
1060 GOTO1090
1070 M$="DEPTH CHARGE EXPLODED RIGHT ON TOP HIM!":GOSUB1:GOSUB1085
1080 GOTO1100
1085 FORZ5=1TO10:SOUND1,1:SOUND2,1:NEXT:RETURN
1090 PRINT" ** BOOM **"
1100 GOSUB1085
1110 M$="TARGET DESTROYED!"+STR$(S)+" ROUNDS EXPENDED.":GOSUB1
1150 S1=S1+S
1160 P=0
1170 P3=0
1180 G=G+1
1190 GOTO250
1200 IFZ0=1THEN2130
1210 M$="SHOT FELL"+STR$(INT(ABS(E1)/2))+" METRES SHORT OF TARGET.":GOSUB1
1230 IFP=1THEN1590
1240 GOTO490
1250 IFZ0=1THEN2160
1260 M$="SHELL OVERSHOT TARGET BY"+STR$(INT(ABS(E1)/2))+" METRES.":GOSUB1
1280 IFP=1THEN1590
1290 GOTO490
1300 M$="YOU SHOT A PROJECTILE, INTO THE AIR, "
1320 M$=M$+"IT FELL TO THE WATER, YOU KNOW NOT WHERE. "
1330 M$=M$+"BUT I DO, YOU IDIOT, YOU JUST SANK YOUR OWN FLEET TANKER!":GOSUB1
1340 S1=S1+1
1350 IFP=1THEN1590
1360 GOTO490
1370 M$="WHAT ARE YOU TRYING TO DO? DRILL A NEW HATCH? THE "
1380 M$=M$+"SHELL EXPLODED IN YOUR SHIP, DESTROYING IT!":GOSUB1
1390 IFP=1THEN1590
1400 GOTO800
1410 M$="WHERE DID YOU LEARN TO TYPE?"+STR$(B)+" DEGREES EXCEEDS 360 BY"+STR$(INT(B-360))+" DEGREES.":GOSUB1
1430 S1=S1+1
1440 IFP=1THEN1590
1450 GOTO490
1460 P=1
1470 GOTO400
1480 M$="THE ENEMY ":P0=Z0:GOSUB3000:M$=M$+" IS FIRING ON YOUR SHIPS!":GOSUB1
1490 P4=(1234*RND(0))+(RND(0)*10)
1500 IFP4>500THEN1490
1510 IFP2=1THEN1600
1520 IFINT(P4)<100THEN1800
1530 IFZ0=1THEN2100
1540 M$="HIS FIRST ROUND FELL"+STR$(INT(P4))+" METRES SHORT.":GOSUB1
1560 S2=S2+1
1570 GOTO490
1590 IFP2=1THEN1490
1600 P1=1250*RND(0)+(RND(0)*10)
1610 IFP1>P4THEN1600
1620 IFP1<(P4-400)THEN1600
1630 IFP1<100THEN1710
1640 P4=P1
1650 S2=S2+1
1660 IFZ0=1THEN2190
1670 M$="THE ENEMY ROUND FELL"+STR$(INT(P1))+" METRES SHORT.":GOSUB1
1690 M=2
1700 GOTO490
1710 S2=S2+1
1720 P2=1
1730 GOSUB1850
1740 IFM=1THEN1490
1750 M$="THE ENEMY ":P0=Z0:GOSUB3000:M$=M$+" SANK ":P0=D0:GOSUB3000:GOSUB1
1760 R=R+1
1770 IFR=9THEN840
1780 IFD0=18THEN840
1790 GOTO830
1800 P2=1
1810 GOSUB1850
1820 M$="IN FACT, HE JUST SANK ":P0=D0:GOSUB3000:GOSUB1
1830 R=R+1
1840 GOTO830
1850 RESTORE 
1860 FORC=1TO(9+O)
1870 READD0
1880 NEXTC
1890 READD0
1900 O=O+1
1910 R=R+1
1920 DATA 1,2,3
1930 DATA 4,5,6
1940 DATA 7,8,9
1950 DATA 10,11
1960 DATA 12,13
1970 DATA 14,15,16
1980 DATA 17,18
1990 RETURN 
2000 M$="YOUR FLAGSHIP HAS DETECTED AN ENEMY SUB APPROACHING AT A DEPTH OF 5 FATHOMS.":GOSUB1
2010 P=1
2020 GOTO410
2030 M$="YOUR SUBMARINE DETECTION EQUIPMENT READS THE RANGE TO THE TARGET "
2040 T=INT(T-1500)
2050 IFT<0THEN410
2060 M$=M$+"AS"+STR$(INT(T/2))+" METRES.":GOSUB1
2080 M$="THE ENEMY SUB HAS COMMENCED FIRING TORPEDOES AT YOUR SHIPS.":GOSUB1
2090 GOTO1490
2100 M$="HIS FIRST TORPEDO EXPLODED"+STR$(INT(P4)-50)+" METRES BEHIND YOUR SHIP.":GOSUB1
2120 GOTO1560
2130 M$="DEPTH CHARGE EXPLODED"+STR$(INT(ABS(E1)/2))+" METRES FORE OF TARGET.":GOSUB1
2150 GOTO1590
2160 M$="DEPTH CHARGE EXPLODED"+STR$(INT(ABS(E1)/2))+" METRES AFT OF TARGET.":GOSUB1
2180 GOTO1590
2190 M$="THE ENEMY TORPEDO EXPLODED"+STR$(INT(P1)-50)+" METRES IN FRONT OF YOUR SHIP.":GOSUB1
2210 GOTO490
2220 END
3000 ONP0GOSUB3010,3020,3030,3040,3050,3060,3070,3080,3090,3100,3110,3120,3130,3140,3150,3160,3170,3180:RETURN
3010 M$=M$+"SUBMARINE":RETURN
3020 M$=M$+"210 MM SHORE GUN":RETURN
3030 M$=M$+"70,000 TONNE CRUISER":RETURN
3040 M$=M$+"BATTLESHIP":RETURN
3050 M$=M$+"TORPEDO BOAT":RETURN
3060 M$=M$+"HEAVY FRIGATE":RETURN
3070 M$=M$+"E-TYPE DESTROYER":RETURN
3080 M$=M$+"GUIDED-MISSLE SHIP":RETURN
3090 M$=M$+"AIRCRAFT CARRIER":RETURN
3100 M$=M$+"ONE OF YOUR DESTROYERS!":RETURN
3110 M$=M$+"YOUR HEAVY CRUISER!":RETURN
3120 M$=M$+"ANOTHER OF YOUR DESTROYERS!":RETURN
3130 M$=M$+"ONE OF YOUR BATTLESHIPS!":RETURN
3140 M$=M$+"YOUR LAST DESTROYER":RETURN
3150 M$=M$+"YOUR AIRCRAFT CARRIER":RETURN
3160 M$=M$+"YOUR LIGHT CRUISER":RETURN
3170 M$=M$+"YOUR LAST AIRCRAFT CARRIER":RETURN
3180 M$=M$+"YOUR LAST BATTLESHIP!":RETURN
