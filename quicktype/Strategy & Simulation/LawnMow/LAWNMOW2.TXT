0 CLEAR400:DIMCC,ZZ,M$,MC,I$:MC=16384:GOTO1000
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN

7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORZZ=CC-LEN(M$)+1TOCC:POKEZZ,PEEK(ZZ)-64:NEXT:RETURN

20 FORZ=1TOG:K=PEEK(2)ANDPEEK(17023):IFKTHEND=.
21 IFK=65ORD=1THENIFX>.ORP<ITHENX=X-1:D=1:POKEP,96:IFP<ITHEN50
22 IFK=68ORD=2THENIFX<31THENX=X+1:D=2:POKEP,96:GOTO26
23 IFK=87ORD=3THENIFY>8THENY=Y-1:D=3:POKEP,96:GOTO26
24 IFK=83ORD=4THENIFY<15THENY=Y+1:D=4:POKEP,96
26 P=Y*32+X+MC+V:O=PEEK(P):POKEP,A(V(D)):IFO=98THENL=L+1
27 IFL=256ORO=JTHENSC=SC+(G-Z):ZZ=G-Z:Z=G+50
28 NEXT:IFZZ=9999THENSOUND1,1:GOSUB400:GOTO1100
29 IFZ<G+50THENPRINT@0,"SLACKER!  SCORE"SC" HIGH"HS:GOSUB400:GOTO1100
30 IFZZ<25THENPRINT@0,"HELLEN KELLER COULD MOW BETTER!":GOTO35
31 IFZZ<50THENPRINT@0,"A BIT OF A SHODDY JOB THAT!":GOTO35
34 PRINT@0,"YOU DESERVE A CORNBEEF SANDWICH!";
35 GOSUB400
40 G=G-1:IFG=350THENG=350
45 GOTO100

50 POKEP-1,96:FORP=I-2TOI-16STEP-1:POKEP,216:FORZZ=1TO100:NEXT:POKEP,223:NEXT:POKEP+1,216:SOUND100,2:SOUND200,2:SOUND1,1
51 IFRND(2)=1THENPOKEP+1,191:GOSUB500:PRINT@0,"FIRE! YOU HAVE BEEN KILLED.":GOTO55
52 PRINT@0,"YOU BROKE MY BLOODY MOWER!":FORZZ=1TO4000:NEXT:PRINT@0,"SILLY GIT! SCORE"SC" HIGH"HS
55 Z=G:ZZ=9999:GOTO28

100 CLS0
110 PRINT"ßßßßßßßßßßßßßßßßßßßßßŸßŸŸŸŸŸßŸßß";
120 PRINT"ßßßßßßßßßßßßßßÞßßßßßßßßßŸŸŸßßßßß";
130 PRINT"ßßßßßßßßßßßßßÞÐÔßßßßßßŸßßßßßŸßßß";
140 PRINT"ßßßßßßßßßßßßÞÐÐÐÔßßßßßßßßŸßßßßßß";
150 PRINT"ßßßßßßßßßßßÞÐÐÐÐÐÔßßßßßßßßßßßßßß";
160 PRINT"ßßßßßßßßßßßßÅÏÏÏÏÕßßßßßßßßßßßßßß";
170 PRINT"ßßßßßßßßßßßßÅÏÐÅÏÕßßßßßßßßßßßßßß";
180 PRINT"ßßßßßßßßßßßßÄÌÐÄÌÕßßßßßßßßßßßßßß";

200 M$="":FORT=1TO32:M$=M$+CHR$(34):NEXT
210 FORT=8TO14
220 PRINT@T*32,M$;:NEXT
230 M$=LEFT$(M$,31):PRINTM$;:POKEMC+511,34+64
240 A(1)=ASC("ˆ"):A(2)=ASC("„"):A(0)=A(1):V(1)=1:V(2)=2:V(3)=1:V(4)=2
290 Y=RND(8)+7:X=RND(32)-1:D=RND(4):P=Y*32+X+MC+V:O=PEEK(P):POKEP,A(V(D)):L=1:GOTO20

400 IFSC>HSTHENHS=SC
405 PRINT@3*32+4,"hit";:PRINT@4*32+4,"any";:PRINT@5*32+4,"key";:FORCC=1TO100:M$=INKEY$:NEXT
450 M$=INKEY$:IFM$=""THEN450
460 RETURN

500 FORT=1TO20:FORY=3TO7:FORX=12TO18:IFPEEK(MC+Y*32+X)<>223THENIFRND(2)=1THENPOKEMC+Y*32+X-1,F(RND(3)):IFRND(9)=1THENGOSUB503
501 NEXT:NEXT:NEXT:RETURN
503 ONRND(3)GOTO504,505:PRINT@RND(3)*32+8+RND(5),"Ï";:RETURN
504 PRINT@RND(3)*32+8+RND(5),"Ð";:RETURN
505 PRINT@RND(3)*32+8+RND(5),"ß";:RETURN

1000 DIMX,Y,K,D,V(4),P,O,L,Z,G,I,T,F(3),SC:I=16641:F(1)=159:F(2)=191:F(3)=255

1100 G=400:SC=0

1200 CLS:PRINTTAB(4):M$="************************":GOSUB7:PRINT
1210 PRINTTAB(4):M$="* LAWN MOWER SIMULATOR *":GOSUB7:PRINT
1220 PRINTTAB(4):M$="************************":GOSUB7:PRINT
1230 M$="THIS IS A POWERFUL AND ACCURATE LAWN MOWING SIMULATION.":GOSUB1
1240 M$="CHOOSE FROM SEVERAL DIFFERENT LAWN MOWING MACHINES AND 'GO GET THAT GRASS!'":GOSUB1
1250 FORCC=1TO100:M$=INKEY$:NEXT:PRINT"PRESS RELEVANT NUMBER."
1260 PRINT"1. CAMPARI GRASSMASTER"
1270 PRINT"2. DAF TURFOMATIC"
1280 PRINT"3. ACME MEGA-CUT 3000"
1290 PRINT"4. ACME LAWN ACE"
1300 PRINT"5. FLYMO GRASSCHUM"
1310 PRINT"6. PATIO SPINTETTE"
1320 M$=INKEY$:IFM$=""THEN1320
1330 CC=VAL(M$):IFCC<1ORCC>6THEN1320

1340 IFCC=1THENM$="CAMPARI GRASSMASTER"
1350 IFCC=2THENM$="DAF TURFOMATIC"
1360 IFCC=3THENM$="ACME MEGA-CUT 3000"
1370 IFCC=4THENM$="ACME LAWN ACE"
1380 IFCC=5THENM$="FLYMO GRASSCHUM"
1390 IFCC=6THENM$="PATIO SPINTETTE"
1395 IFRND(100)=1ORCC=6THEN1450
1400 M$="I'M SORRY, I'M AFRAID THAT THE "+M$+" IS BROKEN AT THE MOMEMENT.":CLS:GOSUB1
1410 FORCC=1TO4000:NEXT:GOTO1200
1450 M$="THE "+M$+" IS WORKING  WELL.":CLS:GOSUB1:PRINT
1460 M$="USE THE wasd KEYS TO STEER YOUR MACHINE.":GOSUB1:PRINT
1470 M$="YOU ONLY HAVE A SHORT TIME TO FINISH THE JOB BEFORE YOUR EMPLOYER BECOMES IRATE.":GOSUB1:PRINT
1480 M$="COMPLETING THE JOB WITHIN TIME WILL EARN YOU A BETTER REPUTATION AND MORE JOBS.":GOSUB1
1481 PRINT:PRINT"PRESS ANY KEY TO START"
1490 M$=INKEY$:IFM$=""THEN1490
1500 GOTO100
2000 REM ADVANCED LAWN MOWER
2010 REM SIMULATOR INSPIRED BY
2020 REM YOUR SINCLAIR 45, 1988
2030 REM APRIL FOOLS PROGRAM
2040 REM JIM & CHARLIE GERRIE
2050 REM FIRE IDEA GREG DIONNE
