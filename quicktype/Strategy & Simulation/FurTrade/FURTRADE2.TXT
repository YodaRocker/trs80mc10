0 CLEAR500:DIMCC,ZZ,M$,I$:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
8 CC=LEN(M$):FORC1=1TOCC:IFMID$(M$,C1,1)="."THENCC=C1
9 NEXT:I$=LEFT$(M$,CC+ZZ):RETURN
10 CLS
20 PRINT@139,"FUR TRADER"
30 PRINT@202,"COPYRIGHT BY"
40 PRINT@263,"CREATIVE COMPUTING"
50 PRINT@322,"MORRISTOWN, NEW JERSEY 07960"
60 P=1:GOSUB1850
70 DIM F(4)
80 PRINT@9,"* FUR TRADER *"
90 GOSUB460
100 I=600
110 PRINT@13*32,"DO YOU WISH TO TRADE FURS";
120 INPUTB$
130 IF LEFT$(B$,1)="Y"THEN160
140 P=1:GOSUB1850
150 END
160 P=1:GOSUB1850
170 M$=STR$(I):GOSUB8:M$="YOU HAVE "+I$+" IN SAVINGS "
180 M$=M$+"AND 190 FURS TO BEGIN THE EXPEDITION.":GOSUB1
190 E1=INT((.15*RND(0)+.95)*10^2+.5)/10^2
200 B1=INT((.25*RND(0)+1)*10^2+.5)/10^2
210 PRINT
220 M$="YOUR 190 FURS ARE DISTRIBUTED AMONG THE FOLLOWING "
230 M$=M$+"KINDS OF PELTS: MINK, BEAVER, ERMINE AND FOX.":GOSUB1
240 GOSUB1800
250 RESTORE
260 FORJ=1TO4
270 READB$
280 PRINT
290 M$="HOW MANY "+B$+" PELTS DO YOU HAVE":GOSUB1
300 INPUTF(J)
310 F(0)=F(1)+F(2)+F(3)+F(4)
320 IFF(0)=190THEN540
330 IFF(0)>190THEN370
340 NEXTJ
350 FORI=1TO1000:NEXT
360 GOTO540
370 PRINT
380 CLS4:PRINT@6*32,"YOU MAY NOT HAVE THAT MANY FURS.";
390 PRINT"DO NOT TRY TO CHEAT.  I CAN ADD."
400 PRINT"YOU MUST START AGAIN.";
410 P=1:GOSUB1850
420 GOTO80
430 PRINT
440 PRINT"WANT TO TRADE FURS NEXT YEAR";
450 GOTO120
460 REM  INTRODUCTION
470 M$="YOU ARE THE LEADER OF A FRENCH FUR TRADING EXPEDITION IN "
480 M$=M$+"1776 LEAVING THE LAKE ONTARIO AREA TO SELL FURS AND GET "
490 M$=M$+"SUPPLIES FOR THE NEXT YEAR.":GOSUB1:M$="YOU HAVE A CHOICE OF THREE "
500 M$=M$+"FORTS AT WHICH YOU MAY TRADE. THE COST OF SUPPLIES "
510 M$=M$+"AND THE AMOUNT YOU RECEIVE FOR YOUR FURS WILL DEPEND "
520 M$=M$+"ON THE FORT THAT YOU CHOOSE.":GOSUB1
530 RETURN
540 CLS:PRINT@38,"* FUR TRADER *
550 PRINT@202,"YOU MAY TRADE FURS AT FORT 1, FORT 2,
560 PRINT@264,"OR FORT 3.  FORT 1 IS FORT HOCHELAGA (MONTREAL)
570 PRINT@328,"AND IS UNDER THE PROTECTION OF THE FRENCH ARMY.
580 PRINT@391,"FORT 2 IS FORT STADACONA (QUEBEC) AND IS UNDER THE
590 PRINT@456,"PROTECTION OF THE FRENCH ARMY.  HOWEVER, YOU MUST
600 PRINT@522,"MAKE A PORTAGE AND CROSS THE LACHINE RAPIDS.
610 PRINT@583,"FORT 3 IS FORT NEW YORK AND IS UNDER DUTCH CONTROL.
620 PRINT@652,"YOU MUST CROSS THROUGH IROQUOIS LAND.
630 PRINT@780,"WHICH FORT WOULD YOU LIKE TO TRADE AT";
640 INPUT B:PRINT
650 IF B=1THEN690
660 IF B=2THEN760
670 IF B=3THEN830
680 GOTO630
690 CLS:PRINT@262,"YOU HAVE CHOSEN THE EASIEST ROUTE.  HOWEVER, THE FORT
700 PRINT@335,"IS FAR FROM ANY SEAPORT.  THE VALUE
710 PRINT@391,"YOU RECEIVE FOR YOUR FURS WILL BE LOW AND THE COST
720 PRINT@453,"OF SUPPLIES HIGHER THAN FORTS STADACONA AND NEW YORK.
730 GOSUB1680
740 IF LEFT$(B$,1)="Y"THEN630
750 GOTO900
760 CLS:PRINT@262,"YOU HAVE CHOSEN A HARD ROUTE.  IT IS, IN COMPARISON,
770 PRINT@327,"HARDER THAN THE ROUTE TO HOCHELAGA BUT EASIER THAN
780 PRINT@388,"THE ROUTE TO NEW YORK.  YOU WILL RECEIVE AN AVERAGE VALUE
790 PRINT@451,"FOR YOUR FURS AND THE COST OF YOUR SUPPLIES WILL BE AVERAGE.
800 GOSUB1680
810 IF LEFT$(B$,1)="Y"THEN630
820 GOTO1000
830 CLS:PRINT@265,"YOU HAVE CHOSEN THE MOST DIFFICULT ROUTE.  AT
840 PRINT@328,"FORT NEW YORK YOU WILL RECEIVE THE HIGHEST VALUE
850 PRINT@395,"FOR YOUR FURS.  THE COST OF YOUR SUPPLIES
860 PRINT@459,"WILL BE LOWER THAN AT ALL THE OTHER FORTS.
870 GOSUB1680
880 IF LEFT$(B$,1)="Y"THEN630
890 GOTO1350
900 I=I-160
910 PRINT
920 M1=INT((.2*RND(0)+.7)*10^2+.5)/10^2
930 E1=INT((.2*RND(0)+.65)*10^2+.5)/10^2
940 B1=INT((.2*RND(0)+.75)*10^2+.5)/10^2
950 D1=INT((.2*RND(0)+.8)*10^2)/10^2
960 P=1:GOSUB1850
970 CLS:PRINT@139,"SUPPLIES AT FORT HOCHELAGA COST $150.00.
980 PRINT@200,"YOUR TRAVEL EXPENSES TO HOCHELAGA WERE $10.00.
990 GOTO1710
1000 I=I-140
1010 PRINT
1020 M1=INT((.3*RND(0)+.85)*10^2+.5)/10^2
1030 E1=INT((.15*RND(0)+.8)*10^2+.5)/10^2
1040 B1=INT((.2*RND(0)+.9)*10^2+.5)/10^2
1050 P=INT(10*RND(0))+1
1060 IF P<=2THEN1100
1070 IF P<=6THEN1170
1080 IF P<=8THEN1200
1090 IFP<=10THEN1260
1100 F(2)=0
1110 CLS:PRINT@137,"YOUR BEAVER WERE TOO HEAVY TO CARRY ACROSS
1120 PRINT@261,"THE PORTAGE.  YOU HAD TO LEAVE THE PELTS, BUT FOUND
1130 PRINT@398,"THEM STOLEN WHEN YOU RETURNED
1140 P=2:GOSUB1850
1150 GOSUB1320
1160 GOTO1730
1170 CLS:PRINT@335,"YOU ARRIVED SAFELY AT FORT STADACONA.
1180 P=2:GOSUB1850
1190 GOTO1300
1200 GOSUB1800
1210 CLS:PRINT@331,"YOUR CANOE UPSET IN THE LACHINE RAPIDS
1220 PRINT@467,"YOU LOST ALL YOUR FURS.
1230 P=2:GOSUB1850
1240 GOSUB1320
1250 GOTO1760
1260 F(4)=0
1270 CLS:PRINT@332,"YOUR FOX PELTS WERE NOT CURED PROPERLY.
1280 PRINT@469,"NO ONE WILL BUY THEM.
1290 P=2:GOSUB1850
1300 GOSUB1320
1310 GOTO1710
1320 PRINT@139,"SUPPLIES AT FORT STADACONA COST $125.00.
1330 PRINT@200,"YOUR TRAVEL EXPENSES TO STADACONA WERE $15.00.":PRINT
1340 RETURN
1350 I=I-105
1360 PRINT
1370 M1=INT((.15*RND(0)+1.05)*10^2+.5)/10^2
1380 D1=INT((.25*RND(0)+1.1)*10^2+.5)/10^2
1390 P=INT(10*RND(0))+1
1400 IF P<=2THEN1440
1410 IF P<=6THEN1480
1420 IF P<=8THEN1520
1430 IFP<=10THEN1580
1440 CLS:PRINT@267,"YOU WERE ATTACKED BY A PARTY OF IROQUOIS.
1450 PRINT@393,"ALL PEOPLE IN YOUR TRADING GROUP WERE KILLED.
1460 PRINT@533,"THIS ENDS THE GAME.
1470 END
1480 CLS:PRINT@330,"YOU WERE FORTUNATE.  YOU ARRIVED SAFELY
1490 PRINT@471,"AT FORT NEW YORK.
1500 P=2:GOSUB1850
1510 GOTO1630
1520 GOSUB1800
1530 CLS:PRINT@328,"YOU NARROWLY ESCAPED AN IROQUOIS RAIDING PARTY.
1540 PRINT@456,"HOWEVER, YOU HAD TO LEAVE ALL YOUR FURS BEHIND.
1550 P=2:GOSUB1850
1560 GOSUB1650
1570 GOTO1760
1580 B1=B1/2
1590 M1=M1/2
1600 CLS:PRINT@328,"YOUR MINK AND BEAVER WERE DAMAGED ON YOUR TRIP.
1610 PRINT@452,"YOU RECEIVE ONLY HALF THE CURRENT PRICE FOR THESE FURS.
1620 P=2:GOSUB1850
1630 GOSUB1650
1640 GOTO1710
1650 PRINT@142,"SUPPLIES AT NEW YORK COST $80.00.
1660 PRINT@200,"YOUR TRAVEL EXPENSES TO NEW YORK WERE $25.00.
1670 RETURN
1680 PRINT:PRINTTAB(13);"DO YOU WANT TO TRADE AT ANOTHER FORT";
1690 INPUT B$
1700 RETURN
1710 REM
1720 PRINTTAB(15);"YOUR BEAVER SOLD FOR ";:PRINTUSING"$###.##";B1*F(2)
1730 PRINTTAB(15);"YOUR FOX SOLD FOR    ";:PRINTUSING"$###.##";D1*F(4)
1740 PRINTTAB(15);"YOUR ERMINE SOLD FOR ";:PRINTUSING"$###.##";E1*F(3)
1750 PRINTTAB(15);"YOUR MINK SOLD FOR   ";:PRINTUSING"$###.##";M1*F(1)
1760 I=M1*F(1)+B1*F(2)+E1*F(3)+D1*F(4)+I
1770 PRINT
1780 PRINTTAB(20);"YOU NOW HAVE ";:PRINTUSING"$###.##";I;:
1790 GOTO430
1800 FORJ=1TO4
1810 F(J)=0
1820 NEXTJ
1830 RETURN
1840 DATA"MINK","BEAVER","ERMINE","FOX"
1850 FORD=1TOP
1860 FORF=1TO1500
1870 NEXTF
1880 NEXTD
1890 CLS
1900 RETURN
