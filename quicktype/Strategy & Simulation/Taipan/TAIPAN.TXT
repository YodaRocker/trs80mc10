10 REM (C) CLOAD
20 CLEAR200:LO$(1)="HONGKONG ":LO$(2)="SHANGHAI "
30 LO$(3)="NAGASAKI ":LO$(4)="SAIGON   "
40 LO$(5)="MANILA   ":LO$(6)="SINGAPORE":LO$(7)="BATAVIA  ":LO$(8)="AT SEA   ":GOTO80
50 FORT=0TO2000:NEXT
60 FORT=0TO1000:NEXT
70 FORT=0TO500:NEXT:RETURN
80 CLS: PRINT@236,"TAIPAN";
90 FORI=1TO32:S$=S$+CHR$(134):NEXTI
100 DIM S(6,1),S$(8)
110 FORI=1TO8:FORJ=1TO8:READA:S$(I)=S$(I)+CHR$(A):NEXTJ:NEXTI
120 CLS:PRINT@161,"ENTER LEVEL OF EXPERIENCE:":PRINT:PRINT"   1=LANDLUBBER"
121 PRINT"   2=APPRENTICE SEAMAN":PRINT"   3=OLD SEA DOG":PRINT"   ";:INPUTDL:IFDL<1 OR DL>3 THEN120
122 DL=64/2^DL:FORI=0TO6:FORJ=0TO1:READS(I,J):NEXTJ:NEXTI
130 L=1:D0=5000:MO=1:YE=1860:CA=400:MW=50:SH=MW
140 CLS:PRINT@168,"WHAT WILL YOU NAME":PRINTTAB(8);"YOUR HONG (FIRM)":PRINTTAB(8);:INPUTA$
150 GOSUB2650:GOSUB2510:GOTO340
160 CLS
170 PRINT@256,S$;
180 PRINT@0,"HONG: ";A$;:PRINT@16,"DATE:";
190 PRINT@32,"WAREHOUSE";:PRINT@48,"HOLD     GUNS";
200 PRINT@66,"OPIUM";:PRINT@82,"OPIUM";
210 PRINT@98,"SILK";:PRINT@114,"SILK";
220 PRINT@130,"ARMS";:PRINT@146,"ARMS";
230 PRINT@162,"GENERAL";:PRINT@178,"GENERAL";
240 PRINT@192,"BANK";:PRINT@208,"CASH";
250 PRINT@224,"DEBT";:PRINT@240,"LOC";
260 RETURN
270 GP=GP*10:PRINT"FAMINE IN SOUTH CHINA, TAIPAN!":PRINT"GENERAL CARGO PRICES":PRINT"HAVE RISEN TOWARD HEAVEN!!";:RETURN
280 OK=OK*10:PRINT"TAIPAN!! A FIRE HAS SWEPT":PRINT LO$(L);" AND THE PRICE OF":PRINT"OPIUM HAS LEAPED BECAUSE OF"
281 PRINT"HOSPITAL USE!";:RETURN
290 SP=SP*10:PRINT"THE ";LO$(L);" WARVES HAVE":PRINT"BEEN BURNED BY REBEL SAMURAI,":PRINT"TAIPAN!! ALL SILK WAS DESTROYED";
291 RETURN
300 SP=SP/5:PRINT"A RIVAL HONG HAS DUMPED SURPLUS":PRINT"SILK ON THE MARKET IN":PRINT LO$(L);"!!";:RETURN
310 AP=AP*10:PRINT"TAIPAN, AN AGENT FOR THE MORO":PRINT"REBELS IS BUYING ARMS AT HIGH"
311 PRINT"PRICES!! ARMS ARE SKY-HIGH HERE!";:RETURN
320 OK=OK/5:PRINT"TAIPAN, A YANKEE CAPTAIN IS":PRINT"SELLING TURKISH OPIUM BELOW"
321 PRINT"THE BRITISH PRICE IN ";LO$(L);"!";:RETURN
330 GP=GP/10:PRINT"MALAY PIRATES HAVE FLOODED ":PRINT LO$(L);" WITH CHEAP GENERAL"
331 PRINT"CARGO FROM THEIR BOOTY, TAIPAN!";:RETURN
340 BA=INT(BA+(BA/24)):D0=INT(D0+(D0/10)):MO=MO+1:TI=TI+1:IF MO>12 THEN YE=YE+1:MO=1:GOTO430
341 GOTO430
350 PRINT@21,MO;"/";YE;
360 PRINT@73,OW;:PRINT@89,OC;
370 PRINT@105,SW;:PRINT@121,SC;
380 PRINT@137,AW;:PRINT@153,AC;
390 PRINT@169,GW;:PRINT@185,GC;
400 PRINT@196,BA;:PRINT@228,D0;: PRINT@212,INT(CA);:PRINT@245,LO$(L);
410 PRINT@52,MW;:PRINT@61,GN;
420 RETURN
430 ON L GOSUB 480,490,500,510,520,530,540
440 IFL=8THENGOSUB1730:L=LD:PRINT@245,LO$(L);:ON L GOSUB480,490,500,510,520,530,540
450 IFL=1THENGOSUB1510
460 GOSUB2350:GOSUB470:GOSUB2090:GOTO550
470 IF RND(100)<8 THEN PRINT@320,"";:ON L GOSUB 270,280,290,300,310,320,330:GOSUB50:GOSUB2510:RETURN
471 RETURN
480 OK=15000:SP=1300:AP=150:GP=10:RETURN
490 OK=16000:SP=1400:AP=160:GP=11:RETURN
500 OK=14000:SP=1500:AP=100:GP=12:RETURN
510 OK=13000:SP=1600:AP=110:GP=13:RETURN
520 OK=12000:SP=1000:AP=120:GP=14:RETURN
530 OK=10000:SP=1100:AP=130:GP=15:RETURN
540 OK=11000:SP=1200:AP=140:GP=16:RETURN
550 IFRND(DL)=1THENBR=0:GOSUB1990:GOSUB1490
551 GOSUB1490
560 PRINT@416,"SHALL I BUY, SELL, OR QUIT";:INPUTQ$:IF Q$="B"THEN570
561 IFQ$="S"THEN580
562 IFQ$="Q"THEN850
563 GOTO560
570 PRINT@416,"WHAT DO YOU WISH ME TO BUY";:INPUTQ$:IFQ$="O"THEN590
571 IFQ$="S"THEN670
572 IFQ$="A"THEN730
573 IFQ$="G"THEN790
574 GOTO570
580 PRINT@416,"WHAT ITEM SHALL I SELL    ";:INPUTQ$:IFQ$="O"THEN630
581 IFQ$="S"THEN690
582 IFQ$="A"THEN750
583 IFQ$="G"THEN810
584 GOTO580
590 TG$="OPIUM":TP=OK:GOSUB2420:INPUTOB$
600 IFOB$="A"THEN OB=INT(CA/OK):GOTO620
601 OB=VAL(OB$)
610 IF OB*OK>CAOROB<0THEN590
620 CA=CA-OB*OK:OC=OC+OB:MW=MW-OB:GOSUB2490:GOTO560
630 TG$="OPIUM":GOSUB2450:INPUTOS$
640 IF OS$="A" THEN OS=OC: GOTO660
641 OS=VAL(OS$)
650 IF OS>OC OR OS<0THEN630
660 CA=CA+OS*OK:OC=OC-OS:MW=MW+OS:GOSUB2490:GOTO560
670 TG$="SILK":TP=SP:GOSUB2420:INPUTSB$:IFSB$="A"THENSB=INT(CA/SP):GOTO680
671 SB=VAL(SB$):IFSB*SP>CA OR SB<0 THEN670
680 CA=CA-SB*SP:SC=SC+SB:MW=MW-SB:GOSUB2490:GOTO560
690 TG$="SILK":GOSUB2450:INPUTSS$
700 IF SS$="A" THEN SS=SC: GOTO720
701 SS=VAL(SS$)
710 IF SS>SC OR SS<0 THEN690
720 CA=CA+SS*SP:SC=SC-SS:MW=MW+SS:GOSUB2490:GOTO560
730 TG$="ARMS":TP=AP:GOSUB2420:INPUTAY$:IFAY$="A"THENAY=INT(CA/AP):GOTO740
731 AY=VAL(AY$):IFAY*AP>CAORAY<0THEN730
740 CA=CA-AY*AP:AC=AC+AY:MW=MW-AY:GOSUB2490:GOTO560
750 TG$="ARMS":GOSUB2450:INPUTAZ$
760 IFAZ$="A"THENAZ=AC:GOTO780
761 AZ=VAL(AZ$)
770 IF AZ>AC OR AZ<0THEN750
780 CA=CA+AZ*AP:AC=AC-AZ:MW=MW+AZ:GOSUB2490:GOTO560
790 TG$="GENERAL":TP=GP:GOSUB2420:INPUTGB$:IFGB$="A"THENGB=INT(CA/GP):GOTO800
791 GB=VAL(GB$):IFGB*GP>CAORGB<0THEN790
800 CA=CA-GB*GP:GC=GC+GB:MW=MW-GB:GOSUB2490:GOTO560
810 TG$="GENERAL":GOSUB2450:INPUTGS$
820 IF GS$="A" THEN GS=GC: GOTO840
821 GS=VAL(GS$)
830 IF GS>GC OR GS<0 THEN810
840 CA=CA+GS*GP:GC=GC-GS:MW=MW+GS:GOSUB2490:GOTO560
850 IF OW=0 AND SW=0 AND AW=0 AND GW=0 AND OC=0 AND SC=0 AND AC=0 AND GC=0 OR L<>1 THEN 1200
860 PRINT@416,"DO YOU WISH TO TRANSFER CARGO";:INPUTTR$:IFTR$="N"THEN1200
870 IF OC=0 THEN910
871 TG$="OPIUM":GOSUB2460:INPUTO1$
880 IF O1$="A" THEN O1=OC:GOTO900
881 O1=VAL(O1$)
890 IF O1>OC THEN 870
900 OW=OW+O1:OC=OC-O1:MW=MW+O1:GOSUB2490
910 IF OW=0 THEN950
911 TG$="OPIUM":GOSUB2470:INPUTO2$
920 IF O2$="A" THEN O2=OW:GOTO940
921 O2=VAL(O2$)
930 IF O2>OW THEN910
940 OC=OC+O2:OW=OW-O2:MW=MW-O2:GOSUB2490
950 IF SC=0 THEN990
951 TG$="SILK":GOSUB2460:INPUTS3$
960 IF S3$="A" THEN S3=SC:GOTO980
961 S3=VAL(S3$)
970 IF S3>SC THEN 950
980 SW=SW+S3:SC=SC-S3:MW=MW+S3:GOSUB2490
990 IF SW=0 THEN1030
991 TG$="SILK":GOSUB2470:INPUTS4$
1000 IF S4$="A" THEN S4=SW:GOTO1020
1001 S4=VAL(S4$)
1010 IF S4>SW THEN 990
1020 SW=SW-S4:SC=SC+S4:MW=MW-S4:GOSUB2490
1030 IF AC=0 THEN1070
1031 TG$="ARMS":GOSUB2460:INPUTA1$
1040 IF A1$="A" THEN A1=AC:GOTO1060
1041 A1=VAL(A1$)
1050 IF A1>AC THEN1030
1060 AW=AW+A1:AC=AC-A1:MW=MW+A1:GOSUB2490
1070 IF AW=0 THEN1110
1071 TG$="ARMS":GOSUB2470:INPUTA2$
1080 IF A2$="A" THEN A2=AW:GOTO1100
1081 A2=VAL(A2$)
1090 IF A2>AW THEN1070
1100 AW=AW-A2:AC=AC+A2:MW=MW-A2:GOSUB2490
1110 IF GC=0 THEN1150
1111 TG$="GENERAL":GOSUB2460:INPUTG1$
1120 IF G1$="A" THEN G1=GC:GOTO1140
1121 G1=VAL(G1$)
1130 IF G1>GC THEN 1110
1140 GW=GW+G1:GC=GC-G1:MW=MW+G1:GOSUB2490
1150 IF GW=0 THEN1190
1151 TG$="GENERAL":GOSUB2470:INPUTG2$
1160 IF G2$="A" THEN G2=GW:GOTO1180
1161 G2=VAL(G2$)
1170 IF G2>GW THEN1150
1180 GW=GW-G2:GC=GC+G2:MW=MW-G2:GOSUB2490
1190 GOSUB1500:GOTO560
1200 IFL=8THEN1270
1201 IF L=1 THENPRINT@416,"DO YOU WISH TO RETURN TO BANK";:INPUTQ$: IF Q$="Y" THENGOSUB1520
1210 IF MW<0 THENPRINT@416,"YOUR SHIP IS OVERLOADED!":GOSUB60:GOSUB1500:GOTO560
1220 GOSUB2510:PRINT@320,"DO YOU WISH TO GO TO":PRINT"1) HONGKONG 2) SHANGHAI"
1221 PRINT"3) NAGASAKI 4) SAIGON 5) MANILA":PRINT"6) SINGAPORE 7) BATAVIA";
1230 INPUTLD:LD=INT(LD)
1240 IF LD=L OR LD<1 OR LD>7 THEN 1220
1250 L=8:GOTO340
1260 L=8
1270 IFLD=8THEN340
1271 L=LD:GOTO340
1280 CLS0:IFCK=1 THENCK=0:RETURN
1290 IF SN=0 THEN CK=1:GOSUB1280:RETURN
1300 IFSN>0THENPP=0:GOSUB2620
1310 IFSN>1THENPP=8:GOSUB2620
1320 IFSN>2THENPP=16:GOSUB2620
1330 IFSN>3THENPP=24:GOSUB2620
1340 IFSN>4THENPP=160:GOSUB2620
1350 IFSN>5THENPP=168:GOSUB2620
1360 IFSN>6THENPP=176:GOSUB2620
1370 IFSN>7THENPP=184:GOSUB2620
1380 IFSN>8THENPRINT@287,"+";
1390 PRINT@320,"";:RETURN
1400 FORJ=1TO3:PRINT@96,S$(6);:GOSUB2700:PRINT@96,S$(4);:GOSUB70:NEXTJ:PRINT@320,"";:RETURN
1410 RETURN
1420 FORJ=1TO1
1430 FORI=1TO5:PRINT@(I-1)*32,S$(8);:NEXTI:GOSUB2710:FORI=1TO5:PRINT@(I-1)*32,S$(I);:NEXTI
1431 GOSUB70:NEXTJ:PRINT@320,"";:RETURN
1440 PRINT@0,S$(7);:FORI=1TO4: PRINT@I*32,S$(I);:NEXTI:GOSUB70
1450 PRINT@32,S$(7);:FORI=1TO3: PRINT@32*(I+1),S$(I);:NEXTI:GOSUB70
1460 PRINT@64,S$(7);:FORI=1TO2:PRINT@32*(I+2),S$(I);:NEXTI:GOSUB70
1470 PRINT@96,S$(7);:PRINT@128,S$(1);:GOSUB70
1480 PRINT@320,"";: GOSUB2720: RETURN
1490 OK=(OK/2)*RND(3):SP=(SP/2)*RND(3):AP=(AP/2)*RND(3):GP=(GP/2)*RND(3)
1500 PRINT@320,"PRESENT PRICES PER UNIT ARE:":PRINT"OPIUM:";OK,"SILK:";SP:PRINT"ARMS:";AP,"GENERAL:";GP:RETURN
1510 GOSUB1860:GOSUB1900:GOSUB1600:GOSUB2510:PRINT@320,"DO YOU WISH TO VISIT THE BANK";:INPUTQ$:IFQ$="N"THENRETURN
1520 GOSUB2510:PRINT@320,"HOW MUCH WILL YOU DEPOSIT";:INPUTD$
1530 IFD$="A"THEND=CA:GOTO1550
1531 D=VAL(D$)
1540 IF D>CA THENPRINT@320,"YOU ONLY HAVE";CA;"IN CASH.":GOSUB50:GOTO1520
1550 BA=BA+D:CA=CA-D
1560 GOSUB2510:PRINT@320,"HOW MUCH WILL YOU WITHDRAW";:INPUTW$
1570 IF W$="A" THENW=BA:GOTO1580
1571 W=VAL(W$)
1580 IF W>BA THEN PRINT@320,"YOU ONLY HAVE";BA;"IN THE BANK.":GOSUB50:GOTO1560
1590 BA=BA-W:CA=CA+W:GOSUB2510:RETURN
1600 IFD0>10000ANDWN<>1THEN1610
1601 GOTO1620
1610 WN=1:GOSUB2510:PRINT@320,"ELDER BROTHER WU HAS SENT";RND(100)+50:PRINT"BRAVES TO ESCORT YOU TO THE WU"
1611 PRINT"MANSION, TAIPAN.":GOSUB60:GOSUB60:GOSUB1840
1620 GOSUB2510:PRINT@320,"DO YOU HAVE BUSINESS WITH ELDER":PRINT"BROTHER WU, THE MONEYLENDER";:INPUTQ$:IFQ$="N"THEN1710
1630 R=0:R$="":IFD0>0THENGOSUB3000
1640 IFR$<>"A"THENR=VAL(R$)
1650 IF R>D0 OR R<0 OR R>CA THEN1630
1660 D0=D0-R:CA=CA-R
1670 GOSUB2510:PRINT@320,"HOW MUCH DO YOU WISH TO BORROW":INPUTB$
1680 IFB$="A"THENB=2*CA:GOTO1700
1681 B=VAL(B$)
1690 IF B<0 THEN1670
1691 IFB>(BA+CA)*2 THENPRINT@320,"HE WON'T LEND YOU THAT MUCH!":GOSUB60:GOTO1670
1700 D0=D0+B:CA=CA+B
1710 IFD0>20000ANDRND(5)=2THENCA=0:GOTO1720
1711 RETURN
1720 GOSUB2510:PRINT@320,"BAD JOSS!!!":PRINT"TWO OF YOUR BODYGUARDS HAVE"
1721 PRINT"BEEN KILLED BY CUTTHROATS AND":PRINT"YOU HAVE LOST YOUR CASH!!!":GOSUB50:GOSUB2510:RETURN
1730 M1=SH:GOSUB2110:IF RND(10)<9THEN1760
1740 GOSUB2510:PRINT@320,"LI YUEN'S PIRATES, TAIPAN!":GOSUB60
1750 IF BR=1 THEN PRINT"GOOD JOSS!! THEY LET US BE!!":GOSUB60:GOTO1760
1751 GOSUB2130
1760 IFRND(100)<95 THEN1820
1770 GOSUB2510:PRINT@320,"STORM, TAIPAN!!":IF RND(100)<75 THEN1810
1780 GOSUB60:PRINT"WE'RE GOING DOWN!!":GOSUB200:GOSUB60
1790 PRINT"WANT TO GIVE IT ANOTHER GO";:INPUTQ$
1800 IF Q$="Y" THENRUN
1801 CLS:END
1810 GOSUB60:PRINT"WE MADE IT!!":GOSUB60
1811 IFRND(5)>3THENGOSUB70:L=RND(7):LD=L:PRINT"WE'VE BEEN BLOWN OFF COURSE TO":PRINTLO$(L):GOSUB60
1820 L=LD:GOSUB2510:PRINT@320,"ARRIVING AT ";LO$(L):GOSUB70:IFRND(5)=1THENGOSUB1950
1830 RETURN
1840 GOSUB2510:PRINT@320,"ELDER BROTHER WU REMINDS YOU":PRINT"TO PAY YOUR DEBTS."
1850 GOSUB50:RETURN
1860 TM=CA+BA-D0:IF TM>=1000000 THEN PRINT@320,"DO YOU WISH TO RETIRE";:INPUTQ$:GOTO1870
1861 RETURN
1870 IF Q$="Y" THEN PRINT@320,"Y O U ' R E   A":PRINT"M I L L I O N A I R E !":GOTO1880
1871 RETURN
1880 PRINT"YOUR RATING IS";INT(TM/TI/100):PRINT"PLAY AGAIN";:INPUTQ$
1890 IFQ$="Y" THEN RUN
1891 CLS:END
1900 IFBR=1THENRETURN
1910 BB=INT(CA*RND(100)/100):BR=0
1920 PRINT@320,"LI YUEN ASKS";BB;"IN DONATION":PRINT"TO THE TEMPLE OF TIN HAU, THE"
1930 PRINT"SEA GODDESS. WILL YOU PAY";:INPUTQ$
1940 IFQ$="Y"THENCA=CA-BB:BR=1:RETURN
1941 RETURN
1950 PS=1000+RND(500)
1951 IFCA>PSTHENPRINT@320,"DO YOU WISH TO TRADE YOUR SHIP":PRINT"FOR ONE WITH 50 MORE CAPACITY":PRINT"BY PAYING";PS;:INPUTQ$
1960 IF CA<PS THENRETURN
1970 IF Q$="N" THEN1980
1971 CA=CA-PS:MW=MW+50:SH=SH+50
1980 GOSUB2510:RETURN
1990 IF L=8 OR RND(10)<7 THEN 2040
2000 RB=INT(CA*RND(100)/100):CA=CA-RB:GOSUB2510
2010 PRINT@320,"TAIPAN, YOU'VE BEEN BEATEN UP"
2020 PRINT"AND ROBBED OF";RB;"IN CASH!!"
2030 GOSUB50:GOSUB70:GOSUB2510:RETURN
2040 IF OC=0 OR L=8 THEN 2100
2050 OF=INT(CA*RND(100)/100): CA=CA-OF
2060 PRINT@320,"LOCALS SEIZE YOUR OPIUM AND"
2070 PRINT"FINE YOU";OF;", TAIPAN!":MW=MW+OC:OC=0:GOSUB50
2080 GOSUB2510:RETURN
2090 IFBR=0ANDRND(10)>6ANDL<>1THENGOSUB3050
2100 RETURN
2110 IFRND(10)>1+(WZ-1)*2THENRETURN
2111 CLS0
2120 SN=INT(RND(SH/20)+1):SA=SN:PRINT@320,"HOSTILE SHIPS APPROACHING!!":GOSUB60:GOSUB1280:GOTO2140
2130 SN=RND(SH+GN)+1:SA=SN:M1=SH:PRINT@320,"SHIPS OF LI YUEN'S PIRATE FLEET":GOSUB60:GOSUB1280
2140 GOSUB2520:IFRND(2)=1THEN2210
2150 GOSUB70:PRINT@320,"THEY'RE FIRING ON US, TAIPAN!":GOSUB1400:GOSUB70
2160 IFRND(SN)<RND(4)THENPRINT@320,"THEY MISSED!!":GOTO2210
2170 GOSUB2630
2180 SH=SH-RND(30):DM=INT((SH/M1)*100):GOSUB2530:PRINT@320,"WE'VE BEEN HIT!!":PRINT"STATE OF REPAIR IS";INT(DM);"%":GOSUB70
2190 IF DM=<0 AND SH=<0 THENGOSUB70:PRINT"THE BUGGERS GOT US, TAIPAN!!":PRINT"IT'S ALL OVER NOW!!":GOSUB2720:GOTO2200
2191 GOTO2210
2200 CK=1:GOSUB1280:GOSUB2510:PRINT@320,"WANT TO TRY AGAIN";:INPUTQ$:IFQ$="Y"THENRUN
2201 CLS:END
2210 GOSUB70:IF RND(WZ*20)=1 AND BR=1 THENGOSUB3070:RETURN
2220 IF GN=0THENPRINT@320,"SHALL WE RUN?":GOTO2230
2221 PRINT@320,"SHALL WE RUN OR FIGHT?"
2230 SOUND200,1:Q1$=INKEY$
2240 Q1$="":FORT=0TO99:Q1$=INKEY$:IFQ1$=""THENNEXT:GOTO2250
2241 T=100:NEXT
2250 IFQ1$="R"ORQ1$="Y"THENIFRND(3)>RND(SN)THENSN=0:PRINT@320,"WE GOT AWAY FROM 'EM!!":GOSUB60:CK=1:GOSUB1280:GOSUB2340:RETURN
2251 PRINT@320,"CAN'T LOSE 'EM!!":GOSUB70
2260 GOSUB2520:IFQ1$<>"R" AND Q1$<>"Y" THEN2270
2261 IFSN>2 AND RND(10)>9THEN3080
2270 IFGN>0 AND Q1$="F" THENPRINT@320,"WE'RE FIRING ON 'EM!!":GOTO2280
2271 IFGN=0 AND Q1$="F"THENPRINT@320,"WITH WHAT, TAIPAN?!?":GOTO2150
2272 GOTO2280
2280 IFGN=0 OR Q1$<>"F"THEN2150
2290 IF RND(GN)>=RND(4)/WZ THENGOSUB70:GOSUB1420:PRINT@320,"WE HIT 'EM!!":GOTO2300
2291 GOSUB70:PRINT@320,"MISSED 'EM!!":GOTO2320
2300 IFRND(100)>25-GN THENSN=SN-1:GOSUB70:GOSUB1440:PRINT"THE BUGGER'S JOINING DAVY JONES":GOSUB60:GOSUB1280:GOSUB2500:GOTO2320
2310 GOSUB70:PRINT@320,"STILL FLOATING!!"
2320 GOSUB2520:GOSUB70:IFSN=0THEN PRINT"WE'VE DONE FOR 'EM ALL!!":GOSUB70:GOSUB2510:GOTO2330
2321 GOTO2150
2330 GOSUB60:PRINT@320,"WE'VE CAPTURED SOME BOOTLE,":CB=RND(10000)*SA:CA=CA+CB:PRINT"IT'S WORTH";CB;"!!"
2331 GOSUB60:GOSUB2340:RETURN
2340 Q1$="":IFRND(10)<2THEN2110
2341 RETURN
2350 G1=RND(1000)+500:IF G1>CA OR RND(4)>1THENRETURN
2360 PRINT@320,"DO YOU WISH TO BUY A SHIP'S GUN":PRINT"FOR";G1;:INPUTQ$
2370 IFQ$="N"THENRETURN
2380 IFMW-10<0THENGOSUB2410:RETURN
2390 CA=CA-G1:GN=GN+1:MW=MW-10
2400 GOSUB2510:RETURN
2410 PRINT@320,"YOUR SHIP WOULD BE OVERBURDENED":GOSUB60:RETURN
2420 GOSUB2490
2430 PRINT@416,"HOW MUCH ";TG$;" SHALL I BUY"
2440 PRINT@448,"(YOU CAN AFFORD";INT(CA/TP);")";:RETURN
2450 PRINT@416,"HOW MUCH SHALL I SELL     ";:RETURN
2460 PRINT@416,"HOW MANY UNITS OF ";TG$:PRINT"MOVE TO WAREHOUSE";:RETURN
2470 PRINT@416,"HOW MANY UNITS OF "TG$:PRINT"MOVE ABOARD SHIP";:RETURN
2480 GOTO2510
2490 GOSUB2510:GOSUB1500:RETURN
2500 IFRND(10)>5 AND SN<(SA/4) AND SN>1 THEN SR=RND(SN):SN=SN-SR:GOSUB1280:SA=SN:PRINT@448,"RAN AWAY, TAIPAN!!":RETURN
2501 RETURN
2510 GOSUB160:GOSUB350:IFL=8THENPRINT@288,"CAPTAIN'S REPORT:";:RETURN
2511 PRINT@288,"COMPRADOR'S REPORT:";:RETURN
2520 PRINT@416,"SHIPS ATTACKING:";SN:PRINT"YOUR SHIP'S GUNS:";GN:PRINT@320,"";:RETURN
2530 IFDM<=0THENDM=0:RETURN
2531 RETURN
2540 DATA128,128,128,195,199,195,194,128
2550 DATA128,229,239,207,207,207,202,128
2560 DATA128,229,239,207,207,207,202,128
2570 DATA161,162,236,238,237,161,163,162
2580 DATA164,175,175,175,175,175,175,168
2590 DATA161,255,236,238,237,255,163,162
2600 DATA224,224,224,224,224,224,224,224
2610 DATA255,255,255,255,255,255,255,255
2620 FORI=0TO4:PRINT@PP+I*32,S$(I+1);:NEXTI:RETURN
2630 CLS8:SOUND100,15
2640 CLS 0: GOSUB1280: RETURN
2650 CLS:PRINT@168,"DO YOU WANT TO BE":PRINTTAB(8);"1) A TRADER":PRINTTAB(8);"2) A PIRATE"
2660 PRINTTAB(8);
2670 INPUTWZ:IFWZ=1THENRETURN
2680 IFWZ=2THEND0=0:GN=5:MW=0:CA=0:RETURN
2690 GOTO2650
2700 SOUND10,3:RETURN
2710 SOUND100,15:RETURN
2720 FORII=0TO6: SOUND S(II,0),S(II,1)
2730 FORJJ=1TO50:NEXT
2740 IFII=3THENFORJJ=1TO100:NEXT
2750 NEXTII:RETURN
2760 DATA 40,8,20,4,80,20,40,8,20,4,80,4,120,20
3000 GOSUB2510:PRINT@320,"HOW MUCH DO YOU WISH TO REPAY":INPUTR$
3010 IFR$="A"THENIFCA>=D0THENR=D0:RETURN
3020 R=CA:RETURN
3050 PRINT@320,"LI YUEN HAS SENT A LIEUTENANT":PRINT"HE SAYS HIS ADMIRAL WISHES TO"
3051 PRINT"SEE YOU IN HONGKONG POSTHASTE.":GOSUB50:GOSUB2510
3070 SN=0:GOSUB2520:CK=1:GOSUB1280:PRINT@320,"LI YUEN'S FLEET DROVE 'EM OFF!!":GOSUB60:GOSUB2340:RETURN
3080 PU=RND(SN-1):SN=SN-PU:SA=SN:GOSUB1280
3081 PRINT"BUT WE ESCAPED FROM";PU:PRINT"OF 'EM, TAIPAN!!":PRINT"ONLY";SN;"STILL ATTACKING!!":GOSUB2520:GOSUB70:GOTO2150
