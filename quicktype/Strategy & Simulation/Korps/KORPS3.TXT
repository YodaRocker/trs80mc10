0 CLS:CLEAR300:DIMCC,ZZ,M$,I$:GOTO100
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 NEXT:M$="":RETURN
3 RESTORE:FORCC=1TOZZ:READA(X),C(X),F(X),M(X),P(X),R(X),J(X):NEXT:RETURN
10 DATA1,5,1,150,1,50,4,3,10,2,100,5,200,3,3,75,9,90,20,700,3,9,75,11,80,35,800,1,10,37,5,60,25,500,1,5,37,5,100,15,500,1
16 DATA3,37,3,160,10,500,1,7,105,40,80,100,3000,2,2,10,2,140,7,200,4,1,5,1,130,2,50,4,7,75,12,80,30,700,1
21 DATA3,20,2,150,10,300,3,7,37,5,60,25,500,1,4,37,5,100,20,500,1,5,75,10,90,30,800,1,7,75,11,60,30,800,1
26 DATA9,75,11,50,35,800,1,5,37,5,110,15,500,1,3,53,3,150,15,500,1,1,5,1,140,3,50,4,9,105,14,80,60,1000,2
100 T=INT(RND(0)*9)+1:K=0:K3=0:M=1:P=0:PRINTTAB(10)"A F R I K A":PRINTTAB(11)"K O R P S"
110 DIMA(70),C(70),D(70),F(70),H(70),J(70),M(70),P(70),R(70),S(70),T(70),V(70),W(70),X(70),Y(70)
120 DATA6,20,2,100,400,30,25,7,50,8,90,600,35,35,7,75,11,80,800,30,50,3,50,6,140,600,45,25,8,75,14,75,900,40,75
170 DATA12,88,15,60,1200,50,100,12,75,12,60,800,35,50,7,75,12,80,800,30,50,1,88,20,1,2000,75,40
300 Q=0:N=0:K1=0:K2=0
400 X=0:ZZ=21:GOSUB3:FORX=1TO9:READA(X),C(X),F(X),M(X),R(X),S(X),P(X):W(X)=4:NEXT:L=INT(RND(0)*10)+1:GOSUB900:GOTO1000
900 PRINT"                     british":PRINT"MOVEMENT CAN BE IN   4  3  2":PRINT"8 DIRECTIONS AS       "CHR$(92)" ! /"
960 PRINT"SHOWN.             5 -- 0 -- 1":PRINT"YOU MOVE OR SHOOT     / ! "CHR$(92):PRINT"YOU CAN'T DO BOTH.   6  7  8"
985 PRINT"                       you":PRINT:RETURN 
990 DATA10,19,19,29,11,15,15,20,20,11,13,17,13,13,21,19,29,11,15,13,16,18,21,23,23,27,24,28,29,30
995 DATA19,10,19,11,12,20,25,26,17,17,14,13,14,22,23,12,22,17,23,23,18,10,11,15,18,29,29,19,30,20
1000 FORX=10TO69:READT(X):NEXT:Z=INT(RND(0)*25)+8:FORX=10TOZ+10:Z1=INT(RND(0)*59)+10:Z2=INT(RND(0)*59)+10:T(Z1)=T(Z2):NEXT
1570 Z=INT(RND(0)*20)+10:E=0:PRINT"  + G O O D   H U N T I N G +":FORX=10TO69:S(X)=INT(RND(0)*30)+5:IFT(X)<>ZTHEN1620
1615 E=E+1:M$="RECONNAISSANCE UNIT REPORTS"+STR$(E)+" ":GOSUB7000:IFE=1THENM$=M$+" UNIT.":GOSUB1:GOTO1620
1618 M$=M$+" UNITS.":GOSUB1
1620 IFT(X)>9ORT(X)<31THENZZ=(T(X)-9):GOSUB3
1890 NEXT 
2000 M$="YOUR PRESENT TERRAIN POSITION IS":GOSUB7700:GOSUB1
2200 FORX=10TO69:IFC(T)=20THENP(X)=INT(P(X)*3)
2220 IFC(T)=50THENP(X)=INT(P(X)*2)
2240 D(X)=INT(RND(0)*(R(T)/100))*100-INT(R(T)/10):D(X)=D(X)+(R(T)-100):W(X)=6:H(X)=INT((RND(0)*10)*10):V(X)=INT(D(X)/2)
2270 Y(X)=D(X)-INT((RND(0)*30)*10):IFY(X)>R(T)THENY(X)=INT(R(T)-(R(T)/10))
2280 X(X)=INT((RND(0)*14)*10)-70:NEXT 
3000 IFRND(0)>.6THEN3100
3020 IFN>0THEN3100
3030 Q=INT(RND(0)*53)+10:Q1=INT(RND(0)*6.4)+1
3040 IFRND(0)>.3THENQ1=INT(RND(0)*3.3)+1
3050 N=0:IFQ<10THEN3000
3060 FORX=QTOQ+(Q1-1):IFW(X)>0THENW(X)=6:N=N+1
3090 NEXT 
3100 IFN=0THEN3200
3110 PRINT"* * * BRITISH MOVING * * * ":M$=""
3130 FORX=QTOQ+(Q1-1):IFW(X)<3THEN3195
3140 Z=INT(RND(0)*60)-30:IFR(X)=>ABS(D(X)+Z)THENW(X)=4
3155 IFR(X)<ABS(D(X)+Z)THENW(X)=6
3160 IFW(T)=1THENIFRND(0)>.7THENW(X)=5
3165 IFABS(D(X))<M(X)THENW(X)=5
3170 IFC=2THENIFW(X)=4THENIFRND(0)>.6THENW(X)=6
3180 IFRND(0)>.9THENW(X)=5
3185 IFJ(X)>2THENIFD(X)<M(X)THENIFRND(0)>.6THENW(X)=7
3190 IFJ(X)<4THENIFS(X)<=INT(RND(0)*3.2)THENW(X)=7
3195 NEXT
3200 IFQ=0THEN4000
3210 N=0:IFRND(0)>.9THENGOSUB6900
3220 FORX=QTOQ+(Q1-1):IFW(X)<2THEN3290
3240 N=N+1:IFW(X)=2THENGOSUB3900:GOTO3290
3260 IFW(X)=4THENGOSUB3300
3270 IFW(X)>4THENGOSUB3800
3280 IFW(T)=0THEN9000
3290 NEXT:GOTO4000
3300 PRINT"+BOOM+"
3330 M$="":GOSUB7000:M$=M$+" SHOOTING FROM"+STR$(D(X))+" METERS.":GOSUB1
3390 GOSUB3400:GOSUB3600:RETURN 
3400 Z1=D(X)-V(X)
3420 Z3=INT(RND(0)*Z1):Z4=INT(RND(0)*H(X)):V(X)=INT(V(X)+(Z3)):H(X)=INT(H(X)-(Z4)):Z1=INT(D(X)-V(X)):Z2=INT(H(X))
3450 H=F(X)/A(T):S(X)=S(X)-1:IFM(T)=0THENH=H+H
3470 IFJ(X)=1THENIFABS(D(X))<=R(X)/2THENH=H*2
3480 IFJ(X)=2THENIFABS(D(X))>R(X)/10THENH=H/2
3490 IFJ(X)=3THENIFABS(D(X))>50THENH=H+1
3500 IFL>3THENIFL<7THENH=H-(RND(0)*(.1*L))
3520 IFL=>7THENH=H/2
3595 RETURN 
3600 H=INT(H):IFV(X)>R(X)THENH=0
3630 IFABS(Z1)>12THENH=0
3640 IFABS(Z2)>8THENH=0
3650 IFH>0THEN3700
3655 IFABS(Z1)<13THENIFABS(Z2)<9THEN3700
3660 PRINT"*  B O O M  *":M$="SHOT ON YOU":IFH<1THENM$=M$+" INEFFECTIVE:"
3690 GOSUB6700:GOSUB1:RETURN 
3700 PRINT"*++* B L A M *++*:PRINT"YOU'VE BEEN HIT!":W=W(T):GOSUB6600:PRINT"YOU ARE...";:IFZ2=2THENW(T)=2
3760 IFZ2=2THENIFRND(0)>.85THENW(T)=3:M(T)=0
3770 IFZ2=0THENW(T)=0:PRINT" DEAD. "
3780 IFW(T)=2THENPRINT" STUNNED!":PRINT 
3785 IFW(T)=3THENPRINT" DAMAGED-- CAN'T MOVE."
3790 IFW(T)>3THENPRINT" JUST DENTED..."
3795 RETURN 
3800 M$="":GOSUB7000:IFM(X)=0THENM$=M$+" STALLED.":GOSUB1:RETURN
3810 IFW(X)=5THENM$=M$+" HALTED.":GOSUB1:RETURN
3820 M$=M$+" MOVING.":GOSUB1:IFW(X)=6THEND=INT((RND(0)*2)+2.5)
3830 IFRND(0)>.95THEND=INT(RND(0)*9)
3840 IFW(X)=7THEND=7:M$=M$+"RETREATING.":GOSUB1
3850 D1=M(X)-INT(RND(0)*30):GOSUB6000:D(X)=INT(D(X)+(V1)):V(X)=INT(V(X)+(V1/2)):H(X)=INT(H(X)+(H1/2))
3860 IFJ(X)=2THENIFM(X)=0THENH(X)=INT(H(X)+(H1/2))
3865 D(X)=INT(D(X)/10)*10:IFT<10THENY(X)=INT(Y(X)+(V1*.8))
3875 IFT=10THENY(X)=INT(Y(X)+(V1*.85)+(RND(0)*M(X)))
3880 IFABS(D(X))>R(T)+150THEN3890
3885 IFABS(D(X))+150>ABS(Y(X))THENY(X)=D(X)*.9
3890 IFABS(D(X))<R(T)*2THENRETURN
3892 GOSUB7000:M$=M$+" HAS ESCAPED!":GOSUB1:W(X)=1:RETURN
3900 Z2=RND(0):IFZ2>.6THENW(X)=4
3940 IFZ2>.9THENW(X)=7
3950 IFS(X)=0THENRETURN
3960 IFM(X)>0THENRETURN
3970 IFZ2>.15THENRETURN
3980 GOSUB7000:S(X)=0:M$=M$+" BEING... ABANDONED.":GOSUB1:RETURN
4000 IFW(T)=2THENPRINT"YOU ARE STUNNED FOR 1 TURN."
4030 IFW(T)=2THENW(T)=4:GOTO3000
4050 IFS(T)<=0THENPRINT"YOU ARE OUT OF SHELLS.":GOTO9000
4060 IFC=2THENIFN<1THENPRINT"DUST CLOUDS...WAIT AGAIN"
4070 INPUT"NEXT COMMAND (0-9)";C
4100 IFC=0THEN5000
4110 IFC=1THEN5100
4120 IFC=2THEN3000
4130 IFC=3THEN5300
4140 IFC=4THEN5400
4150 IFC=5THEN5500
4160 IFC=6THEN5600
4170 IFC=7THEN5700
4180 IFC=8THEN5800
4190 IFC=9THENGOSUB7900:GOTO4000
4200 PRINT"C O M M A N D  S U M M A R Y"
4210 PRINT"0-MOVE 0 TO";M(T);"METERS"
4220 PRINT"1-SCAN OF VISIBLE UNITS"
4230 PRINT"2-WAIT FOR ENEMY"
4240 PRINT"3-SHOOT AT ONE ENEMY"
4250 PRINT"4-WITHDRAW FROM THE AREA"
4260 PRINT"5-ABANDON YOUR VEHICLE"
4270 PRINT"6-GET A STATUS REPORT"
4280 PRINT"7-INFO ON BRITISH UNITS"
4285 PRINT"8-VALUE OF BRITISH UNITS"
4290 PRINT"9-LIST KILLS SO FAR":GOTO4000
4400 D=7:D1=M(T)*2:GOSUB6000:GOSUB7500:M$=M$+"WITHDRAWING "+STR$(D1)+" METERS.":GOSUB1
4430 FORX=QTOQ+(Q1-1):IFW(X)<2THEN4460
4440 D(X)=D(X)+D1:V(X)=V(X)+M(T)
4450 IFW(X)<7THENIFABS(D(X))<R(X)+M(X)THENZ=Z+1
4460 NEXT
4470 IFZ>0THENM$=MID$(STR$(Z)+" BRITISH UNITS STILL IN RANGE.",2):GOSUB1
4480 IFZ=0THENPRINT"WITHDRAWL SUCCESSFUL"
4490 RETURN
4500 M$="YOU ARE DAMAGED AND CAN'T MOVE!":GOSUB1
4590 GOTO4000
4600 M$="HITLER HAS ORDERED EVERY UNIT TO FIGHT TO THE LAST MAN. THERE IS NO RETREAT!":GOSUB1
4630 M$="HOPEFULLY, YOU WILL BE RELIEVED BY THE TIME YOU RUN OUT OF SHELLS.":GOSUB1
4660 M$="YOU CAN'T MOVE WITHOUT TRANSPORT!":GOSUB1:GOTO3000
5000 IFM(T)=0THEN4500
5010 IFT=9THEN4600
5020 INPUT"DIRECTION TO MOVE (1-8)";D
5030 D=INT(D):IFD=0THEN4000
5040 IFD<0ORD>8THENGOSUB900:GOTO4000
5050 INPUT"HOW MANY METERS TO MOVE";D1
5060 D1=INT(D1):IFD1=0THEN4000
5070 IFD1<0THENPRINT"MAX MOVE RATE IS ";M(T):GOTO5050
5075 IFD1>M(T)THENPRINT"MAX RATE IS";M(T):GOTO5050
5080 GOSUB6000:IFL>0THENIFRND(0)>.3THENL=L-1
5087 IFN>0THENIFW(E)=7THENIFRND(0)>.7THENGOSUB6900
5090 GOSUB6100:IFN>0THENGOSUB6800
5095 GOTO3000
5100 IFN<1THEN5700
5130 FORX=QTOQ+(Q1-1):IFW(X)<2THEN5190
5160 GOSUB7000:M$=M$+" DISTANCE:"+STR$(D(X)):IFW(X)=2THENM$=M$+" STUNNED"
5172 IFW(X)=5THENM$=M$+" *HALTED*"
5175 IFM(X)=0THENM$=M$+" +DAMAGED+":GOTO5190
5180 IFW(X)=7THENM$=M$+" RETREATING":GOTO5190
5190 GOSUB1:NEXT
5195 GOTO4000
5300 IFN<1THEN5700
5320 FORZ=QTOQ+(Q1-1):IFW(Z)>1THENE=Z
5340 NEXT
5350 IFN>1THENGOSUB6200
5366 IFE=0THEN4000
5380 X=E:GOSUB6300:GOSUB6400:GOTO3100
5400 Z=0:IFM(T)=0THEN4500
5420 IFT=9THEN4600
5430 IFN=0THEN5470
5440 IFQ=>10THENGOSUB4400
5450 IFZ>0THEN3000
5470 N=0:M$="WITHDRAWING FROM":GOSUB7700:GOSUB1
5480 L=INT((RND(0)*10)+1):GOSUB7600:M$="YOUR NEW POSITION IS:":GOSUB7700:GOSUB1:GOTO4000
5500 IFM(T)=0THENM$="+ DAMAGED + ":GOSUB7500:GOSUB1
5520 IFM(T)=0THENM$=M$+" + ABANDONED + ":GOSUB1:GOTO9000
5540 M$="YOU ARE PENALIZED"+STR$(P(T))+" POINTS FOR ABANDONING A MUCH NEEDED ":GOSUB7500:GOSUB1:P=P-P(T):GOTO9000
5600 PRINT"DAY"M:M$="":GOSUB7500:M$=M$+"STATUS IS:":IFM(T)=0THENM$=M$+" DAMAGED."
5615 IFM(T)>0THENM$=M$+" UN-DAMAGED."
5616 GOSUB1:M$="PRESENT TERRAIN:":GOSUB7700:GOSUB1:PRINT"GUN RANGE:"R(T)TAB(16)CHR$(8)S(T)"SHELLS"
5650 PRINT"MAX MOVE:"M(T)TAB(16)"GUN:"C(T)"MM":PRINT"ARMOUR:"A(T)TAB(16)"FIREPOWER:"F(T):GOTO4000
5700 Z=0:IFN<1THENPRINT" *** NO BRITISH VISIBLE ***":GOTO4000
5720 PRINT"******** DATA ON BRITISH *******";:FORX=QTOQ+(Q1-1):IFW(X)<2THEN5790
5750 M$="":GOSUB7000:M$=M$+" WITH"+STR$(C(X))+" MM GUN":GOSUB1
5760 PRINT"MAX MOVE:"M(X)TAB(16)"GUN RANGE:"STR$(R(X)):PRINT"ARMOUR:"A(X)TAB(16)"FIREPOWER:"STR$(F(X)):Z=Z+1
5775 IFN=3THENIFZ=3THEN4000
5776 IFN=6THENIFZ=6THEN4000
5780 IFZ=3THENGOSUB5799
5785 IFZ=6THENGOSUB5799
5790 NEXT:GOTO4000
5799 INPUT"INPUT ANY NUMBER TO SEE MORE";Y:RETURN
5800 IFN<1THENPRINT" == NO BRITISH VISIBLE ==":GOTO5880
5830 PRINT"++++++++ POINT VALUES ++++++++"
5840 FORX=QTOQ+(Q1-1):IFW(X)<2THEN5870
5850 GOSUB7000:M$=M$+STR$(P(X)):GOSUB1
5870 NEXT
5880 PRINT"YOUR PRESENT SCORE:"P:GOTO4000
6000 IFD=0THENH1=0:V1=0:RETURN 
6010 IFD=1THENH1=D1:V1=INT(D1*.2):RETURN 
6020 IFD=2THENH1=INT(D1*(.8)):V1=INT(D1*(.8))*(-1):RETURN 
6030 IFD=3THENH1=0:V1=D1*(-1):RETURN 
6040 IFD=4THENH1=INT(D1*(.8))*(-1):V1=INT(D1*(.8))*(-1):RETURN 
6050 IFD=5THENH1=D1*(-1):V1=INT(D1*.2):RETURN 
6060 IFD=6THENH1=INT(D1*(.8))*(-1):V1=INT(D1*(.8)):RETURN 
6070 IFD=7THENH1=0:V1=D1:RETURN 
6080 IFD=8THENH1=INT(D1*(.8)):V1=INT(D1*(.8))
6090 RETURN 
6100 IFQ1=0ORN<=0THENRETURN
6140 FORX=QTOQ+(Q1-1):IFW(X)<2THEN6180
6150 D(X)=INT(D(X)+(V1)):H(X)=INT(H(X)+(H1/2)):V(X)=INT(V(X)+(V1/2))
6170 Y(X)=INT(Y(X)+(V1*.3)):X(X)=INT(X(X)+(H1*.2)):IFW(X)=2THENH(X)=INT(H(X)+(H1/2)):V(X)=INT(V(X)+(V1/2))
6180 NEXT:RETURN 
6200 IFN<=0THENRETURN
6220 IFQ<=10THENRETURN 
6230 FORX=QTOQ+(Q1-1)
6235 IFW(X)<2THEN6250
6240 M$=CHR$(8)+STR$(X):IFW(X)=2THENM$=M$+"*"
6243 IFM(X)=0THENM$=M$+"+"
6245 M$=M$+" ":GOSUB7000:M$=M$+" RANGE:"+STR$(D(X))+". IT SHOOTS TO:"+STR$(R(X)):GOSUB1
6250 NEXT:INPUT"SHOOT AT WHICH UNIT";E:E=INT(E):IFE=0THENPRINT"SHOOTING ABORTED":RETURN
6280 IFE<QTHENPRINT"NUMBER TOO LOW":GOTO6200
6285 IFE>Q+Q1THENPRINT"NUMBER TOO HIGH":GOTO6200
6290 IFW(E)=0THENPRINT"*** THAT UNIT IS DEAD ***":GOTO6200
6295 RETURN
6300 S(T)=S(T)-1:Z1=INT(D(E)-Y(E)):Z2=INT(X(E)):Z3=INT((RND(0)*(Z1/2))+(Z1/2)):Z4=INT((RND(0)*(Z2/2))+(Z2/2))
6330 Y(E)=INT(Y(E)+Z3):X(E)=INT(X(E)-Z4):H=F(T)/A(E):IFABS(D(E))<M(E)+100THENY(E)=D(E):X(E)=0
6340 Z1=INT(D(E)-Y(E)):Z2=INT(X(E)):IFRND(0)>.85THENH=H+1
6350 IFABS(D(E))<M(E)+50THENH=H+H
6355 IFW(E)=7THENH=H+1
6356 IFS(E)<=0THENH=H+1
6357 IFM(E)=0THENH=H+1
6358 IFJ(E)>1THENIFABS(D(E))>R(T)/2THENH=H/2
6360 H=INT(H):IFABS(Y(E))>R(T)+10THENH=0
6365 IFJ(E)=4THENW(E)=7
6366 IFJ(E)=3THENIFRND(0)>.8THENW(E)=7
6367 IFJ(E)<3THENIFF(T)>F(E)THENIFRND(0)>.9THENW(E)=7
6370 IFABS(Y(E))>R(T)+10THENY(E)=R(T)-INT(RND(0)*25)
6380 IFABS(Z1)>15THENH=0
6390 IFABS(Z2)>10THENH=0
6395 RETURN
6400 IFC(T)=20THENPRINT" B A M "
6420 IFC(T)=50THENPRINT"+ B O O M +"
6425 IFC(T)=75THENPRINT"== K A B L A A M =="
6430 IFC(T)=>88THENPRINT"<> K A R R O O M M <>"
6435 IFABS(Z1)<16THENIFABS(Z2)<11THENIFH=0THEN6500
6450 X=E:IFH>0THEN6500
6455 PRINT"*BOOM*":M$="SHOT ON ":GOSUB7000
6470 IFABS(D(E))>R(T)+15THENM$=M$+" IS + OUT OF RANGE + "
6480 IFABS(D(E))>R(T)+15THENY(E)=R(T):Z1=INT(ABS(D(E))-Y(E))
6490 GOSUB6700:W(E)=6:IFRND(0)>.2THENW(E)=4
6495 GOSUB1:RETURN
6500 PRINT"-==- B L A M -==-":GOSUB7000
6520 PRINT"#HIT#":W=W(E):GOSUB6600
6540 GOSUB7000:IFZ2=2THENW(E)=2
6550 IFM(E)>0THENIFRND(0)>.8THENW(E)=3:M(E)=0
6555 IFS(T)=0THENIFZ2=2THENZ2=0
6560 IFZ2=0THENW(E)=0:P=P+P(E):K=K+1:K1=K1+1
6565 IFZ2=0THENIFJ(E)=1THENK2=K2+1:K3=K3+1
6570 IFW(E)=0THENPRINT"DESTROYED."
6580 IFW(E)=2THENPRINT"$! STUNNED !$"
6585 IFW(E)=3THENPRINT"+++ DAMAGED --- CAN'T MOVE +++"
6590 IFW(E)>3THENPRINT"/"CHR$(92)" DENTED "CHR$(92)"/"
6595 RETURN
6600 Z=INT(RND(0)*6)+1:Z2=5:W=W(E):IFH=0THENIFZ<3THENZ2=2:RETURN
6630 IFH=1THENIFZ<3THENIFW=2THENZ2=0:RETURN
6635 IFH=1THENIFZ<4THENIFW<>2THENZ2=2:RETURN
6640 IFH>1THENIFZ<2THENZ2=0:RETURN
6650 IFH>2THENIFZ<3THENZ2=0:RETURN
6660 IFH>3THENIFZ<6THENZ2=0:RETURN
6665 IFH>4THENZ2=0:RETURN
6670 IFH=2THENIFZ<4THENIFW=2THENZ2=0:RETURN
6675 IFH=2THENIFZ<6THENIFW<>2THENZ2=2:RETURN
6680 IFH=3THENIFZ<5THENIFW=2THENZ2=0:RETURN
6685 IFH=3THENIFW<>2THENZ2=2:RETURN
6690 IFH=4THENIFW=2THENZ2=0
6695 RETURN 
6700 IFZ1>15THENM$=M$+" SHORT BY"+STR$(Z1)+" METERS."
6710 IFZ1<-15THENM$=M$+" LONG BY"+STR$(Z1)" METERS."
6720 IFABS(Z2)>10THENM$=M$+" OFF TO THE ":GOTO6740
6730 RETURN 
6740 IFZ2>0THENM$=M$+"LEFT BY"+STR$(Z2)+" METERS."
6750 IFZ2<0THENM$=M$+"RIGHT BY"+STR$(ABS(Z2))+" METERS."
6790 RETURN
6800 FORX=QTOQ+(Q1-1):IFW(X)<2THEN6880
6810 IFABS(D(X))<>0THEN6880
6820 IFJ(X)=4THENPRINT"$ CRUNCH $"
6830 IFJ(X)<4THENPRINT"** WHAM ! **"
6840 M$="COLLISION WITH ":GOSUB7000:GOSUB1:IFJ(X)<4THENRETURN
6850 M$="RUN OVER ":Z2=0:GOSUB7000:GOSUB1:E=X:GOSUB6560:RETURN
6880 NEXT:RETURN
6900 IFQ+Q1-1<70THENQ1=Q1+1
6910 X=Q+Q1-1:IFX>71THENRETURN
6920 IFW(X)<=0THENRETURN
6930 W(X)=6:GOSUB7000:M$=M$+" COMING OUT OF A DUST CLOUD":GOSUB1:D(X)=R(T)+INT((RND(0)*50)*10):RETURN
7000 IFT(X)=10THENM$=M$+"JEEP"
7060 IFT(X)=11THENM$=M$+"BREN GUN CARRIER"
7070 IFT(X)=12THENM$=M$+"BREN GUN CARRIER"
7080 IFT(X)=13THENM$=M$+"SHERMAN"
7090 IFT(X)=14THENM$=M$+"MATILDA"
7100 IFT(X)=15THENM$=M$+"STUART"
7110 IFT(X)=16THENM$=M$+"HUMBER ARMOURED CAR"
7120 IFT(X)=17THENM$=M$+"BISHOP SP GUN"
7130 IFT(X)=18THENM$=M$+"DAIMLER SCOUT"
7140 IFT(X)=19THENM$=M$+"3/4 TON TRUCK"
7150 IFT(X)=20THENM$=M$+"GRANT"
7160 IFT(X)=21THENM$=M$+"MORRIS RECON CAR"
7170 IFT(X)=22THENM$=M$+"VALENTINE II"
7180 IFT(X)=23THENM$=M$+"CRUSADER I"
7190 IFT(X)=24THENM$=M$+"CRUSADER III"
7200 IFT(X)=25THENM$=M$+"VALENTINE IV"
7210 IFT(X)=26THENM$=M$+"CHURCHILL III"
7220 IFT(X)=27THENM$=M$+"STUART"
7230 IFT(X)=28THENM$=M$+"HUMBER ARMOURED CAR"
7240 IFT(X)=29THENM$=M$+"TRUCK"
7250 IFT(X)=30THENM$=M$+"SHERMAN"
7290 RETURN 
7500 IFT=1THENM$=M$+"PZKW-II "
7520 IFT=2THENM$=M$+"PZKW-III "
7530 IFT=3THENM$=M$+"PZKW-IV "
7540 IFT=4THENM$=M$+"SDKFZ 232 ARMOURED CAR "
7550 IFT=5THENM$=M$+"PZKW-IVB "
7560 IFT=6THENM$=M$+"TIGER "
7570 IFT=7THENM$=M$+"SEMOVENTE ASSAULT GUN "
7580 IFT=8THENM$=M$+"MARDER SP GUN "
7590 IFT=9THENM$=M$+"88 MM. ANTITANK GUN "
7595 RETURN 
7600 IFQ=0THENRETURN
7620 IFQ1<=0THENRETURN
7630 FORX=QTOQ+(Q1-1)
7640 IFW(X)>0THENW(X)=1
7680 NEXT:RETURN 
7700 IFL<1THENM$=M$+" FLATROCK":RETURN 
7710 IFL=1THENM$=M$+" SAND.":RETURN 
7720 IFL=2THENM$=M$+" OPEN SANDY LAND.":RETURN 
7730 IFL=3THENM$=M$+" A FLAT BRUSHY AREA.":RETURN 
7740 IFL=4THENM$=M$+" HIDDEN IN A ROCK PILE.":RETURN 
7750 IFL=5THENM$=M$+" UNDER CAMOUFLAGE NET.":RETURN 
7760 IFL=6THENM$=M$+" HIDDEN IN A GULLY ":RETURN 
7770 IFL=7THENM$=M$+" CAMOUFLAGED ON A HILL-TOP.":RETURN 
7780 IFL=8THENM$=M$+" SOME RUBBLE.":RETURN 
7790 IFL=9THENM$=M$+" A ROCKY HILL-TOP.":RETURN 
7800 IFL=10THENM$=M$+" HULL-DOWN BEHIND A DUNE.":RETURN 
7900 M$="YOU HAVE DESTROYED"+STR$(K1-K2)+" BRITISH UNITS AND"+STR$(K2)
7915 M$=M$+" BRITISH TANKS SO FAR ON DAY"+STR$(M)+" FOR A TOTAL OF:"+STR$(K1):GOSUB1
7920 Z=0:INPUT"LIST YOUR KILLS (1=YES 0=NO)";Y
7940 IFY=0THEN7990
7950 PRINTCHR$(8)K1;" BRITISH UNITS DESTROYED"
7960 FORX=10TO69:IFW(X)=0THENGOSUB7000:Z=Z+P(X):M$=M$+STR$(P(X))+" PTS":GOSUB1
7985 NEXT 
7990 PRINTCHR$(8);Z;"POINTS TODAY (";MID$(STR$(P),2);"TOTAL)":IFM=1THEN7995
7992 M$="YOUR TOTALS ARE:"+STR(K3)+" TANKS AND "+STR$(K-K3)+"NON-ARMORED UNITS.":GOSUB1
7995 RETURN 
9000 IFW(T)=0THEN9500
9020 IFC=5THEN9400
9100 M=M+1:M$="ROMMEL'S PRAISE ON THE END OF A SUCCESSFUL DAY"+STR$(M):GOSUB1
9130 GOSUB7900
9140 IFM(T)=0THEM$="YOUR DAMAGED TANK HAS BEEN SALVAGED.":GOSUB1
9150 M$="DO YOU WANT ANOTHER MISSION WITH YOUR "
9155 IFM(T)=0THENM$=M$+"REPAIRED "
9160 GOSUB7500:M$=M$+"?":GOSUB1:INPUT"(1=YES 0=NO)";Y:IFY=0THEN9400
9230 GOTO9570
9400 T=INT(RND(0)*9)+1:M$="YOU HAVE BEEN ISSUED A NEW ":GOSUB7500:GOSUB1
9510 M$="YOU'VE DESTROYED"+STR$(K3)+" BRITISH TANKS (TOTAL)":GOSUB1
9520 IFW(T)=0THENGOSUB7900
9530 PRINT"DO YOU WANT TO TRY AGAIN?":INPUT"TYPE IN 1=YES 0=NO";Y
9550 IFY=0THEN9900
9570 RESTORE:IFW(T)=0THENCLEAR:GOTO100
9590 GOTO300
9900 END
9910 REM MC-10 EDITS JIM GERRIE
