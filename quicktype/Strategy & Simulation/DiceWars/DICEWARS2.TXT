0 GOSUB5000:GOSUB1000:GOTO270
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORC1=CC-LEN(M$)+1TOCC:POKEC1,PEEK(C1)-64:NEXT:RETURN
8 Q(Q+1)=Q(C)-64:Q(Q+2)=Q(C)-30:Q(Q+3)=Q(C)+34:Q(Q+4)=Q(C)+64:Q(Q+5)=Q(C)+30:Q(Q+6)=Q(C)-34:Q=Q+7:RETURN
9 IFPEEK(Q(C)+MC)<128ANDPEEK(Q(C)+MC+1)=C(P)THENPRINT@Q(C),C$(P);:R=R+1:GOSUB8
10 C=C+1:IFC>QTHENRETURN
11 GOTO9
12 Y=INT(L(R)/32):X=((L(R)/32)-Y)*32:RETURN
15 PRINT@(P-1)*10,C$(P)HR(P);:RETURN
20 IFFTHENPRINT@L(T),RIGHT$(STR$(D(T)),1);
21 F=-1:IFPEEK(MC+L)>128THENON1-(PEEK(2)=0)GOTO130,110
22 FORC=1TO42:IFL=L(C)THENT=C:C=42:NEXT:GOTO110
23 NEXT:STOP
60 PRINT@L(T),RIGHT$(STR$(D(T)),1);:PRINT@L(T)-32,C$(P);:PRINT@L(T)+32,C$(P);:PRINT@L(T)-1,C$(P);:PRINT@L(T)+1,C$(P);:RETURN
70 HR=0:FORT=1TO42:IFT(T)=PTHENQ=1:C=1:R=0:Q(C)=L(T):GOSUB9:IFR>HRTHENHR=R
72 NEXT:RETURN
80 IFQ=0ANDD(T)=1THENSOUND1,1:GOTO110
81 IFQ>0ANDT(T)=PTHEN92
82 IFQ=0ANDT(T)=PTHENSOUND100,1:SOUND150,1:Q=T:F=0:GOTO110
84 IFQ=0ANDT(T)<>PTHENSOUND1,1:Q=0:F=-1:GOTO110
90 R=T:GOSUB12:A=X:B=Y:R=Q:GOSUB12:IFABS(A-X)<=2ANDABS(B-Y)<=2ANDD(Q)>1THEN94
92 SOUND1,1:PRINT@L(Q),RIGHT$(STR$(D(Q)),1);:Q=0:F=-1:GOTO110
94 D1=0:FORR=1TOD(T):D1=D1+RND(6):NEXT:D2=0:FORR=1TOD(Q):D2=D2+RND(6):NEXT
95 PRINT@(T(T)-1)*10,C$(T(T))D1;:PRINT@(P-1)*10,C$(T(Q))D2;
96 FORR=1TO750:NEXT:IFD1>=D2THEND(Q)=1
97 IFD1<D2THENA(T(T))=A(T(T))-1:A(P)=A(P)+1:D(T)=D(Q)-1:D(Q)=1:T(T)=T(Q):GOSUB60:IFA(P)=42THENR=P:P=3:Z=65000:GOTO220
98 PRINT@L(Q),RIGHT$(STR$(D(Q)),1);:PRINT@L(T),RIGHT$(STR$(D(T)),1);:Q=0:F=-1:GOSUB99:GOTO110
99 PRINT@0,"¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯";:RETURN
100 FORZ=1TO65000:FORP=1TON:T=1:Q=0:F=-1:IFA(P)<1THEN220
110 PRINT@L(T),;:M$=RIGHT$(STR$(D(T)),1):GOSUB7
120 K=PEEK(17023)ANDPEEK(2):IFK=32THEN80
130 IFK=83THENL=L(T)-30:K=65:GOTO20
140 IFK=90THENL=L(T)+30:K=88:GOTO20
150 IFK=65THENL=L(T)-34:K=83:GOTO20
160 IFK=88THENL=L(T)+34:K=90:GOTO20
170 IFK=13THEN190
180 GOTO120
190 PRINT@L(T),RIGHT$(STR$(D(T)),1);:GOSUB70:HR(P)=HR(P)+HR:GOSUB15
192 FORC=43TO85:Q(C)=-1:NEXT:C=0
194 T=RND(42):IFT(T)=PANDQ(42+T)THENQ(42+T)=0:C=C+1:Q(C)=T:IFC=A(P)THEN198
196 GOTO194
198 R=0:FORC=1TOA(P):T=Q(C):R=R+D(T)
200 IFD(T)<8THENR=R+1:HR(P)=HR(P)-1:GOSUB15:D(T)=D(T)+1:?@L(T),;:M$=RIGHT$(STR$(D(T)),1):GOSUB7:IFHR(P)<1THENC=A(P):GOTO205
204 IFRND(2)=1THEN200
205 NEXT:IFR*A(P)<8*A(P)ANDHR(P)>0THEN198
210 GOSUB99:FORC=1TOA(P):T=Q(C):PRINT@L(T),RIGHT$(STR$(D(T)),1);:GOSUB60:NEXT
220 NEXTP:NEXTZ
230 IFZ=65001THENGOSUB99:PRINT@8,C$(R);:M$=" IS VICTORIOUS!":GOSUB7
240 PRINT@487,;:M$=" PLAY AGAIN (Y/N)? ":GOSUB7
250 I$=INKEY$:IFI$=""THEN250
252 IFI$="N"THENEND
254 IFI$="Y"THENRUN
256 GOTO250
270 CLS3:X=RND(28)+1:Y=RND(12)+1:P=1
280 FORT=1TO42:P=P+1:IFP>NTHENP=1
290 Q=Q+1:IFQ>25THENR=RND(T):GOSUB12:Q=0
300 C=RND(8):R=RND(4):IFR=1THENA=2:B=1
320 IFR=2THENA=2:B=-1
340 IFR=3THENA=-2:B=1
360 IFR=4THENA=-2:B=-1
380 IFPEEK(MC+X+A+(Y+B)*32)<>175THEN290
400 IFX+A>29THEN290
420 IFX+A<2THEN290
440 IFY+B>13THEN290
460 IFY+B<2THEN290
470 X=X+A:Y=Y+B:T(T)=P:L(T)=X+Y*32:D(T)=C:A(P)=A(P)+1:GOSUB60:Q=0:NEXT:SOUND1,1:GOTO100
1000 DIMC1,CC,M$,MC,X,Y,A,B,R,C,P,R,Q(288),T(42),L(42),D(42),A(3),C(3),C$(3),HR(3),HR,L,K,N,F,D1,D2,Z
1010 MC=RND(-(PEEK(9)*256+PEEK(10))):MC=16384:C$(1)=CHR$(223):C$(2)=CHR$(239):C$(3)=CHR$(255):C(1)=223:C(2)=239:C(3)=255
1500 INPUT"NUMBER OF PLAYERS (2-3)";N:IFN<2ORN>3THEN1500
1999 RETURN
5000 CLS
5010 PRINT"dicewars IS A SIMPLIFIED VERSIONOF RISK. THE GOAL IS TO CONTROL"
5020 PRINT"ALL 42 TERRITORIES. USE THE aszxKEYS TO MOVE. space SELECTS."
5030 PRINT"ATTACKER WINS IF TOTAL IS > THE DEFENDER. ALL BUT ONE DICE ARE"
5040 PRINT"MOVED. OTHERWISE, THE ATTACKER  IS REDUCED TO 1. enter=END TURN.";
5050 PRINT"LARGEST # CONTIGUOUS TERRITORIES=NEW DICE RANDOMLY DISTRIBUTED"
5060 PRINT"THROUGHOUT PLAYER'S TERRITORIES.MAXIMUM NUMBER OF DICE ALLOWED"
5070 PRINT"PER TERRITORY IS 8. SURPLUS DICEARE STORED. DICE CANNOT BE MOVED";
5080 PRINT"SO FORETHOUGHT IS REQUIRED."
5090 RETURN
