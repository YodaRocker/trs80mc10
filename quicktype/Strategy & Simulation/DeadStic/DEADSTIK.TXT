90 CLS:GOSUB5002:CLS(0)
100 FORI=0TO63
105 SET(I,0,1):SET(I,12,1)
110 SET(I,16,1):SET(I,28,1)
115 NEXT
120 FORI=17TO20:GOSUB1000:NEXT
125 FORI=43TO46:GOSUB1000:NEXT
130 FORI=62TO63:GOSUB1000:NEXT
135 FORI=0TO31
140 SET(0,I,1 ):SET(16,I,1)
145 SET(21,I,1):SET(42,I,1)
150 SET(47,I,1):SET(61,I,1)
155 NEXT
160 FORI=13TO15:GOSUB2000:NEXT
165 FORI=29TO31:GOSUB2000:NEXT
200 PRINT@35,"ASI";
202 PRINT@162,"(MPH)";
204 PRINT@44,"DA(DEGS)";
206 PRINT@140,"DIST(NM)";
208 PRINT@58,"ALT";
210 PRINT@185,"(FT)";
212 PRINT@290,"TURN";
214 PRINT@322,"RATE";
216 PRINT@300,"HDG(DEG)";
218 PRINT@395,"DIST.";
220 PRINT@314,"VSI";
222 PRINT@441,"(FPM)";
224 PRINT@486,"GEAR:UP";
226 PRINT@496,"SPOILERS:";
250 V=880:V2=V:AN=30:D1=-5280
255 AL=30000:PI=3.141592654
300 FORI=1TO12
305 A$=INKEY$
310 IFA$="Z"THENAN=AN-1
315 IFA$="W"THENAN=AN+1
320 IFA$="A"THENTR=TR-1
325 IFA$="S"THENTR=TR+1
330 IFTR<-3THENTR=-3
335 IFTR>3THENTR=3
340 IFA$="G"THENG=-(G=0)
345 IFA$="D"THENS=1
350 IFA$="C"THENS=0
355 NEXT
400 HG=HG+TR
405 IFHG>359THENGOSUB2100
410 IFHG<0THENHG=HG+360
415 RA=PI*AN/180
420 AC=(3220000*SIN(RA)-2.5*V*V-2.5*V*V*S-2.5*V*V*G)/100000
425 V=V+AC:VM=V*15/22
427 VG=V*SIN(PI/2-RA)
430 IFVM<200THENSOUND128,1
435 IFVM<180THEN900
440 DR=V*SIN(RA):AL=AL-DR
445 IFAL<=0THEN950
500 RA=PI*HG/180
505 V1=VG*SIN(RA):D1=D1+V1
510 V2=VG*SIN(PI/2-RA)
515 D2=D2+V2:DM=D2/5280
600 PRINT@98,INT(VM);
602 PRINT@78,AN;
604 PRINT@173,INT(10*DM)/10;
606 PRINT@120,INT(AL);
608 PRINT@387,TR;
610 PRINT@334,HG;
612 IFD1<0THENPRINT@401,"WEST";:GOTO614
613 PRINT@401,"EAST";
614 PRINT@428,INT(ABS(D1));
616 PRINT@376,RIGHT$("     "+STR$(60*INT(DR))+" ",7);
618 IFG=1THENPRINT@491,"DN";
619 IFG=0THENPRINT@491,"UP";
620 IFS=1THENPRINT@505,"OPEN  ";:GOTO625
621 PRINT@505,"CLOSED";
625 IFF=1THEN982
626 GOTO300
900 PRINT@226,"AIRCRAFT STALLED-CRASHED!";:GOTO4000
950 IFDR>7THEN3000
955 IFDM>0THEN3100
960 IFDM<-3THEN3200
965 IFABS(D1)>100THEN3300
970 IFG=0THEN3400
975 IFHG<>180THEN3500
980 PRINT@226,"TOUCHDOWN!";:F=1
981 AN=0:AL=0:DR=0:TR=0
982 FORI=1TO450:NEXT
983 VM=VM-5:IFVM<0THENVM=0
984 IFVM=0THEN5000
985 DM=DM-VM*22/15/5280
986 IFABS(DM)>3THEN3500
987 GOTO600
1000 RESET(I,0):RESET(I,12)
1005 RESET(I,16):RESET(I,28)
1010 RETURN
2000 RESET(0,I):RESET(16,I)
2005 RESET(21,I):RESET(42,I) 
2010 RESET(47,I):RESET(61,I) 
2015 RETURN
2100 HG=HG-360:PRINT@337," ";
2101 RETURN
3000 PRINT@226,"CRASHED-SINK RATE > 7 FPS";:GOTO4000
3100 PRINT@226,"CRASHED SHORT OF RUNWAY";:GOTO4000
3200 PRINT@226,"CRASHED BEYOND RUNWAY";:GOTO4000
3300 PRINT@226,"CRASHED OFF EDGE OF RUNWAY";:GOTO4000
3400 PRINT@226,"LANDED GEAR UP";:GOTO4000
3500 PRINT@226,"RAN OFF RUNWAY";:GOTO4000
4000 SOUND1,10
4001 PRINT@480,"again";:POKE16869,63
4002 A$=INKEY$
4003 IFA$="Y"THENRUN
4004 IFA$="N"THENEND
4005 GOTO4002
5000 PRINT@226,"STOPPED ";
5001 PRINT@98,0;:GOTO4001
5002 PRINT"deadstick IS A PROGRAM TO":PRINT"SIMULATE LANDING A POWERLESS":PRINT"AIRCRAFT.":PRINT
5003 PRINT"PRESS d TO OPEN LIFT DUMPER":PRINT"(SPOILER) AND c TO CLOSE IT."
5004 PRINT"PRESS g TO LOWER GEAR."
5005 PRINT"USE awsz FOR STICK CONTROL.":PRINT"z=PULL STICK & RAISE NOSE.":PRINT
5006 PRINT"MANEUVER ONTO COURSE 180'"
5007 PRINT"AND WITHIN 100 FT + OR -"
5008 PRINT"OF RUNWAY CENTERLINE TO":PRINT"ARRIVE AT RUNWAY THRESHOLD":
5009 PRINT"AT 0 ALT AND SPEED <7 FT/SEC.":PRINT@485,"hit any key to begin";
5010 A$=INKEY$:IFA$=""THEN5010
5020 RETURN
5030 REM DEADSTIK 80-U.S. JOURNAL FEB 1983
