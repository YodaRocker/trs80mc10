REM THIS GAME IS FROM THE VZ200 GIANT BOOK OF GAMES
REM IT IS REMANICENT OF THE CLASSIC GAME 'HAMURABI'
REM PUBLISHED IN CREATIVE COMPUTING.
REM
10 REM RURAL PURSUITS
20 YR=1
40 MO=RND(1000)+7000
50 LA=RND(1000)+100
60 AC=RND(200)+300
70 CS=RND(5)+8
80 BA=0:WH=0:CO=0
90 GOSUB 170
100 GOSUB 290
110 IF YR=10 THEN 750
120 IF MO<1 THEN 790
130 IF LA<1 THEN 820
140 YR=YR+1:AC=AC+INT(AC/RND(100))
150 CS=CS+INT(12.5*CS/100)
160 GOTO 80
170 REM UPDATE
180 CLS
190 PRINT"YOU HAVE $"MO"IN YEAR"YR
210 PRINT"YOU ARE EMPLOYING"LA
220 PRINT"LABOURERS, WORKING FOR"
230 PRINT"YOU ON"AC"ACRES."
250 PRINT"CROPS:"TAB(7)CO"CORN"
260 PRINTTAB(7)BA"BARLEY"
270 PRINTTAB(7)WH"WHEAT"
280 RETURN
290 PRINT:PRINT"IT WILL BE $"CS"IN"
300 PRINT"GENERAL COSTS TO WORK EACH"
310 PRINT"ACRE... AND SO THE MAXIMUM"
320 PRINT"NUMBER OF ACRES YOU CAN"
330 PRINT"WORK THIS YEAR IS";
340 MAX=INT(MO/CS):IF MAX>AC THEN MAX=AC
350 PRINTSTR$(MAX)"."
360 PRINT"HOW MUCH LAND DO YOU WANT TO":PRINT"HARVEST";
370 INPUT L
380 IF L>MAX THEN 370
390 MO=MO-L*CS
400 GOSUB 170
410 PRINT:PRINT"HOW MUCH WILL YOU PAY EACH ":PRINT"WORKER";
420 INPUT W
430 IF W*LA>MO THEN 420
440 MO=MO-LA*W
450 GOSUB 170
460 P=10
470 PRINT"WHAT PROPORTION (OUT OF TEN) DO"
480 PRINT"YOU WISH TO ALLOCATE TO CORN?"
490 INPUT CP
500 IF CP>P THEN 490
510 P=P-CP
520 PRINT:PRINT"OF THE REMAINING"P"OUT OF TEN,"
530 PRINT"HOW MUCH WHEAT DO YOU WANT TO   PLANT";
540 INPUT WP
550 IF WP>P THEN 540
560 P=P-WP
570 GOSUB 870
580 PRINT"STAND BY FOR A YEAR...."
590 FOR Z=1 TO 2500:NEXT Z
600 BA=INT(P*L*LA*W*3/100000)
610 CO=INT(CP*L*LA*W*2.7/17000)
620 WH=INT(WP*L*LA*W*1.4/9300)
630 T=BA+CO+WH
640 GOSUB 170
650 PRINT:PRINTMID$(STR$(T),2)" TONS WERE HARVESTED."
660 RT=INT((.5+8.7*BA+5.94*CO+2.2*WH)*(CS-CX+1))
670 IF BA=0 AND CO=0 AND WH=0 THEN RT=0
680 FOR Z=1 TO 2500:NEXT Z
690 PRINT:PRINT"AND YOUR TOTAL RETURN"
700 PRINT"WAS $"RT
710 MO=MO+RT
720 LA=INT(LA-LA/(W+.01)):PRINT:PRINT"HIT ANY KEY"
730 IFINKEY$=""THEN730
740 RETURN
750 GOSUB 870
760 PRINT"YOU HAVE SURVIVED FOR 10 YEARS"
770 PRINT"CONGRATULATIONS!"
775 IF MO>1E+12 THEN GOSUB 900
780 PRINT"AND GAINED $"MO"MONEY":END
790 GOSUB 870
800 PRINT"YOU HAVE GONE BROKE!"
810 END
820 GOSUB 870
830 PRINT"YOU HAVE NO WORKERS, AND"
840 PRINT"HAVE BEEN FORCED TO SELL"
850 PRINT"YOUR FARM"
860 END
870 CLS
880 PRINT
890 RETURN
900 SOUND221,3:SOUND225,2:SOUND229,3:SOUND229,1:SOUND229,2:SOUND225,1:SOUND229,2:SOUND231,5
910 SOUND229,3:SOUND229,2:SOUND225,3:SOUND225,1:SOUND225,2:SOUND221,2:SOUND225,2:SOUND229,3
920 SOUND221,2:SOUND221,3:SOUND225,2:SOUND229,3:SOUND229,1:SOUND229,2:SOUND225,1:SOUND229,3
930 SOUND231,3:SOUND233,3:SOUND215,5:SOUND215,4:SOUND233,3:SOUND231,2:SOUND229,3:SOUND225,2:SOUND221,5:RETURN
