
10 REM ***
20 REM * a year at the races
30 REM * by Glen Tapanila
40 REM ***
50 REM * initialize variables
60 DIMH$(40):DIMH(40,15):DIMJ$(10):DIMJ(10,4):DIMR$(4):DIMR(4,4):DIMRN(20,4):DIMJS(5):DIMR1(4):DIMR2(4):DIMSC(5,4):DIMBE(4,3)
70 DATAAAA,BBBB,CCCC,DDDD,ONYX,LILY,OPAL,PARK,PASA,SLOT,SLOU,CORP,CLAR,AUBE,AURA,BRAN,BULL,INKY,LEAR,MEAN,XERO,YUGA,DUNK,TRUM,DOLL
80 DATATRAN,WEBB,RAJA,DECI,STIR,NIMM,MILL,YULE,NUTT,TELE,ORLY,CHAT,TIFF,BEAN,MIST
90 DATAYAMADA,SMYTHE,RODRIGES,TAPANILA,MERCKLE,JACKSON,CHAN,SKIPSON,RITHIE,KLEGG
100 DATA300,1400,500,400,1900,750,500,3000,900,900,5000,1500
110 DATA"$800 CLAIMING","1600 CLAIMING","ALLOWANCE","HANDICAP STAKES"
120 FORI=1TO40:READH$(I):NEXT
130 FORI=1TO10:READJ$(I):J(I,4)=300:NEXT
140 FORI=1TO4:READR(I,1),R(I,2),R(I,3):NEXT
150 FORI=1TO4:READR$(I):NEXT
160 FORI=1TO40:H(I,9)=-200:NEXT
170 CLS:PRINT@32,"WELCOME TO A YEAR AT THE RACES":PRINT@64,"YOU HAVE 4 HORSES AND $10,000":PRINT@96,"FIRST YOU MUST NAME YOUR HORSES"
180 FORI=1TO4
190 PRINT"HORSE # "I" IS CALLED ":INPUTH$(I)
200 IFLEN(H$(I))=4 THEN GOTO220
210 PRINT"USE 4 LETTER NAME ONLY":GOTO190
220 NEXT
230 M=10000
240 REM * start the races
260 REM * print main menu
270 CLS:K=1
280 PRINT@33,"A YEAR AT THE RACES":PRINT@99,"ENTER THE NUMBER OF":PRINT@131,"THE PROCESS YOU WANT":PRINT@161,"1. SET UP THE DAYS RACES":PRINT@193,"2. HORSE HISTORY":PRINT@225,"3. START THE DAYS RACES":PRINT@257,"4. JOCKEY STANDINGS"
290 PRINT@289,"5. HOW MUCH MONEY DO I HAVE"
300 PRINT@321,"6. PRINTOUT OF HORSES"
310 PRINT @385,"ENTER # OF ";
320 INPUT"PROCESS";P
330 IFP=1ANDSO=1 THEN CLS:PRINT@481,"RACES ARE ALREADY SET UP":GOTO280
340 IFP=3ANDSO=0 THENCLS:PRINT@480,"MUST SET UP RACES BEFORE RUNNING":GOTO280
350 ON P GOTO390,1070,1180,2650,2730,2850
360 CLS:PRINT@481,"INVALID CHOICE, TRY AGAIN":GOTO280
370 IFBE(A,1)=0 THEN BP=0:GOTO2270
380 REM * setup the days races
390 M=M-250
400 FORI=1TO20:RN(I,1)=0:RN(I,2)=0:NEXT
410 WE=RND(5):IFWE=1 THEN W$="RAIN" ELSE W$="FAIR"
420 DY=DY+1:HC=0
425 IFDY>10 THEN GOTO 2770
430 FORA=1TO4
440 L=RND(2):IFL=1 THEN L$="6 FURLONGS" ELSE L$="1 1/16 MILES"
450 R(A,4)=L
460 CLS:PRINT@8,"BRINGMEE DOWNS":PRINT@40,"DAY "DY" RACE "A
470 PRINT@69,R$(A)
480 PRINT@101,"LENGTH IS "L$
490 PRINT@133,"WEATHER FORECAST IS "W$
500 PRINT@165,"WINNER GETS $";:PRINTUSING"####";R(A,2)
510 PRINT@197,"PLACE  GETS $";:PRINTUSING"####";R(A,3)
520 PRINT@259,"COST OF ENTRY IS $"R(A,1)
530 PRINT@323,"'Y'= ENTER A HORSE":PRINT@355,"'N'= CONTINUE"
540 INPUT"===>";E$
550 IFE$="Y" THEN GOTO570
560 IFE$="N" THEN GOTO580 ELSE GOTO460
570 GOSUB620:GOTO590
580 JK=0:Y2=1:GOSUB910:GOTO590
590 NEXTA
600 GOTO270
610 REM * player enters a race
620 CLS
630 PRINT@7,"DAY "DY" RACE "A
640 PRINT@67,"TYPE IN NUMBER OF":PRINT@99,"HORSE YOU WANT ENTERED":PRINT@133,"OR":PRINT@163,"'H'= HORSE HISTORY"
650 INPUT"===>";HS$
660 IFHS$="H" THEN K=2:GOSUB1070:CLS:GOTO630
670 HS=VAL(HS$)
680 IFHS<1ORHS>4 THEN GOTO690 ELSE GOTO700
690 CLS:PRINT@417,"INVALID ENTRY":PRINT@449,"ENTER 1, 2, 3, 4 OR H":GOTO630
700 IF(HS=RN(1,1))OR(HS=RN(6,1))OR(HS=RN(11,1))OR(HS=RN(16,1)) THEN GOTO710ELSEGOTO720
710 CLS:PRINT@352,"I AM SORRY, BUT YOU HAVE":PRINT@384,"ALREADY ENTERED THAT HORSE":PRINT@416,"IN A RACE.  PASS OR":PRINT@448,"ENTER ANOTHER HORSE":GOTO630
720 HC=HC+1:RN(HC,1)=HS:CLS
730 PRINT@128,"ENTER THE NUMBER OF THE":PRINT@160,"JOCKEY YOU WANT TO RIDE":PRINT@192,H$(HS)
740 PRINT@227,"OR":PRINT@256,"TYPE 'J' TO SEE JOCKEYS"
750 INPUT"===>";JC$
760 IFJC$="J" THEN K=2:GOSUB2650:CLS:GOTO730
770 JC=VAL(JC$)
780 IF(JC<1ORJC>10) THEN GOTO790 ELSE GOTO800
790 CLS:PRINT@448,"ENTER A NUMBER BETWEEN 1 & 10":GOTO730
800 CLS:PRINT@64,"YOU HAVE CHOSEN "J$(JC)
810 PRINT@192,"HIS FEE IS $"J(JC,4):PRINT@256,"ENTER AN 'N' IF YOU":PRINT@288,"WANT ANOTHER JOCKEY"
820 INPUT"===>";JC$
830 IFJC$="N" THEN GOTO840 ELSE GOTO850
840 CLS:GOTO730
850 RN(HC,2)=JC:JS(1)=JC:M=M-J(JC,4)-R(A,1)
860 IFM<0 THEN GOTO870 ELSE GOTO880
870 FORXX=1TO1000:CLS:PRINT@96,"LOSE THIS ONE AND YOU'RE":PRINT@128,"IN HOCK BIG FELLA":NEXTXX
880 JK=1:Y2=2:GOSUB910
890 RETURN
900 REM * fill out the race card
910 Y1=1
920 JK=JK+1:IFJK>5 THEN GOTO1050
930 HC=HC+1
940 HN=RND(40):IFHN<5 THEN GOTO940
950 FORY=1TO(HC-1):IFHN=RN(Y,1) THEN GOTO940
960 NEXTY
970 RN(HC,1)=HN
980 JP=RND(10)
990 FORZ=1 TO(Y1)
1000 IFJP=JS(Z) THEN GOTO980
1010 NEXTZ
1020 JS(Y2)=JP:RN(HC,2)=JP
1030 Y1=Y1+1:Y2=Y2+1
1040 GOTO920
1050 SO=1:RETURN
1060 REM * horse history
1070 CLS:PRINT@33,"ENTER THE NUMBER OF THE HORSE":PRINT@65,"YOU WANT INFORMATION ABOUT":PRINT@97,"===>":INPUTHN
1080 IFHN<1ORHN>40 THEN GOTO 1070
1090 CLS:PRINT@72,H$(HN)"  NUMBER "HN:PRINT@164,"# 1STS ";:PRINTUSING"###";H(HN,1)
1100 PRINT@196,"# 2NDS ";:PRINTUSING"###";H(HN,2):PRINT@228,"# RACES";:PRINTUSING"###";H(HN,7):PRINT@290,"SPEED AVERAGE ";:PRINTUSING"###.#";H(HN,9)
1110 PRINT@322,"LAST RACE     ";:PRINTUSING"###";H(HN,12):PRINT@386,"ENTER ANOTHER NUMBER  OR"
1120 PRINT@418,"TYPE 'R' TO RETURN"
1130 INPUTZ$
1140 IFZ$="R" THEN GOTO1150 ELSE HN=VAL(Z$):GOTO1080
1150 IFK=2 THEN GOTO1170
1160 GOTO270
1170 K=1:RETURN
1180 REM * run the days races
1190 WF=RND(4):IFWF=1 THEN GOTO1200 ELSE GOTO 1210
1200 IFWE>1 THEN WE=1 ELSE WE=2:GOTO 1220
1210 IFWE>1 THEN WE=2
1220 FORX=1TO4:FORY=1TO3:BE(X,Y)=0:NEXTY:NEXTX
1230 FORX=1TO20:HR=RN(X,1)
1240 IFX<6 THEN D=R(1,4)+9:GOTO1270
1250 IFX<11 THEN D=R(2,4)+9:GOTO1270
1260 IFX<16 THEN D=R(3,4)+9 ELSE D=R(4,4)+9
1270 IFX<6 THEN C=3:GOTO1300;:PRINTUSING"#######";RN((CT+V),1)
1280 IFX<11 THEN C=4:GOTO1300
1290 IFX<16 THEN C=5 ELSE C=6
1300 IFH(HR,2)>1 THEN S=2 ELSE S=H(HR,2)
1310 JN=J((RN(X,2)),2)
1320 C1=0:C2=0
1330 IF(H(HR,14)+2)>C THEN C1=1:C2=0
1340 IF(H(HR,14)+2)<C THEN C1=0:C2=1
1350 IFH(HR,1)>1 THEN F=2 ELSE F=H(HR,1)
1360 IFWE=1 AND H(HR,13)=1 THEN MU=1 ELSE MU=0
1370 T=0:IF DY/3<H(HR,7) THEN T=1
1380 IFDY/2<H(HR,7) THEN T=2
1390 RM=0:FORY=1TO4:B1=RND(84):B2=RND(2):IFB2=2 THEN RM=RM-B1 ELSE RM=RM+B1:NEXTY
1400 RM=RM/100
1410 RN(X,3)=RM+.53*H(HR,8)-.08*H(HR,C)-.44*H(HR,D)+.24*JN-.01*H(HR,9)+.1*C1+.12*C2+.3*F+.04*S-.25*MU+.12*T+10
1420 NEXTX
1430 FORA=1TO4:FORX=1TO2:BE(A,B)=0:NEXTX:NEXTA
1440 CT=-5:SO=0
1450 FORA=1TO4:CT=CT+5
1460 CLS:U=96:U1=114
1470 PRINT@7,"BRINGMEE DOWNS":PRINT@40,"DAY"DY"RACE"A:PRINT@96,"HORSE    NUMBER   JOCKEY"
1480 FORV=1TO5:U=U+32:PRINT@U,H$(RN((CT+V),1));:PRINTUSING"#########";RN((CT+V),1)
1490 IFR(A,4)=1 THEN L$="6 FURLONGS" ELSE L$="1 1/16 MILES"
1500 U1=U1+32:PRINT@U1,J$((RN((CT+V),2))):NEXT
1510 PRINT@289,L$:PRINT@303,R$(A):PRINT@322,R(A,2)"TO WIN"R(A,3)"TO PLACE"
1520 PRINT@352,"TRACK IS ":IFWE=1 THEN GOTO1540 ELSE GOTO1530
1530 PRINT@361,"FAST":GOTO1550
1540 PRINT@361,"MUDDY":GOTO1550
1550 PRINT@384,"'R'= RUN RACE       'B'= BET":PRINT@416,"'H'= HORSE HISTORY  'J'=JOCKEYS"
1560 INPUT"===>";B$
1570 IFB$="B" THEN GOTO1610
1580 IFB$="H" THEN GOTO1780
1590 IFB$="J" THEN K=2:GOSUB2650:GOTO1460
1600 IFB$="R" THEN GOTO1790 ELSE GOTO1460
1610 CLS
1620 PRINT@3,"ENTER NUMBER OF HORSE":PRINT@35,"YOU WANT TO BET ON":U=68:FORV=1TO5
1630 U=U+32:PRINT@U,H$(RN((CT+V),1))" # IS";:PRINTUSING"###";RN((CT+V),1):NEXTV
1640 INPUT"===>";HB
1650 FORV=(CT+1) TO (CT+5):IFHB=RN(V,1) THEN GOTO1680
1660 NEXTV
1670 CLS:PRINT@450,"TRY AGAIN":GOTO1620
1680 CLS
1690 PRINT@3,"YOU CAN BET FROM $5 TO $500":INPUT"HOW MUCH?";AB
1700 IFAB<5ORAB>500 THEN GOTO1710 ELSE GOTO1720
1710 CLS:PRINT@450,"ENTER AMOUNT AGAIN":GOTO1690
1720 CLS
1730 PRINT@1,"ENTER 1 FOR WIN OR 2 FOR PLACE"
1740 INPUT"===>";BT
1750 IFBT<1ORBT>2 THEN GOTO 1760 ELSE GOTO1770
1760 CLS:PRINT@450,"'1' OR '2' ONLY":GOTO1730
1770 BE(A,1)=AB:BE(A,2)=BT:BE(A,3)=HB:GOTO1460
1780 K=2:GOSUB1070:GOTO1460
1790 HN=CT+1
1800 SC(1,1)=RN(HN,3):SC(1,2)=RN(HN,1):SC(1,4)=RN(HN,2)
1810 Y1=1
1820 FORX= (HN+1) TO (HN+4)
1830 Y=0:Y1=Y1+1
1840 FORZ= Y1 TO2 STEP-1
1850 IFY=1 THEN GOTO1900
1860 IFSC((Z-1),1)>RN(X,3)GOTO1880
1870 SC(Z,1)=RN(X,3):SC(Z,2)=RN(X,1):SC(Z,4)=RN(X,2):Y=1:GOTO1900
1880 SC(Z,1)=SC((Z-1),1):SC(Z,2)=SC((Z-1),2):SC(Z,4)=SC((Z-1),4)
1890 IFZ=2 THEN SC((Z-1),1)=RN(X,3):SC((Z-1),2)=RN(X,1):SC((Z-1),4)=RN(X,2)
1900 NEXTZ
1910 NEXTX
1920 IFA=1 THEN C=3:GOTO1950
1930 IFA=2 THEN C=4:GOTO1950
1940 IFA=3 THEN C=5 ELSE C=6
1950 D=R(A,4)+9
1960 SC(1,3)=102-RND(40):SC(5,3)=SC(1,3)-RND(40):SD=SC(1,3)-SC(5,3)
1970 SC(4,3)=SC(5,3)+.25*SD:SC(3,3)=SC(5,3)+.5*SD:SC(2,3)=SC(5,3)+.75*SD
1980 WN=SC(1,2):PL=SC(2,2):SH=SC(3,2):FO=SC(4,2):FI=SC(5,2)
1990 H(WN,1)=H(WN,1)+1:H(PL,2)=H(PL,2)+1
2000 H(WN,C)=H(WN,C)+1:IFH(WN,C)>1 THEN H(WN,C)=1
2010 H(PL,C)=H(PL,C)+1:IFH(PL,C)>1 THEN H(PL,C)=1
2020 H(WN,D)=H(WN,D)+1:IFH(WN,D)>1 THEN H(WN,D)=1
2030 H(PL,D)=H(PL,D)+1:IFH(PL,D)>1 THEN H(PL,D)=1
2040 H(WN,7)=H(WN,7)+1:H(PL,7)=H(PL,7)+1:H(SH,7)=H(SH,7)+1:H(FO,7)=H(FO,7)+1:H(FI,7)=H(FI,7)+1
2050 H(WN,8)=((H(WN,7)-1)*H(WN,8)+1)/H(WN,7)
2060 H(PL,8)=((H(PL,7)-1)*H(PL,8)+2)/H(PL,7)
2070 H(SH,8)=((H(SH,7)-1)*H(SH,8)+3)/H(SH,7)
2080 H(FO,8)=((H(FO,7)-1)*H(FO,8)+4)/H(FO,7)
2090 H(FI,8)=((H(FI,7)-1)*H(FI,8)+5)/H(FI,7)
2100 H(WN,9)=(((H(WN,7)-1)*H(WN,9))+SC(1,3))/H(WN,7)
2110 H(PL,9)=(((H(PL,7)-1)*H(PL,9))+SC(2,3))/H(PL,7)
2120 H(SH,9)=(((H(SH,7)-1)*H(SH,9))+SC(3,3))/H(SH,7)
2130 H(FO,9)=(((H(FO,7)-1)*H(FO,9))+SC(4,3))/H(FO,7)
2140 H(FI,9)=(((H(FI,7)-1)*H(FI,9))+SC(5,3))/H(FI,7)
2150 H(WN,12)=DY:H(PL,12)=DY:H(SH,12)=DY:H(FO,12)=DY:H(FI,12)=DY
2160 IFWE=1 THEN H(WN,13)=1
2170 H(WN,14)=A:H(PL,14)=A:H(SH,14)=A:H(FO,14)=A:H(FI,14)=A
2180 H(WN,15)=H(WN,15)+R(A,2)
2190 H(PL,15)=H(PL,15)+R(A,3)
2200 IFWN<5 THEN M=M+R(A,2)
2210 IFPL<5 THEN M=M+R(A,3)
2220 IFBE(A,2)=0 THEN GOTO2230 ELSE GOTO2240
2230 BP=0:GOTO2270
2240 IFBE(A,2)=1 AND WN=BE(A,3) THEN M=M+BE(A,1):BP=1:GOTO2270
2250 IF(BE(A,2)=2)AND((WN=BE(A,3))OR(PL=BE(A,3))) THEN M=M+BE(A,1)*.5:BP=2:GOTO2270
2260 M=M-BE(A,1):BP=3
2270 WN=SC(1,4):PL=SC(2,4):SH=SC(3,4):FO=SC(4,4):FI=SC(5,4)
2280 J(WN,3)=J(WN,3)+1
2290 J(WN,1)=J(WN,1)+1:J(PL,1)=J(PL,1)+1:J(SH,1)=J(SH,1)+1:J(FO,1)=J(FO,1)+1:J(FI,1)=J(FI,1)+1
2300 J(WN,2)=(((J(WN,1)-1)*J(WN,2))+1)/J(WN,1)
2310 J(PL,2)=(((J(PL,1)-1)*J(PL,2))+2)/J(PL,1)
2320 J(SH,2)=(((J(SH,1)-1)*J(SH,2))+3)/J(SH,1)
2330 J(FO,2)=(((J(FO,1)-1)*J(FO,2))+4)/J(FO,1)
2340 J(FI,2)=(((J(FI,1)-1)*J(FI,2))+5)/J(FI,1)
2350 CLS
2360 PRINT@5,"DAY"DY"RACE"A
2370 PRINT@70,"R E S U L T S"
2380 PRINT@160,"HORSE      #  SPEED  JOCKEY"
2390 U=161:U1=180:FORX=1TO5
2400 U=U+32:U1=U1+32:PRINT@U,H$(SC(X,2));:PRINTUSING"#######";SC(X,2);:PRINTUSING"#####.#";SC(X,3);:PRINT@U1,J$(SC(X,4)):NEXTX
2410 IFBP=0 THEN GOTO2440
2420 IFBP=1 THEN GOTO2450
2430 IFBP=2 THEN GOTO2460 ELSE GOTO2470
2440 PRINT@354,"NO BET":GOTO2480
2450 PRINT@354,"YOUR BET PAYS  ";:PRINTUSING"$####";BE(A,1)*2:GOTO2480
2460 PRINT@354,"YOUR BET PAYS  ";:PRINTUSING"$####";BE(A,1)*1.5:GOTO2480
2470 PRINT@354,"YOUR BET LOSES  ";:PRINTUSING"$####";BE(A,1)
2480 PRINT"PRESS ENTER TO CONTINUE"
2490 PRINT"'P' = PRINTOUT OF THIS RACE"
2500 INPUT XX$
2510 IFXX$="P" THEN GOSUB2890
2520 NEXTA
2530 FORA=1TO10
2540 IFJ(A,2)<1.5 THEN J(A,4)=500:GOTO2620
2550 IFJ(A,2)<2 THEN J(A,4)=450:GOTO2620
2560 IFJ(A,2)<2.5 THEN J(A,4)=400:GOTO2620
2570 IFJ(A,2)<3 THEN J(A,4)=350:GOTO2620
2580 IFJ(A,2)<3.5 THEN J(A,4)=300:GOTO2620
2590 IFJ(A,2)<4 THEN J(A,4)=200:GOTO2620
2600 IFJ(A,2)<4.5 THEN J(A,4)=150:GOTO2620
2610 J(A,4)=100
2620 NEXTA
2630 GOTO270
2640 REM * jockey standings
2650 CLS
2660 PRINT@1,"JOCKEYS AND COST PER RACE":PRINT@65,"#  PRICE  WINS   NAME":U=68:FORX=1TO10:U=U+32
2670 PRINTUSING"##";X:PRINT@U,J(X,4);:PRINTUSING"#####";J(X,3):PRINT@(U+14),J$(X):NEXT
2680 INPUT"===>";XX
2690 IFK=2 THEN GOTO2710
2700 GOTO270
2710 K=1:RETURN
2720 FRM ** display of money
2730 CLS:PRINT@130,"YOU HAVE $"M
2740 INPUT"===>";XX
2750 GOTO270
2760 REM * close the year
2770 TW=H(1,1)+H(2,1)+H(3,1)+H(4,1):RR=(DY-1)*4
2780 CLS
2790 PRINT@2,"THE RACING YEAR AT"
2800 PRINT@34,"BRINGMEE DOWNS IS OVER"
2810 PRINT@66,"YOU HAVE $"M
2820 PRINT@98,"YOUR HORSES WON"TW"RACES"
2830 PRINT@130,"OUT OF"RR"RACES"
2840 END
2850 FORI=1TO40
2860 PRINT#-2,H$(I)I" WINS= "H(I,1)" 2NDS= "H(I,2)" RACES= "H(I,7)" WINNINGS= "H(I,15)" SPEED= "H(I,9)
2870 NEXT
2880 GOTO270
2890 PRINT#-2," ":PRINT#-2," DAY "DY" RACE "A
2900 IFWE=1 THEN GOTO2910 ELSE GOTO2920
2910 PRINT#-2,"TRACK IS MUDDY":GOTO2930
2920 PRINT#-2,"TRACK IS FAST"
2930 PRINT#-2,L$
2940 PRINT#-2,"  R E S U L T S"
2950 PRINT#-2,"HORSE   #  SPEED   JOCKEY"
2960 FORX=1TO5
2970 PRINT#-2,H$(SC(X,2))"   "SC(X,2)"   "SC(X,3)"  "J$(SC(X,4)):NEXT
2980 PRINT#-2," "
2990 RETURN
