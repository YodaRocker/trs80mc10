10 REM PANZR.ECB
20 REM ...PANZER...BY RALPH HOPKINS 5-01-79  (3-22-81)
100 T=INT(RND(0)*9)+1
110 K=0:K3=0:M=1:P=0
120 DIM A(80),C(80),D(80),F(80),H(80),I(50),J(80),M(80),P(80)
130 DIM R(80),S(80),T(80),V(80),W(80),X(80),Y(80)
140 DIM I$(35),N$(15),D$(31)
150 I$=" TYPE ANY LETTER TO SEE MORE ---=>"
160 F$=" GERMAN ":E$=" AMERICAN":T$=" TANKS"
170 M$=" METERS.":N$="MAX MOVE RATE: "
180 K$=CHR$(4)+CHR$(4)
190 D$=" +++ DAMAGED --- CAN'T MOVE +++"
200 CLS
205 FOR X=1 TO 4:PRINT ," :": NEXT X
210 PRINT ," : .-=-.": PRINT TAB(11);"//'''''''''''''''',."
220 PRINT TAB(10);"// ";K$;
225 PRINT "  P A N Z E R  @>==============####"
230 PRINT " ,./";
240 FOR X=1 TO 34: PRINT CHR$(24);: NEXT X
250 PRINT ".,": PRINT "/ (";
260 FOR X=1 TO 36: PRINT CHR$(24);: NEXT X
270 PRINT ") "
280 PRINT TAB(3);";( * ) ( * ) ( * ) ( * ) ( * ) ( * );"
290 PRINT TAB(4);"'''''''''''''''''''''''''''''"
300 DATA 3,75,13,140,800,20,30
310 DATA 16,88,20,80,1200,35,150
320 DATA 7,75,11,80,800,30,50
330 DATA 12,75,16,100,1200,40,125
340 DATA 8,75,14,80,900,40,75
350 DATA 12,88,15,60,1200,50,100
360 DATA 6,88,20,80,2000,25,125
370 DATA 18,122,22,50,1200,25,200
380 DATA 7,50,8,90,600,30,25
400 FOR X=1 TO 9
410 READ A(X),C(X),F(X),M(X),R(X),S(X),P(X): W(X)=4
420 NEXT X
430 N=0:K1=0:K2=0
440 L=RND(10): PRINT
500 PRINT " DO YOU NEED INSTRUCTIONS ?": PRINT
510 INPUT" (Y OR N) ---=>";Y$
520 PRINT : IF Y$="N" THEN 1000
600 CLS:PRINT ,"GREETINGS LIEUTENANT!"
610 PRINT "YOU ARE A PANZER COMMANDER IN  THE BATTLE OF THE"
615 PRINT "BULGE (DEC. 1944). YOUR ORDERS ARE TO ADVANCE AND"
620 PRINT "DESTROY AS MANY BRITISH AND    ";E$;" UNITS AS POSSIBLE."
630 PRINT "GERMANY IS DEPENDING ON YOU."
640 PRINT "YOU ARE COMMANDING A":PRINT F$;:GOSUB7500
660 PRINT "TANK WHICH HAS ";A(T);" INCHES OF ARMOUR AND"
670 PRINT "CARRIES A ";C(T);" MM. CANNON AS MAIN ARMAMENT."
680 PRINT ,"IT'S RANGE IS GOOD TO ";R(T);M$
690 PRINT "YOU HAVE ";S(T);" SHELLS TO SHOOT WITH."
700 PRINT I$;:INPUTY$
710 PRINT "&K    +POINTS+ ARE GIVEN FOR EACH +KILL+ AND A"
720 PRINT " +LOG+ IS KEPT OF TOTAL KILLS YOU MAKE...."
730 PRINT "     YOU WILL BE ABLE TO MOVE AND +BUY TIME+"
740 PRINT "IF THE";E$;" SHOTS ARE GETTING TOO CLOSE."
750 PRINT "   MOVE RATE OF YOUR";F$;: GOSUB 7500
760 PRINT : PRINT "TANK IS UP TP ";M(T);M$;""
770 PRINT "+#+ NOTE +#+"
780 PRINT "FIREPOWER DOUBLES AT LESS THAN 1/2 RANGE."
790 PRINT "... YOU +MAY+ RUN OVER TRUCKS AND JEEPS."
800 PRINT I$;: INPUTY$
810 CLS:GOSUB900:GOTO1000
900 PRINT " *";E$;"S *"
910 PRINT "    MOVEMENT CAN BE IN         4  3  2"
920 PRINT " 8 POSSIBLE DIRECTIONS AS        I /"
930 PRINT " SHOWN.               (LEFT) 5 -- 0 -- 1  (RIGHT)"
940 PRINT "    YOU MOVE * OR * SHOOT;      / I "
950 PRINT " YOU CAN'T DO BOTH.            6  7  8"
960 PRINT TAB(32);"*YOU*"
970 PRINT "THE";E$;"S WILL BE IN THE 2,3, OR 4 DIRECTION"
990 RETURN
1000 PRINT "TYPE ANY LETTER TO PRINT A SUMMARY OF"
1010 PRINT "ALL THE AVAILABLE COMMANDS FOR THIS GAME."
1100 PRINT
1400 REM ...TANK TYPES DATA
1410 DATA 10,19,16,29,11,14,15,24,20,11,13,31,13,39,21
1420 DATA 19,13,11,25,13,16,18,21,24,23,27,24,28,29,30
1430 DATA 19,27,25,33,12,22,25,22,17,17,14,13,15,22,23
1440 DATA 12,22,17,23,26,18,10,26,15,18,28,29,16,30,11
1450 DATA 36,35,34,31,38,13,38,37,32,17
1460 FOR X=10 TO 79: READ T(X): NEXT X
1470 IF RND(0)>.7 THENT(54)=40: REM ...PETROL TRUCK
1480 IF RND(0)<.1 THENT(40)=50: REM ...STAFF CAR
1490 IF C(T)<88 THENT(17)=32:T(49)=13:T(52)=14
1500 IF C(T)<=50 THENT(44)=27:T(48)=39:T(33)=33
1510 Z=RND(35)+15:Y=RND(3)+3+(RND(3)/10)
1520 FOR X=10 TO Z+10
1530 Z1=RND(69)+10
1540 Z2=RND(69)+10
1550 IF T(Z2)<50 THENT(Z1)=T(Z2)
1560 NEXT X
1570 FOR X=Z TO Y: T(X)=T(Z):NEXT X
1580 Z=RND(29)+10: E=0
1590 PRINT ,"++++  H I E L   H I T L E R  ++++"
1600 FOR X=10 TO 79
1610 S(X)=RND(30)+(5): IF T(X)<>Z THEN 1650
1620 E=E+1: PRINT " +RECON CARS+ REPORT ...";E;" ";
1630 GOSUB 7000: IF E=1 THENPRINT " UNIT."
1640 IF E>1 THENPRINT " UNITS."
1650 Y=T(X):Q=INT(Y/10)
1660 ON Q GOTO 1670,1680,1690,1946,1947
1670 ON Y-9 GOTO 1700,1710,1720,1730,1740,1750,1760,1770,1780,1790
1680 ON Y-19 GOTO 1800,1810,1820,1830,1840,1850,1860,1870,1880,1890
1690 ON Y-29 GOTO 1900,1910,1920,1930,1940,1941,1942,1943,1944,1945
1700 A=2.01:B=2.4:C=100.005:D=2: GOTO 1950
1710 A=3.01:B=2.4:C=100.005:D=2: GOTO 1950
1720 A=3.075:B=9.4:C=90.02:D=7: GOTO 1950
1730 A=9.075:B=11.1:C=80.035:D=8: GOTO 1950
1740 A=7.075:B=11.1:C=110.03:D=8: GOTO 1950
1750 A=12.09:B=12.1:C=90.09:D=12: GOTO 1950
1760 A=10.076:B=14.1:C=90.06:D=10: GOTO 1950
1770 A=7.105:B=40.2:C=80.125:D=30: GOTO 1950
1780 A=3.01:B=2.1:C=140.007:D=2: GOTO 1950
1790 A=4.09:B=14.1:C=120.05:D=10: GOTO 1950
1800 A=6.09:B=15.1:C=90.06:D=12: GOTO 1950
1810 A=3.02:B=2.3:C=150.01:D=3: GOTO 1950
1820 A=8.075:B=9.1:C=90.035:D=8: GOTO 1950
1830 A=6.076:B=16.1:C=90.065:D=10: GOTO 1950
1840 A=7.105:B=35.2:C=105.125:D=32: GOTO 1950
1850 A=7.075:B=11.1:C=60.03:D=8: GOTO 1950
1860 A=9.075:B=11.1:C=50.035:D=8: GOTO 1950
1870 A=5.037:B=5.1:C=110.015:D=5: GOTO 1950
1880 A=3.053:B=3.1:C=150.012:D=5: GOTO 1950
1890 A=1.005:B=1.4:C=140.003:D=.5: GOTO 1950
1900 A=9.105:B=14.2:C=80.08:D=10: GOTO 1950
1910 A=6.076:B=14.1:C=90.04:D=10: GOTO 1950
1920 A=1.005:B=1.5:C=160.003:D=.8: GOTO 1950
1930 A=2.010:B=2.5:C=140.005:D=2: GOTO 1950
1940 A=4.075:B=4.2:C=100.025:D=12: GOTO 1950
1941 A=2.057:B=9.1:C=1.008:D=5: GOTO 1950
1942 A=2.076:B=12.1:C=1.025:D=10: GOTO 1950
1943 A=1.090:B=15.1:C=1.05:D=20: GOTO 1950
1944 A=2.105:B=40.2:C=1.07:D=32: GOTO 1950
1945 A=4.037:B=5.1:C=100.01:D=5: GOTO 1950
1946 A=1.0022:B=1.5:C=80.04:D=.75: GOTO 1950
1947 A=1.0022:B=1.5:C=150.5:D=.75
1950 A(X)=INT(A):C(X)=(A-INT(A))*1E3
1960 F(X)=INT(B):J(X)=(B-INT(B))*10
1970 M(X)=INT(C):P(X)=(C-INT(C))*1E3
1980 R(X)=D*100
1990 NEXT X
2000 PRINT " YOUR PRESENT POSITION IS ";
2010 GOSUB 7700: Q=0
2200 FOR X=10 TO 79
2205 IF C(T)=37 THENP(X)=INT(P(X)*2)
2210 IF T=7 THENY=1300:GOTO2220
2211 Y=R(T)
2220 D(X)=INT(RND(0)*(Y/100))*100-INT(Y/10)
2230 D(X)=D(X)+(Y-100)
2240 H(X)=RND(20)*10-100
2250 V(X)=INT(D(X)/2)+INT(RND(0)*(D(X)/4))
2255 IF V(X)>R(X) THENV(X)=R(X)
2260 Y(X)=D(X)-(RND(35)*10): W(X)=6
2270 IF Y(X)>Y THENY(X)=INT(Y-(Y/10))
2280 X(X)=(RND(14)*10)-70
2290 NEXT X
3000 IF RND(0)>.6 THEN 3100
3010 IF N>0 THEN 3100
3020 Q=RND(62)+10:Q1=RND(6)+1+(RND(4)/10)
3030 IF RND(0)>.3 THENQ1=INT((RND(3)+.3)+.7+(RND(3)/10))
3040 IF Q<10 THEN 3000
3050 FOR X=Q TO Q+(Q1-1)
3060 IF W(X)>0 THENW(X)=6:N=N+1
3070 NEXT X
3100 IF N=0 THEN 3200
3110 PRINT : PRINT "* * * * * * *";E$;"S ";
3115 IF C=3 THENIF N>1 THENDS=50
3120 PRINT "MOVING";:DS=0
3125 PRINT " * * * * * * *": PRINT
3130 FOR X=Q TO Q+(Q1-1)
3135 IF W(X)<3 THEN 3195
3136 Y=W(X)-INT(W(X))
3140 Z=RND(60)-30
3145 IF J(X)=5 THENIF D(X)<500 AND RND(0)>.4 THENS(X)=0
3150 IF R(X)>=ABS(D(X)+Z) THENW(X)=4+Y
3155 IF R(X)<ABS(D(X)+Z) THENW(X)=6
3160 IF W(T)=1 THENIF RND(0)>.7 THENW(X)=5+Y
3165 IF ABS(D(X))<M(X) AND J(X)<3 THENIF RND(0)>.3 THENW(X)=5+Y
3170 IF C=2 THENIF W(X)=4 AND RND(0)>.6 THENW(X)=6
3175 IF M(X)=.1 THENW(X)=4: IF RND(0)>.6 THENW(X)=3.5
3180 IF RND(0)>.9 THENW(X)=5
3185 IF J(X)>2 THENIF D(X)<M(X) AND RND(0)>.6 THENW(X)=7
3190 IF S(X)<=RND(4)+(RND(4)/10) THENW(X)=7
3195 NEXT X
3200 IF Q=0 THEN 4000: REM ...ENEMY MOVE OR SHOOT
3210 N=0: IF RND(0)>.9 THENGOSUB 6900
3220 FOR X=Q TO Q+(Q1-1)
3230 IF W(X)<2 THEN 3290
3240 N=N+1: IF J(X)<>3 THEN 3250
3245 IF ABS(D(X))>100 AND D(X)<201 AND RND(0)>.3 THENW(X)=6.5
3250 IF W(X)=2 THENGOSUB 3900: GOTO 3290
3255 IF W(X)=4 THENGOSUB 3300: IF RND(0)>.2 THEN 3265
3260 IF J(X)<>3 THEN 3275
3265 IF J(X)=4 AND D(X)<450 AND RND(0)>.4 THENGOSUB 8000
3270 IF RND(0)+(L/50)>.9 AND R(X)>500 THENW(X)=W(X)+.5
3275 IF W(X)>4 THENGOSUB 3800: GOTO 3280
3277 IF J(X)=3 AND RND(0)>.3 THENGOSUB 3800
3280 IF W(T)=0 THEN 9000
3290 NEXT X
3295 GOTO 4000
3300 REM = = =  + ENEMY SHOOT +  = = =
3310 PRINT ,;"+BOOM+"
3320 GOSUB 7000: PRINT "  SHOOTING FROM ";D(X);M$
3330 GOSUB 3400: GOSUB 3600: RETURN
3400 Z1=D(X)-V(X): REM ...CALC. +ENEMY+ HIT
3420 Z3=RND(Z1):Z4=INT(RND(0)*H(X))
3430 V(X)=INT(V(X)+(Z3)):H(X)=INT(H(X)-(Z4))
3440 Z1=INT(D(X)-V(X)):Z2=INT(H(X))
3450 H=F(X)/A(T):S(X)=S(X)-1
3460 IF M(T)=0 THENH=H+H
3470 IF J(X)=1 THENIF ABS(D(X))<=R(X)/2 THENH=H*2
3480 IF J(X)=2 THENIF ABS(D(X))>R(X)/10 THENH=H/1.5
3490 IF J(X)=3 THENIF ABS(D(X))<50 THENH=H+2
3495 IF J(X)=3 THENIF ABS(D(X))<25 THENH=H+2
3500 REM ...TERRAIN ADJ. FOR HIT CALC.
3510 IF L>3 THENIF L<7 THENH=H-(RND(0)*(.1*L))
3520 IF L>=7 THENH=H/2
3530 IF L>8 AND RND(0)>.6 THENH=H-1
3595 RETURN
3600 H=INT(H): REM ...PRINT *ENEMY*  MISS RESULTS
3610 IF F(X)<3 THENH=H-1
3620 IF V(X)>R(X) THENH=0
3630 IF ABS(Z1)>12 THENH=0
3640 IF ABS(Z2)>8 THENH=0
3650 IF H>0 THEN 3700
3660 IF ABS(Z1)<13 THENIF ABS(Z2)<9 THEN 3700
3670 PRINT ,"*  B O O M  *"
3680 PRINT "SHOT ON *YOU* ";: IF H<1 THENPRINT "INEFECTIVE"
3690 GOSUB 6700: RETURN
3700 REM ...PRINT *ENEMY*  #HIT#  RESULTS
3710 PRINT ,"*+#+* B L A M *+#+*"
3720 PRINT TAB(10);"Y O U ' V E   B E E N   H I T !"
3730 E=T:W=W(T)
3740 GOSUB 6600: PRINT " *YOU* ARE...";
3750 IF Z2=2 THENW(T)=2
3760 IF Z2=2 THENIF RND(0)>.85 THENW(T)=3:M(T)=0
3770 IF Z2=0 THENW(T)=0: PRINT " *** YOUR DEAD ***"
3780 IF W(T)=2 THENPRINT " $#! STUNNED !#$"
3785 IF W(T)=3 THENPRINT D$
3790 IF W(T)>3 THENPRINT " /^ JUST DENTED V/"
3795 RETURN
3800 GOSUB 7000: REM = = =  + ENEMY MOVE +  = = =
3805 IF M(X)=0 THENPRINT "...+++STALLED+++": W(X)=INT(W(X)): RETURN
3810 IF M(X)=1 AND INT(W(X))=W(X) THENPRINT " .:. VISIBLE .:.": RETURN
3815 IF W(X)=5 THENPRINT " *** HALTED ***": RETURN
3820 IF W(X)>INT(W(X)) THENPRINT ," HIDDEN...":GOTO3825
3821 PRINT " ...MOVING."
3825 IF INT(W(X))=6 THEND=INT((RND(0)*2)+2.5)
3830 IF RND(0)>.95 THEND=(RND(0)*9)
3835 IF RND(0)>.3 AND T=7 THEND=(RND(0)*5)+1
3840 IF INT(W(X))=7 THEND=7
3845 IF W(X)=7 THENPRINT ,,"+*+ RETREATING +*+"
3850 IF INT(W(X))=7 AND RND(0)>.95 THENM(X)=M(X)-10
3860 IF T=7 THEND1=M(X)+(RND(0)*30):GOTO3865
3861 D1=M(X)-(RND(0)*30)
3865 GOSUB 6000: D(X)=INT(D(X)+(V1)): IF D(X)=0 THEND(X)=5
3870 H(X)=INT(H(X)+(H1/2)):V(X)=INT(V(X)+(V1/2))
3880 Y(X)=INT(Y(X)+(V1*.6)):D(X)=INT(D(X)/10)*10
3885 IF ABS(D(X))<R(T)*2 THEN 3895
3890 GOSUB 7000: PRINT " HAS ESCAPED!!!": W(X)=1
3895 RETURN
3900 REM ...UN-STUN THE ENEMY
3910 Z2=RND(0): IF Z2>.6 THENW(X)=4
3920 IF Z2>.9 THENW(X)=7
3930 IF S(X)=0 THENRETURN
3940 IF M(X)>0 THENRETURN
3950 IF Z2>.15 THENRETURN
3960 PRINT TAB(12);" ";: GOSUB 7000: S(X)=0
3970 PRINT " BEING... ### ABANDONED ###"
3990 RETURN
4000 PRINT
4010 IF W(T)<>2 THEN 4040
4020 PRINT "*YOU* ARE  +*+STUNNED+*+ OUT OF 1 TURN."
4030 W(T)=4: GOTO 3000
4040 IF S(T)<=0 THENGOSUB 4600
4050 IF S(T)<=0 AND Z=0 THEN 9000: REM ...GAME OVER
4060 IF C=2 THENIF N<1 THENPRINT " SEE NOTHING...HEAR NOTHING."
4070 INPUT" NEXT COMMAND ? (0 TO 9) ---=>";C$
4080 IFLEN(C$)=0THENC=2:GOTO3000
4090 C=ASC(C$)-48:DS=0
4100 PRINT:IFC=0THEN5000
4110 IF C=1 THEN 5100: REM ...SCAN
4120 IF C=2 THEN 3000: REM ...WAIT
4130 IF C=3 THEN 5300: REM ...SHOOT
4140 IF C=4 THEN 5400: REM ...WITHDRAW
4150 IF C=5 THEN 5500: REM ...RETREAT
4160 IF C=6 THEN 5600: REM ...STATUS
4170 IF C=7 THEN 5700: REM ...INFO
4180 IF C=8 THEN 5800: REM ...POINTS
4190 IF C=9 THEN 5900: REM ...LOG
4195 IF C=17 THEND=3:D1=M(T): GOTO 5080
4200 PRINT "*=*=*=* C O M M A N D  S U M M A R Y *=*=*=*"
4210 PRINT : PRINT "0 - MOVE 0 TO ";M(T);M$
4220 PRINT "1 - SCAN OF VISIBLE";E$;T$
4230 PRINT "2 - WAIT FOR ALLIES TO MOVE CLOSER."
4240 PRINT "3 - SHOOT AT ONLY *ONE* ENEMY UNIT."
4250 PRINT "4 - WITHDRAW FROM THE AREA. (NOT THE GAME)"
4260 PRINT "5 - ABANDON YOUR CRIPPLED ";: GOSUB 7500
4265 PRINT " TANK"
4270 PRINT "6 - GET A STATUS REPORT."
4280 PRINT "7 - GET INFO ON THE";E$;T$
4290 PRINT "8 - POINT VALUES OF";E$;T$
4300 PRINT "9 - LIST *LOG* OF KILLS SO FAR."
4310 PRINT "A - *#* ADVANCE FULL SPEED FORWARD *#*"
4320 GOTO 4000
4400 D=3:D1=M(T)*2: REM ...ATTEMPT SAFE WITHDRAWL
4410 GOSUB 6000: PRINT "  *** WITHDRAWING ";D1;M$;" ***"
4420 FOR X=Q TO Q+(Q1-1)
4430 IF W(X)<=2 OR W(X)>INT(W(X)) THEN 4460
4440 D(X)=D(X)+D1:V(X)=V(X)+M(T)
4450 IF W(X)<7 THENIF ABS(D(X))<R(X)+M(X) THENZ=Z+1
4460 NEXT X
4470 IF Z<1 THENRETURN
4480 PRINT " *#* ";Z;E$;" UNITS STILL IN RANGE *#*"
4490 RETURN
4500 PRINT " *YOU*  ARE ";D$
4590 GOTO 4000
4600 REM ...EXIT GAME IF NO ENEMY IS VISIBLE
4610 Z=0:D=7:D1=1
4620 PRINT " *YOU* ARE OUT OF SHELLS."
4630 IF Q>9 AND N>0 THENGOSUB 4420
4640 RETURN
5000 IF M(T)=0 THEN 4500: REM ...CMD 0 --- MOVE
5010 INPUT" DIRECTION TO MOVE ? (1-8)---=>";D
5020 D=INT(D): IF D=0 THEN 4000
5030 IF D<0 THENGOSUB 900: GOTO 4000
5040 IF D>8 THENGOSUB 900: GOTO 4000
5050 PRINT : INPUT" HOW MANY METERS TO MOVE ? ---=>";D1
5060 DS=0:D1=INT(D1): IF D1=0 THEN 4000
5070 IF D1<0 OR D1>M(T) THENPRINT N$;M(T): GOTO 5050
5080 GOSUB 6000: IF L>0 THENIF RND(0)>.3 THENL=L-1
5085 IF E=0 THEN 5090
5087 IF N>0 THENIF W(E)=7 THENIF RND(0)>.7 THENGOSUB 6900
5090 GOSUB 6100: IF N>0 THENGOSUB 6800
5095 GOTO 3000
5100 REM ...COMMAND 1 --- SCAN
5110 IF N<1 THEN 5700
5130 FOR X=Q TO Q+(Q1-1)
5140 IF W(X)<2 THEN 5190
5150 GOSUB 7000: PRINT " ";TAB(27);
5160 IF W(X)>INT(W(X)) THENPRINT " ... HIDDEN SOMEWHERE ...";
5165 IF W(X)=INT(W(X)) THENPRINT "DISTANCE: ";D(X);
5170 IF W(X)=2 THENPRINT " *STUNNED*";
5172 IF W(X)=5 THENPRINT " *HALTED*";
5175 IF M(X)=0 THENPRINT " +DAMAGED+": GOTO 5190
5180 IF W(X)=7 THENPRINT " RETREATING": GOTO 5190
5185 PRINT
5190 NEXT X
5195 GOTO 4000
5300 REM ...COMMAND 3 --- SHOOT
5310 IF N<1 THEN 5700
5320 IF S(T)>0 THEN 5340
5321 CLS
5330 PRINT ,"-=<  C  L  I  C  K  >=-": FORZZ=1TO2500:NEXT:GOTO3100
5340 FOR Z=Q TO Q+(Q1-1)
5350 IF W(Z)>1 THENE=Z
5360 NEXT Z
5370 IF N>1 THENGOSUB 6200
5380 IF E=0 THEN 4000
5390 X=E: GOSUB 6300: GOSUB 6400: GOTO 3100
5400 Z=0: REM ...COMMAND 4 --- WITHDRAW
5410 IF M(T)=0 THEN 4500
5420 IF N<=0 THEN 5470
5430 IF Q>=10 THENGOSUB 4400
5440 IF Z>0 THEN 3000
5441 PRINT
5450 IF Z=0 THENPRINT ,"### WITHDRAWL SUCCESSFUL ###"
5460 N=0: PRINT " WITHDRAWING FROM ";: GOSUB 7700
5470 L=(RND(0)*10)+1: PRINT
5480 GOSUB 7600: PRINT ,"+++ YOUR NEW POSITION IS +++"
5490 GOSUB 7700: GOTO 4000
5500 REM ...COMMAND 5 --- ABANDON TANK
5510 IF M(T)=0 THENPRINT "++ DAMAGED ++ ";: GOSUB 7500
5520 IF M(T)=0 THENPRINT " *** ABANDONED *** ": GOTO 9000
5530 PRINT " *YOU* ARE PENNALIZED ";P(T);" *POINTS*"
5540 PRINT " FOR ABANDONNING A MUCH NEEDED"
5550 GOSUB 7500: P=P-P(T): GOTO 9000
5600 REM ...COMMAND 6 --- STATUS
5605 GOSUB 7500: PRINT " TANK'S  *STATUS* IS:";
5610 IF M(T)=0 THENPRINT D$
5615 IF M(T)>0 THENPRINT " + UN-DAMAGED +"
5620 PRINT ": : : PRESENT TERRAIN:  ";: GOSUB 7700: PRINT
5630 PRINT "+++++++++++++++++++ DAY #";M;"  +";
5635 FOR X=1 TO 13: PRINT "++";: NEXT X: PRINT
5640 GOSUB 7500: PRINT TAB(24);"MAX SHOOTING RANGE:";R(T);M$
5650 PRINT N$;M(T);TAB(24);"CANNON: ";C(T);" MM."
5660 PRINT "ARMOUR RATING: ";A(T);TAB(24);
5670 PRINT "FIREPOWER RATING: ";F(T)
5680 PRINT ,"*#* ";S(T);" SHELLS LEFT *#*"
5690 FOR X=1 TO 56: PRINT CHR$(4);: NEXT X
5695 PRINT : GOTO 4000
5700 Z=0: PRINT : REM ...COMMAND 7 --- INFO
5710 IF N<1 THENPRINT "  ### NO ENEMY VISIBLE ###": GOTO 4000
5720 PRINT "**************** DATA ON ALLIED UNITS ********************"
5730 FOR X=Q TO Q+(Q1-1)
5740 IF W(X)<2 THEN 5790
5750 GOSUB 7000: PRINT TAB(26);"MAX SHOOTING RANGE: ";R(X);M$
5760 PRINT "MAX MOVE RATE: ";M(X);TAB(26);"CANNON: ";C(X);" MM."
5765 PRINT "ARMOUR RATING: ";A(X);TAB(26);
5767 Z=Z+1: PRINT "FIREPOWER RATING: ";F(X)
5770 FOR Y=1 TO 58: PRINT "-";: NEXT Y
5775 PRINT : IF N=3 THENIF Z=3 THEN 4000
5776 IF N=6 THENIF Z=6 THEN 4000
5780 IF Z=3 OR Z=6 THENPRINT I$;:INPUTY$
5790 NEXT X
5795 GOTO 4000
5800 REM ...COMMAND 8 --- POINTS
5810 IF N<1 THENPRINT " =#= NO VISBLE";E$;"S =#=": GOTO 5880
5830 PRINT ":^:^:^:^:^:^:^:^: POINT VALUES :^:^:^:^:^:^:^:^:"
5840 FOR X=Q TO Q+(Q1-1)
5845 IF W(X)<2 THEN 5870
5850 PRINT "<=-   ";: GOSUB 7000: PRINT TAB(32);P(X);
5855 PRINT TAB(37);"POINTS  -=>"
5860 FOR Z=1 TO 48: PRINT "";: NEXT Z
5865 PRINT
5870 NEXT X
5880 PRINT ,"YOUR PRESENT SCORE: (+%+ ";P;"  +%+)"
5890 GOTO 4000
5900 GOSUB 7900: REM ...COMMAND 9 --- BATTLE LOG
5910 INPUT"WANT A LIST OF YOUR TYPES OF UNITS? ---=>";Y$
5920 IF Y$="N" THEN 4000
5921 CLS
5930 GOSUB 7500: PRINT " LOG OF";E$;" CLASS OF UNITS ELIMINATED."
5940 FOR I=10 TO 50
5950 IF K>30 THENIF I=21 OR I=32 THENPRINTI$;:INPUTY$
5960 IF I(I)<1 THEN 5980
5970 PRINT ,I(I);" ... ";: GOSUB 7050: PRINT
5980 NEXT I
5990 PRINT K3;T$;" AND ";K-K3;E$;" UNIT(S)."
5995 GOTO 4070
6000 REM ...GOSUB FOR MOVE DIRECTION.
6005 IF D=0 THENH1=0:V1=0: RETURN
6010 IF D=1 THENH1=D1:V1=INT(D1*.2): RETURN
6020 IF D=2 THENH1=INT(D1*(.8)):V1=INT(D1*(.8))*(-1): RETURN
6030 IF D=3 THENH1=0:V1=D1*(-1): RETURN
6040 IF D=4 THENH1=INT(D1*(.8))*(-1):V1=INT(D1*(.8))*(-1): RETURN
6050 IF D=5 THENH1=D1*(-1):V1=INT(D1*.2): RETURN
6060 IF D=6 THENH1=INT(D1*(.8))*(-1):V1=INT(D1*(.8)): RETURN
6070 IF D=7 THENH1=0:V1=D1: RETURN
6080 IF D=8 THENH1=INT(D1*(.8)):V1=INT(D1*(.8))
6090 RETURN
6100 REM ...GOSUB TO CHANGE "DISTANCES".
6120 IF Q1=0 THENRETURN
6130 IF N<=0 THENRETURN
6140 FOR X=Q TO Q+(Q1-1)
6145 IF W(X)<2 THEN 6180
6150 D(X)=INT(D(X)+(V1))
6160 H(X)=INT(H(X)+(H1/2)):V(X)=INT(V(X)+(V1/2))
6170 Y(X)=INT(Y(X)+(V1*.3)):X(X)=INT(X(X)+(H1*.2))
6175 IF W(X)=2 THENH(X)=INT(H(X)+(H1/2)):V(X)=INT(V(X)+(V1/2))
6180 NEXT X
6190 RETURN
6200 IF N<=0 THENRETURN : REM ...SELECT WHICH ENEMY TO SHOOT
6220 IF Q<10 THENRETURN
6230 FOR X=Q TO Q+(Q1-1)
6235 IF W(X)<2 THEN 6250
6240 PRINT X;: IF W(X)=2 THENPRINT "*";
6243 IF M(X)=0 THENPRINT "+";
6245 PRINT " ";: GOSUB 7000: IF W(X)=INT(W(X)) THEN 6247
6246 PRINT TAB(25);"### HIDDEN FROM VIEW ###": GOTO 6250
6247 PRINT TAB(25);"RANGE: ";D(X);
6248 PRINT TAB(39);"IT SHOOTS TO: ";R(X)
6250 NEXT X
6260 PRINT : INPUT" INPUT WHICH UNIT # TO SHOOT AT --->";E
6270 E=INT(E):DS=0
6275 IF E=0 THENPRINT "SHOOTING ABORTED": RETURN
6280 IF E<Q THENPRINT "NUMBER TOO LOW": GOTO 6200
6285 IF E>Q+Q1-1 THENPRINT "NUMBER TOO HIGH": GOTO 6200
6290 IF W(E)=0 THENPRINT "*** THAT UNIT IS DEAD ***": GOTO 6200
6295 RETURN
6300 S(T)=S(T)-1: REM ...GOSUB TO CALCULATE A "HIT".
6310 Z1=INT(D(E)-Y(E)):Z2=INT(X(E))
6320 Z3=INT((RND(0)*(Z1/2))+(Z1/2)):Z4=INT((RND(0)*(Z2/2))+(Z2/2))
6330 Y(E)=INT(Y(E)+Z3):X(E)=INT(X(E)-Z4):H=F(T)/A(E)
6335 IF ABS(D(E))<M(E)+100 THENY(E)=D(E):X(E)=0
6340 Z1=INT(D(E)-Y(E)):Z2=INT(X(E)): IF RND(0)>.85 THENH=H+1
6350 IF ABS(D(E))<M(E)+50 THENH=H+H
6352 IF J(E)=3 THENH=H-.4
6355 IF W(E)=7 THENH=H+1
6356 IF S(E)<=0 THENH=H+1
6357 IF M(E)=0 THENH=H+1
6358 IF J(E)>1 AND T<>7 THENIF ABS(D(E))>R(T)/2 THENH=H/1.5
6359 IF J(E)=1 AND T=7 THENIF ABS(D(E))>R(T)/2 THENH=H/2
6360 H=INT(H): IF ABS(Y(E))>R(T)+10 THENH=0
6365 IF J(E)>=4 THENW(E)=7
6366 IF J(E)=3 THENIF RND(0)>.8 THENW(E)=7
6367 IF J(E)<3 THENIF F(T)>F(E) THENIF RND(0)>.9 THENW(E)=7
6370 IF ABS(Y(E))>R(T)+10 THENY(E)=R(T)-(RND(0)*25)
6380 IF ABS(Z1)>15 THENH=0
6390 IF ABS(Z2)>10 THENH=0
6395 RETURN
6400 PRINT : REM ...RESULT OF YOUR SHOT
6410 IF C(T)<51 THENPRINT ,"+++ B A M +++"
6420 IF C(T)=75 THENPRINT ,"+++ B O O O M +++"
6425 IF C(T)=88 THENPRINT ,"<+> B L A A A M M <+>"
6430 IF C(T)>88 THENPRINT ,K$;" V A R O O O M M ";K$
6440 IF ABS(Z1)<16 THENIF ABS(Z2)<11 THENIF H=0 THEN 6500
6450 PRINT : X=E: IF H>0 THEN 6500
6460 PRINT ,"* BOOM *": PRINT "SHOT ON ";: GOSUB 7000
6470 IF ABS(D(E))>R(T)+15 THENPRINT " IS +*+ OUT OF RANGE +*+"
6480 IF ABS(D(E))>R(T)+15 THENY(E)=R(T):Z1=INT(ABS(D(E))-Y(E))
6485 IF W(E)>INT(W(E)) THENPRINT " ... UNIT IS HIDDEN.": RETURN
6490 GOSUB 6700: W(E)=6: IF RND(0)>.2 THENW(E)=4
6495 RETURN
6500 PRINT : REM ..."HIT" ON ENEMY MESSAGE DISPLAY
6510 GOSUB 7000: PRINT " *HIT*"
6515 IF W(E)>INT(W(E)) THENPRINT ," ...UNCERTAIN...": RETURN
6520 W=W(E): GOSUB 6600: PRINT TAB(10);
6530 GOSUB 7000: IF Z2=2 THENW(E)=2
6540 IF M(E)>0 THENIF RND(0)>.8 THENW(E)=3:M(E)=0
6550 IF S(T)=0 THENIF Z2=2 THENZ2=0
6560 IF Z2=0 THENW(E)=0:P=P+P(E):K=K+1:K1=K1+1
6565 IF Z2=0 THENIF J(E)=1 THENK2=K2+1:K3=K3+1
6570 IF W(E)=0 THENPRINT " *** DESTROYED ***"
6575 IF W(E)=0 THENI(T(E))=I(T(E))+1
6580 IF W(E)=2 THENPRINT " $#! STUNNED !#$"
6585 IF W(E)=3 AND J(E)<>3 THENPRINT D$
6587 IF W(E)=3 AND J(E)=3 THENPRINT " +++ BADLY WOUNDED +++"
6590 IF W(E)>3 AND J(E)<3 THENPRINT " /^ DENTED V/"
6592 IF W(E)>3 AND J(E)>2 THENPRINT " @ SHELL SHOCKED @"
6595 PRINT : RETURN
6600 Z=(RND(0)*6)+1: REM ...COMBAT RESULTS TABLE
6610 Z2=5:W=W(E)
6620 IF H=0 AND Z<3 THENZ2=2: RETURN
6630 IF H=1 THENIF Z<3 AND W=2 THENZ2=0: RETURN
6635 IF H=1 THENIF Z<4 AND W<>2 THENZ2=2: RETURN
6640 IF H>1 THENIF Z<2 THENZ2=0: RETURN
6650 IF H>2 THENIF Z<3 THENZ2=0: RETURN
6660 IF H>3 THENIF Z<6 THENZ2=0: RETURN
6665 IF H>4 THENZ2=0: RETURN
6670 IF H=2 THENIF Z<4 AND W=2 THENZ2=0: RETURN
6675 IF H=2 THENIF Z<6 AND W<>2 THENZ2=2: RETURN
6680 IF H=3 THENIF Z<5 AND W=2 THENZ2=0: RETURN
6685 IF H=3 THENIF W<>2 THENZ2=2: RETURN
6690 IF H=4 THENIF W=2 THENZ2=0
6695 RETURN
6700 IF Z1>15 THENPRINT " SHORT BY ";Z1;M$
6710 IF Z1<-15 THENPRINT " LONG BY ";Z1;M$
6720 IF ABS(Z2)>10 THENPRINT " OFF TO THE ";: GOTO 6740
6730 RETURN
6740 IF Z2>0 THENPRINT "LEFT BY ";Z2;M$
6750 IF Z2<0 THENPRINT "RIGHT BY ";Z2;M$
6790 RETURN
6800 FOR X=Q TO Q+(Q1-1): REM ...COLLISION ?
6810 IF W(X)<2 THEN 6890
6820 IF ABS(D(X))<>0 THEN 6890
6830 IF J(X)>2 THENPRINT ,,K$;" CRUSH ";K$
6840 IF J(X)<3 THENPRINT ,,"+#+ CRAAASSH ! +#+"
6850 PRINT " COLLISION WITH ";: GOSUB 7000
6860 IF (A(T)/A(X))<3.5 THENX=Q+(Q1-1)NEXT:GOTO6895
6870 PRINT : PRINT ,"+++ RUN OVER +++": Z2=0
6880 GOSUB 7000:E=X:GOSUB6560:X=Q+(Q1-1):NEXT:GOTO6895
6890 NEXT X
6895 RETURN
6900 IF Q+Q1-1<80 THENQ1=Q1+1
6910 X=Q+Q1-1: IF X>81 THEN 6995
6920 IF W(X)<=0 THEN 6995
6930 W(X)=6
6940 PRINT "     *#* ";: GOSUB 7000
6950 IF M(X)>1 THENPRINT " COMMING OUT OF THE WOODS *#*"
6960 IF M(X)<=1 THENPRINT " IS NOW VISIBLE *#*"
6970 IF R(T)<1501 THEND(X)=R(T)+(RND(0)*50)*10
6980 IF R(T)>1500 THEND(X)=1500+(RND(0)*50)*10
6985 IF J(X)=4 THEND(X)=INT(D(X)/2)
6990 V(X)=V(X)+INT(RND(0)*(D(X)/5))
6995 RETURN
7000 REM ....ENEMY NAME DISPLAY....
7010 I=T(X):Y=INT(I/10)
7020 ON Y GOTO 7050,7150,7250,7350,7450,7550
7050 IF I=10 THENPRINT "BREN CARRIER";: RETURN
7060 IF I=11 THENPRINT "HALF-TRACK";: RETURN
7070 IF I=12 THENPRINT "HALF-TRACK";: RETURN
7080 IF I=13 THENPRINT "M4 SHERMAN";: RETURN
7090 IF I=14 THENPRINT "M24 *CHAFFE*";: RETURN
7100 IF I=15 THENPRINT "M26 *PATTON*";: RETURN
7110 IF I=16 THENPRINT "M4 SHERMAN (76MM)";: RETURN
7120 IF I=17 THENPRINT "M7 PRIEST HOWITZER";: RETURN
7130 IF I=18 THENPRINT "DAIMLER SCOUT CAR";: RETURN
7140 IF I=19 THENPRINT "M18 TANK-DESTROYER";: RETURN
7150 IF I=20 THENPRINT "M36 TANK-DESTROYER";: RETURN
7160 IF I=21 THENPRINT "M20 RECON CAR";: RETURN
7170 IF I=22 THENPRINT "CROMWELL MK.VII.";: RETURN
7180 IF I=23 THENPRINT "#ACHILLES#";: RETURN
7190 IF I=24 THENPRINT "#SEXTON# HOWITZER";: RETURN
7200 IF I=25 THENPRINT "VALENTINE MK.IV.";: RETURN
7210 IF I=26 THENPRINT "CHURCHILL MK.III.";: RETURN
7220 IF I=27 THENPRINT "M5 STUART";: RETURN
7230 IF I=28 THENPRINT "M8 ARMORED CAR";: RETURN
7240 IF I=29 THENPRINT "TRUCK";: RETURN
7250 IF I=30 THENPRINT "M4 SHERMAN (105MM)";: RETURN
7260 IF I=31 THENPRINT "M10 TANK-DESTROYER";: RETURN
7270 IF I=32 THENPRINT "JEEP";: RETURN
7280 IF I=33 THENPRINT "M3 SCOUT CAR";: RETURN
7290 IF I=34 THENPRINT "WHITE SCOUT CAR";: RETURN
7300 IF I=35 THENPRINT "57MM A.TANK GUN";: RETURN
7310 IF I=36 THENPRINT "76MM A.TANK GUN";: RETURN
7320 IF I=37 THENPRINT "90MM A.TANK GUN";: RETURN
7330 IF I=38 THENPRINT "105MM HOWITZER";: RETURN
7340 IF I=39 THENPRINT "M3 STUART";: RETURN
7350 IF I=40 THENPRINT "PETROL LORRY";: RETURN
7360 IF I=41 THENPRINT "SCOUT PATROL";: RETURN
7370 IF I=42 THENPRINT "MACHINE GUN SQUAD";: RETURN
7380 IF I=43 THENPRINT "MIXED INFANTRY";: RETURN
7390 IF I=44 THENPRINT "RIFLE SQUAD";: RETURN
7400 IF I=45 THENPRINT "INFANTRY";: RETURN
7410 IF I=46 THENPRINT "ARMORED INFANTRY";: RETURN
7420 IF I=47 THENPRINT "BAZOOKA TEAM";: RETURN
7430 IF I=48 THENPRINT "MORTAR UNIT";
7440 IF I=49 THENPRINT "MORTAR TEAM(81MM)";
7450 IF I=50 THENPRINT "GENERAL STAFF CAR";
7490 RETURN
7500 REM ..."YOUR TANK"...
7505 ON T GOTO 7510,7520,7530,7540,7550,7560,7570,7580,7590
7510 PRINT "ARMORED CAR ";: RETURN
7520 PRINT K$;" KING TIGER ";K$;: RETURN
7530 PRINT "PZKW-IV. ";: RETURN
7540 PRINT K$;" PANTHER ";K$;: RETURN
7550 PRINT K$;" PZKW-IVB. ";K$;: RETURN
7560 PRINT "TIGER ";: RETURN
7570 PRINT "NASHORN GUN ";: RETURN
7580 PRINT "JAG-PZ 6 ";: RETURN
7590 PRINT "PZKW III. ";
7595 RETURN
7600 IF Q=0 THEN 7690: REM ...HIDE ENEMY UNITS
7620 IF Q1<=0 THEN 7690
7630 FOR X=Q TO Q+(Q1-1)
7640 IF W(X)>0 THENW(X)=1
7680 NEXT X
7690 RETURN
7700 REM ...YOUR TERRAIN...
7705 IF L<1 THENPRINT " *FLATLAND*": RETURN
7707 ON L GOTO 7710,7720,7730,7740,7750,7760,7770,7780,7790
7710 PRINT " DIRT.": RETURN
7720 PRINT " OPEN GRASS LAND.": RETURN
7730 PRINT " FLAT BRUSHY AREA.": RETURN
7740 PRINT " HIDDEN IN A BRUSH PILE.": RETURN
7750 PRINT " HIDDEN IN A WOODED AREA.": RETURN
7760 PRINT " HIDDEN IN A GULLY.": RETURN
7770 PRINT " CAMOFLAGED ON A HILL-TOP.": RETURN
7780 PRINT " HIDDEN ON A RIDGE. ": RETURN
7790 PRINT " ON A WOODED HILLTOP. ": RETURN
7800 PRINT " HIDDEN AMONG TREES ON A HILL. "
7890 RETURN
7900 PRINT : REM ...YOUR KILLS THIS GAME.
7910 PRINT " YOU HAVE DESTROYED ";K1-K2;E$;" UNITS AND";K2
7915 PRINT E$;T$;" SO FAR ON DAY # ";M;" --TOTAL: ";K1
7920 PRINT : Z=0
7925 INPUT"WANT A LIST OF YOUR KILLS ? ( Y=YES N=NO )-->";Y$
7940 PRINT : IF Y$="N" THEN 7990
7950 PRINT " * * * * * ";K1;" ALLIED UNITS DESTROYED TODAY * * * * *"
7955 PRINT
7960 FOR X=10 TO 79
7970 IF W(X)=0 THENGOSUB 7000: Z=Z+P(X)
7980 IF W(X)=0 THENPRINT TAB(30);P(X);TAB(35);"*#* POINTS *#*"
7985 NEXT X
7990 PRINT TAB(7);"*** ";Z;" POINTS TODAY ***  (";P;" TOTAL)"
7991 IF M=1 THEN 7995
7992 PRINT "###*#* YOUR TOTALS ARE: ";K3;" TANKS AND ";K-K3;" OTHER UNITS."
7995 RETURN
8000 J(X)=3: REM ...UNLOAD INFANTRY FROM A TRANSPORT
8010 GOSUB 7000: PRINT " - - - UNLOADING ... ";
8020 T(X)=(RND(0)*9)+41: REM ...TYPE OF INFANTRY
8030 R(X)=200:C(X)=.1:F(X)=2:A(X)=T(X)-38
8040 IF T(X)=46 THENA(X)=10
8050 IF T(X)=47 THENA(X)=4:F(X)=30:R(X)=50:C(X)=75
8060 IF T(X)=49 THENA(X)=3:F(X)=3:R(X)=1200:C(X)=81
8070 GOSUB 7000: M(X)=30:P(X)=T(X)-40: PRINT
8080 PRINT ,"### TRANSPORT HAS DISAPPEARED ###"
8090 V(X)=D(X):H(X)=0:S(X)=99: RETURN
9000 IF W(T)=0 THEN 9500: REM ...< GAME OVER >
9020 PRINT : IF C=5 THEN 9400
9100 PRINT ,"+*+  WELL DONE LIEUTENENT  +*+": M=M+1
9120 PRINT "+*+  YOU HAVE SURVIVED ANOTHER DAY # ";M
9130 GOSUB 7900
9140 IF M(T)=0 THENPRINT "YOUR DAMAGED UNIT HAS BEEN SALVAGED."
9150 PRINT "DO YOU WANT ANOTHER MISSION WITH YOUR ";
9160 IF M(T)=0 THENPRINT "REPAIRED ";
9170 GOSUB 7500: PRINT : PRINT
9200 INPUT" (TYPE IN:   Y=YES  N=NO ) ---=>";Y$
9210 IF Y$="N" THEN 9400
9230 GOTO 9570
9400 T=(RND(0)*9)+1
9430 PRINT " +YOU+ HAVE BEEN GIVEN A ";: GOSUB 7500
9500 PRINT : REM ...PLAY AGAIN?
9510 PRINT " YOU'VE DESTROYED ";K3;E$;T$;" (TOTAL)"
9520 IF W(T)=0 THENGOSUB 7900: PRINT
9530 PRINT " *#* DO YOU WANT TO TRY AGAIN ? *#*"
9540 INPUT" TYPE IN    Y=YES  N=NO  ----=>";Y$
9550 IF Y$="N" THEN 9900
9570 RESTORE : IF W(T)=0 THENCLEAR : GOTO 100
9590 GOTO 300
9900 END
