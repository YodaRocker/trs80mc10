0 CLEAR1000:CLS:DIMC1,CC,P,K,M$:C1=RND(-TIMER):GOTO100
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
100 REM SWARMS BY RAND K. MILLER
110 REM 1054 S KING ST. HONOLULU, HI 96814
120 CLS:M$="ATTENTION: DEADLY AFRICAN BEES ARE ATTACKING!":GOSUB1
130 PRINT:PRINT
140 PRINT"BEGIN DEFENSE PLAN: ":PRINT
150 PRINT"TIME:    1"
160 M$="ENTER YOUR NAME FOR ID CHECK":GOSUB1:INPUTN$
180 M$="ENTER CODE WORD FOR NUCLEAR CLEARANCE":GOSUB1:INPUTC$
200 REM CREATE TWO INITIAL SWARMS
210 DIM E(21),S(21),A$(21),U(21),G(21),M(21),D(21),C(21)
220 DIM K(21),V(21),R(21)
230 A1=INT(RND(0)*21+1)
240 A2=INT(RND(0)*21+1)
250 IF A1=A2 THEN 230
260 S(A1)=INT(RND(0)*5+2)
270 S(A2)=INT(RND(0)*5+2)
280 E(A1)=INT(RND(0)*9+6)
290 E(A2)=INT(RND(0)*9+6)
300 REM SUBTRACT ONE DAY FROM ALL ETA'S
310 FOR A=1 TO 21
320 IF E(A)=0 THEN 340
330 GOTO 350
340 IF U(A)=0 THEN 560
350 IF S(A)=1 THEN 560
360 IF ABS(U(A))=1 THEN 470
370 IF ABS(E(A))=1 THEN 410
380 E(A)=E(A)-1
390 C(A)=C(A)+(17-E(A))
400 GOTO 560
410 IF E(A)=-1 THEN 450
420 M$="THE BEES HAVE ARRIVED IN THE MAJOR CITY IN SECTION"+STR$(A):GOSUB1
430 E(A)=-1
440 U(A)=6
450 U(A)=U(A)-1
460 GOTO 560
470 IF U(A)=-1 THEN 560
480 M$="THE BEES HAVE DESTROYED THE MAJOR CITY IN SECTION"+STR$(A):GOSUB1
490 U(A)=-1
500 IF V(A)<>-1 THEN 530
510 M$="BUT THE POPULATION HAS BEEN EVACUATED":GOSUB1
520 GOTO 550
530 K(A)=1
540 C(A)=(1E+06*(RND(0)*A+1))+C(A)
550 GOTO 560
560 NEXT
570 REM ADD ONE UNIT TO TIME
580 T=T+1
590 REM CREATE NEW SWARM
600 IF T/30<>INT(T/30) THEN 690
610 A=INT(RND(0)*21+1)
620 IF S(A)<>0 THEN 600
630 IF R(A)<>0 THEN 600
640 IF K(A)<>0 THEN 600
650 S(A)=INT(RND(0)*5+2)
660 E(A)=INT(RND(0)*9+16)
670 C(A)=INT(RND(0)*10)
680 M$="A NEW SWARM IS REPORTED IN SECTION"+STR$(A)+".":GOSUB1
690 REM CHECK COMBATTED SWARMS
700 FOR A=1 TO 21
710 IF T<>G(A) THEN 870
720 IF S(A)<> 1 THEN 810
730 S(A)=0
740 G(A)=0
750 E(A)=0
760 M(A)=0
770 K(A)=0
780 U(A)=0
790 M$="** THE SWARM IN SECTION"+STR$(A)+" IS TOTALLY DESTROYED.":GOSUB1
800 GOTO 870
810 S(A)=S(A)-M(A)
820 M$="** THE PHASE USED IN SECTION"+STR$(A)+" WAS SUCCESSFUL.":GOSUB1
830 IF S(A)>=1 THEN 850
840 S(A)=1
850 G(A)=0
860 M(A)=0
870 NEXT
880 REM ADD ONE UNIT TO EACH UNCOMBATTED SWARM
890 FOR A=1 TO 21
900 IF E(A)=1 THEN 1150
910 IF S(A)=1 THEN 1120
920 IF S(A)=0 THEN 1150
930 IF S(A)+1<9 THEN 1090
940 IF A=1 THEN 1020
950 IF S(A-1)>0 THEN 1010
960 IF R(A-1)<>0 THEN 1150
970 M$="** THE SWARM IN SECTION"+STR$(A)+" HAS SPREAD TO SECTION"+STR$(A-1):GOSUB1
980 S(A-1)=3
990 E(A-1)=INT(RND(0)*10+5)
1000 GOTO 1150
1010 IF A=21 THEN 1150
1020 IF S(A+1)>0 THEN 1150
1030 IF R(A+1)<>0 THEN 1150
1040 M$="** THE SWARM IN SECTION"+STR$(A)+" HAS SPREAD TO SECTION"+STR$(A+1):GOSUB1
1050 IF R(A+1)<>0 THEN 1150
1060 S(A+1)=3
1070 E(A+1)=INT(RND(0)*10+5)
1080 GOTO 1150
1090 S(A)=S(A)+1
1100 C(A)=C(A)+S(A)
1110 GOTO 1150
1120 IF D(A)=1 THEN 1150
1130 M$="** THE SWARM IN SECTION"+STR$(A)+" IS READY TO BE DESTROYED.":GOSUB1
1140 D(A)=1
1150 NEXT
1160 REM WINNER CHECK
1170 W=0
1180 FOR A=1 TO 21
1190 W=S(A)+W
1200 NEXT
1210 IF W>=1 THEN 1280
1220 PRINT "***ALL SWARMS NOW DESTROYED***"
1230 M$="FINAL STATISTICS ON ATTACKS AND DESTRUCTION ARE NOW BEING COMPUTED.":GOSUB1:
1231 FORZZ=1TO10:SOUNDRND(200),1:NEXT:PRINT"FINAL RESULTS FOLLOW:"
1250 A=0
1260 PRINT
1270 GOTO 4690
1280 REM EVACUATION CHECK
1290 FOR A=1 TO 21
1300 IF V(A)<>T THEN 1400
1310 IF U(A)=-1 THEN 1370
1320 IF K(A)>0 THEN 1370
1330 M$="** POPULATION IN SECTION"+STR$(A)+" IS EVACUATED":GOSUB1
1340 C(A)=C(A)+INT(RND(0)*17)
1350 V(A)=-1
1360 GOTO 1400
1370 M$="** POPULATION IN SECTION"+STR$(A)+" WAS DESTROYED BEFORE IT COULD BE EVACUATED.":GOSUB1
1390 V(A)=0
1400 NEXT
1410 REM RETURN EVACUATION CHECK
1420 FOR A=1 TO 21
1430 IF R(A)<>T THEN 1490
1440 IF R(A)<1 THEN 1490
1450 K(A)=0
1460 R(A)=0
1470 V(A)=0
1480 M$="** THE POPULATION HAS RETURNED TO THE CITY IN SECTION "+STR$(A)+".":GOSUB1
1490 NEXT
1500 REM LOSER CHECK
1510 Q=0
1520 FOR A=1 TO 21
1530 Q=C(A)+Q
1540 NEXT
1550 IF Q<7.5E+07 THEN 1650
1560 M$="THE BEES HAVE DEVASTATED THE UNITED STATES AND THERE ARE NOW OVER 75 MILLION CASUALTIES.":GOSUB1
1570 M$="THE BEES ARE NOW CONSIDERED TO BE THE VICTORS OVER MODERN TECHNOLOGY.":GOSUB1
1600 A=0
1610 Q=0
1620 W=0
1630 PRINT
1640 GOTO 4690
1650 REM COMMAND INPUT
1660 PRINT "TIME:   ";T+1
1670 INPUT "COMMAND";COM:IF COM=0 THEN 4920
1680 IF COM>8 OR COM<1 OR COM<>INT(COM) THEN 1670
1690 ON COM GOTO 1700,2210,2620,3840,4060,4660,4920,5190
1700 REM MAP PRINT OUT
1710 CLS:PRINT"1) ATTACK SCAN MAP":GOSUB1730:GOTO1650
1730 FOR A=1 TO 21
1740 IF S(A)>6 THEN 1860
1750 IF S(A)>4 THEN 1840
1760 IF S(A)>1 THEN 1820
1770 IF S(A)>0 THEN 1800
1780 A$(A)= " "
1790 GOTO 1870
1800 A$(A)="."
1810 GOTO 1870
1820 A$(A)="+"
1830 GOTO 1870
1840 A$(A)="*"
1850 GOTO 1870
1860 A$(A)="#"
1870 NEXT
1890 PRINT@31,"/";
1900 PRINT "-----------------------       /";A$(18);
1930 PRINT "! 1 ! 3 ! 6 ! 9  ! 13 !";CHR$(92);"-    /18";
1960 PRINT "! ";A$(1);" ! ";A$(3);" ! ";A$(6);" ! ";A$(9);"  ! ";A$(13);"  !  ";CHR$(92);"--/--/";
1980 PRINT "!   !---!---!----!----! ";A$(16);" ! 19/"
2000 PRINT "!---! 4 ! 7 ! 10 ! 14 ! 16! ";A$(19);" !"
2020 PRINT "! 2 ! ";A$(4);" ! ";A$(7);" ! ";A$(10);"  ! ";A$(14);"  !---!---!"
2050 PRINT "! ";A$(2);" !---!---!----!----! 17! 20!"
2070 PRINT " ";CHR$(92);"  ! 5 ! 8 ! 11 ! 15 ! ";A$(17);" ! ";A$(20);" !"
2100 PRINT "  ";CHR$(92);" ! ";A$(5);" ! ";A$(8);" ! ";A$(11);"  ! ";A$(15);"  !   !---/"
2120 PRINT "   ";CHR$(92);"!---!---!----!----!---!21!"
2130 PRINT "            ";CHR$(92);"- ";A$(12);" !         ";CHR$(92);A$(21);"!"
2150 PRINT "              ";CHR$(92);"12!         ! !"
2160 PRINT "               ";CHR$(92);"-!         !-!"
2190 RETURN
2210 REM ETA REPORT
2220 CLS:PRINT "2) ETA REPORT"
2240 GOSUB1730:INPUT"SECTION (0=ALL)";A:IFA<0ORA>21THEN2240
2260 PRINT "*******************************"
2270 IF A<>0 THEN 2300
2280 D7=-1
2290 FOR A=1 TO 21
2300 IF S(A)=1 THEN 2520
2310 IF R(A)=-1 THEN 2550
2320 IF E(A)=0 THEN 2500
2330 IF U(A)=-1 THEN 2380
2340 IF E(A)=-1 THEN 2410
2350 M$="THE BEES WILL ARRIVE AT THE MAJOR CITY IN SECTION"+STR$(A)+" ON DAY"+STR$(E(A)+T)+".":GOSUB1
2370 GOTO 2570
2380 M$="THE BEES HAVE ALREADY DESTROYED THE CITY IN SECTION"+STR$(A)+" AND ARE NOW INHABITING IT.":GOSUB1
2400 GOTO 2570
2410 M$="THE BEES HAVE ARRIVED AT THE CITY IN "
2420 IF V(A)<>-1 THEN 2460
2430 M$=M$+"SECTION"+STR$(A)+" BUT THE POPULATION HAS BEEN EVACUATED.":GOSUB1
2450 GOTO 2570
2460 M$=M$+"SECTION"+STR$(A)+" AND THE POPULATION OF THAT CITY CAN ONLY SURVIVE FOR ABOUT"+STR$(U(A))+" MORE DAYS.":GOSUB1
2480 M$="USE OF URBAN DEFENSES IS RECOMMENDED.":GOSUB1
2490 GOTO 2570
2500 M$="NO SWARMS REPORTED IN SECTION"+STR$(A):GOSUB1
2510 GOTO 2570
2520 M$="THE BEES IN SECTION"+STR$(A)+" ARE READY TO BE DESTROYED.":GOSUB1
2540 GOTO 2570
2550 M$="SECTION"+STR$(A)+" IS A RADIOACTIVE WASTELAND THAT IS COMPLETELY UNPOPULATED.":GOSUB1
2570 PRINT "*******************************"
2580 IF D7<>-1 THEN 2600
2590 GOSUB6000:NEXT
2600 D7=0
2610 GOTO 300
2620 REM BATTLE PHASE OPTIONS
2630 CLS
2640 PRINT "3) BATTLE PHASE OPTIONS"
2650 GOSUB1730:INPUT"SECTION";A:IFA<1ORA>21THEN2650
2660 GOSUB7000:INPUT"PHASE";P:IFP<1ORP>PTHEN2660
2670 IF V(A)<1 THEN 2700
2680 M$="SECTION"+STR$(A)+" IS BEING EVACUATED.":GOSUB1
2690 GOTO 300
2700 IF G(A)>0 THEN 2720
2710 GOTO 2740
2720 M$="THE SWARM IN SECTION"+STR$(A)+" IS ALREADY BEING COMBATTED.":GOSUB1
2730 GOTO 300
2740 IF S(A)=0 THEN 2760
2750 GOTO 2780
2760 M$="NO SWARM IS REPORTED IN SECTION"+STR$(A)+".":GOSUB1
2770 GOTO 300
2780 IF S(A)<>1 THEN 2820
2790 IF P=5 THEN 2860
2800 M$="THE DESTRUCTION PHASE SHOULD BE USED IN SECTION"+STR$(A)+".":GOSUB1
2810 GOTO 300
2820 IF E(A)<>-1 THEN 2860
2830 IF P=6 THEN 2860
2840 M$="URBAN DEFENSES SHOULD BE USED IN SECTION"+STR$(A)+".":GOSUB1
2850 GOTO 300
2860 N=RND(0)
2870 ON P GOTO 2890,2970,3060,3120,3210,3290
2880 REM PHASE 1
2890 M$="BEE COCKTAIL: PHASE ONE, NOW BEING ATTEMPTED.":GOSUB1
2900 IF N>.96THEN 300
2910 G(A)=T+INT(RND(0)*3+1)
2920 IF S(A)>5 THEN 2950
2930 M(A)=S(A)-2
2940 GOTO 300
2950 M(A)=S(A)-5
2960 GOTO 300
2970 REM PHASE TWO
2980 M$="PROJECT QUEEN: PHASE TWO, NOW BEING ATTEMPTED.":GOSUB1
2990 IF N>.93THEN 300
3000 G(A)=T+INT(RND(0)*3+4)
3010 IF S(A)>3 THEN 3040
3020 M(A)=1
3030 GOTO 300
3040 M(A)=S(A)-1
3050 GOTO 300
3060 REM PHASE THREE
3070 M$="PROJECT BRUSH FIRE: PHASE THREE, NOW BEING ATTEMPTED.":GOSUB1
3080 IF N>.97 THEN 300
3090 G(A)=T+1
3100 M(A)=2
3110 GOTO 300
3120 REM PHASE 4
3130 M$="PROJECT STERILE MALE: PHASE FOUR, NOW BEING ATTEMPTED.":GOSUB1
3140 IF N>.90 THEN 300
3150 G(A)=T+INT(RND(0)*3+3)
3160 IF S(A)>6 THEN 3190
3170 M(A)=S(A)-1
3180 GOTO 300
3190 M(A)=4
3200 GOTO 300
3210 REM PHASE 5
3220 IF S(A)=1 THEN 3250
3230 M$="SWARM TOO LARGE: DESTRUCTION NOT POSSIBLE.":GOSUB1
3240 GOTO 300
3250 M$="DESTRUCTION: PHASE FIVE, NOW BEING ATTEMPTED.":GOSUB1
3260 IF N>.8 THEN 300
3270 G(A)=T+INT(RND(0)*3+2)
3280 GOTO 300
3290 REM PHASE 6
3300 IF E(A)<>-1 THEN 2800
3305 GOSUB8000
3310 PRINT"URBAN DEFENSE CODE":INPUTD$
3320 C(A)=C(A)+INT(200*RND(0))
3330 IF RND(0) >.9 THEN 300
3340 IF D$="A" THEN 3430
3350 IF D$="B" THEN 3490
3360 IF D$="C" THEN 3550
3370 IF D$="D" THEN 3610
3380 IF D$="E" THEN 3670
3390 IF D$="F" THEN 3710
3400 IF D$="G" THEN 3750
3410 PRINT"INVALID URBAN DEFENSE CODE.":PRINT:GOTO 3310
3420 GOTO 300
3430 REM *A*
3440 IF S(A)>5 THEN 3470
3450 S(A)=S(A)-1
3460 GOTO 3780
3470 S(A)=2
3480 GOTO 3780
3490 REM *B*
3500 IF S(A)>4 THEN 3530
3510 S(A)=S(A)-3
3520 GOTO 3780
3530 S(A)=S(A)-2
3540 GOTO 3780
3550 REM *C*
3560 IF S(A)>7 THEN 3590
3570 S(A)=S(A)-3
3580 GOTO 3780
3590 S(A)=1
3600 GOTO 3780
3610 REM *D*
3620 IF S(A)>3 THEN 3650
3630 S(A)=1
3640 GOTO 3780
3650 S(A)=S(A)-3
3660 GOTO 3780
3670 REM *E*
3680 IF RND(0)>.5 THEN 3780
3690 S(A)=2
3700 GOTO 3780
3710 REM *F*
3720 IF RND(0)>.4 THEN 3780
3730 S(A)=1
3740 GOTO 3780
3750 REM *G*
3760 IF RND(0)>.8 THEN 3780
3770 S(A)=S(A)-4
3780 M$="THE URBAN DEFENSE IN SECTION"+STR$(A)+" WAS SUCCESSFUL.":GOSUB1
3790 IF S(A)>1 THEN 300
3800 S(A)=1
3810 D(A)=1
3820 M$="** SWARM IN SECTION"+STR$(A)+" IS READY TO BE DESTROYED.":GOSUB1
3830 GOTO 300
3840 REM EVACUATION PROCEDURE
3850 CLS
3860 PRINT "4) EVACUATION PROCEDURE"
3870 GOSUB1730:INPUT"SECTION";A:IFA<1ORA>21THEN3870
3890 IF S(A)=0 THEN 3950
3900 IF V(A)>0 THEN 3970
3910 IF V(A)=-1 THEN 3990
3920 IF K(A)>0 THEN 4010
3930 IF U(A)=-1 THEN 4010
3940 GOTO 4030
3950 M$="COMPUTER FAILSAFE... NO SWARMS REPORTED IN SECTION"+STR$(A)+".":GOSUB1
3960 GOTO 300
3970 M$="SECTION"+STR$(A)+" IS BEING EVACUATED ALREADY.":GOSUB1
3980 GOTO 300
3990 M$="CITY IN SECTION"+STR$(A)+" IS ALREADY EVACUATED.":GOSUB1
4000 GOTO 300
4010 M$="POPULATION IN SECTION"+STR$(A)+" HAS BEEN DESTROYED.":GOSUB1
4020 GOTO 300
4030 M$="EVACUATION PROCEDURE NOW IN PROGRESS.":GOSUB1
4040 V(A)=5+T
4050 GOTO 300
4060 REM ***** NUCLEAR DESTRUCTION SEQUENCE *****
4070 CLS
4080 PRINT "5) NUCLEAR DESTRUCTION SEQUENCE"
4090 INPUT "ENTER YOUR NAME";N1$
4100 IF N$<>N1$ THEN 4270
4110 INPUT "ENTER YOUR CODE WORD";C1$
4120 IF C$<>C1$ THEN 4270
4130 PRINT "POSITIVE IDENTIFICATION CHECK.":SOUND100,5
4140 PRINT "CODE WORD CHECK IS VALID."
4150 PRINT "ID SEQUENCE COMPLETED."
4160 INPUT "PLEASE ENTER SECTION";A
4170 IF A<1 OR A>21 OR A<>INT(A) THEN PRINT "WRONG!":GOTO 4160
4180 IF E(A)<>-1 THEN 4220
4190 IF S(A)=0 THEN 4250
4200 PRINT
4210 GOTO 4310
4220 M$="COMPUTER FAILSAFE: BEES NOT ARRIVED IN THE MAJOR CITY IN SECTION"+STR$(A)+".":GOSUB1
4240 GOTO 300
4250 M$="COMPUTER FAILSAFE: NO SWARM REPORTED IN SECTION"+STR$(A)+".":GOSUB1
4260 GOTO 300
4270 M$="ID SEQUENCE DEFAULT... IGNORED":GOSUB1
4280 GOTO 300
4290 M$="NUCLEAR DESTRUCTION ABORTED.":GOSUB1
4300 GOTO 300
4310 M$="BOMB IN SECTION"+STR$(A)+" IS NOW ACTIVE.":GOSUB1
4320 IF K(A)=1 THEN 4370
4330 IF V(A)=-1 THEN 4370
4340 M$="SECTION"+STR$(A)+" HAS NOT BEEN EVACUATED.":GOSUB1
4350 INPUT "DO YOU WISH TO CONTINUE";K$
4360 IF LEFT$(K$,1)="Y" THEN 4370
4361 GOTO 4290
4370 INPUT"TYPE 'X' FOR BOMB DETONATION";F$
4380 IF F$<>"X" THEN 4290
4390 CLS
4400 PRINT "!!!!!!! BOMB DETONATED !!!!!!!":SCREEN0,1:FORZZ=1TO25:NEXT:SOUND255,1:SCREEN0,0
4410 PRINT "SWARM HAS BEEN DESTROYED!!!!!!"
4420 PRINT "CITY HAS BEEN DESTROYED!!!!!!!"
4430 C(A)=2413
4440 S(A)=0
4450 E(A)=0
4460 U(A)=0
4470 D(A)=0
4480 G(A)=0
4490 M(A)=0
4500 IF K(A)=1 THEN 4590
4510 IF V(A)<>-1 THEN 4580
4520 M$="THE POPULATION WILL MOVE BACK TO THE CITY IN SECTION"+STR$(A)+" WHEN THE RADIATION LEVEL HAS DECREASED.":GOSUB1
4550 K(A)=2
4560 R(A)=T+7
4570 GOTO 300
4580 C(A)=INT(2E+06*(RND(0)*A+1))+C(A)
4590 M$="** NO SURVIVORS ARE REPORTED IN SECTION"+STR$(A)+".":GOSUB1
4600 M$="** THE BEES WILL NOT ENTER A SECTION WITH NO HUMAN INHABITANTS, SO SECTION"+STR$(A)
4620 M$=M$+" IS COMPLETELY LACKING LIFE OF ANY KIND.":GOSUB1
4630 K(A)=2
4640 R(A)=-1
4650 GOTO 300
4660 REM CASUALTY REPORT
4670 CLS:PRINT"6) CASUALTY REPORT"
4680 GOSUB1730:INPUT"SECTION (0=ALL)";A:IFA<0ORA>21THEN4680
4690 PRINT "*******************************"
4700 IF A<>0 THEN 4750
4710 D7=-1
4720 FOR A=1 TO 21
4730 IF C(A)=0 THEN 4770
4740 IF C(A)>=1E+06 THEN 4790
4750 M$="SECTION"+STR$(A)+":"+STR$(C(A))+" CASUALTIES REPORTED.":GOSUB1
4760 GOTO 4800
4770 M$="SECTION"+STR$(A)+": NO BEE RELATED CASUALTIES.":GOSUB1
4780 GOTO 4800
4790 M$="SECTION"+STR$(A)+":"+STR$(C(A)/1E+06)+" MILLION CASUALTIES.":GOSUB1
4800 PRINT "*******************************"
4810 F=C(A)+F
4820 IF D7<>-1 THEN 4890
4830 GOSUB6000:NEXT
4840 D7=0
4850 IF F<1E+06 THEN 4880
4860 M$="TOTAL CASUALTIES REPORTED:"+STR$(F/1E+06)+" MILLION CASUALTIES.":GOSUB1
4870 GOTO 4890
4880 M$="TOTAL CASUALTIES REPORTED:"+STR$(F):GOSUB1
4890 IF W=0 THEN 5200
4900 F=0
4910 GOTO 300
4920 REM PRINT COMMANDS IN SHORT & LONG
4930 CLS:IFCOM=7THENGOSUB9010:CLS
4940 PRINT "*******COMMANDS*********"
4950 PRINT
4955 PRINT "0) COMMANDS"
4960 PRINT "1) ATTACK SCAN MAP"
4970 PRINT "2) ETA REPORT"
4980 PRINT "3) BATTLE PHASE OPTIONS"
5120 PRINT "4) EVACUATION PROCEDURES"
5130 PRINT "5) NUCLEAR DESTRUCTION"
5140 PRINT "6) CASUALTY REPORT"
5150 PRINT "7) INSTRUCTIONS"
5160 PRINT "8) CANCEL GAME":IF COM=0 THEN 1650
5161 PRINT:GOSUB6000:CLS:GOSUB5162:GOSUB5168:GOSUB9100:CLS:GOTO1650
5162 PRINT "BATTLE PHASE OPTIONS":PRINT:GOSUB9000:PRINT:GOSUB7000:PRINT:GOSUB6000:CLS:RETURN
5168 PRINT"URBAN DEFENSES":PRINT:GOSUB8000:PRINT:GOSUB6000:CLS:RETURN
5175 REM QUIT GAME
5190 PRINT"8) CANCEL GAME":PRINT
5195 INPUT"ARE YOU SURE";ZZ$
5200 IFZZ$="N"THEN 300
5210 END
6000 PRINT "press any key to continue";
6010 ZZ$=INKEY$:IFZZ$=""THEN6010
6020 FORC1=1TO25:PRINTCHR$(8);:NEXT:RETURN
7000 PRINT"1. BEE COCKTAIL (2 DAYS)":PRINT "2. PROJECT QUEEN (5 DAYS)"
7010 PRINT "3. PROJECT BRUSH FIRE (1 DAY)":PRINT "4. PROJECT STERILE MALE (4 DAYS)";
7030 PRINT "5. DESTRUCTION (3 DAYS)"
7050 PRINT "6. URBAN DEFENSES (1 DAY)":RETURN
8000 PRINT "A) FLIGHT PATTERNS":PRINT "B) SONIC BOOM"
8010 PRINT "C) SUPER-SONIC BEAMS":PRINT "D) POLLUTION"
8020 PRINT "E) METHYL PARATHION":PRINT "F) FIRE WALL":PRINT "G) STROBE LIGHT":RETURN
9000 M$="ONE COMMAND WILL WORK BETTER ON LARGER SWARMS ANOTHER ON SMALLER SWARMS. "
9002 M$=M$+"THERE IS A CHANCE THE PHASE MIGHT NOT WORK AT ALL.":GOSUB1:RETURN
9010 CLS:PRINT"IN 1956 APICULTURALIST WARWICK"
9011 PRINT"KERR BROUGHT 20 AFRICAN BEES TO"
9012 PRINT"BRAZIL. KNOWN TO BE FEROCIOUS,"
9013 PRINT"IT WAS HOPED THEY COULD BE "
9014 PRINT"BRED WITH LOCAL BEES TO PRODUCE"
9015 PRINT"A MORE PRODUCTIVE AND GENTLE"
9016 PRINT"HYBRID. UNFORTUNATELY THE BEES"
9017 PRINT"ESCAPED AND ARE MOVING THROUGH"
9018 PRINT"THE AMERICAS AT A RAPID RATE."
9019 PRINT"THEY HAVE NOW REACHED THE USA!"
9020 PRINT"YOU ARE THE PERSON IN CHARGE"
9021 PRINT"OF THE DEFENSE. SWARMS WILL"
9022 PRINT"GROW AND SPREAD IF NOT COMBAT-"
9023 PRINT"TED. THE SIZE IS SHOWN ON THE"
9024 PRINT"MAP (.+*#). DESTROY THE SWARMS!"
9025 GOSUB6000:RETURN
9100 PRINT"COMMAND 4 EVACUATION"
9101 PRINT"   USED TO EVACUATE THE MAJOR"
9102 PRINT"   CITY IN A SECTION (5 DAYS)."
9103 PRINT"   NO BATTLE PHASE CAN BE USED"
9104 PRINT"   UNTIL EVACUATION IS DONE.":PRINT"   IT IS NEVER REQUIRED."
9105 PRINT"COMMAND 5 NUCLEAR DESTRUCTION"
9106 PRINT"   IF BEES HAVE NOT ENTERED A"
9107 PRINT"   CITY YOU WILL NOT BE ALLOWED"
9108 PRINT"   TO USE THIS OPTION. PEOPLE"
9109 PRINT"   WILL RETURN IF EVACUATED."
9110 PRINT"   BEES WILL NOT ENTER A"
9111 PRINT"   DEVASTATED CITY.":GOSUB6000:CLS
9112 PRINT"COMMAND 3 BATTLE PHASE"
9113 PRINT"   BEE COCKTAIL
9114 PRINT"      AIR-DROPPED INSECT"
9115 PRINT"      GROWTH REGULATOR
9116 PRINT"   PROJECT QUEEN"
9117 PRINT"      GM'D QUEENS AIR-DROPPED"
9118 PRINT"      TO INFILTRATE HIVE"
9119 PRINT"   PROJECT BRUSH FIRE"
9120 PRINT"      AIR-DROPPED WORKER BEES":PRINT"      WITH ATTACK PHEROMONES"
9230 PRINT"   PROJECT STERILE MALE"
9240 PRINT"      GM'D DRONES DROPPED TO"
9250 PRINT"      REDUCE QUEEN FERTILITY":GOSUB6000:CLS
9260 PRINT"   DESTRUCTION"
9270 PRINT"      USE ONLY AFTER BEE POP."
9280 PRINT"      REDUCED BY OTHER PHASES."
9290 PRINT"      TRAINED WARDENS ENTER"
9295 PRINT"      AREA AND KILL REMAINING"
9296 PRINT"      BEES"
9300 PRINT"   URBAN DEFENSES"
9310 PRINT"      flight patterns (FOIL"
9320 PRINT"      STRIPS DISRUPT SWARMS)"
9330 PRINT"      sonic boom (JETS FLY"
9340 PRINT"      THROUGH SWARMS)"
9350 PRINT"      super-sonic beams (USED"
9360 PRINT"      TO DISRUPT NAVIGATION)"
9370 PRINT"      pollution (1000S OF"
9375 PRINT"      CANS OF INSECTICIDE)":GOSUB6000:CLS
9376 PRINT"      methyl parathion (POTENT"
9377 PRINT"      ORGANOPHOSPHATE COMPOUND"
9380 PRINT"      SPRAYED BY CROP DUSTERS"
9384 PRINT"      fire wall (KEROSENE"
9390 PRINT"      FILLED TRENCHES)
9392 PRINT"      strobe light (ON HIGH"
9394 PRINT"      BUILDINGS SURROUNDED BY"
9395 PRINT"      ELECTRIC MESH USED TO"
9396 PRINT"      ATTRACT & KILL BEES"
9400 GOSUB6000:CLS:RETURN
