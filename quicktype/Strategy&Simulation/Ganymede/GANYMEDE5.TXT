0 GOTO120
1 W=VARPTR(A$(K)):POKEPEEK(W+2)*256+PEEK(W+3)+J-1,T:RETURN
2 IFK=49ORMID$(A$(K+1),J,1)<>"€"THENRETURN
3 T=B:GOSUB1:K=K+1:T=Y:GOSUB1:GOSUB20:GOSUB85:RETURN
4 IFK=2ORMID$(A$(K-1),J,1)<>"€"THENRETURN
5 T=B:GOSUB1:K=K-1:T=Y:GOSUB1:GOSUB20:GOSUB85:RETURN
6 IFJ=98ORMID$(A$(K),J+1,1)<>"€"THENRETURN
7 T=B:GOSUB1:J=J+1:T=Y:GOSUB1:GOSUB20:GOSUB85:RETURN
8 IFJ=1ORMID$(A$(K),J-1,1)<>"€"THENRETURN
9 T=B:GOSUB1:J=J-1:T=Y:GOSUB1:GOSUB20:GOSUB85:RETURN
10 IFX>1THENX=X-1:GOTO20
11 RETURN
12 IFX<36THENX=X+1:GOTO20
13 RETURN
14 IFV<67THENV=V+1:GOTO20
15 RETURN
16 IFV>1THENV=V-1:GOTO20
17 RETURN
20 ?@.,MID$(A$(X),V,Z)MID$(A$(X+1),V,Z)MID$(A$(X+2),V,Z)MID$(A$(X+3),V,Z)MID$(A$(X+4),V,Z)MID$(A$(X+5),V,Z);
21 ?MID$(A$(X+6),V,Z)MID$(A$(X+7),V,Z)MID$(A$(X+8),V,Z)MID$(A$(X+9),V,Z)MID$(A$(X+10),V,Z)MID$(A$(X+11),V,Z);
22 ?MID$(A$(X+12),V,Z)MID$(A$(X+13),V,Z)MID$(A$(X+14),V,Z);:RETURN
40 ONK(PEEK(2)ANDPEEK(17023))GOSUB2,4,6,8,10,12,14,16
50 GOTO40
85 FORP=1TO3:IFMID$(A$(K+1),J,1)=Q$(P)THENW=1:ONPGOSUB110,90,90
86 IFMID$(A$(K-1),J,1)=Q$(P)THENW=2:ONPGOSUB110,90,90
87 IFMID$(A$(K),J+1,1)=Q$(P)THENW=3:ONPGOSUB110,90,90
88 IFMID$(A$(K),J-1,1)=Q$(P)THENW=4:ONPGOSUB110,90,90
89 NEXT:RETURN
90 IFP=2ANDY$="í"ORP=3ANDY$="ý"THENRETURN
91 ONWGOTO92,93,94,95
92 A$(K+1)=LEFT$(A$(K+1),J-1)+"€"+MID$(A$(K+1),J+1):GOTO100
93 A$(K-1)=LEFT$(A$(K-1),J-1)+"€"+MID$(A$(K-1),J+1):GOTO100
94 A$(K)=LEFT$(A$(K),J)+"€"+MID$(A$(K),J+2):GOTO100
95 A$(K)=LEFT$(A$(K),J-2)+"€"+MID$(A$(K),J):GOTO100
100 IFP=2THENY$="í":Y=237:GOTO102
101 Y$="ý":Y=253
102 SS=SS+1:PRINT@498,"SCORE:"SS;:GOSUB108:A$(K)=LEFT$(A$(K),J-1)+Y$+MID$(A$(K),J+1):GOSUB20:RETURN
108 FORT=1TO5:SOUND176,1:SOUND193,1:SOUND204,1:SOUND218,1:NEXT:RETURN
110 U=RND(3):IFU>1THENRETURN
111 PRINT@480,"LEVEL";NN;"FLOODED";:SOUND1,10
112 FORN1=2TO98:IFMID$(A$(NN),N1,1)="€"THENW=VARPTR(A$(NN)):POKEPEEK(W+2)*256+PEEK(W+3)+N1-1,159
113 NEXT:GOSUB20
114 IFNN<=KORNN=2THENGOSUB2000:SS$=INKEY$:ON-(SS$<>"Y"ANDSS$<>"N")GOTO114:ON-(SS$="N")GOTO1290:RUN
115 NN=NN-1:PRINT@480,"                               ";:PRINT@498,"SCORE:";SS;:RETURN
120 CLS
130 CLEAR5200
140 PRINT@232,"PITS OF GANYMEDE";
150 A=RND(-(PEEK(9)*256+PEEK(10)))
200 DIMA$(50),Q$(3),K(255),W,J,X,K,Y,H,P,V,Z,Y$,A,B,T,C,G,F,L
210 Q$(1)=CHR$(207):Q$(2)=CHR$(255):Q$(3)=CHR$(239):Z=32
220 Y$="í":Y=237
230 K(75)=1:K(73)=2:K(76)=3:K(74)=4:K(87)=5:K(90)=6:K(83)=7:K(65)=8
260 NN=49
270 PRINT@289,"ADVENTURE STARTS IN 45 SECONDS"
280 A$(1)="¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿"
290 A$(50)=A$(1)
300 FORB=2TO49
310 A$(B)="¿¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¿¿"
320 NEXT
330 FORA=1TO5:B=1+RND(96):FORC=2TO49:A$(C)=LEFT$(A$(C),B-1)+"€€€"+MID$(A$(C),B+3):IFRND(4)=1ANDB>2THENB=B-1:GOTO390
381 IFB<46THENB=B+1
390 NEXT:NEXT
400 FORA=1TO5:B=1+RND(46)
420 FORC=2TO96STEP2:FORF=0TO2:A$(B+F)=LEFT$(A$(B+F),C-1)+"€€"+MID$(A$(B+F),C+2):NEXT:IFRND(4)=1ANDB>1THENB=B-1:GOTO480
471 IFB<47THENB=B+1
480 NEXT:NEXT
490 X=1:B=128
500 PRINT@448,"...OR THEREABOUTS."
510 G=RND(20):FORJ=2TO68:IFMID$(A$(3),J,1)="€"THENL=L+1
540 IFL=GTHENA$(3)=LEFT$(A$(3),J-1)+Y$+MID$(A$(3),J+1):GOTO580
550 NEXT
560 L=0
570 GOTO510
580 IFJ<17THENV=1:GOTO590
581 V=J-16
590 M=J
600 FORT=1TO50
610 J=RND(99):K=RND(49):IFMID$(A$(K),J,1)="€"THENA$(K)=LEFT$(A$(K),J-1)+"Ï"+MID$(A$(K),J+1):GOTO640
631 GOTO610
640 NEXT
650 FORL=1TO2
660 FORT=1TO20
670 J=RND(97)
680 K=12+RND(38)
690 IFL=1THENA=255:GOTO700
691 A=239
700 IFMID$(A$(K),J,1)="€"THENA$(K)=LEFT$(A$(K),J-1)+CHR$(A)+MID$(A$(K),J+1):GOTO710
701 GOTO670
710 NEXT:NEXT:GOSUB20
720 K=3:J=M:GOTO40
1290 END
2000 PRINT@Z,"LOST IN THE MIASMIC MIST.":PRINT"FINAL SCORE";SS:PRINT"TAP y KEY TO TRY AGAIN":RETURN
2100 REM*PITS OF GANYMEDE *
2110 REM TRS-80 MC-10 +16K
2120 REM ELMER'S ARCADE
2130 REM RICHARD RAMELLA
2140 REM hot COCO MAY 1984
2150 REM 40=HIGHEST SCORE
