0 CLS:CLEAR500:DIMC1,CC,M$:C1=RND(-(PEEK(9)*256+PEEK(10))):GOTO10
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))+16383:FORC1=CC-LEN(M$)+1TOCC:POKEC1,PEEK(C1)-64:NEXT:RETURN
10 REM MASTER ROUTINE
20 GOSUB 1000
30 CLS
40 GOSUB 1500
50 GOSUB 9000
60 YR=1:SE=1:H=0
70 GOSUB 2000
80 GOSUB 9000
90 GOSUB 2500
100 GOSUB 9000
110 GOSUB 3000
120 GOSUB 9000
130 IF SE=2 THEN GOSUB 4000
140 SE=SE+1
150 IF SE=4 AND YR=L THEN 5000
160 IF SE=4 THEN GOSUB 9900
170 GOTO 70
1000 DIM A(4,5):DIM F(4):DIM P(4)
1010 FOR X=1 TO 4
1020 FOR Y=1 TO 5
1030 A(X,Y)=(RND(0)-RND(0))/5
1040 NEXT Y
1050 P(X)=INT(RND(0)*50+1)
1060 NEXT X
1070 L=INT(RND(0)*6)+3
1080 DIM N$(4):DIM P$(4)
1090 RESTORE
1100 FOR X=1 TO 4
1110 READ I$
1120 READ J$
1130 N$(X)=I$
1140 P$(X)=J$
1150 NEXT X
1160 RETURN 
1500 PRINT:PRINTTAB(6);:M$="THE ROAD TO VALHALLA":GOSUB7:PRINT:PRINT
1520 M$="YOU HAVE EMIGRATED FROM THE KINGDOM OF NORWAY TO ICELAND. LANDING AT HELMSDALE, "
1521 M$=M$+"YOU FOUND A SETTLEMENT IN TRUE VIKING FASHION.":GOSUB1
1530 GOSUB9000
1580 M$="AFTER THE LONG, STORMY VOYAGE, YOU ARE "
1590 IF L>6 THEN M$=M$+"STILL IN THE BEST OF HEALTH.":GOSUB1
1600 IF L<=6 AND L>5 THEN M$=M$+"IN MODERATE HEALTH.":GOSUB1
1610 IF L<=5 AND L>4 THEN M$=M$+"RATHER THE WORSE FOR WEAR.":GOSUB1
1620 IF L<=4 THEN M$=M$+"IN A POOR STATE OF HEALTH.":GOSUB1
1640 M$="YOU KNOW THE TIME HAS COME TO THINK SERIOUSLY OF THE AFTER-LIFE. "
1641 M$=M$+"YOU MUST INCREASE YOUR HONOUR SO YOU CAN REACH VALHALLA WHEN THE TIME COMES.":GOSUB1
1680 M$="YOU ALTER YOUR HONOUR IN YOUR DEALINGS WITH THE OTHER FOUR CLANS OF SETTLERS, "
1681 M$=M$+"LED BY THOR, ODIN, OLAF AND LARS. YOU ALSO GAIN HONOUR BY INCREASING YOUR  WORLDLY WEALTH.":GOSUB1
1730 RETURN 
2000 PRINT 
2010 IF SE=1 THEN M$="SPRING"
2020 IF SE=2 THEN M$="SUMMER"
2030 IF SE=3 THEN M$="AUTUMN"
2040 IF SE=4 THEN M$="WINTER"
2050 M$=M$+" OF YEAR"+STR$(YR):PRINTTAB(INT((32-LEN(M$))/2));:GOSUB7:PRINT:PRINT
2070 M$="YOUR POSSESSIONS ARE:"
2080 FOR X=1 TO 4
2090 M$=M$+STR$(P(X))+" "+P$(X)
2100 NEXT X
2110 GOSUB1:PRINT 
2120 N=0
2130 FOR X=1 TO 4
2140 IF A(X,1)>0 THEN GOSUB 5500
2150 NEXT X
2160 IF N>0 THEN GOSUB 5600
2170 PRINT 
2180 RETURN 
2500 CLS
2510 PRINT "THESE ARE YOUR OPTIONS:"
2520 PRINT "1. GIVE AWAY SOME POSSESSIONS"
2530 PRINT "2. DO NOTHING"
2540 PRINT "3. STEAL SOMEBODY'S POSSESSIONS"
2550 PRINT "4. KILL SOMEBODY'S POSSESSIONS"
2560 PRINT:INPUT"WHICH DO YOU WANT";J:PRINT
2580 IF J<1 OR J>4 OR J<>INT(J)THEN 2500
2590 J=J-2
2600 N=0
2610 K=0
2620 IF J<>0 THEN GOSUB 5800
2630 IF N<>0 THEN F(N)=ABS(F(N)-J)
2640 IF J=-1 THEN GOSUB 6000
2650 IF J=1 THEN GOSUB 6100
2660 IF J=2 THEN GOSUB 6200
2670 FOR X=1 TO 4
2680 IF X=N THEN  2710
2690 A(X,1)=A(X,1)-J*K*A(X,N+1)/5
2700 H=H-SGN((A(X,1)*J*K*A(X,N+1))/2)
2710 NEXT X
2720 RETURN 
3000 FOR X=1 TO 4
3010 S=INT(RND(0)*4)+1
3020 GOSUB 6500
3030 FOR Y=1 TO 4
3040 IF T<>1 THEN A(T-1,S)=A(T-1,S)-J*K*A(Y,T)
3050 NEXT Y
3060 IF T=1 THEN F(X)=ABS(F(X)+J)
3070 NEXT X
3080 S=INT(RND(0)*4)+1
3090 GOSUB 7000
3100 RETURN 
4000 CLS
4020 M$="DO YOU WANT TO GO TO THE MOOT THIS YEAR?":GOSUB1
4030 INPUT A$:PRINT
4040 IF A$<>"" THEN IF LEFT$(A$,1)<>"Y" THEN GOSUB 9000
4050 IF A$<>"" THEN IF LEFT$(A$,1)<>"Y" THEN RETURN
4060 CLS
4070 M$="AT THE MOOT, YOU CAN SETTLE YOUR DIFFERENCES WITH ANY ONE CLAN.":GOSUB1
4090 PRINT 
4100 FOR X=1 TO 4
4110 PRINT MID$(STR$(X),2,1);". ";N$(X)
4120 NEXT X
4130 PRINT:INPUT"WHICH ONE DO YOU WANT";A:PRINT
4150 IF A<1 OR A>4 OR A<>INT(A) THEN4060
4160 F(A)=0
4170 A(A,1)=RND(0)*2
4190 M$="IT IS THE END OF THE MOOT. THE CHIEF OF THE ALTHING FROM REYKJAVIK APPROACHES YOU AND TELLS YOU THAT "
4191 M$=M$+"AS FAR AS YOUR HONOUR IS CONCERNED, YOU ARE DOING "
4240 IF INT(H)>0 THEN M$=M$+"FINE.":GOSUB1
4250 IF INT(H)=0 THEN M$=M$+"AVERAGELY WELL.":GOSUB1
4260 IF INT(H)<0 THEN M$=M$+"BADLY.":GOSUB1
4270 GOSUB 9000
4280 RETURN 
5000 FOR X=1 TO 4
5010 H=H-ABS(F(X))+P(X)/15
5020 NEXT X
5040 M$="YOU HAVE REACHED THE END OF YOUR LIFE.":GOSUB1
5050 D=1+RND(0)*2
5060 IF H>D THEN PRINT "VALHALLA WELCOMES YOU."
5070 IF H<=1 THEN PRINT "LONG MAY YOU ROT IN HELL"
5090 PRINT "DO YOU WISH TO PLAY AGAIN?"
5100 INPUT A$:PRINT
5110 IF A$<>"" THEN IF LEFT$(A$,1)="Y" THEN RUN
5120 GOTO 10000
5500 N=N+1
5510 IF N=1 THEN F$=N$(X)
5520 IF N=2 THEN F$=N$(X)+" AND "+F$
5530 IF N>2 THEN F$=N$(X)+", "+F$
5540 RETURN
5600 M$=F$
5610 IF N=1 THEN M$=M$+" HAS"
5620 IF N>1 THEN M$=M$+" HAVE"
5630 M$=M$+" SENT GOOD WISHES.":GOSUB1
5640 RETURN
5800 FOR X=1 TO 4
5810 PRINT MID$(STR$(X),2);". ";N$(X)
5820 NEXT X
5830 PRINT:INPUT"WHICH ONE DO YOU WANT";N:PRINT
5850 IF N<1 OR N>4 OR N<>INT(N)THEN N=INT(RND(0)*4)+1
5860 FOR X=1 TO 4
5870 PRINT MID$(STR$(X),2);". ";P$(X)
5880 NEXT X
5890 PRINT:INPUT"WHICH TYPE OF POSSESSION";K:PRINT
5910 IF K<1 OR K>4 OR K<>INT(K)THEN K=INT(RND(0)*4)+1
5920 RETURN 
6000 NG=INT(RND(0)*P(K)+.9)
6020 M$=N$(N)+" THANKS YOU FOR THE"+STR$(NG)+" "+P$(K)+".":GOSUB1
6030 P(K)=P(K)-NG
6040 IF A(N,1)<0 THEN H=H-1
6050 IF A(N,1)>=0 THEN H=H+7.5
6060 A(K,1)=A(K,1)+K
6070 RETURN 
6100 NS=INT(RND(0)*40+1)
6120 M$="WE HAVE STOLEN"+STR$(NS)+" OF "+N$(N)+"'S "+P$(K)+".":GOSUB1
6130 P(K)=P(K)+NS
6140 IF A(N,1)>=0 THEN H=H-(A(N,1)+.2)
6150 IF A(N,1)<0 THEN H=H+1.5
6160 A(N,1)=A(N,1)-1.5
6170 RETURN
6200 NK=INT(RND(0)*50+1)
6220 M$="WE HAVE KILLED"+STR$(NK)+" OF "+N$(N)+"'S "+P$(K)+".":GOSUB1
6230 IF A(N,1)>=0 THEN H=H-2*(A(N,1)+.5)
6240 IF A(N,1)<0 THEN H=H+2.5
6250 A(N,1)=A(N,1)-3
6260 RETURN 
6500 T=INT(RND(0)*4)+1
6510 IF T=S+1 THEN 6500
6520 D=A(S,T)+(RND(0)-RND(0))/10
6530 IF D<.01 THEN J=1
6540 IF D<-3 THEN J=2
6550 IF D>=.01 THEN RETURN 
6560 IF J=1 THEN S$="STOLE"
6570 IF J=2 THEN S$="KILLED"
6580 K=1+INT(ABS(A(S,T)))
6590 IF K>4 THEN K=4
6600 IF T=1 THEN GOSUB 7400
6610 IF T<>1 THEN GOSUB 7500
6620 M$=N$(S)+" "+S$+STR$(Q)+" OF "+W$+P$(K)+".":GOSUB1
6630 RETURN
7000 IF A(S,1)+(RND(0)-RND(0))/10<=0 THEN RETURN 
7010 Q=INT(RND(0)*50)+1
7020 K=INT(RND(0)*4)+1
7030 P(K)=P(K)+Q
7040 M$=N$(S)+" SENDS YOU"+STR$(Q)+" "+P$(K)+".":GOSUB1
7050 F(S)=F(S)-1
7060 RETURN 
7400 Q=INT(RND(0)*P(K)+.9)
7410 P(K)=ABS(P(K)-Q)
7420 W$="YOUR "
7430 RETURN 
7500 Q=INT(RND(0)*50)+1
7510 W$=N$(T-1)+"'S "
7520 RETURN 
9000 PRINT:PRINTTAB(4);:M$="PRESS A KEY TO CONTINUE":GOSUB7
9020 A$=INKEY$:IF A$="" THEN 9020
9030 CLS
9040 RETURN 
9500 DATA "THOR","SHEEP"
9510 DATA "ODIN","CATTLE"
9520 DATA "OLAF","HORSES"
9530 DATA "LARS","SERFS"
9900 SE=1
9910 YR=YR+1
9920 RETURN
9930 REM CONVERTED FROM
9940 REM SPECTRUM TO NEWBRAIN
9950 REM BY COLIN APPLEBY
9960 REM AND CHRIS DESPINIDIS
9970 REM 8 MAY 2016
9980 REM MC-10 EDITS JIM GERRIE
10000 END
