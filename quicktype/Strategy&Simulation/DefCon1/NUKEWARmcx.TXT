10 '             WARGAME
20 '
30 '           BY TOM WEBER
40 CLS:GOSUB2020:GOTO90
50 L=LEN(A$):A=0:B=0:A=A+1:D$=MID$(A$,A,1):L$=STRING$(31," ")+D$
60 PRINT@256,L$;:A=A+1:IFA>L THENA=L:IFY=0ORF1=1THEN70ELSEPSET(X,Y,1)ELSE80
70 PRINT@256,STRING$(32," "):RETURN
80 L$=RIGHT$(L$,LEN(L$)-1)+MID$(A$,A,1):SOUND200,2:GOTO60
90 SB$(1)="213778":SB$(2)="223784":SB$(3)="233790":SB$(4)="24379C":SB$(5)="3D37A8":SB=RND(5):SS=SB:RM=RND(2000)
100 UC=10000+RND(89999):CU=10000+RND(89999):PRINT@383,UC;:PRINT@415,CU;:CU$=STR$(CU)+STR$(UC):CU$=RIGHT$(CU$,11):CU$=LEFT$(CU$,5)+RIGHT$(CU$,5):FORSZ=1TO10:OC$(SZ)=MID$(CU$,SZ,1):OC(SZ)=10:NEXT:SU=10:US=10:TIMER=0:DIMX1(50),Y1(50),X2(50),Y2(50)
101 GOTO340
110 'COMPUTER ARC DRAW
120 GOSUB1370:FORX=0TO50:X1(X)=0:Y1(X)=0:NEXT:R=0:ZM=0:F=0:PMODE1,1:SCREEN1,0:XX=X1:XO=X1:YO=Y1:IFX2<X1 THENR=1:X=X1:Y=Y1:X1=X2:Y1=Y2:X2=X:Y2=Y:XO=X2:YO=Y2
130 X1(0)=X1:Y1(0)=Y1:XF=(X1+X2)/2:YF=(Y1+Y2)/2:D=(Y1-Y2):AOS=XF:XV=AOS:YV=(YF+D)/2
140 GOSUB990:IFZM>50THENZM=0
141 IFZM<0THENZM=50
145 XM=(X1+XV)/2:YM=(Y1+YV)/2:XN=(X1+XM)/2:YN=YM:X1(ZM)=X1:Y1(ZM)=Y1:IFF=0THENZM=ZM+1ELSEZM=ZM-1
150 IFINT(XN)=INT(XV)-2THEN180
160 IFINT(XN)=INT(XV)+3 THEN190
170 X1=XN:Y1=YN:GOTO140
180 ZM=ZM-1:IFZM<0THENZM=0
181 IFZM>47THENZM=47
185 F=1:X1(ZM)=XV:Y1(ZM)=YV:X1(ZM+1)=XV:Y1(ZM+1)=YV:X1(ZM+2)=XV:Y1(ZM+2)=YV:X1(ZM+3)=XV:Y1(ZM+3)=YV:ZM=ZM*2:X1=X2:Y1=Y2:GOTO140
190 F=0:ZM=ZM*2:IFR=1THEN260
200 IFZM>50THENZM=50
205 IFY1(ZM)=0THENZM=ZM-1
210 F2=1:A=-1
220 A=A+1:IFA>ZM THEN240ELSEIFX1(A)<0THENX1(A)=XO
221 IFY1(A)<0THENY1(A)=YO
225 PSET(X1(A),Y1(A),1):LINE(XO,YO)-(X1(A),Y1(A)),PSET:XO=X1(A):YO=Y1(A):IFUU=1THEN960ELSEGOSUB990:GOTO220
230 GOTO220
240 F=0:ZM=0:F2=0:RR=0:IFYO>52THEN670ELSEIFUS<1THEN1940
250 RETURN
260 '
270 IFY1(ZM)=0THENZM=ZM-1:GOTO270
280 A2=ZM+1
290 A2=A2-1:IFA2<0THEN310ELSEPSET(X1(A2),Y1(A2),1):LINE(XO,YO)-(X1(A2),Y1(A2)),PSET:XO=X1(A2):YO=Y1(A2):IFUU=1THEN960ELSEGOSUB990
300 GOTO290
310 F=0:ZM=0:RR=0:IFYO>52THEN670ELSEIFUS<1THEN1940
320 RETURN
330 'GET SOVIET COORDINATES
340 RR=1:B=RND(10)
350 ONB GOSUB470,480,490,500,510,520,530,540,550,560:IFU=1THENIFSU(B)=0THENSU=SU-1
360 IFU=1THENSU(B)=1:X4=X7:Y4=Y7:RETURN
370 X1=X7:Y1=Y7:TX=X1:TY=Y1
380 'GET U.S. COORDINATES
390 B=RND(10)
400 ONB GOSUB570,580,590,600,610,620,630,640,650,660:IFU9=1THENIFUS(B)=1THENGOSUB1870
410 IFU=1THENX5=X8:Y5=Y8:X6=X9:Y6=Y9:RETURN
420 X2=X8:Y2=Y8:X3=X9:Y3=Y9:XT=X2:YT=Y2:UX=X3:UY=Y3:IFUS(B)=0THENUS=US-1
430 US(B)=1:GOSUB110
440 PR=SU+1
450 PU=RND(PR):IFPU<>1 THENGOSUB990:GOTO450
460 GOTO340
470 X7=238:Y7=42:RETURN
480 X7=232:Y7=40:RETURN
490 X7=238:Y7=45:RETURN
500 X7=240:Y7=46:RETURN
510 X7=238:Y7=47:RETURN
520 X7=242:Y7=31:RETURN
530 X7=48:Y7=34:RETURN
540 X7=70:Y7=42:RETURN
550 X7=53:Y7=53:RETURN
560 X7=230:Y7=46:RETURN
570 X8=99:Y8=27:RETURN
580 X8=107:Y8=29:RETURN
590 X8=128:Y8=62:X9=67:Y9=140:RETURN
600 X8=132:Y8=66:X9=77:Y9=155:RETURN
610 X8=130:Y8=55:X9=76:Y9=118:RETURN
620 X8=136:Y8=61:X9=105:Y9=135:RETURN
630 X8=142:Y8=70:X9=125:Y9=159:RETURN
640 X8=154:Y8=66:X9=167:Y9=145:RETURN
650 X8=147:Y8=58:X9=141:Y9=125:RETURN
660 X8=126:Y8=54:X9=72:Y9=117:RETURN
670 'U.S. GROUND EXPLOSION
680 IFU=1THENRETURN
690 LINE((XX+X3)/2,109)-(X3,Y3),PSET:FORX=1TO4:CIRCLE(X3,Y3),X:NEXT:PLAY"V6O1L9C":IFUS<1THEN1940ELSEGOTO440
700 'PLAYER'S ARC DRAW
710 FORXL=0TO50:X2(XL)=0:Y2(XL)=0:NEXT:R2=0:ZZ=0:QQ=0:XA=X4:XI=X4:YI=Y4:IFX5<X4 THENR2=1:X=X4:Y=Y4:X4=X5:Y4=Y5:X5=X:Y5=Y:XI=X5:YI=Y5
720 X2(0)=X4:Y2(0)=Y4:XF=(X4+X5)/2:YF=(Y4+Y5)/2:D=(Y4-Y5):AOS=XF:XV=AOS:YV=(YF+D)/2
730 IFZZ>50THENZZ=0
731 IFZZ<0THENZZ=50
735 XM=(X4+XV)/2:YM=(Y4+YV)/2:XN=(X4+XM)/2:YN=YM:X2(ZZ)=X4:Y2(ZZ)=Y4:IFQQ=0THENZZ=ZZ+1ELSEZZ=ZZ-1
740 IFINT(XN)=INT(XV)-2THEN770
750 IFINT(XN)=INT(XV)+3 THEN790
760 X4=XN:Y4=YN:GOTO730
770 ZZ=ZZ-1:QQ=1:X2(ZZ)=XV:Y2(ZZ)=YV:X2(ZZ+1)=XV:Y2(ZZ+1)=YV:X2(ZZ+2)=XV:Y2(ZZ+2)=YV:X2(ZZ+3)=XV:Y2(ZZ+3)=YV:ZZ=ZZ*2:X4=X5:Y4=Y5:GOTO730
780 '
790 ZZ=ZZ*2:UU=1:IFR2=1THEN880
800 IFY2(ZZ)=0THENZZ=ZZ-1:GOTO800
810 IFU=1THENIFYU>52THENLINE(X6,Y6)-((X2(0)+X6)/2,109),PSET
820 AU=-1:FK=1
830 AU=AU+1:IFAU>ZZ THEN850ELSEIFX2(AU)<0THENX2(AU)=XI
835 PSET(X2(AU),Y2(AU),1):LINE(XI,YI)-(X2(AU),Y2(AU)),PSET:XI=X2(AU):YI=Y2(AU):GOSUB990:IFRR=1THEN970ELSEGOTO830
840 GOTO830
850 IFSU=0THEN1900
860 UU=0:FK=0:IFRR=1THEN970ELSERETURN
870 '
880 IFY2(ZZ)=0THENZZ=ZZ-1:GOTO880
890 IFU=1THENIFYU>52THENLINE(X6,Y6)-((X2(0)+X6)/2,109),PSET
900 AU=ZZ+1
910 AU=AU-1:IFAU=-1THEN930ELSEIFX2(AU)<0THENX2(AU)=XI
915 PSET(X2(AU),Y2(AU),1):LINE(XI,YI)-(X2(AU),Y2(AU)),PSET:XI=X2(AU):YI=Y2(AU):GOSUB990:IFRR=1THEN970ELSEGOTO910
920 GOTO910
930 IFSU=0THEN1900
940 UU=0:IFRR=1THEN970ELSERETURN
950 'RETURN TO OTHER'S ARC
960 IFFK=1THEN830ELSE910
970 IFF2=1THEN220ELSE290
980 '
990 'PLAYER INPUT
1000 IFLA=1THENV=RND(5):IFLL=9THENV=1:V=RND(10):OC(V)=VAL(OC$(V)):GOTO1020ELSEIFV=1THENV=RND(10):OC(V)=VAL(OC$(V)):GOTO1020ELSE1020
1010 GOTO1150
1020 LL=0:FORSZ=10TO1STEP-1:IFOC(SZ)<10THENV=OC(SZ):LL=LL+1ELSEV=RND(9)
1030 V$=STR$(V):ONSZ GOSUB1100,1110,1120,1130,1140,1050,1060,1070,1080,1090:NEXT:IFLL=10THENLA=2:GOTO1040ELSEGOTO1170
1040 A$="U.S. LAUNCH CODE RE-ACTIVATED":IFU$=""THENGOSUB50:GOTO1150ELSEFORSZ=1TOLEN(U$):PRINT@479+SZ,MID$(U$,SZ,1);:SOUND240,1:NEXT:GOSUB50:GOTO1150
1050 PRINT@383,V$;:RETURN
1060 PRINT@384,V$;:RETURN
1070 PRINT@385,V$;:RETURN
1080 PRINT@386,V$;:RETURN
1090 PRINT@387,V$;:RETURN
1100 PRINT@415,V$;:RETURN
1110 PRINT@416,V$;:RETURN
1120 PRINT@417,V$;:RETURN
1130 PRINT@418,V$;:RETURN
1140 PRINT@419,V$;:RETURN
1150 IFLA=0ANDTIMER>1000+RM THENPRINT@383,10000+RND(89999):PRINT@415,10000+RND(89999):LA=1:A$="SOVIETS HAVE JAMMED YOUR LAUNCH CODE...":GOSUB50:PRINT@480,"** CHANGES LOCKED OUT **":SOUND5,2
1160 IFLA=1THENA$="COMPUTER ATTEMPTING TO RESTORE CODE...STAND BY...":GOSUB50:PRINT@480,STRING$(30," ")
1170 GOSUB1520:IFU=1THENRETURN
1180 IFSL=1THEN1610
1190 I$=INKEY$:IFI$=""THENRETURN
1200 SOUND240,1:IFI$>"@"ANDI$<CHR$(91)ORI$>"/"ANDI$<":"ORI$="?"ORI$=CHR$(13)ORI$=CHR$(8)ORI$="-"THEN1210ELSERETURN
1210 IFI$=CHR$(8)THENIFI>0THENI=I-1:U$=LEFT$(U$,I):PRINT@480+I," ";:RETURN
1220 IFI$="?"ANDI=0THEN1310
1230 IFHP=1THENGOSUB1310
1240 IFPEEK(338)=191THENIFLA=1THENU$="":I=0:PRINT@269,"STAND BY...":FORSZ=1TO4:PRINT@480,"** ACCESS DENIED **":PLAY"V28O1L6C":PRINT@480,STRING$(20," "):FORZS=1TO150:NEXTZS,SZ:PRINT@269,"           ":PRINT@480,STRING$(20," "):RETURNELSEGOTO1270
1250 U$=U$+I$:PRINT@480+I,I$;:I=I+1:RETURN
1260 PRINT@480,STRING$(32," "):I=0:U$="":RETURN
1270 U=INSTR(U$,CHR$(8)):IFU=0THEN1280ELSEU$=LEFT$(U$,U-1)+MID$(U$,U+1):GOTO1270
1280 IFU$="JAM"THEN1350ELSEIFU$="SUBS?"THEN1390ELSEIFU$="USCON"THEN1430ELSEIFU$="SOVCON"THEN1470
1290 IFU$="TIME?"THEN1510ELSEIFLEFT$(U$,6)="LAUNCH"THENU$=MID$(U$,7):GOTO1720ELSEIFU$="DESUB"THEN1590ELSEGOSUB1260:PRINT@480,"*COMMAND INPUT ERROR*":SOUND240,1:SOUND50,1:GOSUB1260:RETURN
1300 'HELP MENU
1310 GOSUB1260:IFHP=1THEN1330
1320 PRINT@288,"USCON ":PRINT"SOVCON":PRINT"JAM   ":PRINT"SUBS? ":PRINT"DESUB ":PRINT"LAUNCH":PRINT"TIME? ":HP=1:RETURN
1330 HP=0:PRINT@288,"LAUNCH":PRINT" CODE ":PRINT"      ":PRINT"      ":PRINT"      ":PRINT"      ":PRINT@383,UC;:PRINT@415,CU;:LINE(0,109)-(48,109),PSET:LINE(0,179)-(48,179),PSET:GOSUB1260:RETURN
1340 'JAM SOVIET'S CODE
1350 GOSUB1260:IFJM>0THENA$="SOVIETS HAVE SECURED THEIR LAUNCH CODE FROM OUR SENSORS...JAMMING IS INEFFECTIVE":GOSUB50:RETURN
1360 JM=1:A$="SOVIET LAUNCH CODE JAMMED":GOSUB50:J1=0:RETURN
1370 IFJM=1THENIFJ1<350THENJ1=J1+1:GOSUB990:GOTO1370ELSEJM=2:A$="SOVIET LAUNCH CODE RE-ACTIVATED":GOSUB50:RETURNELSERETURN
1380 '# OF SUBS
1390 GOSUB1260:A$="THERE ARE"+STR$(SS)+" ENEMY SUBS REMAINING":GOSUB50:IFSS=0THENRETURN
1400 FORSZ=1TOSB:IFSB$(SZ)=""THENNEXT:RETURNELSESC=VAL("&H"+LEFT$(SB$(SZ),2)):SX=VAL("&H"+MID$(SB$(SZ),3,2)):SY=VAL("&H"+RIGHT$(SB$(SZ),2)):PRINT@263+(SZ+1)*32,CHR$(SC):LINE(SX,SY)-(SX+7,SY),PSET:LINE-(SX+8,SY-1),PSET:LINE-(SX+7,SY-2),PSET
1410 LINE-(SX+5,SY-2),PSET:LINE-(SX+5,SY-3),PSET:LINE-(SX+4,SY-3),PSET:LINE-(SX+4,SY-2),PSET:LINE-(SX,SY-2),PSET:LINE-(SX-1,SY-1),PSET:LINE-(SX-2,SY-2),PSET:LINE-(SX-2,SY),PSET:SOUND200,1:NEXT:RETURN
1420 'U.S. CONDITION
1430 GOSUB1260:A$="UNITED STATES' CONDITION: ":GOSUB50:FORSZ=294TO454STEP32:PRINT@SZ,STRING$(20," "):NEXT:UH=RND(100):UP=RND(100000000):PRINT@294,"PROJECTED KILL RATIO":PRINT@333,UH:PRINT@358,"% HOUSING DESTROYED":PRINT@394,"";
1440 FORSZ=1TO3000:NEXT:FORSZ=294TO454STEP32:PRINT@SZ,STRING$(20," "):NEXT:PRINT@294,"BASES REMAINING:":X=326:FORY=1TO10:IFUS(Y)=1THENNEXTELSEPRINT@X,Y:SOUND10,1:X=X+32:IFX=486THENX=335:NEXTELSENEXT
1450 FORSZ=1TO3000:NEXT:FORSZ=294TO454STEP32:PRINT@SZ,STRING$(20," "):NEXT:PRINT@294,"MISSLES TARGETED":PRINT@330,"AND READY":FORSZ=1TO1000:NEXT:RETURN
1460 'SOVIET CONDITION
1470 GOSUB1260:A$="SOVIET UNION'S CONDITION: ":GOSUB50:FORSZ=294TO454STEP32:PRINT@SZ,STRING$(20," "):NEXT:SH=RND(100):SP=RND(100000000):PRINT@294,"PROJECTED KILL RATIO":PRINT@333,SH:PRINT@358,"% HOUSING DESTROYED":PRINT@394,"";
1480 FORSZ=1TO3000:NEXT:FORSZ=294TO454STEP32:PRINT@SZ,STRING$(20," "):NEXT:PRINT@294,"BASES REMAINING:":X=326:FORY=1TO10:IFSU(Y)=1THENNEXTELSEPRINT@X,Y:SOUND100,1:X=X+32:IFX=486THENX=335:NEXTELSENEXT
1490 FORSZ=1TO3000:NEXT:FORSZ=294TO454STEP32:PRINT@SZ,STRING$(20," "):NEXT:PRINT@294,"MISSLES TARGETED":PRINT@330,"AND READY":FORSZ=1TO1000:NEXT:RETURN
1500 'TIME FUNCTION
1510 GOSUB1260:GOSUB1560:FORSZ=294TO454STEP32:PRINT@SZ,STRING$(20," "):NEXT:PRINT@298,"GAME TIME":PRINT@330," ELAPSED":PRINT@358,THE"HRS"TME"MIN"TSE"SEC":PRINT@394,"ESTIMATED TIME":PRINT@428,"REMAINING":PRINT@454,RH"HRS"RM"MIN"RS"SEC":FORSZ=1TO4000:NEXT:
1520 '
1530 IFTIMER>32768THENTF=1
1540 IFTIMER<32768 AND TF=1THENF4=F4+1:TF=0
1550 GOSUB1780:RETURN
1560 TS=((F4*18)*60)+FIX(TIMER/60.680555):THE=FIX(TS/60/60):TS=FIX(TS-(THE*60*60)):TME=FIX(TS/60):TS=FIX(TS-TME*60):TSE=TS:RH=4-THE:RM=59-TME:RS=60-TSE:RETURN
1570 '
1580 'SUB DESTROY MODE
1590 GOSUB1260:A$="SUBMARINE DESTROY MODE":GOSUB50
1600 XS=128:YS=144:SL=1:S1=XS:S2=YS:C=PPOINT(XS,YS)
1610 PSET(S1,S2,C):C=PPOINT(XS,YS):S1=XS:S2=YS:IFC=0THENPSET(XS,YS,1)ELSEPRESET(XS,YS)
1620 GOSUB1680:I$=INKEY$:IFPEEK(338)=191THEN1700ELSEIFPEEK(340)=191THENSL=0:PSET(XS,YS,C):RETURN
1630 IFPEEK(341)=247THENYS=YS-1:IFYS=115THENYS=116
1640 IFPEEK(342)=247THENYS=YS+1:IFYS=173THENYS=172
1650 IFPEEK(343)=247THENXS=XS-1:IFXS=54THENXS=55
1660 IFPEEK(344)=247THENXS=XS+1:IFXS=201THENXS=200
1670 RETURN
1680 FORSZ=1TOSB:IFSB$(SZ)=""THENNEXT:RETURNELSESX=VAL("&H"+MID$(SB$(SZ),3,2)):SY=VAL("&H"+RIGHT$(SB$(SZ),2)):IFXS=SX+3 AND YS=SY-1 THEN1690ELSENEXT:RETURN
1690 SOUND240,1:RETURN
1700 IFLA=1THEN1240ELSESOUND120,1:CIRCLE(XS,YS),6:IFXS=SX+3 AND YS=SY-1 THENSB$(SZ)="":SS=SS-1:LINE(XS-3,YS+6)-(XS+3,YS+6),PRESET
1710 UZ=U:U=1:B=RND(8)+2:GOSUB400:LINE(X6,Y6)-(XS,YS),PSET:U=UZ:FORX=1TO6:CIRCLE(XS,YS),X:NEXT:FORX=6TO1STEP-1:CIRCLE(XS,YS),X,0:NEXT:PLAY"V6O1L9C":U$="":U=0:GOTO1600
1720 'U.S. LAUNCH ROUTINE
1730 PRINT@480,STRING$(32," "):I=0:IFLEN(U$)<3THENU$="":GOTO1280
1740 SOUND120,1:U=INSTR(1,U$,"-"):UL$=RIGHT$(U$,LEN(U$)-U):UR$=LEFT$(U$,U-1):U=1:U9=1:B=VAL(UL$):IFB>0ANDB<11THENGOSUB350ELSEU$="":U=0:U9=0:GOTO1280
1750 B=VAL(UR$):IFB>0ANDB<11THENGOSUB400ELSEU$="":U=0:U9=0:GOTO1280
1760 U9=0:IFUS(B)=1THENRETURN
1770 UU=1:YU=Y5:U$="":X=X4:Y=Y4:X4=X5:Y4=Y5:X5=X:Y5=Y:GOSUB710:U=0:RETURN:RETURN
1780 'SUB LAUNCH ROUTINE
1790 IFSS=0THENRETURN
1800 IFJM=1THENPP=30ELSEPP=50
1810 RN=RND(PP):IFRN<>10THENRETURN
1820 FORZS=1TOSB:IFSB$(ZS)=""THENNEXT:RETURNELSESX=VAL("&H"+MID$(SB$(ZS),3,2)):SY=VAL("&H"+RIGHT$(SB$(ZS),2)):SX=SX+5:SY=SY-3
1830 UZ=U:U=1:B=RND(8)+2:GOSUB400:LINE(SX,SY)-(X6,Y6),PSET:U=UZ:IFUS(B)=0THENUS=US-1
1840 US(B)=1:FORX=1TO4:CIRCLE(X6,Y6),X:NEXT:PLAY"V6O1L9C":IFUS<1THEN1870ELSERETURN
1850 'NONEXISTENT SOVIET BASE
1860 RR=0:GOTO340
1870 'NONEXISTANT U.S. BASE
1880 IFU=1THENA$="U.S. BASE"+STR$(B)+" NO LONGER EXISTS":GOSUB50:U=0:RETURN
1890 GOTO410
1900 'SOVIET LOSE
1910 PRINT@265,"THEY LOOSE....":FORX=1TO165:CIRCLE(128,96),X:NEXT
1930 GOTO1970
1940 'U.S. LOSE
1950 PRINT@266,"YOU LOOSE....":FORX=1TO165:CIRCLE(128,96),X:NEXT
1970 CLS0:A$=" IS IT A GAME, OR IS IT REAL?         DOES IT REALLY MATTER?   ":GOSUB50:PRINT@257,A$;:PRINT@288,STRING$(32,CHR$(128));
1980 PLAY"V6O1L9C":FORSZ=1TORND(300)
1990 I$=INKEY$:IFI$<>""THEN2000ELSENEXT:GOTO1980
2000 PRINT@480,"";:STOP
2010 'DEFCON DROP
2020 Y=121:X=190
2030 Y=Y+12:IFY>169THENRETURN
2040 GOSUB2050:PAINT(211,Y-11),0,0:GOTO2030
2050 LINE(210,Y)-(253,Y),PSET:LINE-(253,Y+10),PSET:LINE-(210,Y+10),PSET:LINE-(210,Y),PSET:LINE(234,Y)-(234,Y+10),PSET:PAINT(211,Y+1),1,1:SOUNDX,1:X=X-10:RETURN
