0 CLS:PRINT"load GALTREK2":CLEAR224:DIMD(59,3),S$(59),O$(11),N$(28),L$(5),L(11),R(5),D$(1):GOTO46
1 ZZ=1:CC=32:FORCC=CCTOZZ STEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THENPRINTMID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 NEXT:M$="":RETURN
4 GOSUB1:GOTO198
5 PRINT"NOTHING":RETURN
10 RESTORE:FORI=1TO30:READM$:NEXT:IFZZ<6THENM$="THE ENTRANCE TO A TURBOLIFT":RETURN
12 IFZZ<11THENM$="A TURBOLIFT":RETURN
14 IFZZ<19THENM$="A CORRIDOR":RETURN
16 IFZZ<23THENM$="A VENTILATION DUCT":RETURN
18 IFZZ<44THENFORI=23TOZZ:READM$:NEXT:RETURN
20 IFZZ<47THENFORI=23TO44:READM$:NEXT:RETURN
22 FORI=23TO44:READM$:NEXT:FORI=47TOZZ:READM$:NEXT:IFF9=1ANDZZ=50THENM$="A CORRIDOR"
24 RETURN
25 DATAREA,GET,MOV,LOO,PRE,PUS,SAY,JUM,PUL,INV,UNB,LIG,UNL,SCA,GO ,PIC,OPE,CLO,WEA,REM,FIR,DRO,QUI,HEL,ENT,LOC,GIV,INS,DEA,ACT
26 DATASCIENCE STATION,HELM,ASTROGATION CONSOLE,COMBAT CONSOLE,COMMUNICATIONS CONSOLE,TRANSPORTER ROOM,GYMNASIUM,SICK BAY
28 DATAENVIRONMENTAL CONTROL,SCIENCE LAB,SHIP'S LIBRARY,STOREROOM,ARMORY,BRIG,HANGAR BAY,AIRLOCK,MAIN ENGINE CONTROL
30 DATAAUXILIARY ENGINE CONTROL,MAINTENANCE SHOP,THE GALILEO,LOST FOREVER IN A SANDSTORM,SANDY SURFACE OF TYCHO IV
34 DATAOASIS,ENTRANCE TO A BUILDING,TRANSPORTER ROOM OF A ROMULAN BATTLE CRUISER,ROMULAN COMMANDER'S STATEROOM
38 DATAA LARGE ROOM,CORRIDOR WITH STAIRS UP AND DOWN,THIS ISN'T A BRIG--IT'S A TORTURE CELL!,CORRIDOR WITH STAIRS UP
42 DATAROMULAN SECURITY SECTION,ROMULAN ENGINE ROOM,AN AIRLOCK,SHUTTLEBAY,ROMULAN SHUTTLECRAFT
44 DATABRIDGE,PERSONNEL SECTION,TECHNICAL DEPARTMENTS,SECURITY SECTION,ENGINEERING,A SIGN ON THE WALL
46 AT$="A TABLE AND AN OPEN VENTILATION DUCT":AB$="A BENCH AND AN OPEN VENTILATION DUCT":D$(0)="A CLOSED DOOR TO THE ":ZZ=59
48 D$(1)="AN OPEN DOOR TO THE ":GOSUB10:FORI=1TO5:READL$(I):NEXT:READA$:FORI=1TO5:S$(I)=A$:NEXT:FORI=29TO59:READS$(I):NEXT
52 DATAA TABLE,,SEVERAL CHAIRS AROUND A TABLE,,MANY BOOKS ON A TABLE,NEARLY ANYTHING REQUIRED,MANY WEAPONS ON A TABLE,
54 DATATHE SHUTTLECRAFT GALILEO,,,A WORKBENCH,MANY TOOLS ON A WORKBENCH,THE ASTROGATION CONSOLE,,,,A SANDY DESERT
56 DATATHOUSANDS OF FLOWERS,A SIGN ON THE WALL,SEVERAL ARMED ROMULAN GUARDS,MANY GUARDS WITH DRAWN BLASTERS
58 DATASEVERAL ARMED ROMULANS WHO BLAST YOU,AN ARMED ROMULAN GUARD,,,SEVERAL ROMULAN GUARDS
62 DATATHE ROMULAN HYPER-DRIVE ENGINES,,A ONE-MAN SHUTTLECRAFT,A SIMPLE CONTROL PANEL
63 L=25:C=15:BL=1:CA=1:PS=1:RG=1:S$(24)="'FIRE IMPULSE ENGINES'"
64 S$(25)="'ENGAGE WARP DRIVE'":S$(26)="'FIRE PHASERS'":FORI=1TO11:READO$(I):NEXT
65 FORI=1TO28:READN$(I):NEXT:DATAVIALS OF VACCINE,A BATTERY-OPERATED LANTERN,A TRICORDER,,,,,,,,
72 DATAVACC,LANT,TRIC,MANU,COMM,SWIT,PHAS,SUIT,WREN,BLAS,DEVI,WALL,FLOW,TABL,BENC,DOOR,ENTE,COVE,LEVE,PLAN,GUAR,SIGN,BUTT
74 FORI=1TO11:READL(I):NEXT:CLOAD*D,"GALTREK2":GOTO80:DATADUCT,SHUT,ENER,HANG,COOR,30,30,32,-1,-1,-1,-1,38,-1,-1,56
76 PRINT"hit a key"
78 A$=INKEY$:ON-(A$="")GOTO78:RETURN
80 IFL>59THENL=L-65:SOUND50,1
82 IFL=48THENVH=1
84 IFL=32THENP=1
86 IFL=37THENP=0
88 IFL=39THENS=1
90 IFL=10ORL=38THENS=0
92 IFL=30ANDVH=0THENM=1
94 IFL>48THENM=0
96 IFL=44ANDVH=1THEN244
98 IFL<19ORL>22THENID=0:GOTO102
100 ID=1
102 ZZ=L:GOSUB10:M$="LOCATION: "+M$:GOSUB1:IFPANDL<43THENM$="SPOCK, "
104 IFM=1THENM$=M$+"MCCOY, "
106 IFS=1THENM$=M$+"SCOTTY, "
108 IFPORMORSTHENM$="ALSO HERE: "+LEFT$(M$,LEN(M$)-2):GOSUB1
110 IFL=53THEN710
112 IFL=43THENM$="WANDERING UNTIL YOU DIE OF THIRST":GOTO708
114 W=0:PRINT"YOU SEE:":IFL<19ORL>22THEN120
116 IFID=1ANDQ=0THENGOSUB5:GOTO298
118 IFID=1THENPRINT"HANDHOLDS FOR CLIMBING":GOTO180
120 IFL=24ORL=25ORL=26THENPRINT"A BUTTON LABELLED ":W=1
122 IFS$(L)<>""THENM$=S$(L):GOSUB1:W=1
124 IFL=17THENW=1:PRINTD$(DE)"NORTH"
126 IFL=38THENW=1:PRINTD$(DE)"SOUTH"
128 IFL=54THENW=1:PRINTD$(DR)"WEST"
130 IFL=57THENW=1:PRINTD$(DR)"EAST"
132 FORI=1TO11:IFL(I)=LTHENPRINTO$(I):W=1
134 NEXT
136 IFL=56ANDL(11)=56THENPRINT"A CLOAKING DEVICE IS INSTALLED":IFCA=1THENPRINT" AND ACTIVATED"
138 IFL=56ANDL(11)ANDRG=1THENM$="AND IS WATCHED BY AN ARMED GUARD":GOSUB1
140 IFL>5ORRO=0THEN150
142 IFR(L)=1THENPRINT"AN ARMED ROMULAN GUARD":W=1:GOTO180
144 IFR(L)=0THEN150
146 PRINT"TOO SLOW, CAPTAIN!"
148 PRINT"THE ROMULAN GUARD BLASTED YOU!":GOTO710
150 IFL<>49THEN168
152 L(8)=57:O$(8)="A SPACE SUIT"
154 IFWS=1THENPRINT"YOUR SPACE SUIT IS REMOVED AND":WS=0
156 HA=0:FORI=1TO11:IFL(I)=0THENHA=1:I=11
158 NEXT
160 IFHA=0THEN166
162 M$="YOU ARE TOLD TO DROP ALL YOU ARE CARRYING":GOTO4
164 PRINT"YOU DID NOT FOLLOW INSTRUCTIONS":GOTO148
166 M$="YOU ARE TO BE TAKEN TO THE ROMULAN COMMANDER":GOSUB1:GOSUB76:L=50:RC=1:GOTO80
168 IFL=51ORL=55OR(L=50ANDRC=2)THENM$="THEY OVERPOWER YOU AND TAKE YOU TO THE BRIG":GOSUB1:L=53:GOSUB76:GOTO80
170 ON-(L<>50ORRC<>1)GOTO178:M$="THE ROMULAN COMMANDER SAYS-- WE FINALLY MEET, CAPTAIN KIRK! "
172 M$=M$+"I HAVE SENT TROOPS TO CAPTURE THE ENTERPRISE!":GOSUB1
174 GOSUB76:M$="YOUR CREW ARE NOW MY PRISONERS!":GOSUB1:GOSUB76
176 M$="GUARD! TAKE THE CAPTAIN TO THE BRIG FOR INTERROGATION":GOSUB1:GOSUB76:F9=1:RC=2:L=52:GOTO80
178 IFW=0THENGOSUB5
180 PRINT"EXITS: ";:B=0:FORI=0TO3:A=D(L,I)
182 IFA=0THEN196
184 IFA>127THENPRINT"D ";:B=1:GOTO196
186 IFA>63THENPRINT"U ";:B=1:GOTO196
188 IFI=0ANDA>0THENPRINT"N ";:B=1
190 IFI=1ANDA>0THENPRINT"E ";:B=1
192 IFI=2ANDA>0THENPRINT"S ";:B=1
194 IFI=3ANDA>0THENPRINT"W";:B=1
196 NEXT:PRINT:IFB=0THENPRINT"NONE"
198 INPUT"COMMAND";A$:PRINT:IFA$=""THEN80
202 IFL=52ANDLEFT$(S$(52),4)="AN A"THENON-(LEFT$(A$,9)<>"JUMP GUAR")GOTO146:GOTO510
204 IFRG=1ANDL=56ANDL(11)=56ANDCA=1THENON-(LEFT$(A$,3)<>"FIR")GOTO146:GOTO678
206 IFL=49ANDHA=1THEN256
208 IFLEN(A$)>1THEN256
210 IFA$="D"THENB=128:GOTO216
212 IFA$="U"THENB=64:GOTO216
214 GOTO220
216 FORI=0TO3:IFD(L,I)>BTHENL=D(L,I)-B:I=4:NEXT:GOTO80
218 NEXT:GOTO238
220 IFA$="N"THENB=0:GOTO230
222 IFA$="E"THENB=1:GOTO230
224 IFA$="S"THENB=2:GOTO230
226 IFA$="W"THENB=3:GOTO230
228 GOTO198
230 IFQ=0ANDID=1THENM$="YOU FELL DOWN A SHAFT AND DIED!":GOTO708
232 IFQ=1THENC=C-1:IFC=0THENQ=0
234 A=D(L,B):IFA<1ORA>59THEN238
236 L=A:GOTO80
238 SOUND50,1:GOTO80
240 M$=M$+" GALILEO AND IT EXPLODES!":GOTO246
242 M$=M$+" GROUND ALL AROUND YOU!":GOTO246
244 M=0:M$="SUDDENLY, HIGH-ENERGY BEAMS STRIKE THE":ONGLGOTO240:GOTO242
246 GOSUB1:B=2500:GOSUB472:M$="YOU TAKE COVER BEHIND A PILE OF ROCKS!":GOSUB1:GOSUB472
248 M$="THEN SUDDENLY, YOU'RE TRANSPORTED!":GOSUB1
250 ED=0:DE=0:GB=1:S$(46)="THE CHARRED REMAINS OF THE GALILEO"
252 PS=0:M=0:RO=5:ED=0:FORI=1TO5:R(I)=1:NEXT:L=49:GOSUB76:GOSUB254:GOTO80
254 SOUND155,10:RETURN
256 A$=A$+"     ":V$=LEFT$(A$,3):N$="":FORI=3TOLEN(A$):IFMID$(A$,I,1)=" "THENN$=MID$(A$,I+1,4):I=255
258 NEXTI:IFV$="HEL"THEN716
260 IFV$="INV"THEN518
262 IFV$="QUI"THEN710
264 NN=0:VN=0:RESTORE:FORI=1TO30:READM$:IFV$=M$THENVN=I:I=32
266 NEXT
268 FORI=1TO28:IFN$=N$(I)THENNN=I:I=32:NEXTI:GOTO272
270 NEXTI:GOTO286
272 IFL=49ANDHA=1THENON-(V$="INV")GOTO518:ON-(V$="DRO")GOTO694:GOTO146
274 IFL=56ANDRG=1THENON-(V$="FIR"ANDN$="BLAS")GOTO678:GOTO146
276 IFL>5THEN282
278 IFR(L)=0THEN282
280 ON-(V$="FIR"ANDL(7)=0)THEN666:ON1-(V$="FIR")GOTO146:PRINT"HE BLASTS YOU!":GOTO710
282 IFVN<13ANDVN>0THENONVNGOTO300,340,358,754,416,412,474,506,512,518,528,534
284 IFVN>12THENONVN-12GOTO542,550,566,340,634,642,650,660,666,694,712,716,742,748,626,448,406,858
286 M$="I CAN'T "+A$:GOTO4
288 PRINT"I DON'T HAVE IT":GOTO198
290 PRINT"I DON'T SEE ONE HERE":GOTO198
292 PRINT"I ALREADY HAVE IT":GOTO198
294 M$="THE LANTERN BATTERIES ARE DEAD! ":IFID=1THEN298
296 GOTO4
298 M$=M$+"IT'S DANGEROUS TO MOVE IN THE DARK!":GOTO4
300 IFN$="SIGN"ANDL<6THENPRINT"IT SAYS-- DECK"L:PRINTTAB(10)L$(L):GOTO198
302 IFN$="SIGN"ANDL=48THENM$="IT SAYS-- WELCOME TO THE TYCHO IV HOSPITAL":GOTO4
304 IFN$<>"TRIC"THEN318
306 IFL(3)<>0THEN288
308 PRINT"IT REGISTERS"
310 IFL<43ANDRO>0THENPRINTRO"ROMULANS ON BOARD":GOTO198
312 IFL<43THENPRINT" ONLY THE SHIP'S CREW":GOTO198
314 IFL<49THENPRINT" STRONG PLANT LIFE"
316 GOTO198
318 IFN$<>"MANU"THEN286
320 IFL(4)<>0THEN288
322 IFL=27THENM$="SHIP'S SENSORS ARE OPERATED FROM THE SCIENCE STATION":GOTO4
324 IFL=24THENM$="THE BUTTON IS A FIRING SWITCH":GOTO4
326 IFL=28THENM$="TO ACTIVATE THE TRANSPORTER, SAY 'ENERGIZE'":GOTO4
328 IFL=31THENM$="VENTILATION DUCTS RUNS THROUGHOUT DECKS 2-5. THE BRIDGE HAS ITS OWN LIFE SUPPORT SYSTEMS":GOTO4
330 IFL=38THENM$="GIANT HANGAR DOORS PROTECT AGAINST OPEN SPACE. THEY ARE CONTROLLED BY A DEVICE ON THE WALL":GOTO4
332 IFL=40THENM$="EXPERIMENTAL DEVICES ARE TO BE INSTALLED AND TESTED HERE":GOTO4
334 IFL=25THENM$="NEVER ENGAGE WARP DRIVE WHILE ORBITING A CLASS M PLANET":GOTO4
336 IFL(4)=0THENM$="THE MANUAL DOESN'T MENTION ANYTHING ABOUT THIS AREA":GOTO4
338 GOTO288
340 IFV$="PIC"THEN348
342 IFV$<>"GET"THEN286
344 IFNN>11THEN286
346 IFL(NN)=0THEN292
348 IFN$="FLOW"THEN516
350 IFN$="DEVI"ANDCA=1THENM$="IT'S ACTIVE! YOU SUFFERED FATAL RADIATION BURNS!":GOTO708
352 IFL=56ANDN$="DEVI"THENO$(11)="THE ROMULAN CLOAKING DEVICE"
354 IFL(NN)=LTHENL(NN)=0:GOTO80
356 GOTO290
358 IFN$<>"LEVE"THEN390
360 IFL=37THENDO=1:ON-(ED=0)GOTO362:ED=0
362 IFL=58THENDO=1:ON-(RD=0)GOTO364:RD=0
364 M$="THE HANGAR BAY DOORS ARE NOW ":IFED=1THENM$=M$+"OPEN":GOTO368
366 M$=M$+"CLOSED"
368 GOSUB1:IFL=58THEN378
372 IFDE=1ANDED=1THEN384
374 IFED=1ANDWS=0THEN388
376 GOTO198
378 IFDR=1ANDRD=1THEN384
380 IFRD=1ANDWS=0THEN388
382 GOTO198
384 M$="YOU LEFT THE AIRLOCK OPEN. THE ":IFL<43THENM$=M$+"ENTERPRISE DECOMPRESSED AND THE ENTIRE CREW DIED":GOTO708
386 M$=M$+"CRUISER DECOMPRESSED. YOU DIE WHEN YOUR OXYGEN RUNS OUT":GOTO708
388 M$="UNFORTUNATELY, YOU'RE NOT WEARING YOUR SPACE SUIT! YOU'RE DEAD.":GOTO708
390 IFN$="TABL"ORN$="BENC"THEN394
392 GOTO286
394 IFL=35ANDV0=0THENS$(35)="A TABLE AND A CLOSED VENTILATION DUCT"
396 IFL=31THEND(31,2)=20:D(20,0)=31:S$(31)=AT$
398 IFL=29THEND(29,1)=19:D(19,3)=29:S$(29)=AT$
400 IFL=40THEND(40,1)=22:D(22,3)=40:S$(40)=AB$
402 IFL=41THEND(41,3)=22:D(22,1)=41:S$(41)=AB$
404 GOTO80
406 IFN$<>"DEVI"THEN286
408 IFL(11)=LTHENCA=0:M$="THE CLOAKING DEVICE IS NOW DEACTIVATED":GOTO4
410 GOTO290
412 IFN$="LEVE"THEN360
414 IFN$="TABL"ORN$="BENC"THEN396
416 IFN$<>"BUTT"THEN286
418 IFL<>24THEN438
420 IFFS=0THENPRINT"NOTHING HAPPENED":GOTO198
422 IFFS=1ANDL(6)=-2THENM$="YOU LEFT ORBIT ON IMPULSE ENGINES...":GOSUB1
424 IFFS=1ANDL(6)=-2AND(CA=0ORL(11)<>40)THENM$="BUT THE ROMULANS SEE YOU AND BLAST YOU!":GOTO708
426 IFFS=1ANDL(6)=-2ANDL(11)=40ANDCA=1THENWG=1:M$="AND HAVE CAPTURED THE ROMULAN CLOAKING DEVICE INTACT!":GOSUB1
428 IFWG=1ANDDV=0THENM$="BUT PARMELIAN FEVER SPREADS THROUGHOUT THE ENTERPRISE. EVERYONE DIES!":GOTO708
430 IFWG=1ANDM=0THENM$="BUT YOU LEFT DR. MCCOY STRANDED ON TYCHO IV!":GOSUB1:GOTO712
432 IFWG=1ANDRO>0THENM$="HOWEVER, THERE ARE STILL SOME ROMULANS HIDING ON THE ENTERPRISE":GOTO4
434 IFWG=1THENM$="THE VACCINE WORKS! YOU COMPLETED THIS ADVENTURE!":GOTO712
436 GOTO198
438 IFL=25THENM$="YOU ENGAGED THE WARP DRIVE ABOVE A CLASS M PLANET-- KILLING THE POPULATION!":GOTO708
440 IFL<>26THEN290
442 IFPL=1THENPL=0:M$="YOU ZAPPED "+PH$:GOTO4
444 M$="YOU DIDN'T LOCK ONTO A TARGET BEFORE YOU FIRED":GOSUB1
446 M$="UNFORTUNATELY, YOU JUST ZAPPED THE HOSPITAL ON TYCHO IV!":GOTO708
448 IFN$="DEVI"THEN460
450 IFN$<>"SWIT"THEN286
452 IFL<>24THENPRINT"I CAN'T INSTALL IT HERE":GOTO198
454 IFFS=1THENPRINT"IT'S ALREADY INSTALLED":GOTO198
456 IFL(6)>0THEN288
458 FS=1:L(6)=-2:PRINT"OK":GOTO198
460 IFL(11)=0AND(L=40ORL=56ORL=59)THEN464
462 GOTO198
464 M$=O$(11)+" IS SUCCESSFULLY INSTALLED IN":CD=1:L(11)=L:IFL=40THENM$=M$+" THE ENTERPRISE":GOTO4
466 IFL=56THENM$=M$+" THE ROMULAN CRUISER":GOSUB1:GOTO4
468 M$=M$+" THE SHUTTLECRAFT":GOSUB1
470 GOTO198
472 FORI=1TOB:NEXT:RETURN
474 IFN$="ENER"THEN480
476 PRINT"OK-- ";
478 B=900:GOSUB472:PRINTRIGHT$(A$,5,LEN(A$)-4):GOSUB472:GOTO198
480 PRINT"uP OR dOWN?":GOSUB78
482 IFA$="D"THEN496
484 IFA$<>"U"THEN480
486 IFL=28ANDM=0ANDPS=0THENM$="MCCOY IS STILL DOWN THERE, BUT I DON'T HAVE HIS COORDINATES":GOTO4
488 IFL=28ANDM=0ANDCE=1THENGOSUB254:M=1:M$="MCCOY IS SAFELY BEAMED ABOARD":GOTO4
490 IFL>43ANDL(5)<>0THENM$="WITHOUT A COMMUNICATOR TO PROVIDE COORDINATES, YOU CAN'T BEAM UP":GOTO4
492 IFL>43ANDL<49ANDL(5)=0THENGOSUB500:L=28:P=1:GOTO80
494 PRINT"NOTHING WAS BEAMED ABOARD":GOTO198
496 IFL=28THENGOSUB500:L=46:GOTO80
498 M$="I'M ALREADY ON THE PLANET'S SURFACE":GOTO4
500 GOSUB254:IFL(1)=0THENM$="THE SPECIAL PROPERTIES OF THE VACCINE ARE DESTROYED":GOSUB1:L(1)=30:B=2500:GOSUB472
502 RETURN
504 L=44:GOTO80
506 IFN$<>"GUAR"THEN286
508 IFL<>52THEN290
510 PRINT"YOU KNOCKED HIM OUT":L(10)=L:O$(10)="A ROMULAN BLASTER":S$(52)="AN UNCONSCIOUS GUARD":B=2500:GOSUB472:GOTO80
512 IFN$="LEVE"THEN360
514 IFN$<>"FLOW"THEN286
516 PRINT"THE FLOWER EMITS A YELLOW GAS...":B=2500:GOSUB472:M$="YOU CHOKE TO DEATH":GOTO708
518 PRINT"YOU'RE CARRYING:":A=0
520 FORI=1TO11:IFL(I)=0THENPRINTO$(I):A=1
522 NEXTI
524 IFA=0THENGOSUB5
526 GOTO198
528 IFN$<>"COVE"ORL<>35THEN286
530 IFVO=1THENPRINT"IT'S ALREADY OPEN":GOTO198
532 VO=1:D(35,2)=21:D(21,0)=35:PRINT"OK":S$(35)=AT$:B=900:GOSUB472:GOTO80
534 IFN$<>"LANT"THEN286
536 IFL(2)<>0THEN288
538 IFC<1THEN294
540 Q=1:PRINT"OK. IT'S LIT":GOTO198
542 IFN$<>"LANT"THEN286
544 IFL(2)<>0THEN288
546 IFC<1THENPRINT"IT'S ALREADY OFF":GOTO294
548 Q=0:M$="IT'S OFF":GOTO4
550 IFN$="PLAN"THEN554
552 GOTO286
554 IFL<>23THENM$="I CAN'T DO THAT HERE":GOTO4
556 IFP=0THENM$="SPOCK ISN'T HERE TO OPERATE THE CONTROLS":GOTO4
558 PS=1:PRINT"SPOCK SAYS--"
560 IFGB=1ANDM=0THENM$="CAPTAIN, WE'VE GOT DR. MCCOY'S COORDINATES":GOTO4
562 M$="MOST OF THE PLANET IS DESERT. HOWEVER, A FORM OF PLANT LIFE EXISTS IN ISOLATED COLONIES":GOSUB1
564 M$="THERE ARE NO OTHER LIFEFORMS EXCEPT FOR THE HUMANS AT THE OUTPOST":GOTO4
566 IFN$<>"SHUT"THEN574
568 IF(L=37ORL=46)ANDGB=0THENL=42:GOTO80
570 IFL=58THENL=59:GOTO80
572 GOTO286
574 IFN$<>"ENTE"THEN600
576 IFL=59THEN582
578 IFL=42THEN584
580 GOTO286
582 IFRD=0THENM$="THE ROMULAN SHUTTLECRAFT SLAMS INTO THE HANGAR DOOR AND EXPLODES":GOTO708
584 ED=0:DE=0:M$="YOU'RE COMING UP FAST ON THE ENTERPRISE":GOSUB1:B=2500:GOSUB472
586 IFGB=0THEN592
588 IFCA=1THENM$="THE ROMULANS HAVE NOT YET DETECTED YOUR ESCAPE":GOSUB1:GOTO592
590 M$="BUT THE SHUTTLE WAS DETECTED AND DESTROYED!":GOTO708
592 INPUT"COMMAND";A$:A$=A$+"        ":IFLEFT$(A$,9)="RADIO ENT"THENED=1:GOTO596
594 ED=0
596 IFED=0THENM$="YOU SLAMMED INTO HANGAR DOORS AND EXPLODED BECAUSE YOU DID NOT RADIO AHEAD":GOTO708
598 RE=1:M$="ROGER, CAPTAIN. YOU ARE CLEARED TO LAND":GOSUB1:B=2500:GOSUB472:PRINT"NICE TOUCHDOWN, CAPTAIN":GOTO198
600 IFN$<>"PLAN"THEN612
602 IFL<>42THENM$="I CAN'T, UNLESS I'M IN THE SHUTTLECRAFT":GOTO4
604 IFL=42ANDED=0THENM$="THE GALILEO SHOOTS FORWARD, SLAMS THE HANGAR DOORS AND EXPLODES":GOTO708
606 S$(46)=S$(37):S$(37)="A SHUTTLECRAFT SERVICE PLATFORM"
608 M$="YOU PROCEED TO TYCHO IV, BUT FOR UNKNOWN REASONS TOUCH DOWN A KILOMETER FROM THE HOSPITAL":GOSUB1
610 L=46:GL=1:ED=0:DE=0:GOSUB76:GOTO80
612 IFN$<>"HANG"THEN286
614 IFL=42ORL=59THEN618
616 GOTO286
618 IFED=1ANDWS=0THEN388
620 IFL=59ANDRE=1THENL=37:RE=0
622 IFL=42THENL=37
624 GOTO80
626 IFN$<>"VACC"THEN286
628 IFL(1)<>0THEN288
630 IFL<>48THENM$="WAIT UNTIL WE REACH THE HOSPITAL":GOTO4
632 PRINT"THE COLONY'S DOCTOR THANKS YOU. THE PARMELIAN FEVER CAN NOW BE STOPPED":B=2500:GOSUB472
633 PRINT"YOU MUST NOW RETURN TO THE ENTERPRISE":DV=1:L(1)=-1:GOTO198
634 IFN$<>"DOOR"THEN286
636 IFL=17ORL=38THENDE=1:D(17,0)=38:D(38,2)=17
638 IFL=54ORL=57THENDR=1:D(54,3)=57:D(57,1)=54
640 GOTO80
642 IFN$<>"DOOR"THEN286
644 IFL=17ORL=38THENDE=0:D(17,0)=0:D(38,2)=0
646 IFL=54ORL=57THENDR=0:D(54,3)=0:D(57,1)=0
648 GOTO80
650 IFN$<>"SUIT"THEN286
652 IFL(8)=-1THENPRINT"I ALREADY HAVE IT ON":GOTO198
654 IFL(8)=0THENPRINT"OK":L(8)=-1:WS=1:GOTO198
656 IFL(8)=LTHEN288
658 GOTO290
660 IFN$<>"SUIT"THEN286
662 IFWS=0THENPRINT"I'M NOT WEARING IT":GOTO198
664 PRINT"OK":L(8)=0:WS=0:GOTO198
666 IFN$<>"PHAS"THEN676
668 IFL(7)=0THENPRINT"ZZZAPP!"
670 IFL<6THENIFR(L)=1THENR(L)=0:RO=RO-1:M$="YOU GOT HIM!":GOTO4
672 IFP=1THENM$="SPOCK SAYS-- THAT WAS WASTE OF CHARGE":GOTO4
674 GOTO198
676 IFN$<>"BLAS"THEN286
678 IFL=56THENIFBL>0THENRG=0:BL=0:M$="YOU GOT HIM!":GOTO4
680 PRINT"YOUR BLASTER WAS EMPTY!":GOTO148
682 IFL>5THEN690
684 IFR(L)=1THEN686
686 IFL(10)<>0ORBL<1THENPRINT"HE KILLS YOU":GOTO710
688 PRINT"YOU GOT HIM!":R(L)=0:RO=RO-1
690 BL=0:PRINT"ZZZAPP!":B=900:GOSUB472:IFL=52THENS$(52)="A DEAD GUARD":GOTO80
692 GOTO198
694 IFN$="VACC"ANDL(1)=0THENL(1)=30:M$="THE VIALS ARE SHATTERED! THE VACCINE IS LOST!":GOTO4
696 IFN$="DEVI"ANDL(11)=0THENL(11)=-1:CA=1:CD=1:M$="THE CLOAKING DEVICE BROKE WHEN YOU DROPPED IT!":GOTO4
698 IFNN>11THEN288
700 ON1-(L(NN)=0)GOTO288:L(NN)=L
702 IFL=49THENL(NN)=-1
704 IFL=49ANDNN=7THENL(7)=35
706 GOTO80
708 GOSUB1
710 PRINT"YOUR MISSION IS A FAILURE!"
712 PRINT"TRY AGAIN (Y/N)?":GOSUB78:IFA$="Y"THENRUN
714 END
716 IFL=28ANDP=0THENM$="YOU NEED SOMEONE TO OPERATE THE CONTROLS":GOSUB1
718 IFP=0THENM$="YOU'RE ON YOUR OWN CAPTAIN!":GOTO4
720 PRINT"SPOCK SAYS--":IFL=23THENM$="SHIP'S SENSORS CAN SCAN THE PLANET":GOTO4
722 IFL=24THENM$="THE FIRING SWITCH IS BROKEN":GOTO4
724 IFL=34THEN728
726 GOTO732
728 M$=M$+"ALL THE SHIP'S STORES":GOTO4
730 O$(5)="A COMMUNICATOR":O$(6)="AN ELECTRICAL SWITCH":L(5)=34:L(6)=34:M$=M$+O$(5)+"AND "+O$(6):GOTO4
732 IFL=28THENM$="READY TO ENERGIZE, CAPTAIN":GOTO4
734 IFL=38ORL=27ORL=24ORL=28ORL=31ORL=40ORL=25THENM$="I SUGGEST WE CHECK THE SHIP'S MANUAL, CAPTAIN":GOTO4
736 IFL=33THENM$="SEE WHAT'S ON THIS TABLE":GOTO4
738 IFRO>0THENM$="I SENSE DANGER, CAPTAIN":GOTO4
740 M$="I HAVE NO SUGGESTIONS, CAPTAIN":GOTO4
742 IFN$="COOR"ANDPS=1ANDM=0ANDL=23THENCE=1:M$="SPOCK SAYS-- COORDINATES ENTERED, CAPTAIN":GOTO4
744 IFN$="COOR"ANDL=23THENPRINT"OK":GOTO198
746 GOTO286
748 IFN$<>"PHAS"THEN286
750 IFL<23ORL>27THEN286
752 INPUT"ONTO WHAT? (ONE WORD)";PH$:PL=1:B=900:GOSUB472:M$="CHEKHOV REPORTS-- PHASERS LOCKED ON, CAPTAIN":GOTO4
754 IFN$<>"DEVI"THEN764
756 IFL(11)=LORL(11)=0THEN760
758 GOTO290
760 M$="THE CLOAKING DEVICE ":IFCA=1THENM$=M$+"IS ACTIVATED":GOTO4
762 M$=M$+"IS NOT ACTIVATED":GOTO4
764 IFN$<>"SWIT"THEN776
766 IFL=24ANDFS=0THENPRINT"THE SWITCH IS BURNED OUT":GOTO198
768 IFL=25ORL=26THEN856
770 IFL(6)=0THENPRINT"IT LOOKS LIKE A NEW SWITCH"
772 IFL(6)=LTHEN288
774 GOTO290
776 IFN$<>"LANT"THEN790
778 IFL(2)=LORL(2)=0THEN782
780 GOTO786
782 PRINT"THE LANTERN IS ";:IFQ=1THENPRINT"ON"
784 PRINT"OFF"
786 IFL(2)=0ORL(2)=LTHENM$="BATTERY LIFE:"+STR$(C)+" MINUTES":GOTO4
788 GOTO290
790 IFN$<>"BLAS"THEN798
792 IFL(10)<>0THEN288
794 IFBL=1THENPRINT"ONLY ONE CHARGE LEFT":GOTO198
796 PRINT"IT'S EMPTY!":GOTO198
798 IFN$<>"TRIC"THEN806
800 IFL(3)=0THENPRINT"YEP! IT'S THE LATEST MODEL!":GOTO198
802 IFL(3)=LTHEN288
804 GOTO290
806 IFN$="MANU"THEN322
808 IFN$<>"FLOW"THEN814
810 IFL=47THENM$="THEY'RE BLUE AND SMELL SWEET":GOTO4
812 GOTO286
814 IFN$<>"TABL"THEN828
816 IFL=35ANDL(7)=-1THENL(7)=35:O$(7)="A HAND PHASER":M$="I SEE "+O$(7):GOTO4
818 IFL=33ANDL(4)=-1THENL(4)=33:O$(4)="THE SHIP'S TECHNICAL MANUAL":M$="IT'S "+O$(4):GOTO4
820 IFL=33ORL=31ORL=29THEN856
822 IFL=35THENM$="THERE ARE MANY PHASERS":GOTO4
824 IFL=29THEN856
826 GOTO290
828 IFN$<>"BENC"THEN838
830 IFL=41ANDL(9)=-1THENL(9)=41:O$(9)="A WRENCH":M$="A WRENCH AND OTHER TOOLS ARE ON THE WORKBENCH":GOTO4
832 IFL=40THEN856
834 GOTO290
836 O$(8)="TWO SPACE SUITS":GOTO844
838 IFN$<>"WALL"THEN848
840 IFL=37ORL=58THENLV=1:M$="I SEE A LEVER WHICH CONTROLS THE HANGAR DOORS":GOTO4
842 IFL=38THENON-(M=1)GOTO836:O$(8)="A SPACE SUIT"
844 IFL=38THENM$="I SEE "+O$(8):GOTO4
846 GOTO856
848 IFN$<>"DUCT"THEN856
850 IF(L=29ANDD(29,1)>0)OR(L=31ANDD(31,2)>0)OR(L=35ANDD(35,2)>0)THEN872
852 IF(L=40ANDD(40,1)>0)OR(L=41ANDD(41,3)>0)THEN872
854 M$="THERE IS NO OPEN VENTILATION DUCT HERE":GOTO4
856 M$="I SEE NOTHING OF INTEREST":GOTO4
858 IFN$<>"DEVI"THEN286
860 IFL(11)<>LTHEN290
862 IFCD=0THENPRINT"IT'S NOT INSTALLED YET!":B=2500:GOSUB472:PRINT"LOOK OUT!":GOSUB472:PRINT"IT BLEW UP!":GOTO710
864 IFL<42THENM$="THE ENTERPRISE"
866 IFL=59THENM$="THE SHUTTLECRAFT"
868 IFL>48THENM$="THE ROMULAN CRUISER"
870 CA=1:M$=M$+" NO LONGER APPEARS ON ANY SENSORS":GOTO4
872 IF(Q=1ANDL(2)=0)THENM$="THE DUCT RUNS VERTICALLY BETWEEN DECKS":GOTO4
874 GOTO298
