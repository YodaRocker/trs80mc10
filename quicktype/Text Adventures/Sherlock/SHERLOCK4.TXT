0 CLEAR450:DIMCC,ZZ,M$,N$,MC,A$(20),B$(50),TY(50):MC=16384:GOTO20
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:N$=MID$(M$,CC,1):IFN$=" "ORN$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFN$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORZZ=CC-LEN(M$)+1TOCC:POKEZZ,PEEK(ZZ)-64:NEXT:RETURN
9 PRINT"********************************":RETURN
10 PRINT@480,;:M$="HIT A KEY":GOSUB7
11 ON-(INKEY$="")GOTO11:FORCC=1TO9:PRINTCHR$(8);:NEXT:RETURN
12 TM=TM+1
13 GOSUB1:GOTO20000
20 CLS0:W=MC:X=MC:Y=MC+511:Z=MC+511
40 FORI=0TO15:POKEW+I,63:POKEX+32*I,63:POKEY-I,63:POKEZ-32*I,63:NEXTI:FORI=16TO31:POKEW+I,63:POKEY-I,63:NEXT
90 PRINT@32*6+10,;:M$="THE SEARCH FOR":GOSUB7
100 NV=NV+1:READA$(NV):IFA$(NV)="EV"THENNV=NV-1:GOTO115
110 GOTO100
115 FORWA=1TO400:NEXTWA
117 PRINT@8*32+9,;:M$="SHERLOCK  HOLMES":GOSUB7
120 NM=NM+1:READB$(NM)
121 IFB$(NM)="PIPE"THEN H(1)=NM
122 IFB$(NM)="TOBACCO" THEN H(2)=NM
123 IFB$(NM)="MATCHES"THENH(3)=NM
125 IFB$(NM)="EM"THENNM=NM-1:GOTO300
130 GOTO120
300 CLS:PRINTTAB(10)"GAME RULES":M$="ALL COMMANDS MUST BE TWO WORDS, TYPED IN UPPER CASE. "
320 M$=M$+" FIRST WORD MUST BE FROM 'VERBS' LIST. SECOND MUST BE FROM 'MODIFIERS' LIST.":GOSUB1
350 M$="WORDS SEPARATED BY A SINGLE SPACE. WORD LIST MAY BE VIEWED BY TYPING 'GO LIST' AT INPUT PROMPT.":GOSUB1
380 M$="TO REVIEW RULES, TYPE 'GO RULES'. DURING PLAY, YOU MAY CARRY AT MOST FOUR ITEMS AT ONE TIME.":GOSUB1
400 GOSUB10:CLS:PRINTTAB(10)"VERBS"
410 K=0:FORI=1TONV:K=K+1:PRINTA$(I)TAB(10*K);:IFK=3THENK=0:PRINT
420 NEXTI:GOSUB10:CLS:PRINTTAB(10)"MODIFIERS"
440 K=0:FORI=1TONM:K=K+1:PRINTB$(I)TAB(10*K);:IFK=3THENK=0:PRINT
450 NEXTI:GOSUB10:CLS
990 GA=1:M$="THE SEARCH FOR SHERLOCK HOLMES YOUR NAME IS TOBY STUBBINS. IT'S THE "
1010 M$=M$+"YEAR 1893 AND YOU LIVE IN THE BACK ALLEYS OF THE TEEMING METROPOLIS THAT "
1030 M$=M$+"IS LONDON, ENGLAND.":GOSUB1:M$="YOU'VE BEEN HOMELESS FOR ALL OF YOUR FIFTEEN YEARS, "
1050 M$=M$+"BUT YOUR LIFE IS NOT AS HOPELESS AS THOSE AROUND YOU, FOR YOU ARE A BAKER "
1070 M$=M$+"STREET IRREGULAR.":GOSUB1
1080 M$="YOU ARE THE WILY EYES AND EARS OF THE GREATEST DETECTIVE "
1090 M$=M$+"WHO EVER LIVED - SHERLOCK HOLMES.":M$="AS A LIFELONG INHABITANT OF THE BLEAK SIDE "
1110 M$=M$+"OF LONDON, YOU CAN ACCOMPLISH THINGS THAT NO ORDINARY LONDONER CAN.":GOSUB1:GOSUB10:CLS
1130 M$="YOU ARE THE CRUCIAL LINK BETWEEN THE DARK UNDERWORLD AND THE MAN WHO HAS SOLVED "
1150 M$=M$+"MORE 'UNSOLVABLE' CRIMES THAN ANY OTHER PERSON IN HISTORY.":GOSUB1
1200 M$="BUT THIS MORNING, TRAGEDY STRUCK IN THE MOST UNLIKELY MANNER.":GOSUB1
1220 M$="AS YOU APPROACHED NUMBER 221B BAKER STREET, YOU WERE STUNNED BY A COMMOTION THE LIKES YOU'D "
1240 M$=M$+"NEVER SEEN BEFORE. PEOPLE, HORSES, CARRIAGES, AND POLICE WERE ALL IN FRIGHTFUL STIR.":GOSUB1
1260 GOSUB10:CLS:M$="  YOU FOUGHT YOUR WAY THROUGH THE THRONG AND AT LAST SPOTTED DR. WATSON, "
1280 M$=M$+"WHO WAS SPEAKING HEATEDLY WITH A YOUNG LIEUTENANT FROM THE SCOTLAND YARD."
1300 M$="WHEN HE YOU, HE WAVED WEARILY AT THE CONFUSED OFFICER "
1320 M$=M$+"(FROM WHOM HE WAS GETTING LITTLE SATISFACTION) AND RAN TO YOU.":GOSUB1
1360 M$="'THANK HEAVENS IT'S YOU, TOBY. THE MOST AWFUL THING HAS HAPPENED. IT'S HOLMES - "
1380 M$=M$+"HE'S BEEN STOLEN, ER, AH, KIDNAPPED. YES, THAT'S IT - KIDNAPPED!'":GOSUB1
1400 GOSUB10:CLS
1420 M$="'TOBY, YOU MUST FIND HIM. THE ONLY CLUE WE HAVE IS THIS SHOE. I'M SURE
1440 M$="HOLMES COULD MAKE A LOT MORE OF IT THAN I HAVE.":GOSUB1:M$="YOU SEE, IT HAS A PECULIAR TYPE "
1460 M$=M$+"OF MUD CAKED AROUND THE SOLE.":GOSUB1
1470 M$="I'M SURE ITS OWNER HAS SPENT CONSIDERABLE TIME IN THE SEWERS RECENTLY.":GOSUB1
1490 M$="I'M AFRAID THAT'S ALL I CAN MAKE OUT IN MY PRESENT STATE OF MIND. TOBY, WE MUST FIND HIM - "
1510 M$=M$+"AND QUICKLY.":GOSUB1:GOSUB10:CLS:M$="I HAVE REASON TO BELIEVE HE MAY HAVE BEEN DRUGGED. THE POLICE "
1525 M$=M$+"WILL BE OF NO HELP - I'M AFRAID YOU'RE ON YOUR OWN. PLEASE, TOBY - FIND HOLMES!'":GOSUB1:GOSUB10:CLS
1570 M$="AS YOU LOOK AROUND, YOU SEE:"
1580 M$="A PERPLEXED WATSON, HOLDING A SHOE; A HANSOM CAB WAITING PATIENTLY;":GOSUB1
1610 M$="A CLOCK SHOWING THE TIME AS 10 AM; A POUND NOTE IN WATSON'S POCKET;":GOSUB1
1630 M$="THE LANDLADY HOLDING HOLMES' PIPE; A POLICEMAN SCRATCHING HIS HEAD.":GOSUB1
1650 M$="WHAT WILL YOU DO, TOBY? YOUR POCKETS ARE EMPTY AND TIME IS RUNNING OUT.":GOSUB1:GOTO20000
3000 ONFWGOTO6900,3000,3100,6900,3200,3250,6900,3350,6900,3450,6900,6900,6900,6900,6900,6900,3800
3100 IFB$="BUTCHER"THEN4360
3105 IFB$="DRYGOODS"THEN4370
3110 IFB$="SMOKESHOP"THEN4375
3115 IFB$="SEWER"THEN4350
3148 GOTO6927
3200 IFB$="BUTCHER"THEN4200
3205 IFB$="SMOKESHOP"THEN4200
3210 IFB$="DRYGOODS"THEN4200
3248 GOTO6900
3250 IFB$="HAMMER"THENIT=3:GOTO4250
3255 IFB$="KNIFE"THENIT=3:GOTO4250
3260 IFB$="MATCHES"THENIT=2:GOTO4250
3265 IFB$="MEAT"THENIT=1:GOTO4250
3270 IFB$="PIPE"THENIT=2:GOTO4250
3275 IFB$="ROPE"THENIT=3:GOTO4250
3280 IFB$="TOBACCO"THENIT=2:GOTO4250
3298 GOTO6900
3350 IFB$="SEWER"THEN4350
3355 IFB$="221B"THEN4355
3360 IFB$="BUTCHER"THEN4360
3365 IFB$="HANSOM"THEN4365
3370 IFB$="DRYGOODS"THEN4370
3375 IFB$="SMOKESHOP"THEN4375
3380 IFB$="DUNGEON"THEN4380
3398 GOTO6900
3450 IFB$="AROUND"THEN4450
3498 GOTO6900
3800 IFB$="221B"THEN4800
3810 IFB$="SEWER"THEN4350
3848 GOTO6900
4200 IFSH=0THEN6900
4201 SH=0:M$="...YOU'RE BACK IN THE STREET.":GOTO12
4250 IFSH<>ITTHEN6955
4251 IFTY(SW)=3THEN6925
4252 IFNI=4ANDMY=5THENNI=3:GOTO4255
4253 IFNI>3THEN6910
4254 IFMY<5THEN6915
4255 M$="YOU NOW HAVE ";B$;".":GOTO6940
4350 GA=3:GOSUB9:M$="YOU'RE AT THE ENTRANCE TO THE SEWER. A TUNNEL LIES DIRECTLY AHEAD.":GOTO4910
4355 M$="YOU CAN EITHER WALK (SLOW) OR TAKE A HANSOM CAB (10 SHILLINGS)":GOTO4900
4360 M$="YOU'RE IN THE BUTCHER SHOP.":SH=1:GOTO4902
4365 IFMY<10THEN6915
4366 GA=1:PL=0:GOSUB9:M$="YOU'RE IN HOLMES' FLAT. WATSON IS STILL THERE.":RM=1:MY=MY-10:BO=0:GOTO4905
4370 M$="YOU'RE IN THE DRYGOODS SHOP.":SH=3:GOTO4902
4375 M$="YOU'RE IN THE SMOKESHOP.":SH=2:GOTO4902
4380 M$="YOU CAN'T GO DIRECTLY THERE. YOU MUST GO BY WAY OF THE SEWER.":GOTO4900
4450 IFSH>0THEN4460
4451 M$="YOU SEE SEVERAL SHOPS WHICH MAY PROVE USEFUL: A DRYGOODS, A BUTCHER AND A SMOKESHOP.":GOTO12
4460 IFSH>1THEN4470
4461 M$="YOU SEE LOTS OF MEAT! 5 SHILLINGS PER PIECE.":GOTO12
4470 IFSH>2THEN4480
4471 M$="YOU SEE PIPES, TOBACCO AND MATCHES. 5 SHILLINGS PER ITEM.":GOTO12
4480 M$="THE FOLLOWING ITEMS CATCH YOUR EYE: A HAMMER, A KNIFE, AND A ROPE."
4482 M$="THEY COST 5 SHILLINGS EACH.":GOTO12
4800 GA=1:GOSUB9:M$="YOU ARE NOW IN HOLMES' FLAT. WATSON IS LYING ON THE DIVAN.":RM=1:PL=0:BO=0:GOTO4920
4900 GOTO13
4902 TM=TM+2:GOTO13
4903 TM=TM+3:GOTO13
4904 TM=TM+4:GOTO13
4905 IFMY=0THENNI=NI-1
4906 TM=TM+5:GOTO13
4910 TM=TM+10:GOTO13
4920 TM=TM+20:GOTO13
5000 ONFWGOTO5050,5000,5100,5150,5200,5250,6900,5350,6900,5450,5550,6900,6900,6900,6900,5750,5800
5050 IFB$="POLICE"THEN6050
5055 IFB$="WATSON"THEN6055
5098 GOTO6900
5100 IFB$="ROOM"THEN6100
5105 IFB$="221B"THEN6375
5110 IFB$="DUNGEON"THEN6360
5115 IFB$="SEWER"THEN6350
5148 GOTO6900
5150 IFB$="BOOKCASE"THEN6150
5160 IFB$="CHEST"THEN6160
5170 IFB$="SHOE"THEN6170
5180 IFB$="221B"THEN6375
5198 GOTO6900
5200 IFB$="ROOM" THEN6200
5248 GOTO6900
5250 IFB$="MONEY"THEN6250
5255 IFB$="PIPE"THEN6255
5260 IFB$="SHOE"THEN6260
5265 IFB$="TOBACCO"THEN6265
5270 IFB$="LOCKET"THEN6270
5275 IFB$="MATCHES" THEN 6275
5298 GOTO6900
5350 IFB$="SEWER"THEN6350
5355 IFB$="CHEAPSIDE"THEN6355
5360 IFB$="DUNGEON"THEN6360
5361 IFB$="STAIRS"THEN6360
5365 IFB$="HANSOM"THEN6365
5370 IFB$="ROOM"THEN6370
5375 IFB$="221B"THEN6375
5385 IFB$="CHEST"THEN6385
5390 IFB$="BOOKCASE"THEN6390
5395 IFB$="POLICE"THEN6050
5396 IFB$="WATSON"THEN6396
5398 GOTO6900
5450 IFB$="AROUND"THEN6450
5498 GOTO6900
5550 IFB$="DOOR"THEN6550
5555 IFB$="CHEST"THEN6555
5598 GOTO6900
5750 IFB$="MATCHES"THEN6750
5755 IFB$="HANSOM"THEN6365
5798 GOTO6900
5800 IFB$="CHEAPSIDE"THEN6800
5805 IFB$="SEWER"THEN6350
5810 IFB$="221B"THEN6375
5848 GOTO6900
6050 M$="THE POLICE ARE TOTALLY CONFUSED!":GOTO12
6055 IFRM<>1THENM$="WATSON ISN'T MAKING MUCH SENSE.      ":GOTO12
6056 M$="WATSON SUGGESTS THE SIDE ROOM.":GOTO12
6100 IFPL<>2 THEN6370
6101 M$="IT'S DARK IN HERE.":GOTO4900
6150 IFTY(H(1))>2THEN6153
6151 M$="LOTS OF BOOKS, SHEETS OF MUSIC, SOME MATCHES, A PIPE AND SOME TOBACCO.":GOTO12
6153 M$="LOTS OF BOOKS, SHEETS OF MUSIC, SOME MATCHES AND SOME TOBACCO.":GOTO12
6160 IFPL<4THEN6955
6161 IFPL=6THENM$="NOTHING MORE OF VALUE HERE.":GOTO12
6162 M$="IT DOES NOT APPEAR TO BE LOCKED.":GOTO4910
6170 M$="YEP, IT SMELLS LIKE THE SEWER.":GOTO12
6200 M$="...YOU'RE OUT.":PL=2:GOTO12
6250 IFTY(SW)>2THEN6920
6251 IFNI>3THEN6910
6252 M$="YOU NOW HAVE 1 POUND (20 SHILLINGS).":GOSUB1:MY=20:GOTO6950
6255 IFRM<>1THEN6257
6256 IFBO<>1THEN6955
6257 IFTY(SW)>2THEN6920
6258 IFNI>3THEN6910
6259 M$="YOU HAVE HOLMES' PIPE.":GOTO6950
6260 IFTY(SW)>2THEN6920
6261 IFNI>3THEN6910
6262 M$="YOU NOW HAVE A SMELLY SHOE.":GOTO6950
6265 IFRM<>1THEN6900
6266 IFBO<>1THEN6955
6267 IFNI>3THEN6910
6269 M$="YOU NOW HAVE A SLIPPER OF TOBACCO.":GOTO6950
6270 IFPL<>5THEN6955
6271 IFNI>3THEN6910
6272 IFTY(SW)=3ORTY(SW)=4THEN6920
6273 M$="YOU NOW HAVE THE ADLER LOCKET.":PL=6:GOTO6950
6275 IFBO=0THEN6955
6276 IFNI>3THEN6910
6277 IFTY(SW)=3THENM$="YOU ALREADY HAVE SOME.":GOTO4900
6278 M$="YOU NOW HAVE HOLMES' MATCHES.":GOTO6950
6350 M$="YOU CAN'T GO DIRECTLY THERE. YOU MUST GO BY WAY OF CHEAPSIDE.":GOTO4900
6355 M$="YOU CAN EITHER WALK (SLOW) OR TAKE A HANSOM CAB (10 SHILLINGS)":GOTO4900
6360 IFSP=0THENM$="YOU CAN'T GO DIRECTLY THERE.":GOTO4900
6361 RM=0:GA=4:PL=0:BO=0:M$=".YOU'RE BACK IN THE DUNGEON.":GOTO4904
6365 IFMY<10THEN6915
6366 GA=2:PL=0:RM=0:BO=0:GOSUB9:M$="YOU'RE IN CHEAPSIDE, LONDON'S SHOPPING DISTRICT.":GOSUB1:MY=MY-10:GOTO4905
6370 IFRM<>1 THEN6900
6371 M$="THE DOOR'S CLOSED.":PL=1:BO=0:GOTO4900
6375 IFSP=0THEN6379
6376 M$="YOU ARE NOW IN HOLMES' FLAT. WATSON IS LYING ON THE DIVAN.":RM=1:PL=0:BO=0:GOTO4902
6379 M$="THE POLICE HAVE BARRED THE DOOR.":GOTO12
6385 IFPL<>3THEN6900
6386 M$="YOU'RE STANDING IN FRONT OF THE CHEST.":PL=4:GOTO4902
6390 IFRM<>1THEN6900
6391 IFPL>2THEN6955
6392 M$="YOU'RE STANDING BEFORE THE BOOKCASE.":BO=1:GOTO12
6396 IFRM<>1THEN6900
6397 M$="WATSON IS NAPPING FITFULLY.":GOTO12
6450 IFRM<>1THEN6900
6451 IFPL>2THENM$="NOTHING MUCH HERE BUT THE CHEST.":GOTO12
6452 M$="THE FOLLOWING THINGS CATCH YOUR EYE: WATSON NAPPING ON THE COUCH, A BOOKCASE WITH MANY INTERSTING ITEMS AND "
6455 M$=M$+".THE DOOR TO A SIDE ROOM.":GOTO12
6550 IFPL<>1THEN6900
6551 M$="IT'S DARK IN HERE.":PL=2:GOTO12
6555 IFPL<4THEN6955
6556 IFPL>4THENM$="NOTHING MORE OF VALUE HERE.":GOTO12
6557 M$="BINGO! UNDER SOME PAPERS IS A LOCKET.":PL=5:GOTO4900
6750 IFTY(SW)<>3THEN6935
6755 IFPL<>2THEN6900
6756 M$="YOU SEE A SMALL CHEST IN THE CORNER.":PL=3:GOTO12
6800 GA=2:PL=0:GOSUB9:M$="YOU'RE IN CHEAPSIDE, LONDON'S SHOPPING DISTRICT.":GOTO4920
6900 PRINT"TRY ANOTHER COMMAND.":GOTO4900
6905 M$="YOU'VE GOT TO BE KIDDING.":GOTO4900
6910 M$="YOU HAVE TOO MANY ITEMS.":GOTO4900
6915 M$="YOU HAVEN'T THE FUNDS FOR THAT.":GOTO4900
6920 M$="THAT'S NO LONGER AVAILABLE.":GOTO4900
6925 M$="YOU ALREADY HAVE THAT ITEM.":GOTO12
6927 M$="..LEASE BE MORE SPECIFIC.      ":GOTO4900
6930 M$="YOU CAN'T SEE MUCH IN THE DARK.":GOTO12
6935 M$="YOU DON'T HAVE ";B$;".          ":GOTO4900
6940 MY=MY-5:IFMY=0 THEN NI=NI-1
6950 TY(SW)=3:NI=NI+1:TM=TM+1:GOTO13
6955 M$="YOU CAN'T DO THAT... YET.":GOTO4900
7000 ONFWGOTO6900,7000,7100,7150,7200,6900,7300,7350,7400,7450,6900,7550,7650,7600,6900,7750,6900
7100 IFB$="TUNNEL"THEN8100
7105 IF B$="SEWER"THEN8100
7110 IFB$="DUNGEON"THEN8110
7148 GOTO6900
7150 IFB$="WALL"THEN8150
7160 IFB$="TUNNEL"THEN8150
7198 GOTO6900
7200 IFB$="TUNNEL"THEN8200
7248 GOTO6900
7300 IFB$="MEAT"THEN8300
7348 GOTO6900
7350 IFB$="DUNGEON"THEN8110
7355 IFB$="LEFT"THEN8355
7360 IFB$="RIGHT"THEN8360
7365 IFB$="STRAIGHT"THEN8365
7375 IFB$="CHEAPSIDE"THEN8375
7380 IFB$="BACK"THEN8200
7385 IFB$="TUNNEL"THEN8100
7390 IFB$="WALL"THEN8390
7398 GOTO6900
7400 IFB$="DOG"THEN8400
7448 GOTO6900
7450 IFB$="AROUND"THEN8450
7498 GOTO6900
7550 IFB$="WALL"THEN8550
7555 IFB$="DOOR"THEN8550
7598 GOTO6900
7600 IFB$="DOG"THEN8600
7648 GOTO6900
7650 IFB$="MEAT"THEN8650
7655 IFB$="KNIFE"THEN8660
7660 IFB$="HAMMER"THEN8660
7698 GOTO6905
7750 IFB$="HAMMER"THEN8750
7755 IFB$="MATCHES"THEN8755
7760 IFB$="ROPE"THEN8760
7765 IFB$="MEAT"THEN8770
7798 GOTO6905
8100 IFLI=1ANDJU=0THEN8366
8101 IFJU=0THENM$="IT'S TOO DARK TO SEE ANYTHING.":GOTO4902
8102 IFJU=1THENM$="WHICH ONE?":GOTO4900
8103 GOTO6900
8110 IFJU<>2ORWL<>1THEN6900
8112 GA=4:GOSUB9:M$="YOU'VE ENTERED THE LONG-FORGOTTEN DUNGEON OF KING HENRY VIII.":GOTO12
8150 IFJU<2THEN6900
8151 IFJU>2THENM$="IT'S SINGULARLY DULL.":GOTO12
8152 IFDD=0THENM$="THAT DOG SURE LOOKS HUNGRY.":GOTO12
8153 M$="HA! THERE APPEARS TO BE A STONE DOOR IN THE WALL. WITH SOME EFFORT, YOU MAY BE ABLE TO GET THROUGH.":EW=1:GOTO12
8200 IFJU<2THENM$="YOU'RE BACK AT THE SEWER ENTRANCE.":JU=0:GOTO4905
8202 IFJU>1THENM$="YOU'RE BACK AT THE FORK.":JU=1:GOTO4903
8203 GOTO6900
8300 IFJU<>2THEN6900
8301 M$="THE BEAST THANKS YOU, BUT HE'S STILL BLOCKING THE WAY.":TY(SW)=4:GOTO12
8355 IFJU<>1THEN6900
8356 JU=2:M$="YOU'RE WALKING DOWNHILL. THE TERRIBLE NOISE IS GETTING LOUDER BY THE MOMENT.":GOSUB1
8358 M$="IT'S THE GROWLING OF A RAVENOUS DOG, WHO IS BLOCKING YOUR WAY.":GOTO4902
8360 IFJU<>1THEN6900
8361 JU=4:M$="YOU WALK FOR TWO MINUTES ONLY TO DISCOVER YOU'VE REACHED A DEAD END.":GOTO4902
8365 IFLI=0THEN6930
8366 IFJU=0THEN8373
8367 IFJU>2THEN6900
8368 IFDD=0ANDJU=2THEN8761
8369 IFDD=1ANDJU=2THENM$="THERE'S AN INTERSTING WALL HERE.":GOTO12
8370 JU=3:M$="YOU WALK THROUGH WATER (?!?) FOR 5 MINUTES BEFORE CONCLUDING THIS IS NOT THE RIGHT TUNNEL.":GOTO4905
8373 JU=1:M$="YOU HAVE REACHED A FORK: THERE ARE TUNNELS TO YOUR LEFT, RIGHT AND FRONT.":GOTO4903
8375 IFJU=0THEN8378
8376 IFJU>0THENM$="YOU MUST FIND YOUR WAY OUT OF THESE TUNNELS FIRST.":GOTO4900
8378 GA=2:LI=0:GOSUB9:M$="YOU'RE BACK IN CHEAPSIDE.":GOTO4910
8390 IFJU<>2THEN6900
8391 IFDD=0THEN8761
8392 M$="YOU'RE STANDING AT THE WALL.":GOTO12
8400 IFJU<>2THEN6900
8401 M$="THE DOG HAS BITTEN OFF THE END OF YOUR ARM. HOLMES IS DOOMED. BUT PROF. MORIARTY HAS SOME OPENINGS!":GOTO60000
8450 IFLI=0THEN6930
8451 IFJU=0THENM$="YOU SEE A LONG TUNNEL AHEAD.":GOTO12
8452 IFJU=1THENM$="TUNNELS ARE LEFT, RIGHT AND STRAIGHT.":GOTO12
8453 IFJU>2THENM$="NOTHING REVEALING HERE.":GOTO12
8454 IFDD=0THENM$="THE DOG IS SURELY HIDING SOMETHING.":GOTO12
8455 M$="THE WALL AT THE END OF THIS TUNNEL LOOKS AWFULLY INTERESTING.":GOTO12
8550 IFEW<>1THEN6900
8551 M$="GOOD JOB, TOBY! YOU'RE THROUGH THE WALL. YOU SEE WHAT APPEARS TO BE A DUNGEON. .OANING SOUNDS ARE HEARD."
8552 WL=1:GOTO4903
8600 IFJU<>2 THEN 6900
8601 M$="HOW?":GOTO4900
8650 IFJU<>2THEN6900
8651 IFTY(SW)<>3THEN6900
8652 DD=1:M$="GOOD THINKING, TOBY! THE BEAST HAS TAKEN THE BAIT AND IS OUT OF YOUR WAY.":TY(SW)=4:NI=NI-1:GOTO12
8660 IFTY(SW)<>3THEN6900
8661 IFJU<>2THEN6905
8662 M$="YOU PANIC AND MISS MATCH IT...":GOSUB1:
8663 M$="HE'S ATTACKING. YOU TRY TO RUN, BUT YOU STUMBLE AND BECOME THE POOR THING'S FIRST MEAL IN WEEKS. "
8666 M$=M$+"POOR HOLMES.":GOTO60000
8750 IFTY(SW)<>3THEN6935
8751 IFJU<>2THEN6900
8752 IFDD=0THENM$="HOW CRUEL CAN YOU GET?!":GOTO4900
8753 M$="TOO SMALL TO BE EFFECTIVE.":GOTO4902
8755 IFTY(SW)<>3THEN6935
8756 IFLI=1THENM$="- THE MORE LIGHT THE BETTER.":GOTO12
8757 LI=1:M$="OH! THAT'S BETTER. YOU MAY PROCEED.":GOTO12
8760 IFTY(SW)<>3THEN6935
8761 M$="VALIANT TRY, BUT THE DOG HAS FOUND YOUR NECK IRRESISTIBLE. YOU'RE DONE FOR. WATSON WILL NEVER FORGIVE YOU.":GOTO60000
8770 IFJU<>2THEN6900
8771 GOTO6927
8900 GOTO12000
9000 ONFWGOTO9050,9000,9100,6900,9200,9250,9300,9350,9400,9450
9010 ONFW-10 GOTO9550,9500,9650,9600,9700,9750,6900
9050 IFB$= "TROLL" THEN10050
9060 IFB$= "HOLMES" THEN10060
9098 GOTO6900
9100 IFB$="CELL"THEN10100
9105 IFB$="SEWER"THEN10350
9148 GOTO6900
9200 IFB$="DUNGEON" THEN10200
9205 IFB$="STAIRS" THEN10385
9248 GOTO6900
9250 IFB$="KEYS"THEN10250
9298 GOTO6900
9300 IFB$="MONEY" THEN10300
9305 IFB$="LOCKET" THEN10305
9348 GOTO6900
9350 IFB$="SEWER"THEN10350
9355 IFB$="STAIRS"THEN10355
9370 IFB$="221B"THEN10370
9375 IFB$="CELL"THEN10375
9380 IFB$="TROLL" THEN 10380
9385 IFB$="UP" THEN10385
9390 IF B$="HOLMES" THEN10100
9398 GOTO6900
9400 IFB$="TROLL" THEN 10400
9405 IFB$="CHAIN" THEN 10405
9448 GOTO6900
9450 IFB$="AROUND"THEN10450
9498 GOTO6900
9500 IFB$="COVER"THEN10560
9548 GOTO6900
9550 IFB$="DOOR"THEN10550
9555 IFB$="CELL"THEN10550
9560 IFB$="COVER"THEN10560
9598 GOTO6900
9600 IFB$="TROLL"THEN10600
9648 GOTO6900
9650 IFB$="KNIFE"THEN 10650
9655 IFB$="HAMMER"THEN 10650
9700 IFB$="CHAIN"THEN10700
9705 IFB$="DOOR"THEN10700
9710 IFB$="CELL"THEN10700
9748 GOTO6900
9750 IFB$="HAMMER"THEN10750
9755 IFB$="ROPE"THEN10755
9760 IFB$="KNIFE"THEN 10755
9798 GOTO6905
10050 M$="THE TROLL HAS NO TONGUE. HIS GESTURES SADLY HAVE NO MEANING FOR YOU.":GOTO12
10060 M$="HOLMES SAYS ONLY ONE THING WILL SATISFY THE TROLL: A GOLD LOCKET WITH A PICTURE OF IRENE ADLER, WHICH "
10063 M$=M$+"IS SOMEWHERE IN HOLMES' FLAT. HE IS TOO WEAK TO SAY ANY MORE.":GOTO12
10100 IFSR=1THEN6900
10101 IFCE<2THEN6955
10102 M$="YOU'RE IN THE CELL. YOU RUSH TO HOLMES' SIDE AND FIND THAT HE IS STILL ALIVE.":GOTO12000
10200 IFSR=0THEN10350
10201 IFSR<>2THENM$="YOU'RE ALMOST THERE.":GOTO4900
10202 SP=1:GOSUB9:M$="IT'S INCREDIBLE! YOU'RE ON BAKER STREET, STANDING NEAR 221B.":GA=1:GOTO12
10205 GOTO6900
10250 IFTY(SW)=3ORTY(SW)=4THEN6920
10251 IFTR=0THEN6900
10252 IFLK=0THENM$="THE TROLL'S PRICE HAS NOT BEEN MET.":GOTO4900
10253 M$="THE MELANCHOLY TROLL QUIETLY HANDS OVER THE KEYS.":KY=1:GOTO12
10300 IFTR=0THEN6900
10301 M$="HE DOESN'T WANT MONEY.":GOTO12
10305 IFTR=0THEN6955
10306 IFTY(SW)<>3THEN6935
10307 LK=1:M$="HE LOOKS AT THE PICTURE AND TEARS COME TO HIS EYES. SHE WAS THE ONLY WOMAN WHO EVER LOVED HIM."
10308 NI=NI-1:TY(SW)=4:GOTO12
10350 TR=0:SR=0:M$="MUCH TO YOUR DISMAY, THE WALL HAS CLOSED BACK UP AND CAN NOT BE MOVED. "
10352 M$=M$+"BUT NEVER FEAR - THERE IS A WAY OUT.":GOTO12
10355 TR=0:SR=1:M$="YOU'RE AT THE FOOT OF THE STAIRS.":GOTO12
10370 IFSP=1THEN4800
10371 M$="THERE'S NOT ENOUGH TIME TO GO BACK THE WAY YOU CAME. .HE TROLL IS GETTING IMPATIENT.":GOTO4900
10375 SR=0:TR=0:IFCE=0THENM$="THAT CHAIN LOOKS PRETTY STRONG.":GOTO12
10376 IFCE=1THENM$="THE CHAIN IS OFF THE CELL DOOR.":GOTO12
10377 GOTO10102
10380 SR=0:TR=1:M$="YOU'RE STANDING BEFORE THE TROLL.":GOTO12
10385 IFSR<>1THEN6955
10386 M$="YOU'VE REACHED THE TOP AND FIND A MANHOLE COVER. BEYOND IT, YOU HEAR STREET SOUNDS. VERY ENCOURAGING.":GOTO4902
10400 IFTR=0THENM$="YOU'RE TOO FAR AWAY.":GOTO4900
10401 M$="THERE'S NO NEED FOR VIOLENCE.":GOTO4900
10405 M$="WITH WHAT?":GOTO4900
10450 IFSR=1ORCE>0THEN10459
10451 M$="YOU SEE A CELL WITH A LOCKED CHAIN AROUND ITS BARS. INSIDE IS THE BARELY CONSCIOUS FIGURE OF SHERLOCK HOLMES.":GOSUB1
10454 M$="IN A NEARBY CORNER SITS A MENACING TROLL. HE HAS THE KEYS TO THE CELL, BUT YOU DON'T KNOW HIS PRICE. YOU ALSO "
10457 M$=M$+"SEE A SET OF STAIRS REACHING UP OUT OF SIGHT.":GOTO4902
10459 IFCE>0THENM$="HURRY! HOLMES IS GROANING FOR YOU.":GOTO12
10460 M$="AS YOU LOOK UP, YOU SEE A CRACK OF LIGHT AT THE TOP. .OOKS LIKE A QUICK WAY OUT.":GOTO4902
10550 IFCE=0 THEN M$="THE CHAIN IS STILL LOCKED.":GOTO4900
10551 M$="THE CELL DOOR IS OPEN.":CE=2:GOTO4900
10560 IFSR=0THEN6900
10561 M$="THE COVER IS NOW OPEN AND YOU MAY EXIT THE DUNGEON.":SR=2:GOTO12
10600 IFTR=0THEN6900
10601 M$="WITH WHAT?":GOTO4900
10650 IFTY(SW)<>3THEN6935
10651 IFTR<>1THEN6900
10652 M$="HE'S TOO QUICK FOR YOU.HE'S THROWN THE KEYS AWAY. TOO BAD. BAKER ST. WILL NEVER BE THE SAME.":GOTO60000
10700 IFKY=0THEN6955
10702 M$="... THE CHAIN IS OFF THE DOOR.":CE=1:GOTO12
10750 IFTY(SW)<>3THEN6935
10751 IFTR=1THENM$="HE'S REALLY NOT SUCH A BAD FELLOW.":GOTO4900
10752 M$="IT'S TOO STRONG FOR THAT.":GOTO12
10755 IFTY(SW)<>3THEN6935
10756 IFTR=0THEN6900
10757 M$="THE TROLL WILL THROW THE KEYS DOWN A BOTTOMLESS PIT IF YOU GET ANY CLOSER.":GOSUB1:GOTO4900
12000 SN=1:IFTM>200THEN12600
12010 IFTM>150THEN12700
12020 IFTM>100THEN12800
12100 REM
12105 M$="YOU'VE DONE A MARVELOUS JOB, TOBY. THE WORLD IS GRATEFUL TO YOU FOR RESCUING SHERLOCK HOLMES IN TIME.":GOSUB1
12130 IFTY(H(1))=3ANDTY(H(2))=3ANDTY(H(3))=3THEN12200
12131 IFTY(H(1))=3ANDTY(H(2))=3THEN12210
12132 IFTY(H(1))=3THEN12220
12137 M$="TOO BAD YOU FORGOT HIS BELOVED PIPE. BUT HE'LL SURELY RECOVER ANYWAY.":GOTO60000
12200 M$="AND YOU EVEN REMEMBERED HIS PIPE, TOBACCO AND MATCHES. THESE WILL MAKE MAKE HIS DAY. JOB WELL DONE. ":GOTO60000
12210 M$="YOU EVEN REMEMBERED HIS PIPE AND TOBACCO. ALAS, YOU'VE DROPPED YOUR MATCHES. PERHAPS THE TROLL HAS SOME.":GOTO60000
12220 M$="YOU EVEN REMEMBERED HIS PIPE. ALAS, YOU BROUGHT NO TOBACCO, BUT AT LEAST HE HAS A FAMILIAR OBJECT "
12221 M$=M$+"TO CHEW ON.":GOTO60000
12600 M$="BUT, SAD TO SAY, YOU'RE TOO LATE. HOLMES HAS SUFFERED IRREPARABLE BRAIN DAMAGE. SORRY, YOUNG FELLOW, WE KNOW "
12603 M$=M$+"YOU DID YOUR BEST.":GOTO60000
12700 M$=".BUT, DEAR TOBY, DESPITE YOUR BEST EFFORTS, TIME HAS BEEN YOUR ENEMY. THE GREAT DETECTIVE IS NEAR DEATH.":GOSUB1
12703 M$="HE MAY EVENTUALLY RECOVER, BUT THE PROGNOSIS IS GRIM. GOOD TRY, LAD.":GOTO60000
12800 M$=".TOBY, YOU'RE BARELY IN TIME. HOLMES' CONDITION IS PERILOUS, BUT WITH WATSON'S HELP, HE SHOULD RECOVER TO LEAD "
12803 M$=M$+"A NORMAL LIFE.":GOTO60000
20000 A$="":B$="":INPUT"WHAT NOW";I$:LE=LEN(I$):PRINT:IFI$="QUIT"THENM$="GOODBYE":GOTO60000
20010 FORK=1TOLE:IFMID$(I$,K,1)=" "THENA$=LEFT$(I$,K-1):B$=RIGHT$(I$,LE-K):K=LE
20020 NEXTK
20030 FORI=1TONV:IFA$<>A$(I) THEN NEXTI
20040 IFI=NV+1THEN21000
20050 FW=I:I=NV:NEXTI
20060 FORJ=1TONM:IFB$<>B$(J)THEN NEXTJ
20070 IFJ=NM+1THEN21000
20080 SW=J:J=NM:NEXTJ
20200 IFA$="GO"ANDB$="LIST"THEN400
20210 IFA$="GO"ANDB$="RULES"THEN300
20300 IFA$="DROP"THENT=TY(SW):GOTO20500
20310 ONGAGOTO5000,3000,7000,9000
20500 IFT<>3THENM$="YOU DON'T HAVE THAT ITEM.":GOTO20000
20520 M$="YOU NO LONGER HAVE ";B$;".":NI=NI-1:TY(SW)=4
20525 IFB$="MONEY"THENMY=0
20530 GOTO20000
21000 M$="CHECK LIST AND/OR RULES":GOSUB1
21010 GOTO20000
30010 DATAASK,DROP,ENTER,EXAMINE,EXIT,GET,GIVE,GO,HIT,LOOK,OPEN,PUSH,THROW,TIE,UNLOCK,USE,WALK,EV
30090 DATA221B,AROUND,BACK,BOOKCASE,BUTCHER,CELL,CHAIN,CHEAPSIDE,CHEST,COVER,DOG,DOOR
30110 DATADRYGOODS,DUNGEON,HAMMER,HANSOM,HOLMES,KEYS,KNIFE,LEFT,LIST,LOCKET,MATCHES,MEAT,MONEY,PIPE,POLICE
30140 DATARIGHT,ROOM,ROPE,RULES,SEWER,SHOE,SMOKESHOP,STAIRS,STRAIGHT,TOBACCO,TROLL,TUNNEL,UP,WALL,WATSON,EM
50000 IFSN<>1THENM$="CONGRATULATIONS! YOU HAVE FREED SHERLOCK HOLMES":GOTO60000
50010 M$=YOUR TIME IS UP FRIEND"
60000 GOSUB1:GOSUB10
60005 ON-(INKEY$="")GOTO60005:GOTO50000
60010 PRINT@480,"WOULD YOU LIKE TO TRY AGAIN?";
60020 A$=INKEY$:IFA$="N"THENEND
60030 IFA$="Y"THENRUN
60040 GOTO60020
