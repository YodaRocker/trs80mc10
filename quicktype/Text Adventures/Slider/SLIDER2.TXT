0 CLEAR500
9 FE=0:I=0:ST=1000:GOSUB9100:CLS
10 PRINT"WHAT SHALL I DO ?"
14 A$=""
20 NO=0
22 EA=0
24 WE=0
26 SO=0
27 UP=0
28 DO=0
30 I$=""
35 IFST=1240THENGOSUB1240
36 IFST=1120THENGOSUB1120
37 IFST=1270THENGOSUB1270
38 IFST=1300THENGOSUB1300
39 IFST=1330THENGOSUB1330
40 IFST=1390THENGOSUB1390
41 IFST=1420THENGOSUB1420
42 IFST=1000THENGOSUB1000
45 CLS:PRINT I$
50 FO=0
52 P=0
60 GOSUB 9600
80 GOTO 100
90 IF A$="LOOK" AND NOT FOUND THEN PRINT "I CAN'T SEE ANYTHING SPECIAL"
100 REM
102 INPUT "WHAT SHALL I DO ";J$
103 IF J$="" THEN100
104 CLS 
106 A=RND(0)
107 IF ST=1240 AND A>.95 THEN PRINT "THE DOG AWOKE AND KILLED YOU": GOTO 9000
109 GOSUB 500
110 PRINT A$;" ";B$
111 IF B$="" THEN115
112 IF MID$(B$,1,1)=" " THEN B$=MID$(B$,2)
113 IF A$="TAKE" OR A$="GET" OR A$="PUT" OR A$="DROP" THEN B$=B$+"              "
114 IF B$=" " THEN112
115 IF A$="N" AND NO<>0 THEN ST=NO
120 IF A$="S" AND SO<>0 THEN ST=SO
130 IF A$="W" AND WE<>0 THEN ST=WE
140 IF A$="E" AND EA<>0 THEN ST=EA
150 IF A$="D" AND DO<>0 THEN ST=DO
155 IF A$="N" OR A$="S" OR A$="E" OR A$="W" OR A$="D" THEN20
156 IF A$="U" AND ST<>1210 AND ST<>1240 AND ST<>1450 THEN158
157 IF A$="U" THEN6000
160 IF A$="R" THEN45
170 IF A$="HELP" THEN PRINT "NO CHANCE MATE": GOTO 100
180 IF A$="TAKE" OR A$="GET" THEN2000
185 IF A$="SAVE" THEN9800
190 IF A$="PUT" OR A$="DROP" THEN4500
200 IF A$="OPEN" THEN2500
205 IF A$="FEED" THEN3500
210 IF A$="LOOK" THEN60
215 IF A$="SWIM" THEN4000
225 IF A$="LIGHT" THEN7500
230 IF A$="READ" THEN6500
240 IF A$="UNLOCK" THEN3000
250 IF A$="I" THEN GOSUB 5500: GOTO 100
260 IF A$="INSERT" THEN7000
275 IF A$="QUIT" THEN9000
280 IF A$="SAVE" THEN GOSUB 9900
298 PRINT "I CAN'T ";A$;" ";B$
490 IF LEN(A$)=1 THEN PRINT "I CAN'T": GOTO 100
499 GOTO 100
500 IF MID$(J$,1,1)=" " AND LEN(J$)>1 THEN J$=MID$(J$,2):GOTO500
502 SP=0
503 A$=""
504 B$=""
505 FOR N=1 TO LEN(J$)
507 IF MID$(J$,N,1)=" " AND NOT SP THEN SP=1:GOTO520
510 IF SP=0 THEN A$=A$+MID$(J$,N,1)
515 IF SP>0 THEN B$=B$+MID$(J$,N,1)
520 NEXT N
530 RETURN
1000 I$="YOU ARE IN A FOREST. THERE ARE  PATHS LEADING NORTH AND SOUTH."
1010 NO=1390
1015 SO=1030
1020 RETURN 
1030 I$="YOU ARE IN A FOREST, WITH PATHS LEADING NORTH AND EAST AND A    CLEARING TO THE WEST."
1035 NO=1000
1040 EA=1090
1045 WE=1060
1050 RETURN
1060 I$="YOU ARE IN A CLEARING. IN THE   CENTRE IS A TIME MACHINE WHICH  LACKS A POWER SOURCE. EXIT EAST."
1070 EA=1030
1080 RETURN 
1090 I$="YOU ARE IN A FOREST. THERE IS A PATH RUNNING EAST-WEST."
1100 WE=1030
1105 EA=1120
1110 RETURN 
1120 I$="YOU ARE IN THE FOREST. THROUGH  A GAP TO THE NORTH YOU CAN SEE ALAKE. "
1121 I$=I$+"THE PATH RUNS EAST-WEST   AND THERE IS A CAVE TO THE SOUTH"
1125 EA=1300
1130 WE=1090
1135 SO=1150
1140 RETURN 
1150 I$="YOU ARE IN A SMALL CAVE.        DAYLIGHT IS TO THE NORTH. "
1151 I$=I$+"THERE IS A DOOR TO THE WEST AND AN    OPENING TO THE SOUTH."
1160 SO=1180
1165 NO=1120
1170 RETURN 
1180 I$="YOU ARE IN A LARGE CAVERN. THEREIS A CHEST LYING IN ONE CORNER, "
1181 I$=I$+"AND A SKELETON IN ANOTHER. THE  ONLY EXIT IS TO THE NORTH."
1190 NO=1150
1200 RETURN 
1210 I$="I CAN'T SEE A THING."
1220 UP=1150
1230 RETURN 
1240 I$="YOU ARE IN A LONG PASSAGE. YOU  CAN HEAR THE SOUND OF DRIPPING  WATER. "
1241 I$=I$+"TO THE NORTH IS A FLIGHT OF STAIRS WITH A LARGE DOG      SLEEPING ON THE BOTTOM STEP."
1250 SO=1210
1260 RETURN 
1270 I$="YOU ARE ON AN ISLAND IN THE     CENTRE OF THE LAKE. THERE IS A  HOLE IN THE GROUND WITH A STAIR-CASE IN IT."
1280 DO=1240
1290 RETURN 
1300 I$="YOU ARE BESIDE A LAKE. THERE IS AN ISLAND IN THE MIDDLE OF THE  LAKE. "
1301 I$=I$+"TO THE WEST IS THE FOREST AND TO THE NORTH IS A ROCKERY."
1310 NO=1330
1315 WE=1120
1320 RETURN 
1330 I$="YOU ARE IN A ROCKERY. THE LAKE  IS TO THE WEST, AND THERE ARE   SOME REEDS TO THE NORTH."
1340 NO=1360
1345 SO=1300
1350 RETURN 
1360 CLS 
1365 PRINT "I HAVE FALLEN INTO A SWAMP      GLUG.....GLUG.....GLUG........"
1370 GOTO 9000
1390 I$="YOU ARE IN A SHRUBBERY. THE     FOREST IS TO THE SOUTH. THE LAKEIS TO THE EAST, AND MEADOW IS   TO THE NORTH."
1400 NO=1420
1405 SO=1000
1410 RETURN 
1420 I$="YOU ARE IN A MEADOW. THE LAKE ISTO THE SOUTH-EAST. TO THE SOUTH IS THE SHRUBBERY AND TO THE EASTIS A TOWER."
1430 EA=1450
1435 SO=1390
1440 RETURN 
1450 I$="YOU ARE AT THE FOOT AF A TOWER. THERE IS NO DOOR TO BE SEEN. "
1451 I$=I$+"TO THE WEST IS THE MEADOW AND TO   THE EAST ARE SOME REEDS."
1455 EA=1360
1460 WE=1420
1465 UP=1480
1470 RETURN 
1480 I$="YOU ARE AT THE TOP OF THE TOWER.THERE IS NO DOOR UP HERE EITHER."
1490 DO=1450
1500 RETURN 
2000 IF I>=5 THEN PRINT "I CAN'T CARRY ANY MORE.": GOTO 100
2010 IF B$="" THEN PRINT A$;" WHAT ?": INPUT B$: IF B$="" THEN100
2020 FO=0
2025 FOR N=1 TO 12
2030 IF I(N)<>ST THEN2055
2033 IFLEFT$(O$(N)+"                ",15)<>LEFT$(B$,15)THEN2055
2035 PRINT "OK..."
2040 FO=1
2045 I(N)=0
2050 I=I+1
2055 NEXT N
2060 IF FO=0 THEN499
2065 GOTO 100
2500 IF B$="" THEN PRINT "OPEN WHAT ?": INPUT B$: IF B$="" THEN2500
2511 CLS 
2515 IF B$="DOOR" AND ST<>1150 THEN PRINT "WHAT DOOR": GOTO 499
2525 IF B$="DOOR" AND ND=0 THEN PRINT "IT'S LOCKED DUMBO...": GOTO 499
2535 IF B$="DOOR" THEN PRINT "THERE IS A STAIR GOING DOWN.": DO=1210: GOTO 100
2550 IF B$="CHEST" AND ST<>1180 THEN PRINT "I SEE NO CHEST.": GOTO 100
2560 IF B$="CHEST" AND NOT NC THEN PRINT "IT'S LOCKED": GOTO 100
2570 IF B$="CHEST" THEN PRINT "OK.": PRINT "THERE IS NOHING IN THE CHEST.": GOTO 100
2585 GOTO 499
3000 IF B$="" THEN PRINT "UNLOCK WHAT ?": INPUT B$: IF B$="" THEN3000
3011 CLS 
3013 IF MID$(B$,1,1)=" " THEN B$=MID$(B$,2)
3015 IF B$="DOOR" AND ST<>1150 THEN PRINT "WHAT DOOR ?": GOTO 100
3025 IF I(7)>0 THEN499
3030 IF B$="DOOR" THEN ND=1: PRINT "CLICK": GOTO 100
3045 IF B$="CHEST" AND ST<>1180 THEN PRINT "WHAT CHEST ?": GOTO 100
3055 IF B$="CHEST" THEN NC=1: PRINT "CLUNK": GOTO 100
3070 GOTO 499
3500 IF B$="" THEN PRINT "FEED WHAT ?": INPUT B$: IF B$="" THEN3500
3515 IF B$<>"DOG" THEN PRINT "DONT BE SILLY !": GOTO 499
3525 IF ST<>1240 THEN PRINT "THERE IS NO DOG HERE.": GOTO 100
3535 IF I(12)>0 THEN PRINT "I HAVE NO DOG FOOD.": GOTO 100
3545 I=I-1
3550 FE=1
3555 UP=1270
3560 PRINT "MUNCH,MUNCH"
3565 I(12)=1180
3570 GOTO 100
4000 IF ST=1120 OR ST=1270 OR ST=1300 OR ST=1330 OR ST=1390 OR ST=1420 THEN PRINT "BRRR. THE WATER IS COLD.": GOTO 100
4010 PRINT "THERE IS NO WATER HERE."
4015 GOTO 100
4500 IF B$="" THEN PRINT "DROP WHAT ?": INPUT B$: IF B$="" THEN4500
4505 FOR N=1 TO 12
4507 IF MID$(B$,1,1)=" " THEN B$=MID$(B$,2):GOTO4507
4510 IF I(N)<>0 THEN4545
4520 IFO$(N)<>LEFT$(B$,15)THEN4545
4530 I(N)=ST
4532 FO=1
4535 PRINT "OK..."
4540 I=I-1
4545 NEXT N
4555 GOTO 100
5500 FO=0
5505 FOR N=1 TO 12
5510 IF I(N)<>0 THEN5530
5515 IF NOT(FO)THEN PRINT "I HAVE THE FOLLOWING..."
5520 PRINT TAB(10);" A ";O$(N)
5525 FO=1
5530 NEXT N
5555 RETURN 
6000 IF ST=1240 THEN6100
6002 IF ST=1210 THEN ST=UP
6005 IF ST<>1450 THEN ST=UP
6010 IF ST=UP THEN20
6020 IF I(3)>0 THEN PRINT "THERE IS NO WAY UP": GOTO 100
6040 IF UP>0 THEN ST=UP
6050 IF ST=UP THEN20
6060 PRINT "I CAN'T GO UP"
6070 GOTO 100
6100 IF FE=0 THEN PRINT "THERE'S A DOG IN THE WAY": GOTO 100
6120 ST=1270
6130 GOTO 20
6500 IF B$="" THEN PRINT A$;" WHAT ?": INPUT B$: IF B$="" THEN6500
6530 IF B$<>"MANUSCRIPT" THEN499
6540 IF I(11)>0 AND ST<>1480 THEN PRINT "I DO NOT HAVE ";B$: GOTO 100
6555 CLS 
6560 PRINT "THE MANUSCRIPT SAYS:                                                  PP3 - TM     ISLAND "
6570 GOTO 100
7000 IF ST<>1060 OR I(9) THEN499
7010 CLS0:PRINT "YOU HAVE SUCCESSFULLY COMPLETED THE ADVENTURE."
7015 PRINT 
7020 PRINT "YOUR TIME-SHIP TAKES OFF FOR    YOUR OWN TIME."
7025 PRINT@10*32+12,"¥­­  €€";
7026 PRINT@11*32+12,"¥¯¯ €";
7027 PRINT@12*32+12,"§¯¯¢ ";
7030 Z=INT(RND(0)*250)+1995
7040 FOR A=Z TO 1983 STEP -1
7050 PRIN@11*32+20,A;::FORZZ=1TO1000:NEXTZZ
7060 NEXT A
7070 CLS
7100 GOTO 9000
7500 IF B$="" THEN PRINT "LIGHT WHAT ?": INPUT B$: IF B$="" THEN7500
7530 IF I(8)>0 THEN499
7540 IF ST=1210 THEN NO=1240
7550 IF NO>0 THEN PRINT "YOU ARE IN A SMALL ROOM. THERE  IS A STAIRCASE TO THE EAST AND  A DAMP PASSAGE TO THE NORTH."
7560 IF NO<>0 THEN100
7570 PRINT "THAT DIDN'T MUCH GOOD."
7580 GOTO 100
8000 CLS 
8010 PRINT ,,,,,,,,"          TIME   SWITCH                 ================="
8020 PRINT 
8030 PRINT 
8040 PRINT "   YOU ARE STRANDED IN THE DIM  AND DISTANT PAST. YOU MUST FIND YOUR WAY BACK TO YOUR OWN TIME  USING WHAT YOU CAN FIND ON YOUR JOURNEY."
8050 PRINT@485,"PRESS ANY KEY TO START";
8060 IF INKEY$="" THEN8060
8070 GOTO 9500
9000 PRINT "DO YOU WANT TO TRY AGAIN (Y/N) ?"
9010 INPUT B$
9020 IF B$="Y" THEN RUN 
9030 END 
9100 CLS 
9110 DIM I(12):DIM O$(12)
9130 FOR N=1 TO 12
9140 X=INT(RND(0)*15)
9145 I(N)=(X*30)+1000
9147 IF I(N)=1360 THEN9140
9148 IF I(N)>1200 AND I(N)<1271 THEN9140
9149 READ O$(N)
9150 NEXT N
9160 I(9)=1270
9170 I(7)=1480
9180 I(11)=1480
9190 I(3)=1060
9200 DATA"ROPE","ROCK","LADDER","GOLD RING","GUN","13 AMP PLUG","GOLDEN KEY","TORCH","BATTERY","LOG","MANUSCRIPT"
9201 DATA"MARROW-BONE"
9425 ND=0
9475 NC=0
9480 GOTO 8000
9500 RETURN 
9600 FOR N=1 TO 12
9605 P=P+1
9610 IF I(N)=ST THEN9630
9612 IF P=12 THEN90
9620 GOTO 9650
9630 IF NOT FO THEN PRINT "I CAN SEE A ...."
9640 PRINT O$(N)
9650 NEXT N
9700 GOTO 100
