0 CLEAR300:DIMCC,ZZ,M$,I$:GOTO20
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
20 GOSUB950:CLEAR:DIML$(31),A(31,4),O$(29,3),O(29):FORX=1TO31:READL$(X):FORY=1TO4:READA(X,Y):NEXTY,X:FORX=1TO29
21 FORY=1TO3:READO$(X,Y):NEXT:READO(X):NEXT:FORX=1TO4:READD$(X):NEXT:Z$=CHR$(128):Y$=CHR$(8)
22 L=25:FORX=1TO12:READT:O$(9,1)=O$(9,1)+CHR$(T):NEXT
30 CLS:M$="LOCATION: ":IFL<24ORL>29THENM$=M$+"IN THE ":GOTO40
31 M$=M$+"OUTSIDE, "
40 M$=M$+L$(L):GOSUB1:PRINT:PRINT"VISIBLE OBJECTS:":FORX=1TO29:IFO(X)=LTHENM$=O$(X,1):GOSUB1
41 NEXT
50 PRINT:PRINT"DIRECTIONS AVAILABLE:":FORX=1TO4:IFA(L,X)<>LTHENPRINTD$(X);
51 NEXT
60 IFL=2ORL=16ORL=30ORL=20THENPRINT"UP ";
61 IFL=8ORL=31ORL=19ORL=15THENPRINT"DOWN ";
62 IFL=29ANDF9=1THENPRINT"EAST ";
63 IFL=12ANDF5=1THENPRINT"DOWN ";
64 IFL=22ANDF7=1THENPRINT"NORTH ";
70 IFL=29ANDF8=0THENPRINT:PRINT"THE DOOR TO THE TOWER IS LOCKED.";
80 PRINT:PRINTZ$;:B$=""
90 A$=INKEY$:IFLEN(B$)>25THENPRINTY$:GOTO80
91 IFA$=""THEN100
92 IFA$=CHR$(13)THENPRINTY$:GOTO170
93 IFA$=Y$ANDB$=""THEN100
94 IFA$=Y$THENB$=LEFT$(B$,LEN(B$)-1):PRINTY$+Y$+Z$;:GOTO100
95 B$=B$+A$:PRINTY$+A$+Z$;
100 IFB=1THEN140
101 IFL<>22THEN90
102 IFM=1THEN90
103 C=C+1:IFC<110THEN90
104 C=0:K=K+1:IFK=4THENPRINTY$"THE MONSTER FINISHED YOU OFF!   YOU ARE NOW DEAD.":GOTO740
105 PRINTY$:Y=RND(3):ONYGOTO110,120,130
110 M$="BASH! THE MONSTER DEALS A NASTY BLOW!":GOSUB1:GOTO80
120 M$="KONG! SMASH! YOU'RE REALLY GETTING HURT BAD!":GOSUB1:GOTO80
130 M$="KERPOW! BONK! YOU'RE LOSING CONSCIOUSNESS!":GOSUB1:GOTO80
140 B1=B1+1:IFB1<150THEN90
141 PRINTY$"KER-POOOOOWWW!!! THE BOMB GOES  OFF!"
142 IFL=LLORO(21)=99THENM$="YOU WERE TORN APART BY THE BOMB, WHICH WAS RIGHT NEXT TO YOU!":GOSUB1:GOTO740
150 O(21)=0:IFLL=5THENO$(24,1)="A VERY BADLY DAMAGED SAFE. THE DOOR HAS BEEN JARRED LOOSE.":FC=1:B=0:GOTO80
160 M$="THE BOMB WENT OFF, BUT YOU DID NOT USE IT FOR THE RIGHT PURPOSE! ":GOSUB1
161 M$="NOW YOU WILL NOT BE ABLE TO FINISH THIS ADVENTURE.":GOSUB1:B=0
162 IFLL=22THENO$(29,1)="A BADLY DAMAGED MONSTER CORPSE":M=1
163 GOTO80
170 IFLEN(B$)=0THEN30
171 IFLEN(B$)>1THEN230
172 IFB$="N"THENL=A(L,1)
173 IFB$="S"THENL=A(L,2)
174 IFB$="E"THENL=A(L,3)
175 IFB$="W"THENL=A(L,4)
176 IFB$<>"U"THEN181
177 IFL=2THENL=15:GOTO181
178 IFL=20THENL=8:GOTO181
179 IFL=30THENL=31:GOTO181
180 IFL=16THENL=19:GOTO181
181 IFB$<>"D"THEN190
182 IFL=19THENL=16:GOTO190
183 IFL=31THENL=30:GOTO190
184 IFL=15THENL=2:GOTO190
185 IFL=12ANDF5=1THENL=5
186 IFL=12ANDF5=0THENPRINT"YOU HAVE TO OPEN IT.":GOTO80
190 IFB$="D"ANDL=8THEN195
191 GOTO200
195 IFO(1)<>99THENPRINT"IT'S TOO DARK...":GOTO80
196 IFFE=0THENPRINT"LIGHT YOUR TORCH!":GOTO80
197 L=20
200 IFB$="E"ANDL=29THENIFF9=1THENL=30
210 IFB$="N"ANDL=22ANDM=1ANDF7=1THENL=23
220 GOTO30
230 C$=LEFT$(B$,3):FORX=1TO10:IFMID$(B$,X,1)=" "THEND$=MID$(B$,X+1,3)
231 NEXT
240 V=0:FORX=1TO29:IFD$=O$(X,3)THENZ=X:V=1
241 NEXT
250 IFC$="GET"THEN430
251 IFC$="LOO"THEN490
252 IFC$="REA"THEN500
253 IFC$="DRO"THEN540
254 IFC$="BUI"THEN590
255 IFC$="TAK"ANDD$="INV"THEN630
256 IFC$="OPE"THEN650
257 IFC$="UNL"THEN700
260 IFC$<>"USE"THEN270
261 IF(D$="KEY"ORD$="LOC")THENPRINT"TRY 'UNLOCK'.":GOTO80
262 IFD$="SHO"THENC$="DIG":GOTO270
263 IFD$="GUN"THENC$="FIR":GOTO270
264 IFD$="BOM"THENC$="SET":D$="TIM":GOTO270
265 IFD$="TEL"THEN290
266 PRINT"SORRY, YOU CAN'T USE THAT.":GOTO80
270 IFC$<>"SET"THEN300
271 IFD$="TIM"AND(O(21)=99ORO(21)=L)THEN275
272 GOTO280
275 B=1:PRINT"CLICK...TIC, TIC, TIC...":IFO(21)=LTHENLL=L:GOTO80
276 PRINT"YOU HAD BETTER DROP IT...":GOTO80
280 PRINT"HUH?!":GOTO80
290 IFO(13)<>99THENPRINT"WHAT TELESCOPE?":GOTO80
291 IFL=31THEN295
292 PRINT"YOU AREN'T HIGH ENOUGH.":GOTO80
295 M$="YOU SEE WRITING CARVED OUT OF A TREE FAR AWAY: ":GOSUB1
296 M$="THE KEY TO YOUR SUCCESS IS BURIED IN THE FRONT LAWN.":GOSUB1:GOTO80
300 IFC$<>"DIG"THEN310
305 IFO(18)<>99THENPRINT"YOU HURT YOUR HAND.":GOTO80
306 PRINT"DIG...DIG...DIG...":IF(L=25ANDO(9)=0)THENM$="YOUR DIGGING UNCOVERS A GOLDEN KEY!":GOSUB1:O(9)=25:GOTO80
307 PRINT"YOU FIND NOTHING.":GOTO80
310 IFC$<>"LIG"THEN350
311 IFD$<>"MAT"THEN320
315 IFO(20)=99THENPRINT"IT BRIEFLY FLARES UP AND DIES.":GOTO80
316 PRINT"WHAT MATCH?":GOTO80
320 IFD$<>"TOR"THEN330
325 IFFE=1THENPRINT"IT IS ALREADY LIT!":GOTO80
326 IFO(1)<>99THENPRINT"WHAT TORCH?":GOTO80
327 IFO(20)<>99THENPRINT"WITH WHAT?":GOTO80
328 PRINT"THE TORCH IS NOW ABLAZE.":FE=1:GOTO80
330 IFD$<>"POW"THEN340
335 IFO(5)=99ANDO(20)=99THENM$="WHOOOSH! THE POWDER BURSTS INTO FLAME, DESTROYING YOUR FACE!":GOSUB1:GOTO740
336 M$="HOW ARE YOU GOING TO BURN IT?":GOSUB1:GOTO80
340 PRINT"I TRIED, BUT IT WONT BURN.":GOTO80
350 IFC$<>"LOA"THEN360
351 IFD$<>"GUN"THEN360
352 IF(O(5)=99ANDO(7)=99ANDO(8)=99)THENGN=1:PRINT"THE GUN IS FULLY LOADED.":O(5)=0:O(8)=0:F3=0:GOTO80
353 M$="YOU DON'T HAVE EVERYTHING YOU NEED.":GOSUB1:GOTO80
360 IFC$<>"KIL"THEN390
361 IFD$="MON"ANDO(7)<>99ANDL=22ANDM=0THEN365
362 GOTO370
365 M$="YOU GRAB THE MONSTER AND FLING IT TO THE GROUND!":GOSUB1
366 M$="THE MONSTER, IN TURN, GRABS YOU AND PROCEEDS TO POUND YOU INTO THE DAMP CEMENT FLOOR. YOU ARE DEAD."
367 GOSUB1:GOTO740
370 IFO(7)<>99THENPRINT"YOU DON'T HAVE IT.":GOTO80
371 IFD$<>"MON"THEN380
372 IFGN<>1THENPRINT"YOUR GUN ISN'T LOADED.":GOTO80
373 PRINT"BANG!":GN=0:IFL=22ANDM=0THEN375
374 GOTO80
375 PRINT"YOU HAVE KILLED THE MONSTER!":M=1:O$(29,1)="THE DEAD CORPSE OF A MONSTER.":GOTO80
380 M$="YOU REALLY SHOULDN'T KILL UNLESS IT'S NECESSARY.":GOSUB1:GOTO80
390 IFC$<>"WIN"THEN400
391 IFD$<>"CLO"THENPRINT"YOUR CRAZY.":GOTO80
392 IFO(3)=99THENM$="WIND, WIND, WIND, WIND... IT'S TICKING.":GOSUB1:FA=1:O$(3,2)="IT'S FULLY WOUND.":GOTO80
393 PRINT"WHAT CLOCK?":GOTO80
400 IFC$<>"FIR"THEN410
401 IFD$<>"GUN"THENPRINT"TRY 'LIGHT'":GOTO80
402 D$="MON":GOTO370
410 IFC$="CLE"THEN30
411 IFC$="INV"THENC$="TAK":D$="INV":GOTO250
412 IFC$="TAK"THENPRINT"TRY 'GET'.":GOTO80
420 PRINT"I DON'T KNOW WHAT YOU MEAN.":GOTO80
430 UI=0:O1=0:IFD$="POW"ANDF3=0ANDL=21THENM$="YOU GRABBED A SMALL HANDFUL OF IT.":GOSUB1:F3=1:O(5)=99:GOTO80
440 IFV=0THENPRINT"YOU CAN'T GET THAT.":GOTO80
441 FORX=1TO23:IFO(X)=99THENO1=O1+1
442 NEXT
450 IFO1>3THEN455
451 GOTO460
455 O1=0:UI=1
456 M$="UGH! YOU NEARLY BROKE MY BACK!":GOSUB1
457 M$="BECAUSE YOU FEEL YOU SHOULD GET EVERYTHING YOU SEE YOU HAVE DROPPED EVERYTHING!":GOSUB1
458 FORX=1TO23:IFO(X)=99THENO(X)=L
459 NEXT
460 IFUI=1THEN80
461 IFO(Z)=99THENPRINT"YOU ALREADY HAVE IT!":GOTO80
462 IFO(Z)<>LTHENPRINT"I DON'T SEE IT HERE.":GOTO80
463 IFZ>23THENPRINT"IT'S MUCH TOO CUMBERSOME.":GOTO80
470 IFZ=19ANDL=12THENM$="YOU DISCOVER A HIDDEN DOOR UNDERNEATH IT!":GOSUB1:F1=1:GOTO480
471 IFZ=14ANDF2=0THENF2=1:PRINT"YOU FIND A GUN UNDERNEATH!":O(7)=24:GOTO480
472 IFZ=23THENIFM=0THEN730
473 IFZ=23THENM$="YOU FIND A SMALL WOODEN DOOR BEHIND THE DRUM.":GOSUB1:F6=1
480 PRINT"O.K. YOU HAVE IT.":O(Z)=99:GOTO80
490 IF(D$="GAR"ANDL=11)THENM$="WHAT'S SO SPECIAL ABOUT A BUNCH OF ROCKS?":GOSUB1:GOTO80
491 IFV=0THENPRINT"THERE IS NOT MUCH TO SEE.":GOTO80
492 IFO(Z)=99ORO(Z)=LTHENM$=O$(Z,2):GOSUB1:GOTO80
493 PRINT"I DON'T HAVE IT OR SEE IT.":GOTO80
500 IFD$<>"BOO"THEN520
501 IFO(16)=99ORO(16)=LTHEN505
502 GOTO510
505 PP=1:CLS:PRINT:PRINT"HOW TO BUILD A TIME BOMB":PRINT:PRINT"MATERIALS NEEDED:"
506 PRINT"  1) A TIMING DEVICE":PRINT"  2) A SMALL LENGTH OF FUSE":PRINT"  3) A SMALL METAL CONTAINER"
507 PRINT"  4) TWO BATTERIES"
510 IFPP=1THEN515
511 GOTO520
515 PRINT"  5) AN OUNCE OF GUNPOWDER":PRINT"  6) SOME ELECTRICAL WIRING":PRINT"  7) A MATCH"
516 PRINT:PRINT"SOME KNOWLEDGE OF THE USE OF    TIMEBOMBS IS DESIRABLE.":GOTO80
520 IFD$="PAP"THEN525
521 GOTO530
525 IF(O(17)=99ORO(Z)=L)THEN527
526 GOTO530
527 PRINT:PRINT"INSTRUCTIONS FOR USE OF TIME    BOMB:":PRINT:PRINT" 1) BUILD BOMB"
528 PRINT" 2) set timer ON BOMB":PRINT" 3) GET AWAY QUICKLY!":FB=1:GOTO80
530 PRINT"TRY 'LOOK'":GOTO80
540 IFV=0THENPRINT"YOU CAN'T DROP THAT!":GOTO80
541 IFZ=12THEN570
542 IFZ=21THEN550
543 IFO(Z)<>99THENPRINT"YOU DON'T HAVE IT.":GOTO80
544 PRINT"THUD! O.K. YOU DROPPED IT.":O(Z)=L:GOTO80
550 IFO(21)=99THEN555
551 GOTO560
555 O(21)=L:PRINT"CLUNK! IT'S DROPPED.":IFB=1THENLL=L:GOTO80
556 GOTO80
560 PRINT"WHAT BOMB?":GOTO80
570 IF(L=18ANDO(11)=0ANDO(Z)=99)THEN575
571 GOTO580
575 M$="SKELETON: 'THANK YOU VERY MUCH' THE SKELETON DROPS A FUSE AT YOUR FEET.":GOSUB1:O(Z)=0:O(11)=L:GOTO80
576 IFO(Z)=99THENPRINT"O.K. YOU DROPPED IT.":O(Z)=L:GOTO80
580 PRINT"YOU CAN'T.":GOTO80
590 IFD$<>"BOM"THENPRINT"YOU CAN'T BUILD THAT.":GOTO80
591 FORX=2TO6:IFO(X)<>99ANDO(X)<>LTHEN620
592 NEXT:IFO(11)<>99ANDO(11)<>LTHEN620
593 IFO(20)<>99ANDO(20)<>LTHEN620
594 FORX=2TO6:O(X)=0:NEXT:O(11)=0:F3=0
600 IFFA=0THENPRINT"KA-BOOM!!! THE BOMB WENT UP IN  YOUR FACE! (YOU FORGOT TO WIND  THE CLOCK).":GOTO740
610 PRINT"GOOD JOB! YOU HAVE SUCCESSFULLY BUILT THE BOMB!":O(21)=L:GOTO80
620 M$="YOU DON'T HAVE ALL OF THE MATERIALS NEEDED.":GOSUB1:GOTO80
630 PRINT"YOU HAVE THE FOLLOWING:":FORX=1TO23:IFO(X)=99THENM$=O$(X,1):GOSUB1
631 NEXT
640 GOTO80
650 IFD$="DOO"THEN655
651 GOTO660
655 IFL=12ANDF1=1THENF5=1:PRINT"THE HIDDEN DOOR IS OPEN.":GOTO80
656 IF(L=22ANDM=1ANDF6=1)THENM$="CREEEEEK! THE TINY DOOR IS OPEN.":GOSUB1:F7=1:GOTO80
657 IFL=29ANDF8=0THENPRINT"THE DOOR IS LOCKED.":GOTO80
660 IF(D$="DOO"ORD$="SAF")ANDL=5ANDFC=1THEN665
661 GOTO666
665 M$="A LOCKPICK, A NOTE, AND A BULLET FALL OUT OF THE SAFE.":GOSUB1:O(8)=5:O(15)=5:O(10)=5:GOTO80
666 IF(D$="DOO"ORD$="VAU")ANDL=23ANDFD=1THEN720
670 IFD$="DOO"THENIFL=29ANDF8=1THENPRINT"THE DOOR SWINGS INWARD.":F9=1:GOTO80
680 IFD$="BOX"ANDO(13)=0AND(O(22)=LORO(22)=99)THENPRINT"INSIDE LIES A TELESCOPE.":O(13)=L:GOTO80
681 IFD$="CAB"ANDO(6)=0ANDL=9THENPRINT"A SOUP CAN FALLS OUT.":O(6)=9:GOTO80
682 IFD$="BUR"ANDL=17ANDO(1)=0THENPRINT"YOU FIND A TORCH INSIDE.":O(1)=17:GOTO80
690 IF(D$="WIN"ANDL=5)THEN695
691 PRINT"IT WON'T OPEN.":GOTO80
695 M$="UGH! GRUNT! POP! AIIIIIIEEY!":GOSUB1
696 M$="THE WINDOW POPS OPEN AND THE MOMENTUM THROWS YOU OUT THE WINDOW ONTO THE VERANDA. SLAM!":GOSUB1
697 M$="THEN THE WINDOW SHUTS AGAIN.":GOSUB1:L=27:GOTO80
700 IFD$="DOO"ANDL=29ANDO(10)=99ANDF8=0THENF8=1:PRINT"CLICK! IT IS NOW UNLOCKED.":GOTO80
701 IF(D$="DOO"ORD$="VAU")ANDL=23ANDO(9)=99THENPRINT"CLICK...IT'S OPEN!":FD=1:GOTO80
710 PRINT"YOU HAVN'T THE MEANS...":GOTO80
720 M$="YOU OPEN THE VAULT ONLY TO FIND...":GOSUB1:FORX=1TO2000:NEXT:PRINT
721 M$="FOUR MILLION DOLLARS IN GOLD BARS! THE FORTUNE OF THE MANSION IS NOW YOURS!":GOSUB1:FORX=1TO2000:NEXT
722 PRINT:PRINT"     <<<CONGRATULATIONS>>>":END
730 M$="THE MONSTER GRABS YOU AND LIGHTLY TOSSES YOU THROUGH THE WALL.":GOSUB1
740 PRINT"THE GAME IS OVER FOR NOW. DO YOUWISH TO PLAY AGAIN? (Y OR N)"
750 A$=INKEY$:IFA$=""THEN750
751 IFA$="Y"THENRUN
752 PRINT"NOT TOO ADVENTURESOME, EH?":END
760 DATA"ENTRANCE HALL.",25,2,1,1
761 DATA"BALLROOM, THERE IS A WINDING STAIRCASE GOING UP AND A DOORWAY TO THE WEST LEADING OUT.",1,7,3,27
762 DATA"DINING HALL.",1,8,4,2,"BATHROOM.",4,4,4,3
770 DATA"SECRET ROOM, THERE IS A WINDOW FACING WEST, BUT YOU CAN'T SEE THROUGH IT BECAUSE IT'S COATED WITH GRIME."
771 DATA5,5,5,5,"LIBRARY, LEARN TO READ, READ TO LEARN.",6,6,10,6
780 DATA"BAR, LET US HAVE A TOAST TO ADVENTURES!",2,10,7,7,"KITCHEN, A DARK STAIRCASE LEADS DOWN."
781 DATA3,8,9,8,"PANTRY.",9,9,9,8,"STUDY.",7,10,10,6
782 DATA"OVERLOOK, YOU HAVE A NICE VIEW OF THE ROCK GARDEN FROM HERE.",11,11,15,11
790 DATA"MASTER BEDROOM, WHERE THE MASTER SLEPT.",12,15,12,12,"GUEST ROOM.",13,16,13,13,"BATHROOM.",14,14,14,16
791 DATA"LONG CORRIDOR, A STAIRCASE WINDS DOWN BELOW.",12,17,16,11
792 DATA"SHORT HALLWAY, A LADDER RUNS UP TO THE ROOM ABOVE.",13,16,14,15
800 DATA"BEDROOM.",15,18,16,17,"SKELETON CLOSET.",17,18,18,18,"ATTIC, A LADDER LEADS DOWN.",19,19,19,19
801 DATA"CELLAR, THERE IS A STRANGE SMELL COMING FROM THE EAST, A STAIRCASE GOES UP.",20,21,22,20
810 DATA"ARSENAL, THERE IS A BARREL OF GUN POWDER IN THE CORNER.",20,21,21,21
811 DATA"WINE CELLER, YUCK! IT REALLY SMELLS BAD IN HERE!",22,22,22,20
812 DATA"HIDDEN ROOM NORTH OF THE WINE CELLAR. IT IS VERY DUSTY IN THIS ROOM.",23,22,23,23,"IN THE ROCK GARDEN."
820 DATA24,27,25,24,"ON THE FRONT LAWN, THE MAIN ENTRANCE TO THE MANSION IS TO THE SOUTH.",25,1,26,24
821 DATA"IN THE EAST GARDEN.",26,26,26,25,"ON THE VERANDA, AN ENTRANCE TO THE MANSION LIES TO THE EAST.",24,28,2,27
830 DATA"IN THE WOODSY HILLY AREA BEHIND THE MANSION. TO THE SOUTH A STONE TOWER STICKS OUT OF THE TREES."
831 DATA27,29,28,28,"IN VERY DARK WOODS. YOU ARE AT THE TOWER'S ENTRANCE, WHICH IS FACING WEST.",28,29,29,29
840 DATA"TOWER'S LOWER ROOM, A ROPE LADDER GOES UP.",30,30,30,29
841 DATA"TOWER'S OBSERVATION ROOM, YOU CAN SEE A VERY GREAT DISTANCE, A ROPE LADDER GOES DOWN.",31,31,31,31
850 DATA A TORCH,"IT HAS BEEN USED BEFORE.",TOR,0,BATTERIES,"THERE ARE TWO OF THEM."
851 DATABAT,28,AN OLD ALARM CLOCK,"IT NEEDS WINDING.",CLO,13,A SPOOL OF ELECTRICAL WIRE
852 DATA"IT IS LONG AND SKINNY.",WIR,20,A HANDFUL OF GUN POWDER,"IT'S GRAY."
860 DATAPOW,0,AN EMPTY SOUP CAN,"READS: CHICKEN NOODLE",CAN,0,A GUN,"IT IS A VERY OLD PISTOL."
861 DATAGUN,0,A SILVER BULLET,"INSCRIPTION: PROPERTY OF THE LONE RANGER.",BUL,0,"A GOLD KEY:"
862 DATA"INSCRIPTION: FINDERS KEYPERS.",KEY,0,A SILVER LOCKPICK
870 DATA"INSCRIPTION: USE TO UNLOCK...",LOC,0,A SHORT FUSE,"IT'S VERY SHORT.",FUS,0,A SKULL CAP
871 DATA"SKINNY BONES HATS FOR SKELETONS INC.",CAP,28,A TELESCOPE,"READS: USE IN A HIGH PLACE.",TEL,0
880 DATAA LARGE ROCK,"IT MUST WEIGH AT LEAST FIFTY POUNDS.",ROC,24,A NOTE
881 DATA"NOTE READS: LET EVERYBODY KNOW, (INCLUDING THE APES), THAT A TREASURE IS HIDDEN BEHIND ROTTING GRAPES.",NOT,0
890 DATA A BOOK,"IT'S VERY DUSTY.",BOO,6,A SHEET OF PAPER,"INSTRUCTIONS FOR USE OF TIME BOMB.",PAP,4,A SHOVEL
891 DATA"IT IS VERY OLD AND RUSTY.",SHO,30,AN ORIENTAL RUG,"IT HAS VERY INTRICATE DESIGN WORK.",RUG,12
900 DATAA BOOK OF MATCHES,"BOOK READS: 'COLLEGE DEGREES BY MAIL, CALL (612) 542-6660'"
901 DATAMAT,8,A TIME BOMB,"IT APPEARS TO BE MADE OUT OF JUNK.",BOM,0,AN OLD WOODEN BOX
902 DATA"IT HAS SAILING SHIPS ENGRAVED ON THE SIDES.",BOX,19,A LARGE DRUM OF CHABLIS
910 DATA"IT IS CAKED WITH DUST.",DRU,22,A SAFE
921 DATA"IT HAS A COMBINATION LOCK, BUT THE DIAL IS TOO RUSTY TO MOVE, YOU'LL HAVE TO FIND ANOTHER WAY TO OPEN IT."
922 DATASAF,5,A LARGE VAULT,"IT IS LOCKED - YOU NEED A KEY TOOPEN IT.",VAU,23,A CABINET,"IT'S MADE OUT OF WOOD.",CAB,9
930 DATAA BUREAU,"IT'S SOLIDLY BUILT.",BUR,17,A SKELETON,"IT IS KIND OF SKINNY.",SKE,18,A MONSTER
931 DATA"IT HAS FANGS, THREE EYES, A HORN, SHAGGY FUR, BIG FEET, AND IS ABOUT NINE FEET TALL."
932 DATAMON,22,NORTH ,SOUTH ,EAST ,WEST 
940 DATA13,249,246,252,252,253,13,246,249,255,247,247
950 CLS0:FORX=22TO31:FORXX=0TO63:SET(XX,X,1):NEXTXX,X
951 R=23:RR=40:FORX=4TO10STEP2:R=R-1:RR=RR+1:FORR1=R TORR:FORFD=XTOX+1:SET(R1,FD,8):NEXTFD,R1,X
952 FORX=18TO45:FORR=12TO25:SET(X,R,7):NEXTR,X:FORX=14TO17
960 FORR=18TO23:SET(X,R,3):SET(X+32,R,3):NEXTR,X:FORX=21TO24:FORR=17TO22
970 RESET(X,R):RESET(X+18,R):NEXTR,X:FORX=28TO35:FORR=20TO27:SET(X,R,3):NEXTR,X
971 R=20:FORX=27TO31:R=R-1:RESET(X,R):NEXT:R=14:FORX=32TO36:R=R+1:RESET(X,R):NEXT:R=32:RR=31
972 FORX=17TO19:R=R-1:RR=RR+1:FORFD=R TORR:SET(FD,X,3):NEXTFD,X
980 FORX=30TO33:FORR=21TO27:RESET(X,R):NEXTR,X:R=18:FORX=13TO17:R=R-1:FORRR=X TO17:SET(RR,R,8):NEXTRR,X
981 R=12:FORX=46TO50:R=R+1:FORRR=X TO46STEP-1:SET(RR,R,8):NEXTRR,X
990 SOUND166,4:SOUND166,4:SOUND176,4:SOUND176,4:SOUND196,8:SOUND196,8:SOUND206,8:SOUND206,8:SOUND166,4
992 FORX=1TO999:NEXT:PRINT@6,"an"CHR$(128)"unexplored"CHR$(128)"mansion";
993 FORX=1TO1200:NEXT:POKE49151,64:RETURN
998 REM TIM HANSEN
999 REM MC-10 EDITS JIM GERRIE
