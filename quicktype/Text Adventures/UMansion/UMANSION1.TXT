
1 POKE65495,0
10 CLEAR90
20 GOSUB950:CLEAR:DIM L$(31),A(31,4),O$(29,3),O(29):FORX=1TO31:READL$(X):FORY=1TO4:READA(X,Y):NEXTY,X:FORX=1TO29:FORY=1TO3:READ O$(X,Y):NEXT:READO(X):NEXT:FORX=1TO4:READD$(X):NEXT:Z$=CHR$(128):Y$=CHR$(8):L=25:FORX=1TO12:READT:O$(9,1)=O$(9,1)+CHR$(T):NE
30 CLS:PRINT"LOCATION: ";:IFL<24ORL>29THENPRINT"IN THE"ELSEPRINT"OUTSIDE,"
40 PRINTL$(L):PRINT:PRINT"VISIBLE OBJECTS:":FORX=1TO29:IFO(X)=L THENPRINTO$(X,1):NEXTELSENEXT
50 PRINT:PRINT"DIRECTIONS AVALIABLE:":FORX=1TO4:IFA(L,X)<>L THENPRINTD$(X);:NEXTELSENEXT
60 IFL=2ORL=16ORL=30ORL=20THENPRINT"UP "ELSEIFL=8ORL=31ORL=19ORL=15THENPRINT"DOWN "ELSEIFL=29ANDF9=1THENPRINT"EAST "ELSEIFL=12ANDF5=1THENPRINT"DOWN "ELSEIFL=22ANDF7=1THENPRINT"NORTH "
70 IFL=29ANDF8=0THENPRINT:PRINT"THE DOOR TO THE TOWER IS LOCKED."
80 PRINT:PRINTZ$;:B$=""
90 A$=INKEY$:IFLEN(B$)>25THENPRINTY$:GOTO80ELSEIFA$=""THEN100ELSEPLAY"T255A":IFA$=CHR$(13)THENPRINTY$:GOTO170ELSEIFA$=Y$ANDB$=""THEN100ELSEIFA$=Y$THENB$=LEFT$(B$,LEN(B$)-1):PRINTY$+Y$+Z$;:ELSEB$=B$+A$:PRINTY$+A$+Z$;
100 IFB=1THEN140ELSEIFL<>22THEN90ELSEIFM=1THEN90ELSEC=C+1:IFC<110THEN90ELSEC=0:K=K+1:IFK=4THENPRINTY$"THE MONSTER FINISHED YOU OFF!   YOU ARE NOW DEAD.":GOTO740ELSEPRINTY$:Y=RND(3):ON Y GOTO110,120,130
110 PRINT"BASH! THE MONSTER DEALS A NASTY BLOW!":GOTO80
120 PRINT"KONG! SMASH! YOU'RE REALLY      GETTING HURT BAD!":GOTO80
130 PRINT"KERPOW! BONK! YOU'RE LOSING     CONSCIOUSNESS!":GOTO80
140 B1=B1+1:IFB1<150THEN90ELSEPRINTY$"KER-POOOOOWWW!!! THE BOMB GOES  OFF!":IFL=LL ORO(21)=99THENPRINT"YOU WERE TORN APART BY THE      BOMB! THE BOMB WAS RIGHT NEXT   TO YOU WHEN IT WENT OFF!":GOTO740
150 O(21)=0:IFLL=5THENO$(24,1)="A VERY BADLY DAMAGED SAFE. IT   APPEARS THAT THE DOOR HAS BEEN  JARRED LOOSE.":FC=1:B=0:GOTO80
160 PRINT"THE BOMB WENT OFF, BUT YOU DID  NOT USE IT FOR THE RIGHT        PURPOSE! NOW YOU WILL NOT BE    ABLE TO FINISH THIS ADVENTURE.":B=0:IFLL=22THENO$(29,1)="A BADLY DAMAGED MONSTER CORPSE":M=1:GOTO80ELSE80
170 IFLEN(B$)=0THEN80ELSEIFLEN(B$)>1THEN230ELSEIFB$="N"THENL=A(L,1)ELSEIFB$="S"THENL=A(L,2)ELSEIFB$="E"THENL=A(L,3)ELSEIFB$="W"THENL=A(L,4)ELSEIFB$="U"THENIFL=2THENL=15ELSEIFL=20THENL=8ELSEIFL=30THENL=31ELSEIFL=16THENL=19
180 IFB$="D"THENIFL=19THENL=16ELSEIFL=31THENL=30ELSEIFL=15THENL=2ELSEIFL=12ANDF5=1THENL=5ELSEIFL=12ANDF5=0THENPRINT"YOU HAVE TO OPEN IT.":GOTO80
190 IFB$="D"ANDL=8THENIFO(1)<>99THENPRINT"IT'S TOO DARK...":GOTO80ELSEIFFE=0THENPRINT"LIGHT YOUR TORCH!":GOTO80ELSEL=20
200 IFB$="E"ANDL=29THENIFF9=1THENL=30
210 IFB$="N"ANDL=22ANDM=1ANDF7=1THENL=23
220 GOTO30
230 C$=LEFT$(B$,3):FORX=1TO10:IFMID$(B$,X,1)=" "THEND$=MID$(B$,X+1,3):NEXTELSENEXT
240 V=0:FORX=1TO29:IFD$=O$(X,3)THENZ=X:V=1:NEXTELSENEXT
250 IFC$="GET"THEN430ELSEIFC$="LOO"THEN490ELSEIFC$="REA"THEN500ELSEIFC$="DRO"THEN540ELSEIFC$="BUI"THEN590ELSEIFC$="TAK"ANDD$="INV"THEN630ELSEIFC$="OPE"THEN650ELSEIFC$="UNL"THEN700
260 IFC$<>"USE"THEN270ELSEIF(D$="KEY"ORD$="LOC")THENPRINT"TRY 'UNLOCK'.":GOTO80ELSEIFD$="SHO"THENC$="DIG"ELSEIFD$="GUN"THENC$="FIR"ELSEIFD$="BOM"THENC$="SET":D$="TIM"ELSEIFD$="TEL"THEN290ELSEPRINT"SORRY, YOU CAN'T USE THAT.":GOTO80
270 IFC$<>"SET"THEN300ELSEIFD$="TIM"AND(O(21)=99ORO(21)=L)THENB=1:PRINT"CLICK...TIC, TIC, TIC...":IFO(21)=L THENLL=L:GOTO80ELSEPRINT"YOU HAD BETTER DROP IT...":GOTO80
280 PRINT"HUH?!":GOTO80
290 IFO(13)<>99THENPRINT"WHAT TELESCOPE?":GOTO80ELSEIFL=31THENPRINT"YOU SEE WRITING CARVED OUT OF A TREE FAR AWAY:":PRINT"THE KEY TO YOUR SUCCESS IS      BURIED IN THE FRONT LAWN.":GOTO80ELSEPRINT"YOU AREN'T HIGH ENOUGH.":GOTO80
300 IFC$="DIG"THENIFO(18)<>99THENPRINT"YOU HURT YOUR HAND.":GOTO80ELSEPRINT"DIG...DIG...DIG...":IF(L=25ANDO(9)=0)THENPRINT"YOUR DIGGING UNCOVERS A GOLDEN  KEY!":O(9)=25:GOTO80ELSEPRINT"YOU FIND NOTHING.":GOTO80
310 IFC$<>"LIG"THEN350ELSEIFD$="MAT"THENIFO(20)=99THENPRINT"IT BRIEFLY FLARES UP AND DIES.":GOTO80ELSEPRINT"WHAT MATCH?":GOTO80
320 IFD$="TOR"THENIFFE=1THENPRINT"IT IS ALREADY LIT!":GOTO80ELSEIFO(1)<>99THENPRINT"WHAT TORCH?":GOTO80ELSEIFO(20)<>99THENPRINT"WITH WHAT?":GOTO80ELSEPRINT"THE TORCH IS NOW ABLAZE.":FE=1:GOTO80
330 IFD$="POW"THENIFO(5)=99ANDO(20)=99THENPRINT"WHOOOSH!! THE POWDER BURSTS     INTO FLAME, DESTROYING YOUR     FACE!":GOTO740ELSEPRINT"HOW ARE YOU GOING TO BURN IT?":GOTO80
340 PRINT"I TRIED, BUT IT WONT BURN.":GOTO80
350 IFC$<>"LOA"THEN360ELSEIFD$="GUN"THENIF(O(5)=99ANDO(7)=99ANDO(8)=99)THENGN=1:PRINT"THE GUN IS FULLY LOADED.":O(5)=0:O(8)=0:F3=0:GOTO80:ELSEPRINT"YOU DON'T HAVE EVERYTHING YOU   NEED.":GOTO80
360 IFC$<>"KIL"THEN390ELSEIFD$="MON"ANDO(7)<>99ANDL=22ANDM=0THENPRINT"YOU GRAB THE MONSTER AND FLING  IT TO THE GROUND! THE MONSTER,  IN TURN, GRABS YOU AND PROCEEDS TO POUND YOU INTO THE DAMP      CEMENT FLOOR. YOU ARE DEAD.":GOTO740
370 IFO(7)<>99THENPRINT"YOU DON'T HAVE IT.":GOTO80ELSEIFD$="MON"THENIFGN<>1THENPRINT"YOUR GUN ISN'T LOADED.":GOTO80ELSEPRINT"BANG!":GN=0:IFL=22ANDM=0THENPRINT"YOU HAVE KILLED THE MONSTER!":M=1:O$(29,1)="THE DEAD CORPSE OF A MONSTER.":GOTO80ELSE80
380 PRINT"YOU REALLY SHOULDN'T KILL       UNLESS IT'S NECESSARY.":GOTO80
390 IFC$="WIN"THENIFD$<>"CLO"THENPRINT"YOUR CRAZY.":GOTO80ELSEIFO(3)=99THENPRINT"WIND, WIND, WIND, WIND...       IT'S TICKING.":FA=1:O$(3,2)="IT'S FULLY WOUND.":GOTO80ELSEPRINT"WHAT CLOCK?":GOTO80
400 IFC$="FIR"THENIFD$<>"GUN"THENPRINT"TRY 'LIGHT'":GOTO80ELSED$="MON":GOTO370
410 IFC$="CLE"THEN30ELSEIFC$="INV"THENC$="TAK":D$="INV":GOTO250ELSEIFC$="TAK"THENPRINT"TRY 'GET'.":GOTO80
420 PRINT"I DON'T KNOW WHAT YOU MEAN.":GOTO80
430 UI=0:O1=0:IFD$="POW"ANDF3=0ANDL=21THENPRINT"YOU GRABBED A SMALL HANDFUL OF  IT.":F3=1:O(5)=99:GOTO80
440 IFV=0THENPRINT"YOU CAN'T GET THAT.":GOTO80ELSEFORX=1TO23:IFO(X)=99THENO1=O1+1:NEXTELSENEXT
450 IFO1>3THENO1=0:UI=1:PRINT"UGH! YOU NEARLY BROKE MY BACK!  BECAUSE YOU FEEL YOU SHOULD GET EVERYTHING YOU SEE YOU HAVE     DROPPED EVERYTHING!":FORX=1TO23:IFO(X)=99THENO(X)=L:NEXTELSENEXT
460 IFUI=1THENGOTO80ELSEIFO(Z)=99THENPRINT"YOU ALREADY HAVE IT!":GOTO80ELSEIFO(Z)<>L THENPRINT"I DON'T SEE IT HERE.":GOTO80ELSEIFZ>23THENPRINT"IT'S MUCH TOO CUMBERSOME.":GOTO80
470 IFZ=19THENPRINT"YOU DISCOVER A HIDDEN DOOR      UNDERNEATH IT!":F1=1ELSEIFZ=14ANDF2=0THENF2=1:PRINT"YOU FIND A  GUN UNDERNEATH!":O(7)=24ELSEIFZ=23THENIFM=0THEN730ELSEIFZ=23THENPRINT"YOU FIND A SMALL WOODEN DOOR    BEHIND THE DRUM.":F6=1
480 PRINT"O.K. YOU HAVE IT.":O(Z)=99:GOTO80
490 IF(D$="GAR"ANDL=11)THENPRINT"WHAT'S SO SPECIAL ABOUT A BUNCH OF ROCKS?":GOTO80ELSEIFV=0THENPRINT"THERE IS NOT MUCH TO SEE.":GOTO80ELSEIFO(Z)=99ORO(Z)=L THENPRINTO$(Z,2):GOTO80ELSEPRINT"I DON'T HAVE IT OR SEE IT.":GOTO80
500 IFD$<>"BOO"THEN520ELSEIFO(16)=99ORO(16)=L THENPP=1:CLS:PRINT:PRINT"HOW TO BUILD A TIME BOMB":PRINT:PRINT"MATERIALS NEEDED:":PRINT"  1) A TIMING DEVICE":PRINT"  2) A SMALL LENGTH OF FUSE":PRINT"  3) A SMALL METAL CONTAINER":PRINT"  4) TWO BATTERIES"
510 IFPP=1THENPRINT"  5) AN OUNCE OF GUNPOWDER":PRINT"  6) SOME ELECTRICAL WIRING":PRINT"  7) A MATCH":PRINT:PRINT"SOME KNOWLEDGE OF THE USE OF    TIMEBOMBS IS DESIRABLE.":GOTO80
520 IFD$="PAP"THENIF(O(17)=99ORO(Z)=L)THENPRINT:PRINT"INSTRUCTIONS FOR USE OF TIME    BOMB:":PRINT:PRINT" 1) BUILD BOMB":PRINT" 2) set timer ON BOMB":PRINT" 3) GET AWAY QUICKLY!":FB=1:GOTO80
530 PRINT"TRY 'LOOK'":GOTO80
540 IFV=0THENPRINT"YOU CAN'T DROP THAT!":GOTO80ELSEIFZ=12THEN570ELSEIFZ=21THEN550ELSEIFO(Z)<>99THENPRINT"YOU DON'T HAVE IT.":GOTO80ELSEPRINT"THUD! O.K. YOU DROPPED IT.":O(Z)=L:GOTO80
550 IFO(21)=99THENO(21)=L:PRINT"CLUNK! IT'S DROPPED.":IFB=1THENLL=L:GOTO80ELSE80
560 PRINT"WHAT BOMB?":GOTO80
570 IF(L=18ANDO(11)=0ANDO(Z)=99)THENPRINT"SKELETON: 'THANK YOU VERY MUCH' THE SKELETON DROPS A FUSE AT    YOUR FEET.":O(Z)=0:O(11)=L:GOTO80ELSEIFO(Z)=99THENPRINT"O.K. YOU DROPPED IT.":O(Z)=L:GOTO80
580 PRINT"YOU CAN'T.":GOTO80
590 IFD$<>"BOM"THENPRINT"YOU CAN'T BUILD THAT.":GOTO80ELSEFORX=2TO6:IFO(X)<>99ANDO(X)<>L THEN620ELSENEXT:IFO(11)<>99ANDO(11)<>L THEN620ELSEIFO(20)<>99ANDO(20)<>L THEN620ELSEFORX=2TO6:O(X)=0:NEXT:O(11)=0:F3=0
600 IFFA=0THENPRINT"KA-BOOM!!! THE BOMB WENT UP IN  YOUR FACE! (YOU FORGOT TO WIND  THE CLOCK).":GOTO740
610 PRINT"GOOD JOB! YOU HAVE SUCCESSFULLY BUILT THE BOMB!":O(21)=L:GOTO80
620 PRINT"YOU DON'T HAVE ALL OF THE       MATERIALS NEEDED.":GOTO80
630 PRINT"YOU HAVE THE FOLLOWING:":FORX=1TO23:IFO(X)=99THENPRINTO$(X,1):NEXTELSENEXT
640 GOTO80
650 IFD$="DOO"THENIFL=12ANDF1=1THENF5=1:PRINT"THE HIDDEN DOOR IS OPEN.":GOTO80ELSEIF(L=22ANDM=1ANDF6=1)THENPRINT"CREEEEEK! THE TINY DOOR IS      OPEN.":F7=1:GOTO80ELSEIFL=29ANDF8=0THENPRINT"THE DOOR IS LOCKED.":GOTO80
660 IF(D$="DOO"ORD$="SAF")ANDL=5ANDFC=1THENPRINT"A LOCKPICK, A NOTE, AND A       BULLET FALL OUT OF THE SAFE.":O(8)=5:O(15)=5:O(10)=5:GOTO80ELSEIF(D$="DOO"ORD$="VAU")ANDL=23ANDFD=1THEN720
670 IFD$="DOO"THENIFL=29ANDF8=1THENPRINT"THE DOOR SWINGS INWARD.":F9=1:GOTO80
680 IFD$="BOX"ANDO(13)=0AND(O(22)=L ORO(22)=99)THENPRINT"INSIDE LIES A TELESCOPE.":O(13)=L:GOTO80ELSEIFD$="CAB"ANDO(6)=0ANDL=9THENPRINT"A SOUP CAN FALLS OUT.":O(6)=9:GOTO80ELSEIFD$="BUR"ANDL=17ANDO(1)=0THENPRINT"YOU FIND A TORCH INSIDE.":O(1)=17:GOTO80
690 IF(D$="WIN"ANDL=5)THENPRINT"UGH! GRUNT! POP! AIIIIIIEEY!    THE WINDOW POPS OPEN AND THE    MOMENTUM THROWS YOU OUT THE     WINDOW ONTO THE VERANDA. SLAM!  THEN THE WINDOW SHUTS AGAIN.":L=27:GOTO80ELSEPRINT"IT WON'T OPEN.":GOTO80
700 IFD$="DOO"ANDL=29ANDO(10)=99ANDF8=0THENF8=1:PRINT"CLICK! IT IS NOW UNLOCKED.":GOTO80ELSEIF(D$="DOO"ORD$="VAU")ANDL=23ANDO(9)=99THENPRINT"CLICK...IT'S OPEN!":FD=1:GOTO80
710 PRINT"YOU HAVN'T THE MEANS...":GOTO80
720 PRINT"YOU OPEN THE VAULT ONLY TO      FIND...":FORX=1TO1100:NEXT:PRINT:PRINT"FOUR MILLION DOLLARS IN GOLD    BARS! THE MANSION FORTUNE IS    NOW YOURS!":FORX=1TO1500:NEXT:PRINT:PRINT"     <<<CONGRATULATIONS>>>":END
730 PRINT"THE MONSTER GRABS YOU AND       LIGHTLY TOSSES YOU THROUGH THE  WALL."
740 PRINT"THE GAME IS OVER FOR NOW. DO YOUWISH TO PLAY AGAIN? (Y OR N)"
750 A$=INKEY$:IFA$=""THEN750ELSEIFA$="Y"THENRUNELSEPRINT"NOT TOO ADVENTURESOME, EH?":END
760 DATA"ENTRANCE HALL.",25,2,1,1,"BALLROOM, THERE IS A WINDING    STAIRCASE GOING UP AND A DOORWAYTO THE WEST LEADING OUT.",1,7,3,27,"DINING HALL.",1,8,4,2,"BATHROOM.",4,4,4,3
770 DATA"SECRET ROOM, THERE IS A WINDOW  FACING WEST, BUT YOU CAN'T SEE  THROUGH IT BECAUSE IT'S COATED  WITH GRIME.",5,5,5,5,"LIBRARY, LEARN TO READ, READ TO LEARN.",6,6,10,6
780 DATA"BAR, LET US HAVE A TOAST TO     ADVENTURES!",2,10,7,7,"KITCHEN, A DARK STAIRCASE LEADS DOWN.",3,8,9,8,"PANTRY.",9,9,9,8,"STUDY.",7,10,10,6,"OVERLOOK, YOU HAVE A NICE VIEW  OF THE ROCK GARDEN FROM HERE.",11,11,15,11
790 DATA"MASTER BEDROOM, WHERE THE MASTERSLEPT.",12,15,12,12,"GUEST ROOM.",13,16,13,13,"BATHROOM.",14,14,14,16,"LONG CORRIDOR, A STAIRCASE WINDSDOWN BELOW.",12,17,16,11,"SHORT HALLWAY, A LADDER RUNS UP TO THE ROOM ABOVE.",13,16,14,15
800 DATA"BEDROOM.",15,18,16,17,"SKELETON CLOSET.",17,18,18,18,"ATTIC, A LADDER LEADS DOWN.",19,19,19,19,"CELLAR, THERE IS A STRANGE SMELLCOMING FROM THE EAST, A STAIR   CASE GOES UP.",20,21,22,20
810 DATA"ARSENAL, THERE IS A BARREL OF   GUN POWDER IN THE CORNER.",20,21,21,21,"WINE CELLER, YUCK! IT REALLY    SMELLS BAD IN HERE!",22,22,22,20,"HIDDEN ROOM NORTH OF THE WINE   CELLAR. IT IS VERY DUSTY IN THISROOM.",23,22,23,23,"IN THE ROCK GARDEN."
820 DATA24,27,25,24,"ON THE FRONT LAWN, THE MAIN     ENTRANCE TO THE MANSION IS TO   THE SOUTH.",25,1,26,24,"IN THE EAST GARDEN.",26,26,26,25,"ON THE VERANDA, AN ENTRANCE TO  THE MANSION LIES TO THE EAST.",24,28,2,27
830 DATA"IN THE WOODSY HILLY AREA BEHIND THE MANSION. THERE IS JAGGED    STONE TOWER STICKING OUT OF THE WOODS TO THE SOUTH.",27,29,28,28,"IN VERY DARK WOODS. YOU ARE AT  THE TOWERS ENTRANCE, WHICH IS   FACING WEST.",28,29,29,29
840 DATA"TOWER'S LOWER ROOM, A ROPE      LADDER GOES UP.",30,30,30,29,"TOWER'S OBSERVATION ROOM, YOU   CAN SEE A VERY GREAT DISTANCE,  A ROPE LADDER GOES DOWN.",31,31,31,31
850 DATA A TORCH,"IT HAS BEEN USED BEFORE.",TOR,0,BATTERIES,"THERE ARE TWO OF THEM.",BAT,28,AN OLD ALARM CLOCK,"IT NEEDS WINDING.",CLO,13,A SPOOL OF ELECTRICAL WIRE,"IT IS LONG AND SKINNY.",WIR,20,A HANDFUL OF GUN POWDER,"IT'S GRAY."
860 DATAPOW,0,AN EMPTY SOUP CAN,"READS: CHICKEN NOODLE",CAN,0,A GUN,"IT IS A VERY OLD PISTOL.",GUN,0,A SILVER BULLET,"INSCRIPTION: PROPERTY OF THE    LONE RANGER.",BUL,0,"A GOLD KEY:","INSCRIPTION: FINDERS KEYPERS.",KEY,0,A SILVER LOCKPICK
870 DATA"INSCRIPTION: USE TO UNLOCK...",LOC,0,A SHORT FUSE,"IT'S VERY SHORT.",FUS,0,A SKULL CAP,"SKINNY BONES HATS FOR SKELETONS INC.",CAP,28,A TELESCOPE,"READS: USE IN A HIGH PLACE.",TEL,0
880 DATAA LARGE ROCK,"IT MUST WEIGH AT LEAST FIFTY    POUNDS.",ROC,24,A NOTE,"NOTE READS:                      LET EVERYBODY KNOW,             (INCLUDING THE APES),           THAT A TREASURE IS HIDDEN,      BEHIND ROTTING GRAPES.",NOT,0
890 DATA A BOOK,"IT'S VERY DUSTY.",BOO,6,A SHEET OF PAPER,"INSTRUCTIONS FOR USE OF TIME    BOMB.",PAP,4,A SHOVEL,"IT IS VERY OLD AND RUSTY.",SHO,30,AN ORIENTAL RUG,"IT HAS VERY INTRICATE DESIGN    WORK.",RUG,12
900 DATAA BOOK OF MATCHES,"BOOK READS: 'COLLEGE DEGREES BY MAIL, CALL (612) 542-6660'",MAT,8,A TIME BOMB,"IT APPEARS TO BE MADE OUT OF    JUNK.",BOM,0,AN OLD WOODEN BOX,"IT HAS SAILING SHIPS ENGRAVED   ON THE SIDES.",BOX,19,A LARGE DRUM OF CHABLIS
910 DATA "IT IS CAKED WITH DUST.",DRU,22
920 DATAA SAFE,"IT HAS A COMBONATION LOCK, BUT  THE DIAL IS TOO RUSTY TO MOVE,  YOU'LL HAVE TO FIND ANOTHER WAY TO OPEN IT.",SAF,5,A LARGE VAULT,"IT IS LOCKED - YOU NEED A KEY TOOPEN IT.",VAU,23,A CABINET,"IT'S MADE OUT OF WOOD.",CAB,9
930 DATAA BUREAU,"IT'S SOLIDLY BUILT.",BUR,17,A SKELETON,"IT IS KIND OF SKINNY.",SKE,18,A MONSTER,"IT HAS FANGS, THREE EYES,       A HORN, SHAGGY FUR, BIG FEET,   AND IS ABOUT NINE FEET TALL.",MON,22,NORTH ,SOUTH ,EAST ,WEST 
940 DATA13,137,134,140,140,141,13,134,137,143,135,135
950 CLS0:FORX=22TO31:FORXX=0TO63:SET(XX,X,1):NEXTXX,X:R=23:RR=40:FORX=4TO10STEP2:R=R-1:RR=RR+1:FORR1=R TORR:FORFD=X TOX+1:SET(R1,FD,8):NEXTFD,R1,X:FORX=18TO45:FORR=12TO25:SET(X,R,7):NEXTR,X:FORX=14TO17
960 FORR=18TO23:SET(X,R,3):SET(X+32,R,3):NEXTR,X:FORX=21TO24:FORR=17TO22
970 RESET(X,R):RESET(X+18,R):NEXTR,X:FORX=28TO35:FORR=20TO27:SET(X,R,3):NEXTR,X:R=20:FORX=27TO31:R=R-1:RESET(X,R):NEXT:R=14:FORX=32TO36:R=R+1:RESET(X,R):NEXT:R=32:RR=31:FORX=17TO19:R=R-1:RR=RR+1:FORFD=R TORR:SET(FD,X,3):NEXTFD,X
980 FORX=30TO33:FORR=21TO27:RESET(X,R):NEXTR,X:R=18:FORX=13TO17:R=R-1:FORRR=X TO17:SET(RR,R,8):NEXTRR,X:R=12:FORX=46TO50:R=R+1:FORRR=X TO46STEP-1:SET(RR,R,8):NEXTRR,X
990 PLAY"T4L4O2AABBL8EEGGL1A":FORX=1TO999:NEXT:PRINT@6,"an"CHR$(128)"unexplored"CHR$(128)"mansion";:PLAY"L4T4C":FORX=1TO1200:NEXT:SCREEN0,1:RETURN
