1 CLEAR1000
2 DEFINTA-Z:DEFSTRS:ONERRORGOTO150
3 CLS:PRINTCHR$(23):POKE16553,255:PRINT"       SORCERER'S CASTLE":PRINT"        BY GREG HASSETT":CP=49:S1="I DON'T SEE IT HERE.":S2="DON'T BE RIDICULOUS.":GOTO179
4 GOTO160
5 IFRND(85)=20THEN125
6 IFZZ>2ANDRND(100)=50THEN132
7 IF RND(27)=8THEN141
8 T=T+1:GOSUB152:IFVB$="ALA"THENIFCP=32THENCP=17:GOTO4ELSEPRINT"NOTHING HAPPENED.":GOTO8
9 IFVB=-1AND(NO>21ANDNO<30)VB=1
10 IFNO$="JES"PRINT"I SEE NO JESTER HERE.":GOTO8
11 IFVB$="CRO"IF(CP=52ORCP=53)IFCP=53CP=52:GOTO4ELSECP=53:GOTO4
12 IFVB$="TUR"PRINT"YOU HAVE USED";T;"TURNS SO FAR.":GOTO8
13 IFVB<>30AND(VB>10ORVB=2ORVB=6)ANDNO$=""PRINT"YOU MUST SUPPLY A DIRECT OBJECT.":GOTO8
14 IFVB$="HI"IFRND(2)=1PRINT"HELLO":GOTO8ELSEPRINT"NICE WEATHER WE'VE BEEN HAVING LATELY.":GOTO8
15 IFNO$="GUN"NO=1
16 IFVB=30THEN115
17 IFNO$="BAG"NO=5
18 IFVB=-1ANDNO<>0AND(NO<22ORNO>29)PRINT"YOU MUST SUPPLY A VERB.":GOTO8
19 IFVB<1ANDNO=0PRINT"I DON'T KNOW HOW TO "CHR$(34);N1$;CHR$(34)" ANYTHING.":GOTO8
20 IFVB=6AND(NO$="EVE"ORNO$="ALL")THEN22
21 IFNO=0ANDVB>10PRINT"I DON'T KNOW WHAT A "CHR$(34);N1$;CHR$(34)" IS.":GOTO8
22 ONVBGOTO23,30,4,37,40,42,48,49,53,57,59,60,63,72,74,82,85,91,92,92,63,100,100,102,103,106,109,112,113,115
23 IF(NO<22ORNO>29)ANDNO$<>""PRINT"I DON'T KNOW HOW TO DO THAT.":GOTO8
24 IFNO$=""PRINT"WHERE?":GOTO8
25 IFNO>25NO=NO-4
26 NO=NO-22:IFP(CP,NO)=0PRINT"THERE IS NO WAY TO GO IN THAT DIRECTION.":GOTO8
27 IFCP=1ANDNO=1ANDDF=0PRINT"THE CASTLE DOOR IS LOCKED.":GOTO8
28 IFCP=17ANDNO=1ANDCF=0PRINT"THE CRACK IS FAR TOO SMALL FOR YOU TO FOLLOW.":GOTO8
29 CP=P(CP,NO):GOTO4
30 IFOB(NO,0)=-1PRINT"YOU'VE ALREADY GOT IT!":GOTO8
31 IFNO=0PRINT"WHAT'S A ";N1$;"?":GOTO8
32 IFOB(NO,0)<>CPPRINTS1:GOTO8
33 IFNO=17ORNO=21ORNO=20ORNO=16PRINTS2:GOTO8
34 IFZZ+1>7PRINT"YOU ARE UNABLE TO CARRY ANY MORE.":GOTO8
35 IFNO=9ANDPF=0PRINT"THE MOVEMENT OF THE PAINTING REVEALS A VAULT IN THE WALL.
THE VAULT IS LOCKED.":PF=1:OB(16,0)=CP:OB(9,0)=-1:ZZ=ZZ+1:GOTO8
36 PRINT"OKAY.":ZZ=ZZ+1:OB(NO,0)=-1:GOTO8
37 IFZZ=0PRINT"YOU ARE EMPTY HANDED.":GOTO8
38 PRINT"YOU ARE HOLDING THE FOLLOWING :":FORI=1TOLO:IFOB(I,0)=-1PRINTOB$(I)
39 NEXTI:GOTO8
40 J=0:FORI=1TOLO:IFOB(I,0)=1J=J+OB(I,1):NEXTIELSENEXTI
41 PRINT"OUT OF A MAXIMUM OF 225 POINTS, YOU HAVE";J;"POINTS.":GOTO8
42 IFNO<>0ANDOB(NO,0)<>-1PRINT"YOU'RE NOT CARRYING IT!":GOTO8
43 IFNO$<>"EVE"ANDNO$<>"ALL"THEN46
44 IFZZ=0PRINT"YOU'RE NOT CARRYING ANYTHING!":GOTO8ELSEFORI=1TOLO:IFOB(I,0)=-1OB(I,0)=CP
45 NEXTI:ZZ=0:PRINT"OK.":GOTO8
46 IFNO=0PRINT"I'VE NEVER HEARD OF A ";N1$;".":GOTO8
47 OB(NO,0)=CP:PRINT"DROPPED.":ZZ=ZZ-1:GOTO8
48 PRINT"SORRY, THIS IS YOUR ADVENTURE, NOT MINE!":GOTO8
49 CLS:INPUT"PRESS <ENTER> WHEN CASSETTE PREPARATIONS ARE MADE ";XX$
50 FORI=1TOLO:PRINT#-1,OB(I,0)
51 NEXTI:PRINT#-1,CP,T,DF,VF,CF,PF,SF%,WF,ZZ
52 GOTO4
53 CLS:PRINT"PRESS <ENTER> WHEN CASSETTE PREPARATIONS ARE MADE ";XX$
54 FORI=1TOLO:INPUT#-1,OB(I,0)
55 NEXTI:INPUT#-1,CP,T,DF,VF,CF,PF,SF%,WF,ZZ
56 GOTO4
57 INPUT"DO YOU REALLY WANT TO QUIT NOW";QN$:IFLEFT$(QN$,1)<>"Y"THEN8ELSEINPUT"ARE YOU SURE";Q2$:IFLEFT$(Q2$,1)<>"Y"PRINT"I DIDN'T THINK SO.":GOTO8
58 GOTO145
59 PRINT"ALL RIGHT...";N1$;:GOTO8
60 IFOB(NO,0)<>-1PRINT"YOU'RE NOT HOLDING IT HENCE IT IS IMPOSSIBLE TO DRINK IT.":GOTO8
61 IFNO<>7PRINTS2:GOTO8
62 PRINT"OKAY. GULP GULP GULK. SOMETHING SEEMS DIFFERENT NOW...":ZZ=ZZ-1:OB(7,0)=0:CF=1:GOTO8
63 IFNO<>31ANDNO<>16ANDNO<>30PRINT"I KNOW NOT HOW TO OPEN SUCH A THING.":GOTO8
64 IFNO=16ANDOB(16,0)<>CPPRINT"WHAT VAULT?":GOTO8
65 IFNO=16ANDOB(2,0)<>-1PRINT"YOU DON'T HAVE THE KEY.":GOTO8
66 IFNO=16PRINT"THE VAULT IS OPEN.":VF=1:IFOB(15,0)=0PRINT"A GOLD BAR FALLS OUT!":OB(15,0)=CP
67 IFNO=16THEN8
68 IFNO=31THEN122
69 IFCP<>1PRINT"WHAT DOOR?":GOTO8
70 IFOB(2,0)<>-1PRINT"YOU DON'T SEEM TO HAVE THE KEY.":GOTO8
71 PRINT"THE DOOR IS OPEN.":DF=1:GOTO8
72 IFOB(NO,0)<>-1PRINT"YOU ARE NOT CARRYING IT.":GOTO8
73 PRINT"I'VE KNOWN STRANGE PEOPLE, BUT ATTACKING ";OB$(NO);"?":GOTO8
74 IFNO=20THEN79
75 IFNO=17PRINT"WHAT KNIGHT?":GOTO8
76 IFNO=21PRINT"I SEE NO PIRATE HERE.":GOTO8
77 IFOB(I,0)<>-1PRINT"YOU ARE NOT CURRENTLY HOLDING THAT.":GOTO8
78 PRINT"IT ISN'T ALIVE!":GOTO8
79 IFCP<>32PRINT"WHAT SORCERER?":GOTO8
80 IFOB(8,0)<>-1PRINT"YOU HAVE NO SWORD TO DO IT WITH.
   OH MY! THE SORCERER HAS JUST KILLED YOU!":FORI=1TO3000:NEXTI:GOTO145
81 PRINT"THE SORCERER DIED AND THEN VANISHED. HIS SCEPTER REMAINS.":OB(14,0)=CP:OB(20,0)=0:GOTO8
82 IFOB(NO,0)<>-1PRINT"YOU DON'T HAVE THAT RIGHT NOW.":GOTO8
83 IFNO<>3PRINT"HOW DO YOU EXPECT TO READ ";OB$(NO);"?":GOTO8
84 PRINT"IT SAYS :
MAGIC WORD : ALAKAZAM.":GOTO8
85 IFOB(NO,0)<>-1ANDOB(NO,0)<>CPPRINT"I DON'T SEE THAT HERE.":GOTO8
86 IFOB(1,0)<>-1PRINT"YOU DON'T HAVE A GUN!":GOTO8
87 IFNO=17PRINT"THE BULLET DISSOLVES AS IT HITS THE ARMOR.":GOTO8
88 PRINT"THE GOLDEN BULLET FROM THE GOLDEN PISTOL VAPORIZES THAT."
89 IFOB(NO,0)=-1ZZ=ZZ-1
90 OB(NO,0)=0:GOTO8
91 IFOB(NO,0)=-1PRINTS2:GOTO8ELSEPRINT"YOU'RE NOT HOLDING IT.":GOTO8
92 IFNO=16ORNO=30ORNO=31THEN95
93 IFOB(NO,0)<>-1PRINT"YOU ARE NOT HOLDING THAT TO MY KNOWLEDGE.":GOTO8
94 PRINT"I DON'T KNOW HOW TO CLOSE SUCH A THING.":GOTO8
95 IFNO=16ANDOB(16,0)<>CPPRINT"WHAT VAULT?":GOTO8
96 IFNO=16PRINT"THE VAULT IS CLOSED AND LOCKED.":VF=0:GOTO8
97 IFNO=31THEN119
98 IFCP<>1PRINT"WHAT DOOR?":GOTO8
99 PRINT"THE DOOR IS CLOSED AND LOCKED.":DF=0:GOTO8
100 IFOB(NO,0)<>-1PRINT"YOU'RE NOT HOLDING IT.":GOTO8
101 PRINT"NO, THESE ITEMS HAVE TO BE USED ON THE NEXT ADVENTURE, TOO.":GOTO8
102 PRINT"THE VALUE OF THAT IS";OB(NO,1);"POINTS.":GOTO8
103 IFOB(NO,0)<>-1PRINT"YOU DON'T SEEM TO HAVE IT.":GOTO8
104 IFNO<>8PRINT"WOW THIS IS FUN!":GOTO8
105 PRINT"WHOOOSH!":GOTO8
106 IFOB(NO,0)<>-1PRINT"YOU'RE NOT CURRENTLY CARRYING IT.":GOTO8
107 IFNO<>8PRINTS2:GOTO8
108 PRINT"IT IS ALREADY RAZOR SHARP.":GOTO8
109 IFOB(NO,0)<>-1PRINT"TU NE LE PORTES PAS (YOU'RE NOt CARRYING IT).":GOTO8
110 IFNO<>19PRINT"HOW?":GOTO8
111 PRINT"VERY GOOD! NOW YOU MAY GO TO SECOND GRADE!":GOTO8
112 PRINT"TRY DROP.":GOTO8
113 PRINT"I CAN ONLY TELL YOU WHAT IS IN THE ROOM WITH YOU. SORRY,"
114 PRINT"I CANNOT FIND ANYTHING.":GOTO8
115 IFCP<>44IFRND(2)=1PRINT"WHEEEE!"ELSEPRINT"ARE YOU ENJOYING YOURSELF?"
116 IFCP<>44THEN8
117 IFWF=0PRINT"THE WINDOW IS ONLY SLIGHTLY AJAR.":GOTO8
118 PRINT"OKAY...YOU FALL SAFELY IN THE PILE OF LEAVES.":CP=48:PRINT"YOU SEEM TO BE LOST IN A FOREST OF SPRUCE TREES.":GOTO8
119 IFCP<>44PRINT"WHAT WINDOW? I DON'T SEE A WINDOW!":GOTO8
120 IFWF=0PRINT"IT IS ALREADY CLOSED.":GOTO8
121 PRINT"HAVING OPENED IT EARLIER, YOU FIND IT IMPOSSIBLE TO CLOSE.":GOTO8
122 IFCP<>44PRINT"WHAT WINDOW?":GOTO8
123 IFWF=1PRINT"IT IS ALREADY OPEN.":GOTO8
124 PRINT"THROUGH A GREAT EFFORT, YOU OPEN THE WINDOW. THERE IS A PILE
OF LEAVES OUTSIDE THE WINDOW.":WF=1:GOTO8
125 PRINT"HOLY MOLY! A BLACK KNIGHT WALKED IN TO KILL YOU!":GOSUB152
126 IFVB<>17ORNO<>17THEN129
127 IFOB(1,0)<>-1PRINT"YOU HAVE NO GUN!":GOTO129
128 PRINT"THE KNIGHT DISINTIGRATED.":GOTO8
129 PRINT"THE KNIGHT IS RUNNING TOWARDS YOU...";
130 IFRND(5)=2PRINT"IT KILLS YOU":FORI=1TO3000:NEXTI:GOTO145
131 PRINT"IT MISSES YOU
AND CRASHES INTO THE WALL...WAIT! HE HAS DISAPPEARED!":GOTO8
132 PRINT"A PIRATE JUST APPEARED AND CHARTLED,"
133 PRINT"WELL SHIVER ME TIMBERS! NOT ANOTHER ONE! HAR HAR HAR,"
134 PRINT"I'LL JUST SNATCH ALL THIS BOOTY AND HIDE IT"
135 PRINT"DEEP IN THE MAZE."
136 PRINT"WITH THAT HE STEALS ALL OF YOUR TREASURE!"
137 FORI=1TOLO:IFOB(I,0)<>-1THEN140
138 IFOB(I,1)=0THEN140
139 OB(I,0)=RND(6)+34:ZZ=ZZ-1
140 NEXTI:GOTO8
141 PRINT"JINGLE JINGLE JINGLE!":PRINT"CHESTER THE JESTER POPS UP AND CHANTS,"
142 PRINTCHR$(34);C$(RND(8)-1);" TEE HEE HEE!";CHR$(34)
143 PRINT"AND THEN VANISHES IN A CLOUD OF PINK SMOKE."
144 GOTO8
145 CLS:J=0:FORI=1TOLO:IFOB(I,0)=1J=J+OB(I,1)
146 NEXTI
147 PRINT"OUT OF A MAXIMUM OF 225 POINTS, YOU HAVE SCORED";J;"POINTS."
148 PRINT:INPUT"DO YOU WISH TO TRY THIS ADVENTURE AGAIN ";AG$:AG$=LEFT$(AG$,1):IFAG$="Y"THENRUNELSEIFAG$<>"N"THEN148
149 END
150 PRINT"WHAZZAT?":GOTO8
151 STOP
152 INPUT"***** WHAT DO YOU WANT TO DO";CM$
153 N1$="":V1$="":NO=0:VB=0:NO$="":VB$=""
154 CM=LEN(CM$):FORI=1TOCM:IFMID$(CM$,I,1)<>" "V1$=V1$+MID$(CM$,I,1):NEXTI
155 VB$=LEFT$(V1$,3):FORI=1TONV:IFVB$(I)=VB$VB=I:GOTO157ELSENEXTI
156 VB=-1:N1$=V1$:GOTO158
157 IFLEN(V1$)+1>=LEN(CM$)NO=0:RETURNELSEN1$=RIGHT$(CM$,LEN(CM$)-1-LEN(V1$))
158 NO$=LEFT$(N1$,3):FORI=1TONN:IFNO$(I)=NO$NO=I:RETURNELSENEXTI
159 NO=0:RETURN
160 CLS:PRINT"YOU'RE ";P$(CP):FORI=1TOLO
161 IFOB(I,0)=CPPRINT"  *** THERE IS ";OB$(I);" HERE."
162 NEXTI
163 IFCP=1ANDDF=0PRINT"THE DOOR IS LOCKED."
164 IFCP=18ANDVF=0ANDOB(16,0)=18PRINT"THE VAULT IS LOCKED."
165 IFCP=17ANDCF=0PRINT"A SMALL CRACK LEADS SOUTHWARD."
166 IFCP=1ANDDF=1PRINT"THE DOOR IS OPEN."
167 IFCP=18ANDVF=1ANDOB(16,0)=18PRINT"THE VAULT IS OPEN."
168 IFCP=44ANDWF=0PRINT"THE WINDOW IS SLIGHTLY AJAR."
169 IFCP=17ANDCF=1PRINT"A TOWERING HOLE LEADS SOUTHWARD."
170 IFCF=0P(17,1)=0
171 IFCP=44ANDWF=1PRINT"THE WINDOW IS WIDE OPEN. THERE IS A RATHER
LARGE PILE OF LEAVES OUTSIDE THE WINDOW."
172 K=0:PRINT"YOU CAN GO ";:FORI=0TO3:IFP(CP,I)=0THEN175
173 IFK=1PRINT", ";
174 PRINTD$(I);:K=1
175 NEXT:IFK=0PRINT"NOWHERE!"
176 IFK=1PRINT
177 P(17,1)=18
178 PRINT"===============================================================":GOTO5
179 NP=53:LO=35:NN=31:NV=30:DIMP(NP,3),P$(NP),VB$(NV),NO$(NN),OB(LO,1),OB$(LO)
180 P$(1)="OUTSIDE A MEDIEVAL CASTLE.":DATA5,8,3,2,4,7,1,0,6,7,0,1
181 P$=" SIDE OF THE CASTLE.":P$(2)="AT THE WEST"+P$:P$(3)="AT THE EAST"+P$
182 P$(4)="IN A VAST FOREST STRETCHING OUT OF SIGHT.":DATA4,2,4,4,53,1,6,0
183 P$(5)=P$(4):P$(6)=P$(4):DATA6,6,6,5
184 P$(7)="AT THE SOUTH"+P$+"
THERE IS A TOWER WITH A SMALL WINDOW IN THE FAR, FAR DISTANCE.":DATA2,0,0,0
185 P$(8)="IN A SPLENDID CHAMBER 30 FEET HIGH.":DATA1,11,0,10
186 P$(9)="IN A DAMP, MUSTY LIBRARY.":DATA0,10,0,0
187 P$(10)="IN THE MASTER BEDROOM.":DATA9,0,8,0
188 P$="IN A VAST CORRIDOR STRETCHING OUT OF
SIGHT TO THE ":P$(11)=P$+"SOUTH.":DATA8,12,14,0
189 P$(12)=P$+"NORTH AND SOUTH.":DATA11,13,15,0
190 P$(13)=P$+"NORTH.":DATA12,0,16,17
191 P$="IN A BEDROOM WITH A ":P$(14)=P$+"STONE FLOOR.":P$(15)=P$+"WOODEN FLOOR.":P$(16)=P$+"DIRT FLOOR.":DATA0,0,0,11,0,0,0,12,0,33,0,13
192 P$(17)="IN A DUSTY PANTRY. MARKINGS IN THE DUST READ,
GO SOUTH TO DUNGEON.":DATA0,18,13,0
193 P$(18)="IN A PRIMATIVE ART GALLERY.":DATA17,0,0,19
194 P$(19)="IN A MAZE OF TWISTY LITTLE PASSAGES."
195 P$(20)="IN A TWISY MAZE OF LITTLE PASSAGES."
196 P$(21)="IN A LITTLE MAZE OF TWISTY PASSAGES."
197 P$(22)="IN A TWISTING MAZE OF LITTLE PASSAGES."
198 P$(23)="IN A LITTLE TWISTY MAZE OF PASSAGES."
199 DATA0,20,18,0,20,22,20,19,21,21,22,23,21,22,20,22,21,24,23,23
200 P$(24)="IN THE KITCHEN.":DATA23,27,25,0,0,27,26,24,29,28,30,25
201 P$(25)="IN THE DINING ROOM.":P$(26)="IN THE ALCOVE TO THE STUDY.":P$(27)="IN THE OFFICE OF THE SORCERER (?)":DATA24,0,28,0
202 P$(28)="IN THE DRAWING ROOM.":DATA0,0,0,27,0,26,0,0,0,31,0,26
203 P$(29)="IN THE PARLOR.":P$(30)="IN THE STUDY.":P$(31)="IN THE SITTING ROOM.":DATA0,0,0,0,0,0,0,0
204 P$(32)="IN THE DUNGEON!"
205 FORI=1TONP:FORJ=0TO3:READP(I,J):NEXTJ,I:P(31,RND(4)-1)=32
206 P$(33)="IN A PRIMEVAL CONFERENCE ROOM.":DATA16,0,35,0
207 P$(34)="IN THE TOWER. IT OVERLOOKS A HUGE KINGDOM DOWN
A MONSTROUS MOUNTAIN. THE GRASS IS GREENER THAN GREEN ITSELF.":DATA0,0,36,0
208 P$(35)="IN A MAZE OF TWISTY LITTLE PASSAGES, ALL ALIKE.":FORI=36TO40:P$(I)=P$(35):NEXT:DATA36,35,35,33,38,34,37,35,36,0,39,0,40,39,39,36,38,37,39,38,41,38,0,0,42,40,0,0
209 P$(41)="AT THE BRINK OF A HUGE PIT.":P$(43)="IN A BARREN CUBICAL, WITH PASSAGES LEADING OFF TO THE
NORTH AND SOUTH.":P$(42)="IN THE PIT. A GOOD PASSAGE EXITS TO THE NORTH."
210 P$(44)="AT THE END OF THE CASTLE. YOU CAN SEE FOREST OUT
A SMALL WINDOW.":DATA43,0,41,0,44,42,0,0,0,0,0,0
211 P$(45)="LOST IN A DARK FOREST MADE UP CHIEFLY OF SPRUCE TREES.":FORI=46TO48:P$(I)=P$(45):NEXTI:DATA45,46,45,49,45,47,46,50,46,51,47,48,50,48,47,48
212 P$(49)="ON AN OLD PATH MADE BY HORSES IN MEDIEVAL TIMES.":P$(51)="IN THE MIDDLE OF A CLEARING. TO THE SOUTH IS A BRIDGE.":P$(52)="ON THE NORTH SIDE OF THE BRIDGE":P$(53)="ON THE SOUTH SIDE OF THE BRIDGE.":DATA0,50,45,0,49,48,46,0,47,52,0,0,51,53   
213 DATA0,0,52,5,0,0,PIS,KEY,BOO,RUG,COI,SAP,LIQ,SWO,PAI,OPA,SIL,PEN,PIL,SCE,BAR,VAU,KNI,CUP,CRO,SOR,PIR,NOR,SOU,EAS,WES,N,S,E,W,DOO,WIN
214 DATA GO,GET,LOO,INV,SCO,DRO,HEL,SAV,LOA,QUI,SAY,DRI,OPE,HIT,KIL,REA,SHO,EAT,CLO,LOC,UNL,DES,BRE,VAL,SWI,SHA,WEA,THR,FIN,JUM
215 FORI=1TONN:READNO$(I):NEXTI:FORI=1TONV:READVB$(I):NEXTI
216 DATA A GOLD PISTOL,4,10,A SET OF KEYS,2,0,A BOOK ON SORCERY,9,0,A PERSIAN RUG,10,15,A BAG OF COINS,3,15,A PRICELESS SAPHIRE,14,10,A FLASK OF LIQUID,15,0,A GOLDEN SWORD,42,10,A PRICELESS PAINTING,18,15
217 DATA A GIANT OPAL,22,15,A SET OF SILVERWARE,24,15,A PLATINUM PEN,27,5,A VELVET PILLOW,43,20,THE SORCERER'S SCEPTER,0,50,A GOLD BAR,0,20,A VAULT IN THE WALL,0,0,A BLACK KNIGHT,0,0,A PEWTER CUP,25,15,A KING'S CROWN,30,10,XXX
218 DATA32,0,AN EVIL PIRATE,0,0
219 FORI=1TO21:READOB$(I),OB(I,0),OB(I,1):NEXTI
220 DATA NORTH,SOUTH,EAST,WEST
221 FORI=0TO3:READD$(I):NEXTI
222 P$(19)=P$(19)+"
A NOTE ON THE WAlL READS: THIS IS NOT THE MAZE WHERE THE PIRATE
HIDES HIS TREASURE. --GH"
223 OB$(20)="AN EVIL SORCERER JUST WAITING TO CAST A SPELL
      ON YOU"
224 P$(50)="AT THE END OF THE PATH, WITH FOREST SURROUNDING
YOU IN ALL DIRECTIONS EXCEPT NORTH, WHERE YOU WERE BEFORE."
225 P(50,3)=48:P$(47)=P$(47)+"
TO THE SOUTH THERE SEEMS TO BE LIGHT."
226 FORI=52TO53:P$(I)=P$(I)+"
ACROSS THE BRIDGE IS MORE FOREST.":NEXT
227 P(36,2)=37
228 C$(0)="PAY ATTENTION IN THE MAZE, TO THE WORDING OF THE
PHRASE! IF YOU DO YOU'LL BE UN-MAZED."
229 C$(1)="GULP GULP GULP, DRINK IT DOWN, I'M NOT SUCH A
STUPID CLOWN!"
230 C$(2)="OUT OF A WINDOW YOU MAY FALL. JUST LOOK OUT,
THAT IS ALL!"
231 C$(3)="THE KNIGHTS CAN'T BE KILLED, AS FAR AS I KNOW. IF
YOU FIND THEY CAN, PLEASE TELL ME SO!"
232 C$(4)="DON'T BRING THE SWORD TO THE SORCERER'S PLACE. IF
YOU DO, HE MIGHT CONFRONT YOU FACE TO FACE!"
233 C$(5)="HOCUS POCUS, ALAKAZAM! DON'T READ THE BOOK OR
YOU'LL BE SORRY! (YOU TRY TO RHYME ALAKAZAM)!"
234 C$(6)="PASSAGES THAT SEEM ALL ALIKE CAN SEEM QUITE
DIFFERENT, JUST HOPE YOU'RE RIGHT!"
235 C$(7)="NOT ALL TREASURE LIES UNDER A ROOF. SOME MIGHT BE
UNDER A TREE!"
236 GOTO4
