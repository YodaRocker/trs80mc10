55 CLS:INPUT"WHAT IS YOUR NAME";NM$
60 CLEAR1000:DEFINTA-Z:DEFSTRS:ONERRORGOTO9998
100 CLS:PRINTCHR$(23):POKE16553,255:PRINT"       SORCERER'S CASTLE":PRINT"        BY GREG HASSETT":CP=49:S1="I DON'T SEE IT HERE.":S2="DON'T BE RIDICULOUS.":GOTO50000
110 GOTO10200
111 IFRND(85)=20THEN5000
112 IFZZ>2ANDRND(100)=50THEN5500
113 IF RND(27)=8THEN6000
120 T=T+1:GOSUB10000:IFVB$="ALA"IFCP=32CP=17:GOTO110ELSEPRINT"NOTHING HAPPENED.":GOTO120
121 IFVB=-1AND(NO>21ANDNO<30)VB=1
122 IFNO$="JES"PRINT"I SEE NO JESTER HERE.":GOTO120
123 IFVB$="CRO"IF(CP=52ORCP=53)IFCP=53CP=52:GOTO110ELSECP=53:GOTO110
124 IFVB$="TUR"PRINT"YOU HAVE USED";T;"TURNS SO FAR.":GOTO120
125 IFVB<>30AND(VB>10ORVB=2ORVB=6)ANDNO$=""PRINT"YOU MUST SUPPLY A DIRECT OBJECT.":GOTO120
126 IFVB$="HI"IFRND(2)=1PRINT"HELLO":GOTO120ELSEPRINT"NICE WEATHER WE'VE BEEN HAVING LATELY.":GOTO120
127 IFNO$="GUN"NO=1
128 IFVB=30THEN1000
129 IFNO$="BAG"NO=5
130 IFVB=-1ANDNO<>0AND(NO<22ORNO>29)PRINT"YOU MUST SUPPLY A VERB.":GOTO120
135 IFVB<1ANDNO=0PRINT"I DON'T KNOW HOW TO "CHR$(34);N1$;CHR$(34)" ANYTHING.":GOTO120
136 IFVB=6AND(NO$="EVE"ORNO$="ALL")THEN200
140 IFNO=0ANDVB>10PRINT"I DON'T KNOW WHAT A "CHR$(34);N1$;CHR$(34)" IS.":GOTO120
200 ONVBGOTO211,300,110,400,450,500,550,560,565,570,575,580,600,650,700,720,740,790,800,800,600,850,850,860,865,900,950,970,975,1000
211 IF(NO<22ORNO>29)ANDNO$<>""PRINT"I DON'T KNOW HOW TO DO THAT.":GOTO120
212 IFNO$=""PRINT"WHERE?":GOTO120
213 IFNO>25NO=NO-4
215 NO=NO-22:IFP(CP,NO)=0PRINT"THERE IS NO WAY TO GO IN THAT DIRECTION.":GOTO120
220 IFCP=1ANDNO=1ANDDF=0PRINT"THE CASTLE DOOR IS LOCKED.":GOTO120
225 IFCP=17ANDNO=1ANDCF=0PRINT"THE CRACK IS FAR TOO SMALL FOR YOU TO FOLLOW.":GOTO120
299 CP=P(CP,NO):GOTO110
300 IFOB(NO,0)=-1PRINT"YOU'VE ALREADY GOT IT!":GOTO120
301 IFNO=0PRINT"WHAT'S A ";N1$;"?":GOTO120
305 IFOB(NO,0)<>CPPRINTS1:GOTO120
310 IFNO=17ORNO=21ORNO=20ORNO=16PRINTS2:GOTO120
330 IFZZ+1>7PRINT"YOU ARE UNABLE TO CARRY ANY MORE.":GOTO120
331 IFNO=9ANDPF=0PRINT"THE MOVEMENT OF THE PAINTING REVEALS A VAULT IN THE WALL.
THE VAULT IS LOCKED.":PF=1:OB(16,0)=CP:OB(9,0)=-1:ZZ=ZZ+1:GOTO120
335 PRINT"OKAY.":ZZ=ZZ+1:OB(NO,0)=-1:GOTO120
400 IFZZ=0PRINT"YOU ARE EMPTY HANDED.":GOTO120
410 PRINT"YOU ARE HOLDING THE FOLLOWING :":FORI=1TOLO:IFOB(I,0)=-1PRINTOB$(I)
420 NEXTI:GOTO120
450 J=0:FORI=1TOLO:IFOB(I,0)=1J=J+OB(I,1):NEXTIELSENEXTI
460 PRINT"OUT OF A MAXIMUM OF 225 POINTS, YOU HAVE";J;"POINTS.":GOTO120
500 IFNO<>0ANDOB(NO,0)<>-1PRINT"YOU'RE NOT CARRYING IT!":GOTO120
501 IFNO$<>"EVE"ANDNO$<>"ALL"THEN510
502 IFZZ=0PRINT"YOU'RE NOT CARRYING ANYTHING!":GOTO120ELSEFORI=1TOLO:IFOB(I,0)=-1OB(I,0)=CP
503 NEXTI:ZZ=0:PRINT"OK.":GOTO120
510 IFNO=0PRINT"I'VE NEVER HEARD OF A ";N1$;".":GOTO120
520 OB(NO,0)=CP:PRINT"DROPPED.":ZZ=ZZ-1:GOTO120
550 PRINT"SORRY, THIS IS YOUR ADVENTURE, NOT MINE!":GOTO120
560 CLS:INPUT"PRESS <ENTER> WHEN DISK PREPARATIONS ARE MADE ";XX$:OPEN"O",1,NM$+"/SOR"
561 FORI=1TOLO:PRINT#1,OB(I,0)
562 NEXTI:PRINT#1,CP,T,DF,VF,CF,PF,SF%,WF,ZZ:CLOSE1
563 GOTO110
565 CLS:INPUT"PRESS <ENTER> WHEN DISK PREPARATIONS ARE MADE ";XX$:OPEN"I",1,NM$
566 FORI=1TOLO:INPUT#1,OB(I,0)
567 NEXTI:INPUT#1,CP,T,DF,VF,CF,PF,SF%,WF,ZZ:CLOSE1
568 GOTO110
570 INPUT"DO YOU REALLY WANT TO QUIT NOW";QN$:IFLEFT$(QN$,1)<>"Y"THEN120ELSEINPUT"ARE YOU SURE";Q2$:IFLEFT$(Q2$,1)<>"Y"PRINT"I DIDN'T THINK SO.":GOTO120
571 GOTO9000
575 PRINT"ALL RIGHT...";N1$;:GOTO120
580 IFOB(NO,0)<>-1PRINT"YOU'RE NOT HOLDING IT HENCE IT IS IMPOSSIBLE TO DRINK IT.":GOTO120
585 IFNO<>7PRINTS2:GOTO120
590 PRINT"OKAY. GULP GULP GULK. SOMETHING SEEMS DIFFERENT NOW...":ZZ=ZZ-1:OB(7,0)=0:CF=1:GOTO120
600 IFNO<>31ANDNO<>16ANDNO<>30PRINT"I KNOW NOT HOW TO OPEN SUCH A THING.":GOTO120
605 IFNO=16ANDOB(16,0)<>CPPRINT"WHAT VAULT?":GOTO120
610 IFNO=16ANDOB(2,0)<>-1PRINT"YOU DON'T HAVE THE KEY.":GOTO120
615 IFNO=16PRINT"THE VAULT IS OPEN.":VF=1:IFOB(15,0)=0PRINT"A GOLD BAR FALLS OUT!":OB(15,0)=CP
620 IFNO=16THEN120
621 IFNO=31THEN2000
625 IFCP<>1PRINT"WHAT DOOR?":GOTO120
630 IFOB(2,0)<>-1PRINT"YOU DON'T SEEM TO HAVE THE KEY.":GOTO120
635 PRINT"THE DOOR IS OPEN.":DF=1:GOTO120
650 IFOB(NO,0)<>-1PRINT"YOU ARE NOT CARRYING IT.":GOTO120
655 PRINT"I'VE KNOWN STRANGE PEOPLE, BUT ATTACKING ";OB$(NO);"?":GOTO120
700 IFNO=20THEN708
702 IFNO=17PRINT"WHAT KNIGHT?":GOTO120
704 IFNO=21PRINT"I SEE NO PIRATE HERE.":GOTO120
705 IFOB(I,0)<>-1PRINT"YOU ARE NOT CURRENTLY HOLDING THAT.":GOTO120
706 PRINT"IT ISN'T ALIVE!":GOTO120
708 IFCP<>32PRINT"WHAT SORCERER?":GOTO120
710 IFOB(8,0)<>-1PRINT"YOU HAVE NO SWORD TO DO IT WITH.
   OH MY! THE SORCERER HAS JUST KILLED YOU!":FORI=1TO3000:NEXTI:GOTO9000
712 PRINT"THE SORCERER DIED AND THEN VANISHED. HIS SCEPTER REMAINS.":OB(14,0)=CP:OB(20,0)=0:GOTO120
720 IFOB(NO,0)<>-1PRINT"YOU DON'T HAVE THAT RIGHT NOW.":GOTO120
725 IFNO<>3PRINT"HOW DO YOU EXPECT TO READ ";OB$(NO);"?":GOTO120
730 PRINT"IT SAYS :
MAGIC WORD : ALAKAZAM.":GOTO120
740 IFOB(NO,0)<>-1ANDOB(NO,0)<>CPPRINT"I DON'T SEE THAT HERE.":GOTO120
745 IFOB(1,0)<>-1PRINT"YOU DON'T HAVE A GUN!":GOTO120
750 IFNO=17PRINT"THE BULLET DISSOLVES AS IT HITS THE ARMOR.":GOTO120
755 PRINT"THE GOLDEN BULLET FROM THE GOLDEN PISTOL VAPORIZES THAT."
760 IFOB(NO,0)=-1ZZ=ZZ-1
765 OB(NO,0)=0:GOTO120
790 IFOB(NO,0)=-1PRINTS2:GOTO120ELSEPRINT"YOU'RE NOT HOLDING IT.":GOTO120
800 IFNO=16ORNO=30ORNO=31THEN820
805 IFOB(NO,0)<>-1PRINT"YOU ARE NOT HOLDING THAT TO MY KNOWLEDGE.":GOTO120
810 PRINT"I DON'T KNOW HOW TO CLOSE SUCH A THING.":GOTO120
820 IFNO=16ANDOB(16,0)<>CPPRINT"WHAT VAULT?":GOTO120
825 IFNO=16PRINT"THE VAULT IS CLOSED AND LOCKED.":VF=0:GOTO120
826 IFNO=31THEN1100
830 IFCP<>1PRINT"WHAT DOOR?":GOTO120
835 PRINT"THE DOOR IS CLOSED AND LOCKED.":DF=0:GOTO120
850 IFOB(NO,0)<>-1PRINT"YOU'RE NOT HOLDING IT.":GOTO120
855 PRINT"NO, THESE ITEMS HAVE TO BE USED ON THE NEXT ADVENTURE, TOO.":GOTO120
860 PRINT"THE VALUE OF THAT IS";OB(NO,1);"POINTS.":GOTO120
865 IFOB(NO,0)<>-1PRINT"YOU DON'T SEEM TO HAVE IT.":GOTO120
870 IFNO<>8PRINT"WOW THIS IS FUN!":GOTO120
875 PRINT"WHOOOSH!":GOTO120
900 IFOB(NO,0)<>-1PRINT"YOU'RE NOT CURRENTLY CARRYING IT.":GOTO120
910 IFNO<>8PRINTS2:GOTO120
920 PRINT"IT IS ALREADY RAZOR SHARP.":GOTO120
950 IFOB(NO,0)<>-1PRINT"TU NE LE PORTES PAS (YOU'RE NOt CARRYING IT).":GOTO120
955 IFNO<>19PRINT"HOW?":GOTO120
960 PRINT"VERY GOOD! NOW YOU MAY GO TO SECOND GRADE!":GOTO120
970 PRINT"TRY DROP.":GOTO120
975 PRINT"I CAN ONLY TELL YOU WHAT IS IN THE ROOM WITH YOU. SORRY,"
980 PRINT"I CANNOT FIND ANYTHING.":GOTO120
1000 IFCP<>44IFRND(2)=1PRINT"WHEEEE!"ELSEPRINT"ARE YOU ENJOYING YOURSELF?"
1010 IFCP<>44THEN120
1020 IFWF=0PRINT"THE WINDOW IS ONLY SLIGHTLY AJAR.":GOTO120
1030 PRINT"OKAY...YOU FALL SAFELY IN THE PILE OF LEAVES.":CP=48:PRINT"YOU SEEM TO BE LOST IN A FOREST OF SPRUCE TREES.":GOTO120
1100 IFCP<>44PRINT"WHAT WINDOW? I DON'T SEE A WINDOW!":GOTO120
1110 IFWF=0PRINT"IT IS ALREADY CLOSED.":GOTO120
1120 PRINT"HAVING OPENED IT EARLIER, YOU FIND IT IMPOSSIBLE TO CLOSE.":GOTO120
2000 IFCP<>44PRINT"WHAT WINDOW?":GOTO120
2010 IFWF=1PRINT"IT IS ALREADY OPEN.":GOTO120
2020 PRINT"THROUGH A GREAT EFFORT, YOU OPEN THE WINDOW. THERE IS A PILE
OF LEAVES OUTSIDE THE WINDOW.":WF=1:GOTO120
5000 PRINT"HOLY MOLY! A BLACK KNIGHT WALKED IN TO KILL YOU!":GOSUB10000
5010 IFVB<>17ORNO<>17THEN5100
5020 IFOB(1,0)<>-1PRINT"YOU HAVE NO GUN!":GOTO5100
5030 PRINT"THE KNIGHT DISINTIGRATED.":GOTO120
5100 PRINT"THE KNIGHT IS RUNNING TOWARDS YOU...";
5110 IFRND(5)=2PRINT"IT KILLS YOU":FORI=1TO3000:NEXTI:GOTO9000
5120 PRINT"IT MISSES YOU
AND CRASHES INTO THE WALL...WAIT! HE HAS DISAPPEARED!":GOTO120
5500 PRINT"A PIRATE JUST APPEARED AND CHARTLED,"
5510 PRINT"WELL SHIVER ME TIMBERS! NOT ANOTHER ONE! HAR HAR HAR,
I'LL JUST SNATCH ALL THIS BOOTY AND HIDE IT DEEP IN THE MAZE.
WITH THAT, HE STEALS ALL OF YOUR TREASURE!"
5550 FORI=1TOLO:IFOB(I,0)<>-1THEN5600
5560 IFOB(I,1)=0THEN5600
5570 OB(I,0)=RND(6)+34:ZZ=ZZ-1
5600 NEXTI:GOTO120
6000 PRINT"JINGLE JINGLE JINGLE!":PRINT"CHESTER THE JESTER POPS UP AND CHANTS,"
6010 PRINTCHR$(34);C$(RND(8)-1);" TEE HEE HEE!";CHR$(34)
6011 PRINT"AND THEN VANISHES IN A CLOUD OF PINK SMOKE."
6020 GOTO120
9000 CLS:J=0:FORI=1TOLO:IFOB(I,0)=1J=J+OB(I,1)
9001 NEXTI
9010 PRINT"OUT OF A MAXIMUM OF 225 POINTS, YOU HAVE SCORED";J;"POINTS."
9250 PRINT:INPUT"DO YOU WISH TO TRY THIS ADVENTURE AGAIN ";AG$:AG$=LEFT$(AG$,1):IFAG$="Y"THENRUNELSEIFAG$<>"N"THEN9250
9300 END
9998 CLOSE:IFERR=106PRINT"NO SAVED DATA.":RESUME120ELSEPRINT"WHAZZAT?":RESUME120
9999 STOP
10000 INPUT"WHAT DO YOU WANT ME TO DO";CM$
10010 N1$="":V1$="":NO=0:VB=0:NO$="":VB$=""
10060 CM=LEN(CM$):FORI=1TOCM:IFMID$(CM$,I,1)<>" "V1$=V1$+MID$(CM$,I,1):NEXTI
10070 VB$=LEFT$(V1$,3):FORI=1TONV:IFVB$(I)=VB$VB=I:GOTO10090ELSENEXTI
10080 VB=-1:N1$=V1$:GOTO10100
10090 IFLEN(V1$)+1>=LEN(CM$)NO=0:RETURNELSEN1$=RIGHT$(CM$,LEN(CM$)-1-LEN(V1$))
10100 NO$=LEFT$(N1$,3):FORI=1TONN:IFNO$(I)=NO$NO=I:RETURNELSENEXTI
10110 NO=0:RETURN
10200 CLS:PRINT"YOU'RE ";P$(CP):FORI=1TOLO
10226 IFOB(I,0)=CPPRINT"  *** THERE IS ";OB$(I);" HERE."
10220 NEXTI
10225 IFCP=1ANDDF=0PRINT"THE DOOR IS LOCKED."
10226 IFCP=18ANDVF=0ANDOB(16,0)=18PRINT"THE VAULT IS LOCKED."
10230 IFCP=17ANDCF=0PRINT"A SMALL CRACK LEADS SOUTHWARD."
10235 IFCP=1ANDDF=1PRINT"THE DOOR IS OPEN."
10236 IFCP=18ANDVF=1ANDOB(16,0)=18PRINT"THE VAULT IS OPEN."
10237 IFCP=44ANDWF=0PRINT"THE WINDOW IS SLIGHTLY AJAR."
10240 IFCP=17ANDCF=1PRINT"A TOWERING HOLE LEADS SOUTHWARD."
10245 IFCF=0P(17,1)=0
10247 IFCP=44ANDWF=1PRINT"THE WINDOW IS WIDE OPEN. THERE IS A RATHER
LARGE PILE OF LEAVES OUTSIDE THE WINDOW."
10250 K=0:PRINT"YOU CAN GO ";:FORI=0TO3:IFP(CP,I)=0THEN10280
10260 IFK=1PRINT", ";
10270 PRINTD$(I);:K=1
10280 NEXT:IFK=0PRINT"NOWHERE!"
10290 IFK=1PRINT
10300 P(17,1)=18
10500 PRINTSTRING$(63,"-"):GOTO111
50000 NP=53:LO=35:NN=31:NV=30:DIMP(NP,3),P$(NP),VB$(NV),NO$(NN),OB(LO,1),OB$(LO)
50005 P$(1)="OUTSIDE A MEDIEVAL CASTLE.":DATA5,8,3,2,4,7,1,0,6,7,0,1
50010 P$=" SIDE OF THE CASTLE.":P$(2)="AT THE WEST"+P$:P$(3)="AT THE EAST"+P$
50015 P$(4)="IN A VAST FOREST STRETCHING OUT OF SIGHT.":DATA4,2,4,4,53,1,6,0
50020 P$(5)=P$(4):P$(6)=P$(4):DATA6,6,6,5
50025 P$(7)="AT THE SOUTH"+P$+"
THERE IS A TOWER WITH A SMALL WINDOW IN THE FAR, FAR DISTANCE.":DATA2,0,0,0
50030 P$(8)="IN A SPLENDID CHAMBER 30 FEET HIGH.":DATA1,11,0,10
50040 P$(9)="IN A DAMP, MUSTY LIBRARY.":DATA0,10,0,0
50050 P$(10)="IN THE MASTER BEDROOM.":DATA9,0,8,0
50060 P$="IN A VAST CORRIDOR STRETCHING OUT OF
SIGHT TO THE ":P$(11)=P$+"SOUTH.":DATA8,12,14,0
50065 P$(12)=P$+"NORTH AND SOUTH.":DATA11,13,15,0
50070 P$(13)=P$+"NORTH.":DATA12,0,16,17
50075 P$="IN A BEDROOM WITH A ":P$(14)=P$+"STONE FLOOR.":P$(15)=P$+"WOODEN FLOOR.":P$(16)=P$+"DIRT FLOOR.":DATA0,0,0,11,0,0,0,12,0,33,0,13
50080 P$(17)="IN A DUSTY PANTRY. MARKINGS IN THE DUST READ,
GO SOUTH TO DUNGEON.":DATA0,18,13,0
50085 P$(18)="IN A PRIMATIVE ART GALLERY.":DATA17,0,0,19
50090 P$(19)="IN A MAZE OF TWISTY LITTLE PASSAGES."
50095 P$(20)="IN A TWISY MAZE OF LITTLE PASSAGES."
50100 P$(21)="IN A LITTLE MAZE OF TWISTY PASSAGES."
50105 P$(22)="IN A TWISTING MAZE OF LITTLE PASSAGES."
50110 P$(23)="IN A LITTLE TWISTY MAZE OF PASSAGES."
50115 DATA0,20,18,0,20,22,20,19,21,21,22,23,21,22,20,22,21,24,23,23
50120 P$(24)="IN THE KITCHEN.":DATA23,27,25,0,0,27,26,24,29,28,30,25
50125 P$(25)="IN THE DINING ROOM.":P$(26)="IN THE ALCOVE TO THE STUDY.":P$(27)="IN THE OFFICE OF THE SORCERER (?)":DATA24,0,28,0
51030 P$(28)="IN THE DRAWING ROOM.":DATA0,0,0,27,0,26,0,0,0,31,0,26
51035 P$(29)="IN THE PARLOR.":P$(30)="IN THE STUDY.":P$(31)="IN THE SITTING ROOM.":DATA0,0,0,0,0,0,0,0
51040 P$(32)="IN THE DUNGEON!"
51045 FORI=1TONP:FORJ=0TO3:READP(I,J):NEXTJ,I:P(31,RND(4)-1)=32
51046 P$(33)="IN A PRIMEVAL CONFERENCE ROOM.":DATA16,0,35,0
51047 P$(34)="IN THE TOWER. IT OVERLOOKS A HUGE KINGDOM DOWN
A MONSTROUS MOUNTAIN. THE GRASS IS GREENER THAN GREEN ITSELF.":DATA0,0,36,0
51048 P$(35)="IN A MAZE OF TWISTY LITTLE PASSAGES, ALL ALIKE.":FORI=36TO40:P$(I)=P$(35):NEXT:DATA36,35,35,33,38,34,37,35,36,0,39,0,40,39,39,36,38,37,39,38,41,38,0,0,42,40,0,0
51049 P$(41)="AT THE BRINK OF A HUGE PIT.":P$(43)="IN A BARREN CUBICAL, WITH PASSAGES LEADING OFF TO THE
NORTH AND SOUTH.":P$(42)="IN THE PIT. A GOOD PASSAGE EXITS TO THE NORTH."
51051 P$(44)="AT THE END OF THE CASTLE. YOU CAN SEE FOREST OUT
A SMALL WINDOW.":DATA43,0,41,0,44,42,0,0,0,0,0,0
51052 P$(45)="LOST IN A DARK FOREST MADE UP CHIEFLY OF SPRUCE TREES.":FORI=46TO48:P$(I)=P$(45):NEXTI:DATA45,46,45,49,45,47,46,50,46,51,47,48,50,48,47,48
51053 P$(49)="ON AN OLD PATH MADE BY HORSES IN MEDIEVAL TIMES.":P$(51)="IN THE MIDDLE OF A CLEARING. TO THE SOUTH IS A BRIDGE.":P$(52)="ON THE NORTH SIDE OF THE BRIDGE":P$(53)="ON THE SOUTH SIDE OF THE BRIDGE.":DATA0,50,45,0,49,48,46,0,47,52,0,0,51,53   
51054 DATA0,0,52,5,0,0,PIS,KEY,BOO,RUG,COI,SAP,LIQ,SWO,PAI,OPA,SIL,PEN,PIL,SCE,BAR,VAU,KNI,CUP,CRO,SOR,PIR,NOR,SOU,EAS,WES,N,S,E,W,DOO,WIN
51055 DATA GO,GET,LOO,INV,SCO,DRO,HEL,SAV,LOA,QUI,SAY,DRI,OPE,HIT,KIL,REA,SHO,EAT,CLO,LOC,UNL,DES,BRE,VAL,SWI,SHA,WEA,THR,FIN,JUM
51060 FORI=1TONN:READNO$(I):NEXTI:FORI=1TONV:READVB$(I):NEXTI
51065 DATA A GOLD PISTOL,4,10,A SET OF KEYS,2,0,A BOOK ON SORCERY,9,0,A PERSIAN RUG,10,15,A BAG OF COINS,3,15,A PRICELESS SAPHIRE,14,10,A FLASK OF LIQUID,15,0,A GOLDEN SWORD,42,10,A PRICELESS PAINTING,18,15
51070 DATA A GIANT OPAL,22,15,A SET OF SILVERWARE,24,15,A PLATINUM PEN,27,5,A VELVET PILLOW,43,20,THE SORCERER'S SCEPTER,0,50,A GOLD BAR,0,20,A VAULT IN THE WALL,0,0,A BLACK KNIGHT,0,0,A PEWTER CUP,25,15,A KING'S CROWN,30,10,XXX
51075 DATA32,0,AN EVIL PIRATE,0,0
51080 FORI=1TO21:READOB$(I),OB(I,0),OB(I,1):NEXTI
51090 DATA NORTH,SOUTH,EAST,WEST
51100 FORI=0TO3:READD$(I):NEXTI
51105 P$(19)=P$(19)+"
A NOTE ON THE WAlL READS: THIS IS NOT THE MAZE WHERE THE PIRATE
HIDES HIS TREASURE. --GH"
51110 OB$(20)="AN EVIL SORCERER JUST WAITING TO CAST A SPELL
      ON YOU"
51115 P$(50)="AT THE END OF THE PATH, WITH FOREST SURROUNDING
YOU IN ALL DIRECTIONS EXCEPT NORTH, WHERE YOU WERE BEFORE."
51120 P(50,3)=48:P$(47)=P$(47)+"
TO THE SOUTH THERE SEEMS TO BE LIGHT."
51125 FORI=52TO53:P$(I)=P$(I)+"
ACROSS THE BRIDGE IS MORE FOREST.":NEXT
51130 P(36,2)=37
51135 C$(0)="PAY ATTENTION IN THE MAZE, TO THE WORDING OF THE
PHRASE! IF YOU DO YOU'LL BE UN-MAZED."
51140 C$(1)="GULP GULP GULP, DRINK IT DOWN, I'M NOT SUCH A
STUPID CLOWN!"
51145 C$(2)="OUT OF A WINDOW YOU MAY FALL. JUST LOOK OUT,
THAT IS ALL!"
51150 C$(3)="THE KNIGHTS CAN'T BE KILLED, AS FAR AS I KNOW. IF
YOU FIND THEY CAN, PLEASE TELL ME SO!"
51155 C$(4)="DON'T BRING THE SWORD TO THE SORCERER'S PLACE. IF
YOU DO, HE MIGHT CONFRONT YOU FACE TO FACE!"
51160 C$(5)="HOCUS POCUS, ALAKAZAM! DON'T READ THE BOOK OR
YOU'LL BE SORRY! (YOU TRY TO RHYME ALAKAZAM)!"
51165 C$(6)="PASSAGES THAT SEEM ALL ALIKE CAN SEEM QUITE
DIFFERENT, JUST HOPE YOU'RE RIGHT!"
51170 C$(7)="NOT ALL TREASURE LIES UNDER A ROOF. SOME MIGHT BE
UNDER A TREE!"
60000 GOTO110
