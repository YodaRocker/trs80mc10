2 CLS:PRINTCHR$(23):A$="        ENCHANTED  ISLE":B$="        BY GREG HASSETT":FORI=64TO448STEP64:PRINT@I-64,"                             "::PRINT@I,A$:PRINT@960-I,B$:FORJ=1TO50:NEXTJ,I:CLEAR600:POKE16553,255
20 DEFINTA-Z:DEFSTRM:M0="I DON'T SEE IT HERE.":M1="I'M NOT HOLDING IT.":M2=" WON'T LET ME.":M3="DON'T BE RIDICULOUS.":M4="GIVE ME A BREAK!":M5="DON'T BE FUNNY.":M6="OKAY.":M7="IT'S NOT HUNGRY.":M8="HOW?":M9="A VALIENT ATTEMPT.":GOTO30000
25 GOTO100
26 GOSUB30:PRINT"> ";:IFNO<9ANDNO<>0AND(VB=0ORVB=1)VB=1
27 IFVB-27ANDVB-28ANDLEN(N1$)>0ANDNO=0PRINT"I DON'T KNOW WHAT "CHR$(34)N1$CHR$(34)" IS.":GOTO26
28 IFVB-32ANDVB-33ANDVB>8ANDN1$=""PRINT"PLEASE SUPPLY A DIRECT OBJECT.":GOTO26
29 IFVB-27ANDVB-28ANDVB-34ANDVB-14ANDVB>8GOSUB250:GOTO200ELSEGOTO200
30 T=T+1:PRINT"   -----";CHR$(94);:INPUT" WHAT SHOULD I DO";CM$
32 IFLEN(CM$)=0THEN30
35 VB$="":NO$="":VB=0:NO=0
40 LC=LEN(CM$):FORI=1TOLC:A$=MID$(CM$,I,1):IFA$<>" "VB$=VB$+A$:NEXTI
43 IFLEN(VB$)+1>=LEN(CM$)NO=0:GOTO50
45 NO$=RIGHT$(CM$,LC-I)
50 N1$=NO$:V1$=VB$:VB$=LEFT$(VB$,3):NO$=LEFT$(NO$,3):FORI=1TONV:IFVB$=VB$(I)THENVB=I:GOTO70ELSENEXTI:VB=0
55 IFN1$=""NO$=VB$
60 VB=0:GOTO80
70 IF VB$=CM$THENO=0:NO$="":RETURN
80 FORI=1TONN:IFNO$=NO$(I)NO=I:RETURNELSENEXTI:NO=0:NO$="":RETURN
100 II=0:IFLEFT$(P$(CP),1)="/"P$=RIGHT$(P$(CP),LEN(P$(CP))-1):II=1:GOTO105
104 P$="I'M "+P$(CP)
105 IFCP>28ANDCP<45ANDOB(9,0)<>-1ANDOB(9,0)-CPCLS:PRINT"I CAN'T SEE, IT'S TOO DARK.":GOTO170
110 CLS:PRINTP$".":K=0:FORI=1TOLO:IFOB(I,0)<>CPTHEN130
120 IFK=0PRINT"
VISABLE ITEMS: ";
122 IFPOS(0)+LEN(OB$(I))>60PRINT
125 PRINTOB$(I)".  ";:K=K+1
130 NEXTI:IFK>0PRINT
140 IFCP=45THEN170
145 K=0:PRINT"
OBVIOUS EXITS ARE: ";:FORI=0TO3:IFP(CP,I)=0THEN156
150 IFK>0PRINT", ";
155 PRINTD$(I);:K=K+1
156 NEXTI:PRINT".":IFCP=32ANDST=0PRINT"THE SKULL SAYS, "CHR$(34)"THE RUBY WAS BIMBO'S."CHR$(34):ST=1
157 IFOB(I,0)=-1ANDI<>31OB(I,0)=29
170 PRINTSTRING$(63,140):GOTO26
200 ONVBGOTO400,300,25,500,600,700,800,900,1000,1100,1200,1300,1400,1500,1600,1620,1700,1800,1900,2000,2100,2200,2300,2400,2500,2600,2700,2800,2900,3000,3100,3200,3300
205 PRINT"I KNOW NOT HOW TO "CHR$(34)V1$CHR$(34)" SOMETHING.":GOTO26
210 STOP
250 X=0:IFOB(NO,0)<>-1ANDOB(NO,0)-CPPRINTM0:GOTO26ELSEX=OB(NO,0):RETURN
300 GOSUB250:IFX=-1PRINT"I'VE ALREADY GOT IT.":GOTO26
305 IFNO=0PRINT"YOU'LL HAVE TO SUPPLY A NOUN.":GOTO26
310 IFNO=14ORNO=43PRINTM9:GOTO26
320 IFNO=15ORNO=19ORNO=26ORNO=28ORNO=36ORNO=40ORNO=42PRINTM4:GOTO26
330 IFNO=11PRINT"IT IS BEYOND MY REACH.":GOTO26
340 IFNO=25ANDOB(26,0)=CPPRINT"BARBARIAN"M2:GOTO26
350 IFNO=27ANDOB(28,0)=CPPRINT"BEAR"M2:GOTO26
360 IFNO=30ANDOB(42,0)=CPPRINT"MEDICINE MAN"M2:GOTO26
370 IFNO=41ANDOB(40,0)=CPPRINT"TIGER"M2:GOTO26
375 IFNO=37ANDOB(36,0)=CPPRINT"VULTURE"M2:GOTO26
376 IFNO=18ANDOB(19,0)=CPPRINT"WARLOCK"M2:GOTO26
380 IFNO=32ANDOB(21,0)<>-1PRINT"IT IS EMBEDDED IN THE ROCK WALL.":GOTO26
390 IFZZ+1>7PRINT"I'M CARRYING TOO MUCH. CLUE: DROP SOMETHING.":GOTO26
391 PRINTM6:ZZ=ZZ+1:OB(NO,0)=-1:GOTO26
400 IFNO>8PRINT"PLEASE USE COMPASS POINTS.":GOTO26
405 IFNO>4NO=NO-5ELSENO=NO-1
406 IFNO=-1PRINT"WHERE?":GOTO26
410 IFP(CP,NO)=0PRINT"THERE IS NO WAY TO GO IN THAT DIRECTION.":GOTO26
415 IFP(CP,NO)=7CLS:PRINT"I'M IN THE OCEAN! HELP! I'M DEAD.":FORI=1TO1200:NEXTI:GOTO9000
420 CP=P(CP,NO):GOTO25
500 IFZZ=0PRINT"I'M EMPTYHANDED.":GOTO26ELSEPRINT"I AM CARRYING:  ";
510 FORI=9TOLO:IFOB(I,0)<>-1THEN540
520 IFPOS(0)+LEN(OB$(I))>60PRINT
530 PRINTOB$(I)". ";
540 NEXTI:PRINT:GOTO26
600 SC=0:PRINT"OUT OF A MAXIMUM OF 140 POINTS, YOU HAVE";:FORI=9TOLO:IFOB(I,0)=1SC=SC+OB(I,1)
610 NEXTI:PRINTSC;"POINTS.":IFSC=0PRINT"I SEE YOU'RE NEW TO THE GAME..."
620 GOTO26
700 CLS:INPUT"HIT <ENTER>";XX$:PRINT"SAVING...":FORI=9TOLO:PRINT#-1,OB(I,0);:NEXTI
705 PRINT#-1,NO$(9);CP;ZZ
710 GOTO25
800 CLS:INPUT"HIT <ENTER>";XX$:PRINT"RESTORING...":FORI=9TOLO:INPUT#-1,OB(I,0):NEXTI
805 INPUT#-1,NO$(9),CP,ZZ
810 GOTO25
900 IFOB(NO,0)<>-1PRINTM1:GOTO26
905 IFNO=24ANDOB(19,0)=CPPRINT"THE WARLOCK SLIPS AND BREAKS HIS NECK. THEN HE VANISHES.":OB(19,0)=0
910 ZZ=ZZ-1:OB(NO,0)=CP:PRINTM6:GOTO26
1000 IFNO=9PRINT"IT'S ALREADY LIT.":GOTO26
1005 IFOB(20,0)<>-1PRINT"I'VE NO LIGHTER!":GOTO26
1010 IFNO-10PRINT"IT WON'T IGNITE.":GOTO26
1015 IFOB(NO,0)<>-1PRINTM1:GOTO26
1020 NO$(9)="LAN":OB(10,0)=0:OB(9,0)=-1:PRINTM6:GOTO26
1100 IFNO-9PRINTM8:GOTO26
1110 NO$(9)="<X>":OB(9,0)=0:OB(10,0)=-1:PRINTM6:GOTO26
1200 IFNO-11ANDNO-13ANDNO-17ANDNO-43PRINT"THERE IS NO WRITING THERE.":GOTO26
1210 PRINT"IT SAYS:":IFNO=11PRINT"BRING *TREASURES* HERE AND DROP THEM. SAY: SCORE"
1220 IFNO=13PRINT"HOLY SMOKES! A TIGER!
BARBS LIKE BAN..."
1230 IFNO=17PRINTR1$:PRINTR2$:PRINT"BRE BOT
MAGIC WORD: BIMBO"
1240 IFNO=43PRINT"SLIP WAR. HERACLES' THIRD LABOR..."
1250 GOTO26
1300 IFNO-44ANDNO-12PRINT"IT WON'T BREAK!":GOTO26
1310 IFNO=44PRINT"THE BALL CLASHES INTO BITS. A HUGE PUFF OF DENSE GRAY SMOKE
APPEARS IN FRONT OF ME. OUT WALKS A TALL WIZARD AND SAYS:":PRINTCHR$(34)"SO YOU ARE THE EVIL MEDICINE MAN!"CHR$(34):PRINT"WITH A PASS OF HIS HANDS, I VANISH."
1311 IFNO=44PRINT"PRESS ANY BUTTON TO CONTINUE."
1312 IFNO=44A$=INKEY$:IFA$=""THEN1312ELSE9000
1320 IFCP-51PRINT"THERE ISN'T ANYTHING HARD ENOUGH HERE.":GOTO26
1330 PRINT"THE BOTTLE BREAKS AND THE BROKEN GLASS VANISHES. SOMETHING
FELL OUT! TRY 'LOOK'.":IFX=-1ZZ=ZZ-1
1340 OB(12,0)=0:OB(13,0)=CP:GOTO26
1400 IFNO-10ANDNO-12PRINTM8:GOTO26
1405 IFNO=10PRINT"NO NEED. THE LANTERN IS ENCHANTED, AND NEVER RUNS OUT OF FUEL.":GOTO26
1410 IFCP-14PRINT"THERE IS NOTHING TO FILL IT WITH HERE!":GOTO26
1420 PRINT"THE WATER PASSES RIGHT THROUGH THE BOTTLE (IT MUST BE MAGIC!)":GOTO26
1500 IFNO-14PRINTM4:GOTO26
1505 IFCP-3ANDCP-45PRINTM0:GOTO26
1510 IFCP=3CP=45ELSECP=3
1520 GOTO25
1600 IFNO-15ANDNO-19ANDNO-26ANDNO-28ANDNO-36ANDNO-40ANDNO-42XX=0ELSEXX=-1
1605 IFVB<>15RETURN
1610 PRINT"IT DOESN'T QUITE APPROVE. CLUE: BE MORE SPECIFIC.":GOTO26
1620 IFNO-44PRINT"SORRY CHARLIE. I CAN ONLY THROW THE BALL.":GOTO26
1630 IFOB(44,0)<>-1PRINTM1:GOTO26
1640 IFCP-29PRINT"OKAY. POOF! A HUGE CLOUD OF GRAY SMOKE EMERGES FROM THE BROKEN
GLASS. A TALL WIZARD WALKS OUT, LOOKS AROUND, AND VANISHES.":OB(44,0)=0:ZZ=ZZ-1:GOTO26
1650 PRINT"OKAY. PAVOOM! A HUGE FOG OF GRAY SMOKE FILLS THE ROOM. A TALL
FIGURE CAN BE MADE OUT. HE LOOKS AT THE MEDICINE MAN AND SAYS,
"CHR$(34)"AHA! I HAVE FOUND YOU, EVIL ONE!"CHR$(34)"
THE SMOKE CLEARS AND THE TWO ARE GONE.":ZZ=ZZ-1
1660 OB(42,0)=0:OB(44,0)=0:GOTO26
1700 GOSUB1600:IFXX=0PRINT"OKAY.":GOTO26ELSEIFNO-15PRINT"I'D RATHER NOT. IT MIGHT HIT ME BACK!":GOTO26
1710 IFOB(29,0)<>-1PRINT"I'VE NO WEAPON, SO I'D RATHER NOT.":GOTO26
1720 PRINT"THE DEER RUNS AWAY, BUT LEAVES SOMETHING BEHIND! TRY 'LOOK'":OB(15,0)=0:OB(16,0)=CP:GOTO26
1800 IFNO-20PRINTM8ELSEPRINT"A DEEP VOICE ECHOS..."CHR$(34)"STOP FLICKING YOUR BIC!"CHR$(34):GOTO26
1900 IFNO-23ANDNO-38PRINTM3:GOTO26
1910 IFNO=23PRINT"SMAK! MMM.":OB(23,0)=0:OB(24,0)=-1:GOTO26
1920 PRINT"DEEEELICIOUS.":ZZ=ZZ-1:OB(38,0)=0:GOTO26
2000 IFNO-23PRINTM8:GOTO26ELSEPRINT"THE BANANA VANISHED AS I PEELED IT! (YOU MUST REMEMBER...THIS
ISLAND IS ==>ENCHANTED<==)":OB(23,0)=0:OB(24,0)=-1:GOTO26
2100 GOSUB1600:IFXX=0PRINTM5:GOTO26
2110 IFNO=28THEN2150
2120 PRINT"WE ENGAGE IN A WILD FIGHT AND WHEN THE SMOKE CLEARS I
FIND MYSELF BEAT TO A PULP. I'M DEAD. PRESS ANY KEY TO CONTINUE"
2130 IFINKEY$=""THEN2130ELSE9000
2150 PRINT"THE BEAR AND I ENGAGE IN A WICKED BATTLE AND AMAZINGLY, I
WIN!!! THE BEAR IS DEAD!!!!!":OB(28,0)=0:GOTO26
2200 IFNO-27PRINTM3:GOTO26
2210 IFOB(20,0)<>-1PRINT"I'VE NO LIGHTER!":GOTO26
2220 PRINT"OKAY...PUFF...PUFF...PUFF":IFOB(40,0)-CPTHEN26
2230 PRINT"THE TIGER SMELLS THE SMOKE, LOOKS AT ME WITH DISGUST, AND
STROLLS AWAY.":OB(40,0)=0:GOTO26
2300 IFNO-30PRINTM8:GOTO26
2310 IFOB(NO,0)<>-1PRINTM1:GOTO26
2320 IFOB(18,0)<>-1PRINT"I HAVE NO KEY!":GOTO26
2330 PRINT"OKAY. SOMETHING FALLS OUT! THE CRATE THEN DISAPPEARS!!!!!!
TRY 'LOOK'.":IFOB(30,0)=-1ZZ=ZZ-1
2340 OB(30,0)=0:OB(31,0)=CP:GOTO26
2400 IFNO-30PRINTM8:GOTO26
2410 IFOB(NO,0)<>-1PRINTM1:GOTO26
2420 PRINT"IT'S ALREADY CLOSED AND LOCKED.":GOTO26
2500 GOTO2300
2600 GOTO2400
2700 PRINT"OKAY..."N1$:IFLEFT$(N1$,3)<>"BIM"THEN26
2705 IFOB(35,0)<>-1PRINT"NOTHING HAPPENED.":GOTO26
2710 PRINT"I AM ENGULFED IN A DENSE CLOUD OF GRAY SMOKE. WHEN THE SMOKE
CLEARS, I AM SOMEWHERE ELSE. TRY 'LOOK'."
2720 IFCP>11ANDCP<29CP=31:GOTO26
2730 CP=27:GOTO26
2800 PRINT"OKAY..."N1$"!!!!!":GOTO26
2900 PRINT"I'M BEWILDERED TOO!":GOTO26
3000 GOTO300
3100 GOSUB1600:IFXX=0PRINTM5:GOTO26ELSEIFNO-36ANDNO-26PRINTM7:GOTO26
3110 IFNO=36QQ=38ELSEQQ=23
3120 IFOB(QQ,0)<>-1PRINT"I HAVE NOTHING IT WANTS TO EAT.":GOTO26
3130 IFNO=26THEN3160
3140 PRINT"THE VULTURE SWALLOWS UP THE FOOD AND FLIES AWAY.":ZZ=ZZ-1:OB(36,0)=0:OB(38,0)=0:GOTO26
3160 PRINT"THE BARBARIAN EATS THE BANANA AND LEAVES. HE DROPS THE PEEL.":ZZ=ZZ-1:OB(23,0)=0:OB(24,0)=CP:OB(26,0)=0:GOTO26
3200 INPUT"DO YOU REALLY WANT TO QUIT NOW";A$:A$=LEFT$(A$,1):IFA$="Y"THEN9000ELSE26
3300 IFCP-45PRINT"NOT HERE.":GOTO26ELSECP=3:GOTO25
9000 CLS:FORI=9TOLO:IFOB(I,0)=1FS=FS+OB(I,1)
9001 NEXTI:PRINT"YOU SCORED"FS"OUT OF A MAXIMUM OF 140."
9005 IFFS=140PRINT"I BOW MY HEAD IN TRIBUTE. YOU COULD BE A WIZARD. PICK UP YOUR
APPLICATION FORM AT YOUR NEARBY ADVENTURE MARKET.":GOTO9070
9010 IFFS>130PRINT"YOU ARE AN EXPERT ADVENTURER.":GOTO9070
9015 IFFS>110PRINT"AND A WISE ADVENTURER YOU ARE.":GOTO9070
9020 IFFS>100PRINT"NOT BAD, IF I DO SAY SO MYSELF!":GOTO9070
9025 IFFS>80PRINT"YOU COULD USE A =LOT= OF PRACTICE.":GOTO9070
9030 IFFS>60PRINT"YOU ===MUST=== BE NEW AT THE GAME. NO ONE COULD DO SO POORLY
AFTER A FEW TIMES OF PLAYING!":GOTO9070
9035 IFFS>35PRINT"YOU ARE A TERRIBLE ADVENTURE PLAYER!":GOTO9070
9040 IFFS>20PRINT"HA HA HA HA HEE TEE HEE HA TA HEE HA HA HA HA!!!":GOTO9070
9045 IFFS>0PRINT"YOU ACHIEVED THE LOWEST RANK NEXT TO ZERO. SUPER!
                                   SARCASTIC [":GOTO9070
9050 PRINT"A ZERO!!!!!!!! I DON'T BELIEVE IT!!!!!!!!! BAAAA HAHAHAH!!!!"
9070 PRINT:PRINTR1$:PRINTR2$:PRINT:PRINT"PLAY AGAIN (Y/N)";
9080 A$=INKEY$:IFA$=""THEN9090
9090 IFA$="Y"RUN
9100 IFA$="N"CLS:END
9110 GOTO9080
30000 CP=1:NP=51:LO=44:NN=44:NV=33:DIMP(NP,3),P$(NP),OB$(LO),OB(LO,1),NO$(NN),VB$(NV)
30010 DATA50,2,7,0,0,3,0,1,0,4,8,2,51,0,0,3,0,6,10,0,0,0,11,5,0,0,0,0,3,9,0,0,0,0,14,8,5,0,15,0,6,0,0,0,7,13,12,12,13,13,18,12,9,15,0,0,10,0,0,14,16,17,16,16,17,18,19,16,13,18,18,17,17,19,20,19,20,20,23,19,0,22,24,0,0,0,25,21,0,24,26,20,21,0,0,23,22,0,28,0
30020 DATA23,27,0,0,0,28,0,26,25,0,0,27,0,0,33,0,0,31,35,0,0,0,36,30,0,33,37,0,29,34,38,32,0,0,39,33,30,36,0,0,31,0,41,35,32,0,0,0,33,0,42,0,34,40,0,0,0,41,44,39,36,0,0,40,38,43,47,0,0,44,0,42,40,0,0,43,0,0,0,0,0,47,50,0,42,0,0,46,0,49,51,0,0,0,0,48,46,0,1,0,48
30030 DATA0,4,0
30040 FORI=1TONP:FORJ=0TO3:READP(I,J):NEXTJ,I
30050 P$(1)="ON THE SHORE OF AN ISLAND NOT FAR FROM AUSTRALIA":P$(2)="WALKING ALONG A SANDY BEACH":P$(3)="STANDING IN AN OASIS NEAR A SANDY BEACH":P$(4)="WALKING ALONG A PATH THAT EXTENDS NORTH AND WEST":P$(5)="A PILE OF BRUSH"
30060 P$(6)="ON SPYGLASS HILL":P$(7)="THE OCEAN. I'M DEAD":P$(8)="ATOP A SAND DUNE":P$(9)="WALKING ALONG A ROCKY PATHWAY THAT EXTENDS TO THE SOUTH
AND WEST":P$(10)="BESIDE A HEDGE":P$(11)="/I'M STANDING IN A SHADY SPOT NEAR A LARGE PILE OF BRUSH"
30070 P$(12)="LOST IN A DARK MARSH":P$(13)=P$(12):P$(16)=P$(12):P$(17)=P$(12):P$(18)=P$(12):P$(19)=P$(12):P$(20)=P$(12):P$(14)="BESIDE A CASCADING WATERFALL":P$(15)="ON THE EDGE OF A CLIFF":P$(21)="IN A BAMBOO FOREST. THE BAMBOO STALKS HERE ARE 6'' THICK"
30080 P$(22)="DEEP IN A BAMBOO FOREST. THE BAMBOO STALKS HERE ARE 7 OR
8'' THICK":P$(23)="IN A FOREST LOOKING SOUTH ON A GRASS HUT":P$(24)="IN A SMALL MARSH":P$(25)="WALKING ALONG A SANDY PATH":P$(26)="IN A SMALL GRASS HUT WITH EXITS TO THE NORTH AND EAST."
30090 P$(27)="IN A WET FOREST WITH A GRASS HUT TO THE WEST":P$(28)="ON THE COAST OF THE ISLAND"
30100 P$(29)="IN A STRANGE CAVE-ROOM WITH OBSCURE PATTERNS IN THE
DUSTY FLOOR":P$(30)="/DEAD END":P$(31)="IN A SACRED CHAMBER DEEP WITHIN THE CAVES":P$(32)="IN AN EERIE PRISON CELL":P$(33)="IN AN ALCOVE TO A HUGE CHASM"
30110 P$(34)="CRAWLING THROUGH A TIGHT PASSAGE LEADING SOUTH AND WEST":P$(35)="IN THE PEBBLE ROOM. THE FLOOR IS MADE OF LOOSE PEBBLES":P$(36)="IN A DARK MUSTY PIT. PASSAGES LEADING OUT OF HERE ALL LOOK
THE SAME, EXEPT THE ONE GOING NORTH."
30120 P$(37)="/DEAD END":P$(38)="IN A DEEP CHASM WITH AN ALCOVE TO THE NORTH":P$(39)="IN THE ENTRY CORRIDOR":P$(40)="IN A RADIENT CAVERN WITH A CELING ABOUT 40 FEET ABOVE ME.
ON THE CELING HANG OMINOUS STALACTITES. ODD, THERE ARE NO 
STALAGMITES"
30130 P$(40)=P$(40)+" ON THE FLOOR HERE":P$(41)="IN A WIDE VALLEY WITH A MOSSY GROWTH COVERING THE FLOOR":P$(42)="IN A MAJESTIC CHAMBER WHICH SEEMS TO SERVE AS AN ENTRANCE
TO A LARGE CAVE":P$(43)="CRAWLING THROUGH A ROUGH SLOPING PASSAGE"
30140 P$(44)="IN A HIGH CAVERN. THE CELING IS SO HIGH I CAN'T EVEN
SEE IT. HIGH ABOVE ME GLOWS AN ERRIE RED LIGHT":P$(45)="AT THE TOP OF A PALM TREE. I CAN SEE A SMALL JUNGLE OVER
TO THE SOUTH SOMEWHERE. ODD, I SEE NO PASSAGE GOING TO IT"
30150 P$(46)="IN A THICKET OF TALL GRASS":P$(47)="STANDING ON A ROCKY FLAT. THERE IS A CAVE OPENING TO
THE NORTH OF ME. IT LOOKS DARK IN THERE":P$(48)="IN A PATCH OF TALL REEDS":P$(49)="STANDING IN A SMALL DEPRESSION IN THE SAND"
30160 P$(50)="WALKING ALONG THE SHORE OF THE ISLAND":P$(51)="WALKING ALONG A LONG NORTH/SOUTH PATH":D$(0)="NORTH":D$(1)="EAST":D$(2)="SOUTH":D$(3)="WEST"
30170 DATA0,0,49,0,1,0,2,0,0,0,3,0,6,0,0,15,8,0,11,10,11,0,15,0,12,0,17,10,18,0,0,0,26,10,26,0,28,15,28,0,22,0,29,0,0,15,30,15,31,15,32,0,37,10,45,0,45,15,46,0,46,0,21,0,21,10,29,0,51,0,16,0
30180 FORI=9TOLO:READOB(I,0),OB(I,1):NEXTI
30190 DATANOR,EAS,SOU,WES,N,E,S,W,<X>,LAN,SIG,BOT,NOT,TRE,DEE,ANT,BOO,KEY,WAR,LIG,PIC,COC,BAN,PEE,SPI,BAR,CIG,BEA,POS,CRA,CHA,EME,RIN,SKU,RUB,VUL,FEA,FOO,LIQ,TIG,SKI,MAN,ROC,BAL
30200 DATAGO,GET,LOO,INV,SCO,SAV,RES,DRO,LIG,UNL,REA,BRE,FIL,CLI,KIL,THR,HIT,FLI,EAT,PEE,ATT,SMO,OPE,CLO,UNL,LOC,SAY,YEL,HEL,TAK,FEE,QUI,JUM
30210 FORI=1TONN:READNO$(I):NEXTI:FORI=1TONV:READVB$(I):NEXTI
30220 DATAA SHINING LANTERN,AN UNLIT LANTERN,A SIGN,A GREEN BOTTLE,A SMALL NOTE,A PALM TREE,A WILD DEER,*GOLDEN ANTLERS*,A DUSTY BOOK,*A SILVER KEY*,A WICKED WARLOCK,A BIC LIGHTER,AN IRON PICK,*A JEWEL ENCRUSTED COCONUT*
30230 DATAA BANANA,A BANANA PEEL,*RARE SPICES*,A SAVAGE BARBARIAN,*CUBAN CIGARS*,A WILD BEAR,A BAMBOO POST,A WOODEN CRATE,*A GOLDEN CHAIN*,*AN ENORMOUS EMERALD*,*A GOLD RING*
30240 DATAA HUMAN SKULL,*A MAMMOTH RUBY*,A MEAN VULTURE,*A GOLDEN FEATHER*,SOME TASTY FOOD,SOME GREEN LIQUID,A FIERCE TIGER,*A PRICELESS GIRAFFE SKIN*,AN EVIL MEDICINE MAN,A BIG ROCK WITH WRITING ON IT
30250 DATAA GLOWING GLASS BALL
30260 FORI=9TOLO:READOB$(I):NEXTI
59000 R1$="TRY SOME OTHER GREG HASSETT ADVENTURES. THEY ARE:
        JOURNEY TO THE CENTER OF THE EARTH
        THE HOUSE OF SEVEN GABLES
        ENTRY INTO KING TUT'S TOMB
        SORCERER'S CASTLE
        VOYAGE TO ATLANTIS"
59005 R2$="CALL MAD HATTER SOFTWARE TO PLACE AN ORDER
              (617)-682-8131"
60000 GOTO25
