5 RANDOMIZE TIMER
10 CLEAR 100:DEFINT A-Z:CLS:print CHR$(23);"     JOURNEY  TO  THE CENTER OF       THE EARTH ADVENTURE BY GREG HASSETT"
20 CP=1:GOSUB 1850
40 GOSUB 1750:GOSUB 1640
50 IF CP=18 AND (VB<>25 OR NO<>21) THEN print "HORRORS! THE TROLL THROWS AN AXE AT ME! I AM DEAD":END
60 IF CP=18 THEN PRINT M2$:F5=1:GOSUB 1640
70 IF NO<23 OR VB>1 OR NO>30 THEN 160
80 IF NO<=26 THEN FD=FD+1 ELSE FD=-10000
90 IF NO>26 THEN NO=NO-4
100 NO=NO-22
110 IF CP=10 AND NO=1 THEN PRINT " AND  FALL IN TO  THE LAVA??? FAT CHANCE!":GOSUB 1640:goto 70
120 IF CP=11 AND NO=2 THEN PRINT "DO YOU KNOW WHAT MOLTEN LAVA IS??? IT'S HOT, THAT'S WHAT!":GOSUB 1640:goto 70
130 IF CP=29 AND OB(10,0)=29 AND (NO=4 OR NO=2) THEN print "THE HYDRA WON'T LET ME THROUGH!":GOSUB 1640:goto 70
140 IF P(CP,NO)=0 THEN PRINT "I CAN'T GO THAT WAY!":GOSUB 1640:goto 70
150 CP=P(CP,NO):goto 40
160 IF VB<>2 AND VB<>20 THEN 240
170 IF NO=10 AND OB(33,0)=CP THEN PRINT "IT IS SORT OF HEAVY, YOU KNOW.":GOSUB 1640:goto 70
180 IF NO=0 THEN PRINT "I DON'T KNOW WHAT A ";CHR$(34);NO$;CHR$(34);" IS.":GOSUB 1640:goto 70
190 IF OB(NO,0)<>CP THEN PRINT "I DON'T SEE IT HERE.":GOSUB 1640:goto 70
200 IF NO=10 OR NO=20 THEN PRINT "OH GIVE ME A BREAK!!":GOSUB 1640:goto 70
210 IF OB(NO,0)=-1 THEN PRINT "I'M ALREADY CARRYING IT.":GOSUB 1640:goto 70
220 IF OB(NO,0)=CP AND NT>=6 THEN PRINT "I CAN'T CARRY ANY MORE. YOU'LL HAVE  TO  DROP SOMETHING.":GOSUB 1640:goto 70
230 IF OB(NO,0)=CP THEN PRINT "CHECK.":NT=NT+1:OB(NO,0)=-1:GOSUB 1640:goto 70
240 IF VB<>4 AND VB<>29 THEN 290
250 F3=0
260 print "I AM CURRENTLY CARRYING:":FOR I=1 TO LO:IF OB(I,0)=-1 THEN PRINT OB$(I):F3=1
270 NEXT I:IF F3=0 THEN PRINT "NOTHING."
280 GOSUB 1640:goto 70
290 IF VB=3 THEN 40
300 IF NO=0 AND VB=0 THEN print "THAT DOESN'T MAKE ANY SENSE  TO  ME.":GOSUB 1640:goto 70
310 IF VB<>5 THEN 370
320 J=0
330 FOR I=1 TO LO:IF OB(I,0)=1 THEN J=J+OB(I,1)
340 NEXT I:print "OUT OF A MAXIMUM OF 175 POINTS, YOU HAVE";J
350 IF J=0 THEN PRINT "BOY, YOU STINK!"
360 GOSUB 1640:goto 70
370 IF VB<>6 AND VB<>22 THEN 440
380 IF NO$<>"EVERYTHING" AND NO$<>"ALL" THEN 420
390 FOR I=1 TO LO:IF OB(I,0)=-1 THEN OB(I,0)=CP
400 NEXT I:NT=0
410 print "OKAY.":GOSUB 1640:goto 70
420 IF OB(NO,0)<>-1 THEN PRINT "HOW CAN I DROP WHAT I'M NOT HOLDING???":GOSUB 1640:goto 70
430 OB(NO,0)=CP:print "O.K.":NT=NT-1:GOSUB 1640:goto 70
440 IF VB<>7 AND VB<>28 THEN 480
450 IF H$(CP)="" THEN PRINT "YOU GOT ME IN TO  THIS MESS, NOW YOU GET ME OUT!"
460 print H$(CP):H$(CP)=""
470 GOSUB 1640:goto 70
480 IF VB<>8 THEN 550
490 CLS:print "SAVING...":OPEN "JOURNEY.SAV" FOR OUTPUT AS 1
500 FOR I=1 TO LO:IF OB$(I)="" THEN 520
510 print #1,OB(I,0)
520 NEXT I
530 print #1,CP:CLOSE
540 goto 40
550 IF VB<>9 THEN 610
560 CLS:print "LOADING...":OPEN "JOURNEY.SAV" FOR INPUT AS 1
570 FOR I=1 TO LO:IF OB$(I)="" THEN 590
580 INPUT #1,OB(I,0)
590 NEXT I
600 INPUT #1,CP:CLOSE:goto 40
610 IF VB<>10 THEN 630
620 goto 1410
630 IF VB<>11 THEN 660
640 IF NO=0 THEN PRINT "SAY WHAT?":GOSUB 1640:goto 70
650 print "OKAY, IF  IT IS REALLY NECESSARY...";NO$:GOSUB 1640:goto 70
660 IF VB<>12 THEN 700
670 IF NO<>12 THEN PRINT "I DON'T DO STUPID THINGS.":GOSUB 1640:goto 70
680 IF OB(12,0)<>-1 THEN PRINT "I DON'T HAVE ANY COKE  TO  DRINK!":GOSUB 1640:goto 70
690 print M3$:OB(12,0)=0:OB(13,0)=-1:GOSUB 1640:goto 70
700 IF VB<>13 THEN 770
710 IF OB(31,0)<>-1 THEN PRINT "BUT I HAVE NO FOOD!!":GOSUB 1640:goto 70
720 IF OB(NO,0)<>CP THEN PRINT "I DON'T SEE IT.":GOSUB 1640:goto 70
730 IF NO=10 THEN PRINT "THE HYDRA WHARFS DOWN THE FOOD.":OB(31,0)=0:OB$(10)="A GIANT HYDRA RUBBING HIS STOMACH":GOSUB 1640:goto 70
740 IF NO=20 THEN PRINT "THE BUGS IN THESE CAVES ONLY EAT HUMAN FLESH.":GOSUB 1640:goto 70
750 IF NO<>3 THEN PRINT "YOU CAN'T BE SERIOUS.":GOSUB 1640:goto 70
760 print "IT REFUSES  TO  EAT THE FOOD.":GOSUB 1640:goto 70
770 IF VB<>14 THEN 850
780 IF NO=10 AND OB(33,0)=CP THEN PRINT "THE UNDERTAKER JUST  TOOK HIM AWAY.":OB(33,0)=0:GOSUB 1640:goto 70
790 IF OB(NO,0)<>CP AND OB(NO,0)<>-1 THEN PRINT "I DON'T SEE IT  AND  I'M NOT HOLDING IT, SO HOW CAN I HIT IT?":GOSUB 1640:goto 70
800 IF NO=3 THEN PRINT "NO!!! I KIND OF LIKE THE PARROT! POLLY WANT CWAKER?"
810 IF NO=10 THEN PRINT "OKAY... THE HYDRA GRUNTS >>DARN MOSQUITOES<<"
820 IF NO=20 THEN PRINT "YUCH! I DON'T WANT  TO   TOUCH IT!!!"
830 IF NO=3 OR NO=10 OR NO=20 THEN GOSUB 1640:goto 70
840 print "OKAY... OUCH! I HURT MY HAND!":GOSUB 1640:goto 70
850 IF VB=15 THEN PRINT "I CAN'T KILL ANYTHING. I'M  TOO WEAK. BUT MAYBE IF  YOU WERE A BIT MORE SPECIFIC...":GOSUB 1640:goto 70
860 IF VB<>16 THEN 950
870 IF OB(33,0)=CP AND NO=10 THEN PRINT "IT IS ALREADY BURNED  TO  A CRISP.":GOSUB 1640:goto 70
880 IF OB(6,0)<>-1 THEN PRINT "I DON'T HAVE A LIGHTER  OR  MATCHES!!":GOSUB 1640:goto 70
890 IF NO=3 THEN PRINT "NEVER!! YOU KNOW SOMETHING, YOU'RE A SADIST.":GOSUB 1640:goto 70
900 IF NO=10 AND OB(10,0)=CP THEN OB(10,0)=0:OB(33,0)=CP:print "THE HYDRA CRUMBLES  TO  THE GROUND DEAD.":GOSUB 1640:goto 70
910 IF NO=20 THEN PRINT "THE BUG CANNOT BE BURNED. <GULP>":GOSUB 1640:goto 70
920 IF NO=10OB(10,0)=0
930 print "IT DOESN'T BURN."
940 GOSUB 1640:goto 70
950 IF VB=17 THEN PRINT " AND  JUST WHO DO YOU THINK I AM, ROCKY???":GOSUB 1640:goto 70
960 IF VB=10 THEN PRINT "I DON'T KNOW HOW  TO  LIGHT THINGS. IF  YOU WANT ME  TO  BURN SOMETHING, TELL ME SO!":GOSUB 1640:goto 70
970 IF VB<>19 THEN 1060
980 IF NO$="COIN" THEN NO$="QUARTER":NO=7
990 IF NO=0 THEN PRINT "I DON'T KNOW WHAT ONE IS.":GOSUB 1640:goto 70
1000 IF NO=7 THEN 1020
1010 IF OB(NO,0)<>-1 THEN PRINT "I'M NOT HOLDING IT RIGHT NOW.":GOSUB 1640:goto 70
1020 IF CP<>27 THEN PRINT "I DON'T SEE ANY COKE MACHINES HERE.":GOSUB 1640:goto 70
1030 IF NO<>7 THEN PRINT "THAT IS RIDICULOUS.":GOSUB 1640:goto 70
1040 IF OB(NO,0)<>-1 THEN PRINT "I HAVE NO MONEY.":GOSUB 1640:goto 70
1050 IF OB(7,0)=-1 AND CP=27 THEN PRINT "ALL RIGHT.   CHIKCLIKCHINGGGG! A COKE POPPED OUT. THERE IS NOW A BOTTLE OF COKE HERE.":OB(7,0)=0:NT=NT-1:OB(12,0)=27:GOSUB 1640:goto 70
1060 REM
1070 IF VB<>21 THEN 1150
1080 IF NO<>18 AND OB(NO,0)=-1 THEN PRINT "IT'S KIND OF HEAVY, YOU KNOW.":GOSUB 1640:goto 70
1090 IF OB(NO,0)<>-1 THEN PRINT "FIRST YOU HAVE  TO  HELP ME FIND IT, DUMMY!":GOSUB 1640:goto 70
1100 IF CP=10 OR CP=11 THEN 1120
1110 print "NOTHING HAPPENED":GOSUB 1640:goto 70
1120 IF CP=10 THEN CP=11:goto 1140
1130 CP=10
1140 goto 40
1150 IF VB=23 THEN VB=1:goto 70
1160 IF VB=24 THEN PRINT "TRY KILL ";NO$:GOSUB 1640:goto 70
1170 IF VB<>25 THEN 1200
1180 IF NOC21 THEN PRINT "NOTHING HAPPENED":GOSUB 1640:goto 70
1190 print "SIR WHAT??? I DON'T UNDERSTAND!":GOSUB 1640:goto 70
1200 IF VB<>26 THEN 1270
1210 IF CP=1 AND OB(14,0)=-1 AND OB(22,0)=1 THEN PRINT "OKAY!!!":goto 1400
1220 IF CP<>1 THEN PRINT "I CAN'T.":GOSUB 1640:goto 70
1230 IF VB<>26 THEN 1270
1240 IF OB(14,0)<>-1 THEN PRINT "I CAN'T. I HAVE NO  TOOLS.":GOSUB 1640:goto 70
1250 print "I CAN'T, I DON'T SEE THE FRIBULATING GONKULATOR AROUND ME.";:IF OB(22,0)=-1 THEN PRINT " I THINK I HAVE  TO  DROP IT FIRST." ELSE print 
1260 GOSUB 1640
1270 IF VB<>27 THEN 1350
1280 IF NO=0 THEN PRINT "WHAT THE DEVIL IS A ";NO$;"??":GOSUB 1640:goto 70
1290 IF NO=20 OR NO=10 THEN IF OB(NO,0)=CP THEN PRINT "YUCH!!! I'D NEVER EAT A ";NO$;"!!!":GOSUB 1640:goto 70
1300 IF NO=10 AND OB(33,0)=CP THEN PRINT "WELL...IT'S COOKED NOW, BUT IT STILL LOOKS KIND OF GROSS.":GOSUB 1640:goto 70
1310 IF OB(NO,0)<>-1 THEN PRINT "I'M NOT HOLDING IT.":GOSUB 1640:goto 70
1320 IF NO=3 AND OB(3,0)=-1 THEN PRINT "I'D RATHER NOT. I DON'T CARE FOR  PARROT MEAT.":GOSUB 1640:goto 70
1330 IF NO=31 AND OB(31,0)=-1 then NT=NT-1:print "HUMM! IT WAS FILET MIGNON, MY FAVORITE DISH.":OB(31,0)=0:GOSUB 1640:goto 70
1340 print "I THINK THAT ";OB$(NO);" WOULD GIVE ME INDIGESTION, AND  I DON'T HAVE ANY PEPTP-BISMOL.":GOSUB 1640:goto 70
1350 IF VB<>30 THEN 1390
1360 IF CP<>10 AND CP<>11 THEN PRINT "NOTHING HAPPENED.":GOSUB 1640:goto 70
1370 print "NICE TRY, BUT THAT'S AN OLD, WORN OUT MAGIC WORD."
1380 GOSUB 1640:goto 70
1390 print "I DON'T KNOW HOW  TO  DO THAT.":GOSUB 1640:goto 70
1400 FOR I=1 TO 1000:NEXT I
1410 print "OUT OF A MAXIMUM OF 175 POINTS, YOU HAVE SCORED";:J=0
1420 FOR I=1 TO LO:IF OB(I,0)=1 THEN J=J+OB(I,1)
1430 NEXT I
1440 print J;"POINTS."
1450 IF J=175 THEN PRINT "ALL OF ADVENTURERDOM GIVES TRIBUTE  TO  YOU, ADVENTURER GRAND MASTER!":END
1460 IF J>165 THEN PRINT "YOUR SCORE PUTS YOU IN MASTER ADVENTURER CLASS A.":END
1470 IF J>150THEN print "YOUR SCORE PUTS YOU IN MASTER ADVENTURER CLASS B.":END
1480 IF J>140 THEN PRINT "YOU HAVE REACHED A ";CHR$(34);"JUNI OR  MASTER";CHR$(34);" STATUS":END
1490 IF J>120 THEN PRINT "YOU MAY NOW CONSIDER YOURSELF A ";CHR$(34);"SEASONED ADVENTURER";CHR$(34):END
1500 IF J>110 THEN PRINT "YOU HAVE ACHIEVED THE RATING: ";CHR$(34);"EXPERIENCED ADVENTURER";CHR$(34);".":END
1510 IF J>100 THEN PRINT "YOUR SCORE QUALIFIES YOU AS A NOVICE CLASS ADVENTURER.":END
1520 print "YOU ARE OBVIOUSLY A RANK AMATEUR. BETTER LUCK NEXT  TIME.":END
1530 print "GADZOOKS!!! A GAINT BUG JUST WALKED IN  TO  KILL ME!!!"
1540 FF=1
1550 GOSUB 1640
1560 IF NO$="SESAME" AND VB$="OPEN" THEN PRINT "THE BUG HAS SHRUNK  TO  IT'S PROPER SIZE  AND  SCURRIED AWAY.":goto 1620
1570 IF NO<>20 OR VB<>15 THEN PRINT "HELP ME!!! I'M BEING DEVOURED BY A GAINT BUG!!! THE END IS NEAR! OW!! THIS HURTS!! I'M DYING...DYING...DEAD.":END
1580 IF OB(2,0)<>-1 THEN NO=2:print "I DON'T HAVE A SWORD!!!":goto 1570
1590 IF SW=8 THEN PRINT "MY SWORD IS DULL, REMEMBER???":NO=2:goto 1570
1600 print "DING! DONG! THE BUG IS DEAD, THE BUG IS DEAD!!!"
1610 SW=SW+1:IF SW=8 THEN PRINT "MY SWORD IS DULL NOW."
1620 GOSUB 1640:goto 70
1630 END
1640 print:INPUT "ENTER YOUR COMMAND";CM$
1645 CLS
1650 IF CM$="ABRA CADABRA" AND F5<3 AND FF=1 THEN NO$="SESAME":VB$="OPEN":FF=0:F5=F5+1:RETURN
1660 VB$="":NO$="":VB=0:NO=0
1670 LC=LEN(CM$):FOR I=1 TO LC:IF MID$(CM$,I,1)<>" " THEN VB$=VB$+MID$(CM$,I,1):NEXT I
1680 FOR I=1 TO NV:IF VB$(I)=VB$ THEN VB=I:goto 1700 ELSE NEXT I
1690 VB=0:NO$=VB$:goto 1720
1700 IF LEN(VB$)+1>=LEN(CM$) THEN NO=0:RETURN
1710 NO$=RIGHT$(CM$,LEN(CM$)-1-LEN(VB$))
1720 FOR I=1 TO NN:IF NO$(I)=NO$ THEN 1740 ELSE NEXT I
1730 NO=0:RETURN
1740 NO=I:RETURN
1750 print P$(CP):print 
1760 FOR I=1 TO LO:IF OB(I,0)=CP THEN print "THERE IS ";OB$(I);" HERE."
1770 NEXT I
1780 print "I CAN GO:      ";:F2=0
1790 FOR I=1 TO 4:IF P(CP,I)<>0 THEN PRINT NO$(I+22);"   ";:F2=1
1800 NEXT I:IF F2=0 THEN PRINT "NOWHERE!"
1810 IF CP=18 AND OB(3,0)=-1 THEN PRINT:print M1$
1820 IF INT(RND(1)*35)+1=5 AND CP<>1 AND CP<>37 AND CP<>27 THEN PRINT:goto 1530
1830 IF FD>4 THEN PRINT:print "IF  YOU'D LIKE, YOU CAN MAKE IT EASIER ON YOURSELF BY TYPING N, S, E,  OR  W INSTEAD OF NORTH, SOUTH, ECT.":FD=0
1840 RETURN
1850 NP=37:DIM P$(NP)
1870 P$(1)="I AM IN A SHIP. ON A COMPUTER SCREEN IN HERE IT SAYS: SHIP WILL NOT FUNCTION -- FRIBULATING GONKULATOR  IS BURNED OUT. IT IS OBVIOUS THAT THIS SHIP HAS CRASHED."
1880 DATA   2, 6, 5, 4:P$(2)="I'M IN A COBBLESTONE HALLWAY. I CAN HEAR CHIRPING NEARBY. SOUNDS LIKE BUGS...":DATA   8, 1, 3, 5
1890 P$(3)="I'M IN A DARK ROOM...OH NO!!! THERE ARE GIANT BUGS ALL AROUND ME!!!":DATA   0, 0, 0, 2
1900 P$(4)="I'M IN A SMALL ROOM. A SCRIBBLING HERE SAYS: HAR HAR HAR! YOU'LL NEVER MAKE IT! >> ARBADAC ARBA <<":DATA   0, 7, 1, 0
1910 P$(5)="I'M IN A DUSTY COBBLESTONE HALLWAY.":DATA   0, 25, 2, 1
1920 P$(6)="I'M IN A DUSTY PLACE. IT IS VERY DARK IN HERE. I'M SCARED!":DATA   1, 24, 0, 0
1930 P$(7)="I'M IN A LOW CRAWL SPACE. IT SEEMS AS IF  WE'RE NOT SUPPOSED  TO  BE HERE...":DATA 4, 0, 15, 0
1940 P$(8)="I'M IN A ROOM WITH ROCK WALLS. IMBEDDED IN THE WALLS ARE SCATTERED RUBIES.":DATA   9, 2, 0, 13
1950 P$(9)="I'M IN A DARK ROOM. THIS ONE IS NOT SO SCARY. I THINK I HEAR GRUNTS  AND  GROANS NEARBY.":DATA   10, 8, 23, 0
1960 P$(10)="I'M ON THE SOUTH SIDE OF A CHASM. MOLTEN LAVA IS BENEATH ME.":DATA   11, 9, 0, 0
1970 P$(11)="I'M ON THE NORTH SIDE OF THE CHASM.":DATA   16, 10, 0, 0
1980 P$(12)="I'M IN A MAZE OF TWISTY LITTLE PASSAGES.":DATA   12, 12, 13, 14
1990 P$(13)=P$(12):DATA   13, 13, 8, 12
2000 P$(14)=P$(12):DATA   14, 15, 14, 12
2010 P$(15)=P$(12):DATA   14, 15, 15, 7
2020 P$(16)="I'M ON A WOOD PLATFORM. SIGN WARNS: GO BACK! BEWARE! GO SOUTH!!!    THERE IS A RICKETY BRIDGE  TO   THE NORTH.":DATA   17, 11, 0, 0
2030 P$(17)="I'M IN A DUSTY PASSAGE. A SIGN POINTS WEST  AND  SAYS: TO  TROLLS PLACE --->   A RICKETY BRIDGE EXTENDS SOUTHWARD. MARKINGS IN THE DUST INDICATE THAT SOMEONE WAS HERE RECENTLY.":DATA   0, 16, 0, 18
2040 P$(18)="A VOICE BOOMS OUT: WHO DARES  TO  ENTER MY PALACE??? (SOUNDS LIKE A TROLL  TO  ME!)":DATA   0, 0, 0, 19
2050 P$(19)="I'M IN THE TROLL'S PALACE. IT'S BREATHTAKING!!! THE CEILING IS SO HIGH I CAN'T EVEN SEE IT!":DATA   0, 20, 0, 0
2060 P$(20)="I'M AT ONE END OF A SECRET PASSAGE.":DATA   19, 21, 0, 0
2070 P$(21)="I'M IN A SECRET PASSAGE.":DATA   21, 21, 20, 22
2080 P$(22)=P$(21):DATA   21, 22, 22, 23
2090 P$(23)=P$(20):DATA   23, 9, 22, 23
2100 P$(24)="I'M IN A HUGE ICE CAVERN. THE CEILING  AND  WALLS ARE COMPLETELY ICE.":DATA   6, 26, 25, 0
2110 P$(25)="I'M IN A ROOM WITH MANY ANIMALS... WAIT! THEY'VE ALL RAN AWAY":DATA   5, 0, 0, 0
2120 P$(26)="I AM IN A ROOM WITH TRACES OF SLIME ON THE FLOORS  AND  WALLS.":DATA   24, 29, 27, 28
2130 P$(27)="I'M IN...AL'S DINER??? THERE'S A COKE MACHINE HERE. IT SAYS: ENJOY COCA-COLA. 25 CENTS (NO CANADIAN COINS, QUARTER ONLY)":DATA   0, 0, 0, 26
2140 P$(28)="I'M IN A HUGE PIT."
2150 DATA   0, 0, 26, 0
2160 P$(29)="I'M IN THE HYDRA ROOM. THE WALLS ARE ALL SLIME. BOY, I'VE NEVER SEEN ANYTHING SO GROSS IN MY LIFE!":DATA   26, 30, 31, 32
2170 P$(30)="I'M IN THE DRAGON ROOM. IT SEEMS  TO  BE DECORATED WITH DRAGONS.":DATA   29, 34, 0, 33
2180 P$(31)="I'M IN A TORTURE CHAMBER. LET'S GET OUT OF HERE QUICK!!":DATA   31, 29, 31, 31
2190 P$(32)="THIS IS THE CLAM ROOM. THE WALLS ARE SOLID CLAMSHELL EMBEDDED IN LIMESTONE.":DATA   0, 0, 29, 0
2200 P$(33)="I'M IN A MAMMOTH GARDEN. THIS IS ALL PLANTS.":DATA   0, 0, 30, 0
2210 P$(34)="I'M IN THE VELVET ROOM. THE WALLS  AND  CEILING ARE VELVET.":DATA   30, 34, 35, 0
2220 P$(35)="I'M IN AN ARABIAN ROOM. ARABIAN GUITARS ARE PLAYING NEARBY. THERE IS AN OIL SLICK HERE. LOOKS LIKE MACHINE OIL, 10-W-30.":DATA   36, 37, 0, 34
2230 P$(36)="I'M IN THE TREASURE ROOM. MANY DWARVES GUARD SOME PRETTY SCRAWNY TREASURE. NOTHING  TO  FLIP OVER, ANYWAY.":DATA   0, 35, 0, 0
2240 P$(37)="I'M AT MAC'S EARTHDIGGER BODY SHOP.":DATA   0, 0, 0, 35
2250 DIM P(37,4):FOR I=1 TO 37:FOR J=1 TO 4:READ  P(I,J):NEXT J,I
2260 LO=33:DIM OB$(LO),OB(LO,1)
2270 OB$(1)="A SET OF KEYS":DATA   6, 0:OB$(2)="A SHINY SWORD":DATA   10, 0
2280 OB$(3)="A PARROT IN A CAGE":DATA   25, 5:OB$(4)="A GOLD NUGGET":DATA   4, 10
2290 OB$(5)="A 35,000 CARAT DIAMOND":DATA   7, 10:OB$(6)="A CIGARETTE LIGHTER":DATA   28, 0
2300 OB$(7)="A QUARTER":DATA   28, 0:OB$(8)="AN ENORMOUS EMERALD":DATA   32, 15
2310 OB$(9)="SOME SILVER BARS":DATA   33, 15:OB$(10)="A GIANT HYDRA WITH NINE HEADS":DATA   29, 0
2320 OB$(11)="A MING VASE":DATA   30, 10:OB$(12)="A BOTTLE OF COKE":DATA   0, 0
2330 OB$(13)="AN EMPTY COKE BOTTLE":DATA   0, 0:OB$(14)="SOME  TOOLS":DATA   34, 0
2340 OB$(15)="A LARGE RUBY":DATA   8, 15:OB$(16)="GOLD BARS":DATA   19, 20
2350 OB$(17)="A DIAMOND NECKLACE":DATA   19, 25:OB$(18)="A MAGIC WAND ":DATA   14, 0
2360 OB$(22)="A FRIBULATING GONKULATOR ":DATA   37, 50
2370 OB$(20)="A GIANT BUG ASLEEP":DATA   31, 0
2380 OB$(31)="SOME TASTY FOOD":OB$(32)="SOME HUMAN SKELETONS":OB(32,0)=31:OB(32,1)=0
2390 OB$(33)="A DEAD HYDRA ON THE GROUND"
2400 FOR I=1 TO 20:READ  OB(I,0),OB(I,1):NEXT I
2410 OB(22,0)=OB(19,0):OB(22,1)=OB(19,1)
2420 OB(19,1)=0:OB(19,0)=0
2430 OB(31,0)=27:OB(31,1)=0
2440 DATA   KEYS, SWORD, PARROT, NUGGET, DIAMOND, LIGHTER, QUARTER, EMERALD, SILVER, HYDRA, VASE, COKE, BOTTLE,  TOOLS, RUBY, GOLD, NECKLACE
2441 DATA   WAND,SIGN, BUG, ALEXANDER, GONKULATOR, NORTH, SOUTH, EAST, WEST
2450 DATA   N, S, E, W, FOOD, SKELETONS
2460 NN=32:DIM NO$(NN)
2470 FOR I=1 TO NN:READ  NO$(I):NEXT I
2480 DATA   GO, GET, LOOK, INVEN, SCORE, DROP, HELP, SAVE, LOAD, QUIT, SAY, DRINK, FEED, HIT, KILL, BURN, FIGHT, LIGHT, INSERT, TAKE, WAVE
2481 DATA   RELEASE, CONTINUE,DESTROY, SIR, FIX, EAT, ?, INVENTORY,HOCUS
2490 NV=30:DIM VB$(NV):FOR I=1 TO NV:READ  VB$(I):NEXT I
2500 M1$="THE PARROT CHANTS: SIR ALEXANDER...SIR ALEXANDER!"
2510 M2$="THE TROLL SCURRIES AWAY IN FEAR."
2520 M3$="GULP GLUK GULP!  (BURP) THAT WAS REFRESHING!"
2530 DIM H$(NP)
2540 H$(25)="THIS PARROT TALKS..."
2550 H$(29)="CIGAR? CIGARETTE? TIPPARILLO?"
2560 H$(10)="YOU KNOW, THIS WORLD IS ENCHANTED..."
2570 H$(12)="YOU CAN MAKE THE PASSAGES LOOK LESS ALIKE BY DROPPING THINGS."
2580 H$(13)=H$(12):H$(14)=H$(12):H$(15)=H$(12)
2590 H$(1)="I THINK WE'RE SUPPOSED  TO  LEAVE WHAT WE FIND HERE."
2600 H$(35)="OFTEN TIMES OIL MEANS AN AUTO BODY SHOP IS NEARBY."
2610 H$(31)="THE BUG WILL NOT BOTHER YOU. IT IS ASLEEP. HINT: IF  YOU LINGER  TOO  LONG IN THIS ROOM IT WILL WAKE UP!"
2620 H$(2)="YOU KNOW, IN THIS ENCHANTED WORLD, MANY THINGS THAT ARE SMALL ON EARTH ARE VERY LARGE..."
2630 H$(20)="TRY  TO  FIND YOUR WAY  TO  THE OTHER END, DUMMY!!":H$(23)=H$(20)
2640 INPUT "WHAT IS YOUR NAME";A$:NA$=A$
2650 RETURN
