0 CLEAR3000:DIMCC,ZZ,Z$,I$:GOTO20
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(Z$,CC,1):IFI$=" "ORI$=""THEN?MID$(Z$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:Z$="":RETURN
20 DIMD$(32),O$(73),L(67),P(32,6):T1=50:L=21:F$="WOMAN-- ":M$="MAN-- ":P$="PERSON IS "
25 GOSUB2220:GOTO170
30 PRINT"CAN'T DO IT!":GOTO50
40 PRINT"O.K."
50 GOTO340
60 T1=T+50:CLS:Z$="BUILDING IS"+STR$(INT(T/5))+" PERCENT FILLED WITH DEADLY GAS":GOSUB1
70 PRINT"*WARNING*":GOSUB150:IFT=500THENPRINT"YOU'RE DEAD":GOTO70
75 CLS:RETURN
80 IF LEN(C$)>1THENC$=LEFT$(C$,2):RETURN
90 L$=D$(L)
100 IFL>20THENL$="MAIN CONCOURSE"
110 IFL>26THENL$="SERVICE TUNNEL"
120 RETURN
130 N=N+1:CLS:Z$="THANKS FOR SAVING ME! I'LL JOIN YOU!":GOSUB1:GOSUB150
135 CLS:IFN>1THENP$="PEOPLE ARE "
140 GOTO720
150 FORX=1TO3000:NEXT:RETURN
160 FORY=1TO30:NEXTY:RETURN
170 FORX=1TO20:READD$(X):NEXTX
180 DATAFRONT OF AUTO STORE,AUTO STORE,OUTSIDE A PET STORE,PET STORE,BOOK STORE
182 DATAMAINTANANCE ROOM,CLOSET,SPORTS SHOP,THEATER,PAINT STORE,HARDWARE STORE,DRUG STORE
184 DATAGARDEN SHOP,GROCERY STORE,TOY STORE,ELEVATOR,BALCONY
190 DATARESTAURANT,RECORD STORE,INSIDE OF TRUCK
200 FORX=1TO73:READO$(X):IFX>6THENREADL(X-6)
210 NEXTX
220 DATAEAST,WEST,NORTH,SOUTH,UP,DOWN,ELECTRIC WIRE,1,JACK,2,TIRES,2,PUPPY,90,KITTEN,90
222 DATAMONGOOSE,90,BIRD,90,LARGE SNAKE,4,BOOKS,5,NEWSPAPERS
224 DATA5,FIRE EXTINGUISHER,7,PAIL,6,BROOM,6,ROPE,8,SKIES,8,RUBBER RAFT,8,POOL ACID,8
230 DATA SMALL LADDER,10,CANS-PAINT,10,TURPENTINE,10,SAW,11,HAMMER,11,WRENCHES
232 DATA11,SMELLING SALTS,12,SPLINTS,12,MEDICINE,12,FERTILIZER,13,SHOVEL,13
234 DATA RUBBER GLOVES,13,BAKING SODA,14,CHOCOLATE BARS,14,APPLES,14,E.T. FLASHLIGHT,15
236 DATATOY AUTOS,15,VIDEO GAMES
240 DATA15,JIGSAW PUZZLES,15,MODEL AIRPLANES,15,ICE CREAM,18,RECORD ALBUMS,19
242 DATASTEROES,19,SIGN,26,TOOL BOX,30,PHILIPS SCREWDRIVER,90,WATER RUNNING INTO HOLE
244 DATA24,TRUCK KEYS,90
250 DATANI-CAD BATTERIES,11,CLOSET DOOR,6,VALVE,7,PILE OF RUBBLE,9,SCREEN,9,SEATS,9
252 DATAPUSH-BUTTONS(UP/DOWN),16,RESET SWITCH,16,WOOD BEAM,18,IGNITION SWITCH,20
254 DATATRUCK,21,HEAVY METAL DOORS,21
260 DATAFOUNTAIN,24,IVORY STATUE,24,DEEP HOLE,24,ELEVATOR,26,RV,26,JEEP
262 DATA23,ANIMAL CAGES,4,STEERING WHEEL,20,GAS PEDAL,20,FIRE,5
270 FORX=1TO32:FORY=1TO6:READP(X,Y):NEXTY,X
280 DATA,17,,,,,,1,,,,,22,,,,,,,,,,,27,,23,,,,,24,,,,,,,,,6,,,,25,,,,,26,,,,,,,26,,,,
282 DATA25,,,,,,,24,,,,,23,,,,,,,22,,,,,,21,,,,,,,,,,26
290 DATA1,19,,16,,,21,,,,,,17,,,,,,,,,,,21,15,18,21,22,,,14,3,21,23,,,5,13,22,24,,,12,6
292 DATA23,25,,,8,11,24,26,,,10,9,25,,,,
300 DATA,,28,,,,,27,29,,,,,28,30,,,,0,29,31,,,,,30,32,,,,,31,,26,
310 W$="GOLORETAGEOPCLMOTUTOTHDIJAPUSACUDRTILIPOUSEAOF"
320 O$="EAWENOSOUPDOWIJATIPUKIMOBISNBONEEXPABRROSKRAACLACATUSAHAWRSMSPMEFESHGLBACHAPFL"
322 O$=O$+"AUVIJIAIICALSTSITOPHWAKENICLVARUSCSEBUSWBEIGTRHEFOIVHOELRVJEANWHGAFI"
330 CLS:GOTO720
340 T=T+1:PRINT:INPUT"YOUR COMMAND";A$:B$="":FORX=1TOLEN(A$)
350 IFMID$(A$,X,1)=" "ANDLEN(A$)>1THENB$=MID$(A$,X+1,2):X=255
360 NEXTX:F=0:S=0
370 IFOP=0THEN2240
380 IFT>T1THENGOSUB60
390 IFW=0ANDT1>300THEND=1
400 IFW=0ANDT1>500THENW1=1
410 P(26,5)=0:IFL(18)=26ANDE=0THENP(26,5)=16
420 IFLEFT$(A$,3)="SPR"THENA$="LI":GOTO430
421 IFLEN(A$)>1THENA$=LEFT$(A$,2)
430 IFA$="LO"ANDB$=""THEN720
440 IFA$="IN"THEN1320
450 IFA$="DI"THEN1690
460 FORX=1TOLEN(W$)STEP2:IFMID$(W$,X,2)=A$THENF=(X+1)/2
470 NEXTX:FORX=1TOLEN(O$)STEP2:IFMID$(O$,X,2)=B$THENS=(X+1)/2
480 NEXTX:Z=S-6
490 IFF=19ANDS=39THEN580
500 IFF=1ANDS=5THEN580
510 IFL>26ANDF1=0THENPRINT"IT'S DARK HERE":GOTO30
520 IFF=2ANDN>0THENIFB$="MA"ORB$="WO"THENPRINT"INJURED":GOTO50
530 IFB$="MA"ORB$="WO"THENPRINT:GOTO50
540 IFB$="WA"THENIFF=17ORF=10ORF=20THEN2290
550 IFF=6ANDB$="AC"THEN2170
560 IFB$="AC"THEN50
580 IFF=0ORS=0THENPRINT"WHAT??":GOTO50
590 IFF>2ANDS<7THEN30
600 IFF>16THEN650
610 IFS<7THEN630
620 IFL(Z)<>0ANDL(Z)<>LTHENPRINT"THAT'S NOT HERE":GOTO50
630 ONF-1GOTO970,1110,1160,1160,1350,1470,1520,1560,1650,1650,1690,1730,1750,1830,1830
640 GOTO670
650 IFL(Z)<>0THENPRINT"YOU DON'T HAVE THAT!":GOTO50
660 ONF-16GOTO1860,1890,1930,1950,2000,2060,2130
670 IFS=62ANDTR=1THENL=20:GOTO720
680 IFS=62THENPRINT"TURN OVER TRUCK":GOTO50
690 IFS>6THENPRINT"CAN'T GO THAT WAY":GOTO50
700 IFP(L,S)=0THENPRINT"CAN'T GO THAT WAY":GOTO50
710 IFP(L,S)>0THENL=P(L,S)
720 IFF1=1ANDL(46)<>L(33)THENF1=0:PRINT"FLASHLIGHT OUT"
730 GOSUB90:Z$="YOU ARE AT: "+L$:GOSUB1
740 IFNTHENZ$=MID$(STR$(N),2)+" "+P$+"WITH YOU":GOSUB1
750 IFL=3THENPRINT"ENTRANCE IS COLLAPSED"
755 IFL=3THENIFP2=0THENZ$="SOMEONE INSIDE IS CALLING FOR HELP":GOSUB1
760 IFL=24ANDPP=0ANDD=0THENPRINT"SOMEONE CALLING FOR HELP"
770 IFL=26THENIFP1=0ORP8=0THENPRINT"CRIES OF HELP-- ABOVE YOU"
780 IFL>26ANDW1=1THENPRINT"YOU DROWN":END
790 IFL>26THENIFL(33)<>0ORF1=0THENPRINT"IT'S TOO DARK TO SEE":GOTO50
800 Y=0:PRINT"YOU SEE:":FORX=1TO67:IFL(X)=LTHENZ$=O$(X+6):GOSUB1:Y=Y+1
810 NEXTX:IFL=1ANDP1=0THENY=1:Z$=F$+"TRAPPED BY ELECTRIC WIRE":GOSUB1
820 IFL=4ANDP2=0THENY=1:Z$=F$+"TRAPPED BY SNAKE":GOSUB1
830 IFL=5ANDP3=0THENY=1:Z$=F$+"TRAPPED BY FIRE":GOSUB1
840 IFL=8ANDP4=0THENY=1:Z$=M$+"COVERED WITH ACID":GOSUB1
850 IFL=9ANDP5=0THENY=1:Z$=M$+"BURIED IN RUBBLE":GOSUB1
860 IFL=16ANDP6=0THENY=1:Z$=F$+"UNCONCIOUS":GOSUB1
870 IFL=18ANDP7=0THENY=1:Z$=F$+"UNDER WOOD BEAM":GOSUB1
880 IFL=19ANDP8=0THENY=1:Z$=M$+"WITH BROKEN LEG":GOSUB1
890 IFL=21ANDP9=0THENY=1:Z$=M$+"PINNED UNDER TRUCK":GOSUB1
900 IFL=26ORL=4ORL=23THENZ$="ACCESS PANEL-- FLOOR":GOSUB1
910 IFL=27ORL=32ORL=30THENZ$="ACCESS COVER-- CEILING":GOSUB1
920 P(26,5)=0:IFL(18)=26ANDE=0THENP(26,5)=16:P(16,6)=26
930 IFS=66ANDD=1THENPRINT"NOTHING BUT WATER NOW"
940 IFY=0THEN:PRINT"NOTHING"
950 PRINT"OBVIOUS EXITS ARE:":Z$="":FORX=1TO6:IFP(L,X)<>0THENZ$=Z$+O$(X)+" "
960 NEXTX:GOSUB1:GOTO50
970 IFS>6THEN1010
980 IFS<7ANDP(L,S)<>0THENL$=D$(P(L,S)):GOTO990
985 GOTO50
990 IFP(L,S)>20THENL7=L:L=P(L,S):GOSUB100:L=L7
1000 PRINTL$:GOTO50
1010 IFL>26ANDW1=1THENPRINT"IT'S FLOODED NOW":GOTO340
1020 IFS=70THENZ$="RED, GREEN, BLUE AND YELLOW":GOSUB1:GOTO50
1030 IFS=67ANDDE=0THENPRINT"STUCK-- 5 FEET UP":GOTO50
1040 IFS=62THENZ$="MIGHT RUN-- IF TURNED UPRIGHT":GOSUB1:GOTO50
1050 IFS=66ANDPP=0ANDD=0THENPRINT"MAN TRAPPED IN PIT":GOTO50
1060 IFS=66THENPRINT"SOME WATER":GOTO50
1070 IFS=44THENPRINT"BANANA SPLIT":GOTO50
1080 IFS=59THENPRINT"PHILLIPS SLOT":GOTO50
1090 IFS=57THENPRINT"NOBODY ELSE HERE":GOTO50
1100 IFS=14THENPRINT"A HUNGRY BOA":GOTO50
1110 IFS=47THENPRINT"AUTO SHOW-- FRIDAY 13TH":GOTO720
1120 IFS=56THENCLS:PRINT"CLOSE ENCOUNTERS":GOTO720
1130 IFS=15ORS=16THENIFP3=0THENPRINT"THEY ARE BURNING":GOTO30
1135 IFS=15ORS=16THENIFP3<>0THENPRINT"YOU DON'T HAVE TIME!":GOTO50
1140 IFS=63THENPRINT"EXIT":GOTO50
1150 PRINT"NOTHING SPECIAL":GOTO50
1160 IFS=73THENPRINT"YOU ARE PLAYING WITH FIRE!"
1170 IFS>52THENPRINT"CAN'T TAKE THAT!":GOTO50
1180 IFL(Z)=0THENPRINT"YOU HAVE THAT!":GOTO50
1190 IFC>12THENPRINT"CAN'T CARRY ANY MORE":GOTO50
1200 IFS=7ANDL(29)<>0THEN2150
1210 IFS=7THENZ$="TOO LONG TO PICK UP-- YOU MIGHT MOVE THEM":GOSUB1:GOTO50
1220 IFS=23ANDL(29)<>0THENPRINT"THAT'LL BURN YOU!":GOTO50
1230 IFS=14THENPRINT"IT WILL BITE!":GOTO50
1240 IFS=13THENL(7)=90:PRINT"BIRD FLEW TO SAFETY":GOTO50
1250 IFS=50ANDL(12)<>0THENPRINT"NO CONTAINER":GOTO50
1260 IFS=50ANDW4=1THENPRINT"PAIL IS FULL":GOTO30
1270 IFS=50THENW4=0:O$(18)="PAIL FULL OF WATER":W4=1:GOTO40
1280 IFS=20THENR=0:O$(65)="LARGE IVORY STATUE"
1290 IFS=49THENPS=1
1300 IFS=39ANDL(46)=LTHENL(46)=0:C=C+1
1310 L(Z)=0:C=C+1
1320 PRINT"YOU ARE CARRYING--":FORX=1TO46:IFL(X)=0THENPRINTO$(X+6)
1330 NEXTX:IFC=0THENPRINT"NOTHING"
1340 GOTO50
1350 IFS=70THENPRINT:INPUT"WHICH ONE";C$:GOSUB80
1360 IFC$="BL"ANDL(5)=90THENL(5)=4:GOTO40
1370 IFC$="GR"ANDL(4)=90THENL(4)=4:GOTO40
1380 IFC$="YE"ANDL(7)=90THENL(7)=4:GOTO40
1390 IFC$="RE"ANDL(6)=90THENL(6)=4:GOTO40
1400 IFS=69ORS=68THENPRINT"IT'S LOCKED!"
1410 IFS=53ANDP(6,3)=0THENP(6,3)=7:GOTO40
1420 IFS=53THENPRINT"ALREADY OPEN":GOTO30
1430 IFS=63THENPRINT"IT'S JAMMED":GOTO30
1440 IFS=48ANDL(43)=90THENL(43)=30:PRINT"AHA!":GOTO50
1450 IFS=48THEN40
1460 PRINT"CAN'T OPEN THAT!":GOTO50
1470 IFS=53ANDP(6,3)=7THENP(6,3)=0:GOTO40
1480 IFS=53THENPRINT"ALREADY CLOSED":GOTO50
1490 IFS=48ANDPS=0THENL(43)=90:GOTO40
1500 IFS=48THEN40
1510 PRINT"CAN'T CLOSE THAT":GOTO50
1520 IFS=7ANDP1=0ANDL(29)=0THENP(1,1)=2:L(1)=17:P1=1:GOTO130
1530 IFS=7ANDL(29)<>0THEN2150
1540 IFS=7ANDL=17THENPRINT"WHY?-- DON'T NEED TO!":GOTO50
1550 GOTO30
1560 IFS=62ANDTR=0THENIFN=0THENPRINT"CAN'T DO IT BY YOURSELF!":GOTO50
1565 IFS=62ANDTR=0THENIFN<>0THENIFN<10THENPRINT"NEED MORE HELP!":GOTO50
1567 IFS=62ANDTR=0THENIFN<>0THENIFN>=10THENPRINT"TRUCK IS UPRIGHT!":P(21,5)=20
1568 IFS=62ANDTR=0THENIFN<>0THENIFN>=10THENTR=1:O$(62)="TRUCK":GOTO50
1570 IFS=62THENPRINT"IT'S UPRIGHT!":GOTO30
1580 IFS=71THENPRINT"YOU ARE HEADING RIGHT":GOTO50
1590 IFS=59THENINPUT"WITH WHAT";C$:GOSUB80:IFC$="PH"THEN1630
1595 IFS=59THENIFC$<>"PH"THEN30
1600 IFS=54ANDW=0THENW=1:PRINT"WATER STOPS":O$(50)="WATER":GOTO50
1605 IFS=54THENW=0:PRINT"YOU HEAR WATER RUNNING":O$(50)="WATER-RUNNING INTO HOLE":GOTO50
1610 IFS=61ANDL(45)=0THENPRINT"ENGINE RUNNING":IG=1:GOTO50
1615 IFS=61THENPRINT"NO KEY":GOTO30
1620 GOTO50
1630 IFL(43)<>0THENPRINT"DON'T HAVE IT!":GOTO30
1635 IFP=0THENP=1:PRINT"POWER ON!":GOTO50
1637 PRINT"ALREADY RESET":GOTO340
1650 IFR=1ANDPP=0ANDS=20THENZ$="ROPE GOES INTO PIT-- MAN CLIMBS ROPE TO SAFETY":GOSUB1
1655 IFR=1ANDPP=0ANDS=20THENPP=1:GOSUB150:GOTO130
1660 IFS=20ANDR=0THENL(14)=90:PRINT"ROPE DISAPPEARS INTO PIT":C=C-1:GOTO50
1670 IFS=44THENPRINT"YOU MADE A MESS!":GOTO50
1680 IFL(Z)=0THENL(Z)=L:C=C-1:GOTO40
1685 PRINT"DON'T HAVE THAT":GOTO30
1690 IFL(28)<>0THENPRINT"NO SHOVEL":GOTO50
1700 IFL<>9THENPRINT"CAN'T DIG HERE":GOTO50
1710 IFH=2ANDP5=0THENP5=1:PRINT"THE MAN IS FREE":GOSUB150:GOTO130
1720 H=H+1:Z$="YOU ARE DIGGING IN A PILE OF RUBBLE":GOSUB1:GOTO50
1730 IFL(2)<>0THENPRINT"NO JACK":GOTO50
1740 L(2)=L:IFL=21ANDP9=0THEN2160
1745 GOTO30
1750 IFS<>58THEN1810
1760 INPUT"WHICH ONE";C$:GOSUB80:IFP=0THENZ$="NO POWER-- RESET IS TRIPPED":GOSUB1:GOTO50
1770 IFC$="UP"THENIFE=0ORE=1THENP(16,3)=17
1775 IFC$="UP"THENIFE=0ORE=1THENE=2:GOTO2140
1777 IFC$="UP"THENIFE<>0ANDE<>1THEN1800
1780 IFC$="DO"THENIFE=0ORE=2ANDE=2THENE=1:P(16,3)=26:P(26,4)=16:GOTO2140
1785 IFC$="DO"THENIFE<>0ANDE<>2ORE<>2THEN1800
1790 GOTO30
1800 Z$="CAN'T GO ANY FARTHER THAT WAY":GOSUB1:GOTO50
1810 IFS=72ANDIG=1THEN2260
1815 IFS=72THENZ$="IGNITION OFF-- NOTHING HAPPENS":GOSUB1:GOTO50
1820 GOTO30
1830 IFL(21)<>0THENPRINT"NO SAW":GOTO50
1840 IFS=60ANDP7=0THENP7=1:PRINT"WOMAN IS O.K.!":GOSUB150:GOTO130
1850 Z$="WHY ARE YOU SAWING THE "+O$(S)+"?":GOSUB1:GOTO50
1860 IFS=12ANDL=4ANDP2=0THEN:L(8)=90:L(6)=4:C=C-1
1865 IFS=12ANDL=4ANDP2=0THENP2=1:Z$="THE MONGOOSE FRIGHTENS AWAY THE SNAKE":GOSUB1:GOSUB150:GOTO130
1870 IFS=39ANDL(46)=0THENL(46)=L:C=C-1
1880 L(Z)=L:C=C-1:GOTO40
1890 IFS<>20THENPRINT"CAN'T TIE THAT!":GOTO50
1900 INPUT"TO WHAT";C$:GOSUB80
1910 IFL<>24THENPRINT"THAT'S NOT HERE":GOTO30
1920 IFC$="ST"ANDR=0THENO$(65)="IVORY STATUE-- ROPE ATTACHED":L(14)=L
1925 IFC$="ST"ANDR=0THENR=1:C=C-1:GOTO40
1927 GOTO30
1930 IFS=39ANDL(46)=0ANDF1=0THENF1=1:PRINT"LIGHT ON":GOTO50
1940 IFS=39ANDL(46)<>0THENPRINT"NO BATTERIES":GOTO30
1950 IFS=36ANDP4=0ANDL=8THENL(30)=L:L(17)=90:Z$="THE BAKING SODA NEUTRALIZED THE ACID!":GOSUB1:GOSUB150
1955 IFS=36ANDP4=0ANDL=8THENP4=1:GOTO130
1960 IFS=36THENPRINT"NO EFFECT":GOTO50
1970 IFS=26ANDL=4ANDP3=0THENZ$="FIRE OUT OF CONTROL-- YOU BURNED TO DEATH":GOSUB1:END
1980 IFS=25ORS=44THENPRINT"YOU MADE A MESS":L(Z)=L:C=C-1:GOTO50
1990 GOTO30
2000 IFS=31ANDL=19ANDP8=0THENP8=1:GOTO130
2010 IFS=17ANDL=5ANDP3=0THEN2050
2020 IFS=30ANDL=16ANDP6=0THENPRINT"WOMAN IS CONSCIOUS NOW!":P6=1:GOSUB150:GOTO130
2030 IFS=8THEN1740
2040 PRINT"HOW??":GOTO50
2050 P3=1:L(67)=90:PRINT"FIRE IS OUT":GOSUB150:GOTO130
2060 IFS=32ORS=36ORS=33THENPRINT"TASTES TERRIBLE!":GOTO50
2070 IFS=38THENPRINT"THAT'S GOOD!":GOTO50
2080 IFS=44THENL(38)=90:PRINT"YUM-YUM!":C=C-1:GOTO50
2090 IFS=37THENPRINT"THAT'S DELICIOUS":GOTO50
2100 IFS=23THENPRINT"YOU DON'T WANT TO DO THAT!":GOTO50
2110 IFS=14THENPRINT"NOW YOU'RE BEING SILLY!"
2120 Z$="HOW CAN YOU EAT THE "+O$(S)+"?":GOSUB1:GOTO50
2130 IFS=39ANDF1=1THENF1=0:PRINT"LIGHT OFF":GOTO50
2135 GOTO30
2140 FORX=1TO15:PRINT"YOU'RE MOVING":GOSUB160:NEXT:P(16,6)=0:GOTO720
2150 CLS:FORX=1TO15:PRINT"440 VOLTS":GOSUB160:NEXTX:GOSUB150:CLS:GOTO50
2160 P9=1:PRINT"MAN IS FREE AHA!":L(45)=21:GOSUB150:GOTO130
2170 IFL=26ANDP(26,6)=0THENP(26,6)=32:GOTO40
2180 IFL=27ANDP(27,5)=0THENP(27,5)=4:GOTO40
2190 IFL=26ORL=27THENPRINT"ALREADY"
2200 IFL=23ORL=30THENPRINT"IT'S STUCK"
2210 GOTO30
2220 CLS:PRINT"earthquake":PRINT"COPYRIGHT aardvark":P9=1:GOSUB150:RETURN
2240 CLS:PRINT"YOU HEAR RUMBLING!":FORX=1TO500
2242 NEXTX:Z=8:FORX=15TO1STEP-1:POKE49151,64:FORY=1TO50:NEXT:Z=Z+16
2243 IFZ>248THENZ=8
2244 SOUNDZ,1:NEXT
2245 O$(62)="TRUCK OVERTURNED":OP=1:P9=0:P(21,3)=0:CLS:GOTO720
2260 CLS:FORX=1TO19:PRINT"YOU'RE MOVING":GOSUB150:NEXT:CLS
2265 Z$="TRUCK CRASHES THROUGH THE DOORS! A CHEERING CROWD WELCOMES YOU...":GOSUB1
2270 POKE49151,64:FORX=1TO500:NEXT
2275 SOUND1,1:END
2290 IFL=5ANDL(12)=0ANDW4=1ANDP3=0THENO$(18)="PAIL":W4=0:GOTO2320
2300 IFW4=1ANDL(12)=0THENW4=0:O$(18)="PAIL":PRINT"YOU MADE A MESS":GOTO50
2310 PRINT"NO WATER":GOTO30
2320 PRINT"FIRE IS SMALLER":F7=F7+1:IFF7=3THENPRINT"MUST BE AN EASIER WAY!"
2325 IFF7=3THEN50
2330 IFF7=6THEN2050
2335 GOTO50
3000 REM MC-10 EDITS
3010 REM JIM GERRIE
