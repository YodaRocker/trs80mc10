0 CLS:CLEAR255:DIMC1,CC,M$,A(45,7):C1=RND(-(PEEK(9)*256+PEEK(10))):GOTO10
1 C1=1:CC=32:FORCC=CCTOC1STEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,C1,CC-C1):C1=CC+1:CC=C1+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 GOSUB1:RETURN
4 GOSUB1:GOTO36
7 PRINT"NOTHING HAPPENS":GOTO36
8 FORCC=1TO2500:NEXT:RETURN
9 M$="YOU CAN'T "+A$:GOSUB1:RETURN
10 CLS:M$="YOUR STRANGE FRIEND DR. TEMPUS HAS CALLED YOU TO HIS HOME. 'I HAVE CREATED A":GOSUB1
11 PRINT:PRINTTAB(9)"*TIME MACHINE*":PRINT:M$="I CALL IT TM & IT SEEMS TO BE TEMPERMENTAL-- ONLY GOES TO SEVEN TIME ZONES, "
12 M$=M$+"BUT JUST THINK, WE COULD RETURN WITH A DINOSAUR EGG OR THE HOLY GRAIL... AND WHO KNOWS WHAT ELSE!":GOSUB1
13 PRINTTAB(11)"LET'S GO!'"
14 FORRO=1TO45:FORCC=1TO7:READA(RO,CC):NEXT:NEXT:FORCC=1TO8:READT$(CC):NEXT:FORCC=1TO7:READE$(CC):NEXT
16 PRINT@485,"HIT ANY KEY WHEN READY";:GOSUB900:H=30:RO=0:GOTO770
24 GOSUB8
26 H=H-1:IFH<1THENPRINT"HUNGER HAS TAKEN YOU BOTH":GOTO348
28 CLS:PRINT"YOU ARE--":GOSUB88:IFH<7THENPRINT"DR.T SAYS'I FEEL VERY HUNGRY'"
32 IFA(RO,6)>20THENU=A(RO,6)-20:GOSUB8:PRINT:PRINT"HORRORS! ";E$(U)
34 IFA(RO,6)=2ANDSW=0THENPRINT"ON THE GROUND LIES A SWORD"
36 PRINT:INPUT"WHAT WILL YOU DO";A$:IFA$=""THEN36
38 B$=LEFT$(A$,3):N=1:M=LEN(A$):IFA$="HELP"THENM$="DON'T ASK ME, ASK DR.T":GOTO4
42 IFA$="LOOK"THEN524
44 IFB$="FIG"THEN264
46 IFB$="QUI"THEN348
48 IFB$="INV"THENGOSUB736:GOTO36
50 IFA$="CAROUSE"THENM$="YAHOO!":GOTO4
52 IFA$="SING"THEN62
54 IFMID$(A$,N,1)=" "THENC$=MID$(A$,N+1,3):ON-(LEFT$(C$,1)<>" ")GOTO60:M$="TWO WORDS PLEASE":GOTO4
56 IFN<MTHENN=N+1:GOTO54
60 C1=0:FORCC=1TO15:IFB$=MID$("  GO WALRUNSINGETDROEATASKUNLDRIWHIKNOOPESHOLOO",CC*3,3)THENC1=CC
61 NEXT:ONC1GOTO382,382,382,724,546,600,428,448,496,510,520,518,478,730,536:GOSUB86:GOTO36
86 M$="I DON'T UNDERSTAND "+A$:GOSUB1:RETURN
88 IFRO<21THENONROGOSUB92,94,112,114,118,120,126,128,130,132,144,150,156,158,160,164,166,168,170,172:RETURN
89 ONRO-20GOSUB178,180,182,184,188,190,192,198,200,202,206,212,216,218,222,226,228,230,236,238,240,250,252,254,256:RETURN
92 M$="BESIDE TM ON A GRASSY ISLAND WITH SWAMP W & S":GOTO3
94 M$="ON A GRASSY ISLAND WITH SWAMP TO N & W. BEFORE YOU LOOMS A T-REX":GOSUB1:TI=0
98 TI=TI+1:IFTI>3600THEN124
99 INPUT"WHAT WILL YOU DO";A$:B$=LEFT$(A$,5):IFA$="RUN"THENPRINT"WHERE?":GOTO98
100 IFB$="FIGHT"THENPRINT"YOU CAN'T FIGHT REX!":GOTO98
102 IFB$="RUN S"THENRO=1:GOTO26
104 IFB$="RUN E"THENRO=3:GOTO26
106 IFLEFT$(A$,2)="GO"THENPRINT"DR.T YELLS 'FASTER...FASTER!'":GOTO98
108 IFB$="RUN W"ORB$="RUN N"THENRO=7:GOTO26
110 M$="WITH A DINOSAUR CHASING YOU, YOU WANT TO "+A$+"?":GOSUB1:GOTO98
112 M$="ON A GRASSY ISLAND. N IS SWAMP; E IS A VOLCANO.":GOTO3
114 M$="ON A GRASSY ISLAND. S IS SWAMP; E IS A CLIFF. YOU SEE A BUSH WITH BERRIES AND A MAMA TRICERATOPS "
115 IFE=0THENM$=M$+"WITH AN EGG"
116 GOTO3
118 M$="ON A VOLCANO. SWAMP-NORTH & EAST; WEST-THE PLAIN. SOUTH-A DARK CAVE":GOTO3
120 PRINT"IN A DARK CAVE"
122 SOUND1,20:M$="FROM BELOW EMERGES A FOUL DINOSAUR":GOSUB1
124 SOUND1,20:M$="HE GOT YOU! YOU END YOUR DAYS AS A SNACK IN THE MESOZOIC AGE":GOSUB1:GOTO348
126 PRINT"SLOSHING IN THE SWAMP":GOTO122
128 M$="ON PALATIN HILL NEXT TO TM. NORTH IS THE ROMAN FORUM, SOUTH THE APPIAN WAY, WEST A TEMPLE. "
129 M$=M$+"EAST YOU SEE CLOSED BUILDINGS & MEAN STREETS.":GOTO3
130 A(10,6)=0:M$="IN THE ROMAN FORUM. WEST & NORTH ARE MEAN STREETS. EAST IS THE COLOSSEUM; SOUTH IS PALATIN HILL.":GOTO3
132 M$="IN THE COLOSSEUM. IT IS FULL OF PEOPLE. YOU ARE CAUGHT BY A GUARD AND PRESENTED TO CAESAR WHO CRIES:":GOSUB1:GOTO136
134 M$="RAINING COINS UPON YOUR HEAD. THE ONLY EXIT YOU SEE IS WEST":GOTO3
136 ONRND(4)GOTO138,140:RO=15:SL=0
137 M$="'LET'S PARTY! A CHEERING CROWD LEEDS YOU TO THE PALACE!":GOSUB1:GOTO24
138 M$="'LET THEM FIGHT THE GLADIATORS'":GOSUB1:GOTO358
140 PRINT"'THROW THEM TO THE LIONS'":GOTO370
144 M$="LOST IN A MAZE OF STREETS. A CROWD OF RUFFIANS APPEAR AND FEARFUL OF YOUR STRANGE ATTIRE, STONE YOU!":GOSUB1
148 M$="THEY GOT YOU! YOU ARE LOST IN THE MISTS OF TIME":GOSUB1:GOTO348
150 M$="IN THE TEMPLE OF THE VESTAL VIRGINS. THE TIBER FLOWS PAST WEST & SOUTH. NORTH ARE MEAN STREETS. "
151 M$=M$+"EAST, THE PALATINE HILL. YOU SEE AN ALTAR":IFBX=0THENM$=M$+" ON WHICH RESTS A BOX"
154 GOTO3
156 M$="ON THE APPIAN WAY, WHICH WINDS FROM NORTH TO EAST":GOTO3
158 KL$="IN A MAZE OF CATACOMBS":ML$="MOLDY LOAF OF BREAD":DL$="THE SOUND OF HYMNS SEEMS TO ECHO AROUND YOU"
159 TL$="A SACRED SCROLL":GOTO646
160 PRINT"IN CAESAR'S PALACE.":IFSL=0THENPRINT"EVERYONE IS CAROUSING.":GOTO162
161 PRINT"EVERYONE IS ASLEEP"
162 M$="THE DOOR N HAS BEEN BARRED; S YOU SEE THE APPIAN WAY; W IS PALATINE HILL":GOTO3
164 A(20,7)=2:M$="BESIDE TM IN A CLEARING IN DARK WOODS. A LIGHT GLOWS TO THE N.":GOTO3
166 M$="AT CAMELOT. W IS A HALL; S IS A PATH":GOTO3
168 M$="IN A HALL GOING N/E":GOTO3
170 M$="IN A HALL GOING S/E":GOTO3
172 M$="IN A ROOM WITH A LARGE ROUND TABLE. ":IFBO=0THENM$=M$+"YOU SEE A SMALL JEWELLED CASKET. "
174 IFA(20,7)=2THENM$=M$+"ON THE TABLE IS A LOAF OF BREAD. "
176 M$=M$+"N IS A PATH THROUGH DARK WOODS, W A HALL":GOTO3
178 M$="ON N/S PATH THROUGH THE WOODS":GOTO3
180 M$="ON N/S PATH THROUGH DEEP WOODS. NORTH YOU SEE A BEAUTIFUL TEMPLE":GOTO3
182 M$="IN DARK FOREST. BANDITS SURROUND YOU!":GOSUB1:GOTO148
184 M$="IN A BEAUTIFUL TEMPLE.":IFGR=0THENM$=M$+" ON THE ALTAR STANDS... THE HOLY GRAIL!"
186 GOTO3
188 A(27,7)=2:M$="BESIDE TM IN A DESERT. S & W IS UNCLIMBABLE CLIFF":GOTO3
190 A(27,6)=21:GD=0:M$="IN A DESERT. UNCLIMBABLE CLIFF W & N; E IS A WALLED SETTLEMENT":GOTO3
192 M$="IN A SPANISH SETTLEMENT.":IFA(27,7)=2THENM$=M$+" THERE'S A TABLE WITH BREAD ON IT."
194 IFGD=0THENM$=M$+" AND GOLD!"
196 GOTO3
198 KL$="SURROUNDED BY DESERT":ML$="CACTUS":DL$="NOTHING BUT TUMBLEWEED FOR MILES":TL$="A TREASURE CHEST OF GOLD":GOTO646
200 M$="AT 221B BAKER ST. SHERLOCK HOLMES STUDIES YOU & TM LACONICALLY AND REMARKS THAT YOU CAN GO ANY DIRECTION "
201 M$=M$+"IN THE CAB OUTSIDE":GOTO3
202 M$="IN REGENTS PARK. A VENDOR SELLS BREAD AND WINE.":IFBG=0THENM$=M$+" ON A BENCH YOU SEE A BAG."
204 A(30,7)=2:M$=M$+" THE CAB WAITS":GOTO3
206 M$="AT PROFESSOR MORIARITY'S HOUSE.":IFA(31,6)=0THENM$=M$+" IT IS CLOSED UP TIGHT"
210 GOTO3
212 M$="AT SCOTLAND YARD.":IFA(33,6)<1THEN3
213 M$=M$+" A SERGEANT TELLS YOU THERE'S A STRANGE ILLNESS IN THE CITY. HE CONFIDES HE THINKS A VAMPIRE IS RESPONSIBLE.":GOTO3
216 M$="AT A LARGE HOUSE":GOTO3
218 A(35,7)=2:M$="ON AN AVENUE THAT ENDS W AT THE POLICE STATION. N & S ARE BUILDINGS, E A MAZE OF STREETS. TM WAITS FOR YOU."
220 M$=M$+" A MAN SIDLES UP TO YOU AND MUTERS 'LOOKING FOR HOOCH?' AND NODS AT A DOOR N":GOTO3
222 PRINT"IN A DARK & GLOOMY SPEAKEASY. THE ONLY WAY OUT IS THE   DOOR TO THE SOUTH."
223 IFA(RO,7)=2THENGOSUB8:PRINT"A WAITER BRINGS BREAD AND FOUL  SMELLING BREW"
224 RETURN
226 M$="IN A POLICE STATION. SERGEANT O'HARA GIVES YOU A LECTURE ON THE EVILS OF DEMON RUM. THE ONLY EXIT IS E":GOTO3
228 KL$="IN A MAZE OF STREETS.":ML$="CRUST OF BREAD ON THE STREET":DL$="RAUCOUS NOISE EMINATES FROM A DINGY DOOR"
229 TL$="A DIAMOND STICKPIN":GOTO646
230 PRINT"IN A LARGE AUDITORIUM   WITH TM IN CORNER.":PRINT"THERE ARE DOORS NORTH,WEST&SOUTHEAST IS A THICK GLASS AND ";
231 PRINT"LEAD  DOOR THAT SHOWS AN ALIEN & DESO-LATE LAND WHERE NOTHING SEEMS TOBE ALIVE"
232 O=RND(3):IFO=1THENA(39,6)=27:GOTO234
233 GOSUB4200
234 A(41,6)=27:RETURN
236 PRINT"IN AN L-SHAPED ROOM WITHDOORS S/E/W.  A NORTH DOOR IS   LABELLED danger-radiation zone":RETURN
238 PRINT"IN LONG E/W HALL":RETURN
240 PRINT"ON A ROCKET SHIP WITH   PORTALS EAST AND SOUTH.":IFA(41,7)>0THENPRINT"AGAINST THE WALL ARE FOOD TUBES":GOSUB8
242 IFA(41,6)=27THENPRINT"THE ROCKET SUDDENLY SHAKES AND  ROARS AS IT TAKES OFF...WITH YOUIN IT!"
244 IFA(41,6)=1THENPRINT"THROUGH THE WINDOWS YOU CAN SEE STARS AND PLANETS AND GALAXIES"
246 IFA(41,6)=0THENPRINT"THE ROCKET HAS JUST RETURNED TO EARTH"
248 RETURN
250 PRINT"IN A LABORATORY":PRINT"A HEAVY DOOR TO WEST IS LABELED     'danger-radiation zone'"
251 PRINT"NORTH AND EAST ARE REGULAR DOORS":RETURN
252 PRINT"IN A HALL":PRINT"THERE ARE DOORS WEST AND NORTH":RETURN
254 PRINT"IN AN UNINHABITABLE      radiation zone":GOSUB8:PRINT"YOU FEEL YOURSELVES BECOMING    ";
255 PRINT"SICK AND YOU SINK TO THE GROUND":GOSUB8:PRINT"YOUR BODIES ARE NOW LOST IN THE FUTURE":GOTO348
256 PRINT"IN THE CAB.":PRINT"THE CABBIE SAYS YOU NEED A TOUR OF LONDON":GOSUB8:IFRND(2)=1THENPRINT"'HERE'S HYDE PARK":GOTO258
257 PRINT"'HERE'S BUCKINGHAM PALACE"
258 GOSUB8:IFRND(2)=1THENPRINT"THERE'S THE TOWER OF LONDON":GOTO260
259 PRINT"THERE'S BEDLAM..ER..BETHLEHELM  LUNATIC ASYLUM"
260 GOSUB8:IFRND(2)=1THENPRINT"AND THE BRITISH MUSEUM'":GOTO262
261 PRINT"AND MADAME TUSSAUD'S'"
262 GOSUB8:RO=29:M$="AND HE BRINGS YOU BACK TO BAKER STREET":GOSUB1:GOTO24
264 IFA(RO,6)<15THENM$="THERE'S NOTHING TO FIGHT HERE":GOTO4
266 GOSUB8:IFSW=1THENJ=RND(7):GOTO270
268 J=RND(4)
270 IFJ=1THENPRINT"HE GOT YOU":GOTO286
272 IFJ=2THENPRINT"HE STRIKES A TELLING BLOW":GOTO266
274 IFJ=3THENPRINT"YOU HAVE WOUNDED HIM":GOTO266
276 IFRO=33THENA(33,6)=0:M$="DRACULA TURNED INTO A BAT AND FLEW AWAY":GOSUB1:GOTO24
278 IFRO=31THENA(31,6)=0:M$="YOU KILLED THE ARCH CRIMINAL MORIARITY AND MADE THE WORLD SAFER FOR GOOD MEN":GOSUB1:GOTO24
280 IFRO=41THENA(RO,6)=1:M$="YOU GOT HIM":GOSUB1:GOTO24
282 IFRO=10THENA(10,6)=3:M$="YOU BESTED HIM. MAGNANIMOUSLY YOU LET HIM GO.":GOSUB1:GOTO24
284 A(RO,6)=0:M$="YOU GOT HIM":GOSUB1:GOTO24
286 IFRO=33THENPRINT"NOW YOU ARE A VAMPIRE TOO":GOTO348
288 IFRO<>22THEN348
289 M$="THE SORCEROR HAS TURNED YOU BOTH INTO FROGS...":GOSUB1:GOSUB8:PRINT"I HOPE YOU LIKE FLIES.":GOTO348
300 DATA2,7,4,7,1,,,7,1,3,7,,,,7,4,5,2,,,,3,7,,1,,,1,7,6,7,3,,,,,,,,,,,,,,,,,,9,13,11,12,1,2,,11,8,10,11,,,
308 DATA0,,,9,,,,,,,,,,,11,,8,,,3,,8,,14,,,,,14,14,14,13,,4,,,13,,8,,,2,17,23,23,23,1,,2,,16,,18,,2,
316 DATA19,,17,,,,,,18,20,,,,,21,,,19,,5,2,22,20,23,23,,22,,24,21,23,23,,23,,,,,,,,,,22,,,,6,,26,,28,,1,,,,25,27,,,,
324 DATA0,,,26,,21,2,28,28,28,25,,,,30,31,32,33,1,,,45,29,32,33,,3,2,29,45,32,33,,24,,30,31,45,29,,,
330 DATA30,31,29,45,,25,,,,37,36,1,26,,0,34,,,,1,2,,,34,,,,,37,37,37,34,,,,39,43,44,39,1,,,44,38,38,40,,,
338 DATA0,,39,41,,,,,42,40,,,27,2,41,,43,44,,9,,38,,,42,,,,,,,,,,,30,30,30,30,,,,DIAMOND,SWORD,COINS,SCROLL,CROSS
339 DATAHOLY GRAIL,GOLD,VACCINE,A CONQUISTADOR,A DRAGON,A SORCERER,PROF MORIARITY,DRACULA,A GANGSTER,A ROBOT
348 GOSUB8:INPUT"ANOTHER GAME? (Y/N)";A$:IFA$="Y"THENRUN
350 END
358 PRINT"THE GLADIATORS APPEAR"
360 INPUT"WHAT WILL YOU DO";A$
362 IFA$="STAB"ORA$="FIGHT"ORA$="HIT"THEN266
364 GOSUB9:GOTO360
366 M$="YOU HAVE BESTED THEM. MAGNANIMOUSLY YOU SPARE THEM.":GOSUB1
368 M$="THE CROWD CHEERS AND COINS RAIN UPON YOUR HEAD. THE ONLY EXIT IS WEST":GOTO4
370 PRINT"THE LIONS APPEAR":T=RND(3):IFSW=1THEN376
374 T=RND(9)
376 GOSUB8:IFT=2THENA(10,6)=3:M$="YOU HAVE SLAIN THE LIONS.":GOSUB1:GOTO24
380 GOTO148
382 IFA(RO,6)>19THENPRINT"YOUR FOE WILL NOT LET YOU GO":GOTO24
384 IFC$="HOU"ANDRO=33THEN422
386 IFY<1THENA(33,6)=25
388 IFRO=15ANDSL=0THENPRINT"CAESAR SHOUTS--YOU CAN'T GO. EAT-DRINK-SING-CAROUSE":GOTO24
390 IFRO=4ANDE=1THEN590
392 IFRO=41ANDA(RO,6)>0THENPRINT"YOU DON'T WANT TO LEAVE THE ROC-KET WHILE IT'S IN OUTER SPACE":GOTO24
394 IFC$="TM"ANDA(RO,5)=0THENPRINT"IT ISN'T HERE":GOTO36
396 IFC$="TM"GOTO770
398 D$=LEFT$(C$,1):IFD$="N"ANDA(RO,1)=0THENPRINT"YOU CAN'T GO THAT WAY":GOTO36
402 IFD$="S"ANDA(RO,2)=0THEN422
404 IFD$="E"ANDA(RO,3)=0THENPRINT"NO EAST EXIT":GOTO36
406 IFD$="W"ANDA(RO,4)=0THEN422
408 IFD$="N"ANDRO=22THEN424
410 IFD$="N"THENRO=A(RO,1):GOTO26
412 IFD$="S"THENRO=A(RO,2):GOTO26
414 IFD$="E"THENRO=A(RO,3):GOTO26
416 IFD$="W"THENRO=A(RO,4):GOTO26
418 IFC$="TEM"ANDRO=8THENRO=12:GOTO26
420 IFC$="TEM"ANDRO=22THEN424
422 GOSUB9:GOTO36
424 IFL=0THENPRINT"THE DOOR IS LOCKED":GOTO36
426 RO=24:GOTO24
428 IFC$="BER"AND(A(RO,7)=1ORB=1)THEN444
432 IFC$="BRE"ANDA(RO,7)=2THENPRINT"RATHER TASTY AND VERY FILLING":GOTO446
434 IFC$="BRE"ANDBR>0THENPRINT"RATHER TASTY":BR=BR-1:GOTO446
436 IFC$="BRE"THENPRINT"YOU DON'T HAVE ANY":GOTO36
440 IFC$="FOO"AND(A(RO,7)>0ORF=1)THEN444
442 PRINT"YUCK":GOTO36
444 PRINT"MMM,NOT BAD"
446 H=H+15:GOTO36
448 IFC$="DR."THEN456
450 IFRO=34ANDC$="FOR"ORC$="JOE"THEN454
452 PRINT"WHO?":GOTO36
454 PRINT"THE DOOR OPENS ON A DINGY BAR":RO=35:GOTO24
456 IFRO=15THENFORC1=1TO5:SOUND200,1:SOUNDC1,1:NEXT:GOTO36
458 IFRO=22THENPRINT"ONE MUST GO WHERE ALL ROADS LEAD":GOTO36
460 IFRO=5THENPRINT"DIAMOND'RE A DROP IN THE BUCKET":GOTO36
462 IFRO=34THENSOUND1,5:GOTO36
464 IFRO=33ANDY<1THENPRINT"ALL MUST CROSS HIS PATH SOMETIME":GOTO36
466 IFRO=41ORRO=35ORRO=43THENPRINT"SEEK AND YE SHALL FIND":GOTO36
468 ONRND(3)GOTO470,472:IFRO>7THENM$="HALF A LOAF IS BETTER THAN NONE":GOSUB1:GOTO36
469 M$="BEWARE THE DARK,DAMP,OR MEAN":GOSUB1:GOTO36
470 M$="COMMON SENSE IS NOT SO COMMON":GOSUB1:GOTO36
472 M$="NOBODY CAN GIVE YOU WISER ADVICE THAN YOURSELF":GOSUB1:GOTO36
478 IFC$="BOX"THEN490
480 IFC$="CAS"THEN492
482 IFC$="DOO"ANDRO=22ANDL=0THENPRINT"IT IS LOCKED":GOTO36
484 IFC$="DOO"ANDRO=22ANDL=1THENPRINT"IT IS OPEN":GOTO36
486 IFC$="BAG"ANDRO=30THENPRINT"COINS FELL OUT":GOTO36
488 PRINT"IT CANNOT BE OPENED":GOTO36
490 IFRO=12ORBX=1THENPRINT"THE BOX IS OPEN";:IFKY=0THENPRINT", AND INSIDE YOU SEE A KEY"
492 IFRO=20ORBO=1THENPRINT"THE CASKET IS OPEN ";:IFCR=0THENPRINT"AND INSIDE   YOU SEE A SMALL GOLD CROSS"
494 GOTO36
496 IFKY<>1THEN504
498 IFRO=22ANDL=1THEN508
499 INPUT"WITH WHAT";A$
500 IFRO=22ANDA$="KEY"THEN506
502 IFRO=22ANDA$="WITH KEY"THEN506
504 PRINT"YOU DO NOT HAVE A KEY FOR THAT  DOOR":GOTO36
506 SOUND150,1:L=1
508 PRINT"THE DOOR IS UNLOCKED":GOTO36
510 IFC$="WIN"THENPRINT"GAGGG, YOU ARE ABOUT TO BE SICK":SOUND55,10:H=H-6:GOTO36
512 IFC$="RUM"ORC$="BRE"THENPRINT"BARF, WHAT FOUL ROT THEY DRINK":H=H-5:GOTO36
514 IFC$="WAT"ANDRO>7THENPRINT"MMM, MOST REFRESHING":H=H+5:GOTO36
516 PRINT"YOU WANT TO DRINK WHAT???":GOTO36
518 IFRO=34ANDC$="THR"THEN454
520 IFRO=34ANDC$="LOW"ORC$="JOE"THEN454
522 GOTO7
524 IFRO=35ANDA(35,6)=1THENPRINT"HEY!!THAT FLAPPER JUST DROPPED ADIAMOND ON OUR TABLE!":GOTO36
526 IFRO=42THENPRINT"ALONG THE WALLS ARE SHELVES FILLED WITH VARIOUS CONTAINERS":GOTO24
528 IFRO=41ANDA(41,6)>0THENPRINT"THE ROBOT HAS A DIAMOND":GOTO36
530 IFRO=3THENPRINT"PTERADACTYLS GRACE THE SKY":GOTO24
532 IFRO=9THENPRINT"A GROUP OF SENATORS CONVERSE IN THE CORNER":GOTO24
534 GOTO26
536 IFRO=42THENPRINT"DR.T EXCLAIMS'WE'RE RICH--A VAC-CINE FOR THE COMMON COLD!":GOTO36
538 IFC$="DOO"ANDRO<38THENPRINT"IT'S A HEAVY WOODEN DOOR ";:GOTO4100
540 IFC$="VOL"ANDRO<8THENPRINT"IT EMITS WHIFFS OF SMOKE":GOTO36
542 IFC$="TEM"THENPRINT"BEAUTIFUL WHITE MARBLE GLEAMS INTHE SUNLIGHT":GOTO36
544 PRINT"YOU SEE NOTHING SPECIAL":GOTO36
546 IFC$="BER"ANDRO=4THENB=1:PRINT"YOU HAVE SOME BERRIES":GOTO24
548 IFC$="BRE"ANDA(RO,7)=2THENBR=BR+1:A(RO,7)=0:W$="BREAD":GOTO588
550 IFRO=41ANDC$="TUB"ORC$="FOO"THENW$="FOOD":F=1:A(RO,7)=0:GOTO588
552 IFC$="DIA"ANDA(RO,6)=1THEND=D+1:W$=T$(1):A(RO,6)=0:GOTO588
554 IFC$="EGG"ANDRO=4THENE=1:PRINT"YOU GOT THE EGG BUT MAMA IS VERYUPSET":GOTO36
556 IFC$="SWO"ANDA(RO,6)=2THENSW=1:W$=T$(2):GOTO588
558 IFC$="BOX"ANDRO=12THENW$="BOX":BX=1:GOTO588
560 IFC$="CAS"ANDRO=20THENBO=1:W$="CASKET":GOTO588
562 IFC$="KEY"AND(RO=12ORBX=1)THENKY=1:Z=Z+1:W$="KEY":GOTO588
566 IFC$="VAC"ANDRO=42THENV=1:W$=T$(8):GOTO588
568 IFC$="BAG"ANDRO=30ANDA(30,6)=3THENPRINT"COINS FELL OUT":BG=1:GOTO36
570 IFC$="BAG"THENPRINT"IT IS EMPTY":GOTO36
572 IFC$="COI"ANDA(RO,6)=3THENZ=Z+1:W$=T$(3):GOTO588
574 IFC$="GOL"ANDRO=27ANDGD=0THEN4120
576 IFC$="GOL"ANDRO=27ANDGD=1THENPRINT"THERE'S NO TIME. MORE MEN ARE   COMING":GOTO36
578 IFC$="CRO"AND(RO=20ORBO=1)THENCR=1:Z=Z+1:W$=T$(5):GOTO588
582 IFC$="WIN"THENW$="WINE":GOTO588
584 IFC$="GRA"ORC$="HOL"ANDRO=24THENGR=1:W$=T$(6):GOTO588
586 M$="YOU CANNOT "+A$:GOSUB1:GOTO36
588 M$="YOU HAVE THE "+W$:GOSUB1:GOTO24
590 IFB$="RUN"THEN398
592 O=RND(3)
594 IFO=1THENPRINT"DR.T YELLS FASTER":GOTO36
596 IFO=2THENPRINT"MAMA IS ALMOST ON YOU. YOU DROP THE EGG":E=0:GOTO36
598 IFO=3THENPRINT"SHE'S GAINING!":GOTO36
600 IFRO=5ANDC$="BER"ANDB=1THENW$="BERRIES":B=0:GOTO632
602 IFRO=5ANDC$="EGG"ANDE=1THENW$="EGG":E=0:GOTO632
604 IFC$="BER"ANDB=1THEN PRINT"SQUISH":B=0:GOTO36
606 IFC$="SWO"ANDSW=1THEN642
608 IFC$="EGG"ANDE=1THENPRINT"YOU HAVE DROPPED THE EGG AND IT CRACKED.":E=0:GOTO36
610 IFC$="DIA"ANDD>0THENW$=T$(1):D=0:GOTO632
612 IFC$="VAC"THENV=0:PRINT"IT SPILLS ACROSS THE GROUND":GOTO36
614 IFC$="BOX"ANDBX=1THENW$="BOX":BX=2:GOTO632
616 IFC$="CAS"ANDBO=1THENW$="CASKET":BO=2:GOTO632
618 IFC$="SCR"ANDSC=1THENW$=T$(4):SC=2:GOTO632
620 IFC$="GOL"ANDG>0THENW$=T$(7):G=0:GOTO632
622 IFC$="KEY"ANDKY=1THENW$="KEY":KY=2:Z=Z-1:GOTO632
624 IFC$="CRO"ANDCR=1THENW$=T$(5):CR=2:Z=Z-1:GOTO632
626 IFC$="GRA"ORC$="HOL"ANDGR=1THENPRINT"YOU DON'T WANT TO DROP THE ";T$(6):GOTO36
628 IFC$="BAG"ANDBG=1THENW$="BAG":BG=0:GOTO632
630 PRINT"YOU DON'T HAVE IT":GOTO36
632 PRINT"YOU HAVE DROPPED THE "W$
634 IFRO=5THENPRINT"INTO THE VOLCANO.IT ERUPTED BUT ALL THAT CAME OUT WAS A DIAMOND":A(5,6)=1:GOTO36
636 IFRO<8THENPRINT" AND IT FALLS INTO A HOLE":GOTO24
638 IFRO>37THENPRINT" AND A ROBOT GRABS IT AND DISAPPEARS":GOTO24
640 PRINT"A STRANGE MAN CRIES OUT IN GLEE,GRABS IT AND RUNS OFF":GOTO24
642 SW=0:IFRO=5THEN632
643 IFA(RO,6)=0THENA(RO,6)=2:PRINT"YOU DROPPED THE SWORD":GOTO24
644 GOTO632
646 K=100
648 CLS:H=H-1:IFH=0THENPRINT@225,"YOU HAVE DIED OF HUNGER":GOTO348
650 IFK=99THENRO=A(RO,4):GOTO26
652 M$="YOU ARE "+KL$:GOSUB1
654 IFK=303ORK=6THENPRINT"YOU SEE A ";ML$
656 IFK=506ANDRO=14ANDSC=1THEN662
658 IFK=506THENM$="YOU SEE "+TL$:GOSUB1
660 IFK=107ORK=204ORK=-103THENM$=DL$:GOSUB1
662 IFH<7THENPRINT"DR.T GROANS'I FEEL HUNGRY'"
664 GOTO668
666 GOSUB8
668 INPUT"WHAT WILL YOU DO";A$:B$=LEFT$(A$,4):C$=LEFT$(A$,3):D$=MID$(A$,5,3)
670 IFB$="GO N"THENK=K-100:GOTO648
672 IFB$="GO S"THENK=K+100:GOTO648
674 IFB$="GO E"THENK=K+1:GOTO648
676 IFB$="GO W"THENK=K-1:GOTO648
678 IFC$="EAT"THEN698
680 IFA$="HELP"THENPRINT"DON'T ASK ME ASK DR.T":GOTO666
682 IFA$="ASK DR.T"THENPRINT"EVERY ACTION HAS AN EQUAL AND   OPPOSITE REACTION":GOTO666
684 IFC$="GET"THEN710
686 IFA$="LOOK"THENPRINT"IT LOOKS THE SAME IN EVERY DIRECTION":GOTO666
688 IFC$="INV"THENGOSUB736:GOTO666
690 IFC$="FIG"ORC$="OPE"ORC$="GO "THENGOSUB9:GOTO666
692 IFC$="QUI"THEN348
694 IFB$="DROP"THENPRINT"DON'T "A$" YOU'LL NEED IT LATER":GOTO666
696 GOSUB86:GOTO668
698 IFK=303ORK=6ORBR>0ORB=1THEN700
699 GOTO706
700 IFD$="CAC"ORD$="FOO"THENPRINT"UGH..";:GOTO708
702 IFD$="BER"ANDB=1THENPRINT"SOUR";:GOTO708
704 IFD$="BRE"ORD$="CRU"THENPRINT"A BIT MOLDY";:GOTO708
706 PRINT"YUCK":GOTO666
708 PRINT" BUT YOU FEEL BETTER":H=H+10:GOTO666
710 IFD$="SCR"ANDK=506THENSC=1:W$=T$(4):GOTO722
712 IF(D$="DIA"ORD$="STI")ANDK=506THEND=D+1:W$=T$(1):GOTO722
714 IFD$="GOL"ANDK=506THENW$="GOLD":G=G+1:GOTO722
716 IFD$="CHE"THENPRINT"IT IS TOO HEAVY":GOTO666
718 IFD$="BRE"THENBR=BR+1:W$="BREAD":GOTO722
720 GOSUB9:GOTO668
722 M$="YOU HAVE THE "+W$:GOSUB1:GOTO666
724 IFC$="LUL"ANDRO=15THENPRINT"EVERYONE IS LULLED TO SLEEP AND YOU MAY GO YOUR WAY":SL=1:GOTO36
726 IFC$="HYM"ANDRO=33ANDA(33,6)=25THENPRINT"DRACULA FLEES BACK TO HIS HOUSE":A(33,6)=0:GOTO36
728 PRINT"LA-LA-LA":SOUND233,5:GOTO36
730 IFC$="CRO"ANDCR=1ANDRO=33THEN4140
732 GOTO7
734 Y=2:GOSUB8:PRINT"IN GRATITUDE THE MAYOR OF LONDONGIVES YOU THE KEY TO THE CITY   AND A REWARD OF 500GRAND"
735 Z=Z+500:A(33,6)=0:GOTO24
736 M$="YOU HAVE HOPE, ":IFSW=1THENM$=M$+"A SWORD, "
740 IFB=1THENM$=M$+"SOME BERRIES, "
742 IFBR>0THENM$=M$+"SOME BREAD, "
744 IFF=1THENM$=M$+"TUBES OF FOOD, "
746 IFE=1THENM$=M$+"A DINOSAUR EGG, "
748 IFG>0THENM$=M$+"GOLD WORTH $"+STR$(G*10000)+", "
750 IFD>0THENM$=M$+STR$(D)+"DIAMOND(S), "
752 IFV=1THENM$=M$+T$(8)", "
754 IFKY=1THENM$=M$+"A GOLD KEY, "
756 IFBX=1THENM$=M$+"A BOX, "
758 IFCR=1THENM$=M$+"A SILVER CROSS, "
760 IFBO=1THENM$=M$+"A JEWELLED CASKET, "
762 IFGR=1THENM$=M$+T$(6)+", "
764 IFSC=1THENM$=M$+"A SACRED SCROLL, "
766 IFZ>0THENM$=M$+"TREASURE WORTH $"+STR$(Z*1000)+", "
768 M$=M$+"AND MUCH COURAGE.":GOSUB1:RETURN
770 CLS:M$="YOU ARE IN THE TIME MACHINE. IT FLASHES THE TIMES ZONES:":GOSUB1:PRINT:O=0
772 IFRO=1THEN778
774 IFRND(3)=1THENPRINT"AGE OF dINOSAURS":O=1
776 IFRO=8THEN782
778 IFRND(3)=1THENPRINT"ANCIENT rOME":O=1
780 IFRO=16THEN786
782 IFRND(3)=1THENPRINT"KING aRTHUR'S COURT":O=1
784 IFRO=25THEN790
786 IFRND(3)=1THENPRINT"eLDORADO":O=1
788 IFRO=29THEN794
790 IFRND(3)=1THENPRINT"sHERLOCK HOLMES' LONDON":O=1
792 IFRO=34THEN798
794 IFRND(3)=1THENPRINT"ROARING tWENTIES":O=1
796 IFRO=38THEN802
798 IFRND(3)=1THENPRINT"THE fUTURE":O=1
800 IFRO=0THEN804
802 IFRND(5)=1THENPRINT"RETURN TO THE pRESENT"
804 IFO<1THEN772
806 GOSUB900:IFA$="D"THENRO=1:DO=1
810 IFA$="R"THENRO=8:R=1
812 IFA$="A"THENRO=16:AR=1
814 IFA$="E"THENRO=25:EL=1
816 IFA$="S"THENRO=29:S=1:BG=0
818 IFA$="T"THENRO=34:RT=1
820 IFA$="F"THENRO=38:FU=1
822 IFA$="P"THEN828
824 IFA$="X"THEN770
826 P=P+1:IFRO=0THEN770
827 GOTO26
828 M=Z+10*G+100*D+500*V+1000*GR:ZN=DO+R+AR+EL+S+RT+FU
830 IFSC=1THENM=M+1000
832 CLS:PRINT:M$="CONGRATULATIONS! YOU HAVE RETURNED ALIVE FROM YOUR JOURNEY BEYOND TIME HAVING MADE"+STR$(P):GOSUB1
833 M$="TIME JUMPS & VISITED"+STR$(ZN)+" TIME ZONES":GOSUB1
834 M$="YOU BROUGHT BACK ":IFE=1THENM$=M$+"A DINOSAUR EGG AND "
836 IFM=0THENM$=M$+"NO TREASURE":GOTO840
838 M$=M$+"TREASURE WORTH"+STR$(M*1000)
840 GOSUB1:M$="":IFM>0THENM$=M$+"YOU SELL EVERYTHING":GOTO842
841 M$=M$+"YOU HAVE  NOTHING, "
842 IFE=1THENM$=M$+"BUT THE DINO EGG "
844 M$=M$+"& ARE LIVING IN "
846 IFM>3420THENM$=M$+"PALATIAL SPLENDOR IN SOUTHERN CALIFORNIA":GOTO854
848 IFM>999THENM$=M$+"A CONDO IN MIAMI":GOTO854
850 IFM>50THENM$=M$+"A COTTAGE IN PROSPECT, KY":GOTO854
852 M$=M$+"A TENEMENT IN NYC"
854 IFE=1THENM$=M$+" WITH YOUR TWO-TON FRIEND"
856 GOSUB1:GOTO348
900 A$=INKEY$:ON-(A$="")GOTO900:RETURN
4100 IFRO=15THENPRINT"WITH AN IRON BAR ACROSS IT":GOTO36
4110 PRINT"WITH A BRASS HANDLE":GOTO36
4120 G=G+1:PRINT"YOU SCOOPED UP SOME OF THE GOLD-THERES MORE LEFT BUT NO TIME TO GET IT ALL NOW":GD=1:GOTO24
4140 PRINT"HE SHRIEKS AND FLEES FOREVER FORTHAT WAS A SACRED CROSS BORNE BYSIR GALAHAD OF THE ROUND TABLE":GOTO734
4200 IFO=2THENA(42,6)=27:RETURN
4210 IFO=3THENA(43,6)=27:RETURN
4220 RETURN
