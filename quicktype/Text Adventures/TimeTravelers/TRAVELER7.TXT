0 CLS:CLEAR350:DIMM$,A(45,7),T$(8),E$(7):GOTO10
1 C1=1:CC=32:FORCC=CCTOC1STEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,C1,CC-C1):C1=CC+1:CC=C1+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 M$=M$+".":GOSUB1:RETURN
4 GOSUB3:GOTO36
5 GOSUB3:GOTO24
6 PRINT:INPUT"WHAT WILL YOU DO";A$:ON-(A$="")GOTO6:RETURN
7 M$="NOTHING HAPPENS":GOTO4
8 FORCC=1TO3500:NEXT:RETURN
9 M$="YOU CAN'T "+A$:GOTO3
10 CLS:M$="YOUR STRANGE FRIEND DR. TEMPUS HAS CALLED YOU TO HIS HOME. 'I HAVE CREATED A":GOSUB1
11 PRINT:PRINTTAB(9)"*TIME MACHINE*":PRINT:M$="I CALL IT TM & IT SEEMS TO BE TEMPERMENTAL-- ONLY GOES TO SEVEN TIME ZONES, "
12 M$=M$+"BUT JUST THINK, WE COULD RETURN WITH A DINOSAUR EGG OR THE HOLY GRAIL... AND WHO KNOWS WHAT ELSE!":GOSUB1
13 PRINTTAB(11)"LET'S GO!'":FORC1=1TO45:FORCC=1TO7:READA(C1,CC):NEXT:NEXT:FORCC=1TO8:READT$(CC):NEXT
14 FORCC=1TO7:READE$(CC):NEXT:PRINT@485,"HIT ANY KEY WHEN READY";:GOSUB900:H=30:GOTO770
24 H=H-1:IFH<1THEN645
28 PRINT:PRINT"YOU ARE--":GOSUB88:PRINT"EXITS: ";:FORCC=1TO4:IFA(X,CC)THENPRINTMID$("NSEW",CC,1)" ";
29 NEXT:PRINT
30 IFH<7THENGOSUB665
32 IFA(X,6)>20THENU=A(X,6)-20:GOSUB8:PRINT:M$="HORRORS! "+E$(U):GOSUB3
34 IFA(X,6)=2ANDSW=0THENM$="ON THE GROUND LIES A SWORD":GOSUB3
36 GOSUB6:B$=LEFT$(A$,3):N=1:M=LEN(A$):IFA$="HELP"THENM$="DON'T ASK ME, ASK DR.T":GOTO4
42 ON-(B$="LOO")GOTO524:ON-(B$="FIG")GOTO264:ON-(B$="QUI")GOTO348
48 IFB$="INV"THENGOSUB736:GOTO36
50 IFA$="CAROUSE"THENPRINT"YAHOO!":GOTO36
54 IFMID$(A$,N,1)=" "THENC$=MID$(A$,N+1,3):ON-(LEFT$(C$,1)<>" ")GOTO60:M$="TWO WORDS PLEASE":GOTO4
56 IFN<MTHENN=N+1:GOTO54
60 C1=0:FORCC=1TO15:IFB$=MID$("  GO WALRUNSINGETDROEATASKUNLDRIWHIKNOOPESHOLOO",CC*3,3)THENC1=CC
61 NEXT:ONC1GOTO382,382,382,724,546,600,428,448,496,510,520,518,478,730,536:GOSUB86:GOTO36
86 GOSUB87:GOSUB3:RETURN
87 M$="I DON'T UNDERSTAND "+A$:RETURN
88 IFX<21THENONXGOSUB92,94,112,114,118,120,126,128,130,132,144,150,156,158,160,164,166,168,170,172:RETURN
89 ONX-20GOSUB178,180,182,184,188,190,192,198,200,202,206,212,216,218,222,226,228,230,236,238,240,250,252,254,256:RETURN
92 M$="BESIDE TM ON A GRASSY ISLAND":GOTO3
94 M$="ON A GRASSY ISLAND. BEFORE YOU LOOMS A T-REX":GOSUB1:TI=0
98 TI=TI+1:IFTI>3600THEN124
99 GOSUB6:B$=LEFT$(A$,5):IFA$="RUN"THENPRINT"WHERE?":GOTO98
100 IFB$="FIGHT"THENA$="FIGHT REX":GOSUB9:GOTO98
102 IFB$="RUN S"THENX=1:GOTO24
104 IFB$="RUN E"THENX=3:GOTO24
106 IFLEFT$(A$,2)="GO"THENGOSUB901:GOTO98
108 IFB$="RUN W"ORB$="RUN N"THENX=7:GOTO24
110 M$="WITH A DINOSAUR CHASING YOU, YOU WANT TO "+A$+"?":GOSUB1:GOTO98
112 M$="ON A GRASSY ISLAND":GOTO3
114 M$="ON A GRASSY ISLAND. YOU SEE A BUSH WITH BERRIES AND A MAMA TRICERATOPS":IFE=0THENM$=M$+" WITH AN EGG"
116 GOTO3
118 M$="ON A VOLCANO":GOTO3
120 PRINT"IN A DARK CAVE"
122 SOUND1,20:M$="FROM BELOW EMERGES A FOUL DINOSAUR!":GOSUB1
124 SOUND1,20:M$="HE GOT YOU! YOU END YOUR DAYS AS A SNACK IN THE MESOZOIC AGE":GOSUB3:GOTO348
126 PRINT"SLOSHING IN THE SWAMP":GOTO122
128 M$="ON PALATINE HILL NEXT TO TM. A TEMPLE IS WEST":GOTO3
130 A(10,6)=0:M$="IN THE ROMAN FORUM":GOTO3
132 M$="IN THE COLOSSEUM. IT IS FULL OF PEOPLE"
133 IFA(10,6)=0THENM$=M$+". YOU ARE CAUGHT BY A GUARD AND PRESENTED TO CAESAR WHO CRIES:":GOSUB1:GOTO136
134 M$=M$+". COINS RAIN DOWN UPON YOUR HEAD":GOTO3
136 ONRND(4)GOTO138,140:X=15:SL=0:M$="'LET'S PARTY! A CHEERING CROWD LEEDS YOU TO THE PALACE":GOTO5
138 M$="'LET THEM FIGHT THE GLADIATORS'":GOSUB1:GOTO358
140 PRINT"'THROW THEM TO THE LIONS'":GOTO370
144 M$="LOST IN A MAZE OF STREETS. A CROWD OF RUFFIANS APPEAR AND FEARFUL OF YOUR STRANGE ATTIRE, STONE YOU!":GOSUB1
148 M$="THEY GOT YOU! YOU ARE LOST IN THE MISTS OF TIME":GOSUB3:GOTO348
150 M$="IN THE TEMPLE OF THE VESTAL VIRGINS. YOU SEE AN ALTAR":IFBX=0THENM$=M$+" ON WHICH RESTS A BOX"
154 GOTO3
156 M$="ON THE APPIAN WAY":GOTO3
158 KL$="IN A MAZE OF CATACOMBS":ML$="MOLDY LOAF OF BREAD":DL$="THE SOUND OF HYMNS SEEMS TO ECHO AROUND YOU"
159 TL$="A SACRED SCROLL":GOTO646
160 M$="IN CAESAR'S PALACE":IFSL=0THENM$=M$+". EVERYONE IS CAROUSING":GOTO162
161 M$=M$+". EVERYONE IS ASLEEP"
162 M$=M$+". THE DOOR NORTH HAS BEEN BARRED":GOTO3
164 A(20,7)=2:M$="BESIDE TM IN A CLEARING IN DARK WOODS. A LIGHT GLOWS TO THE NORTH":GOTO3
166 M$="AT CAMELOT":GOTO3
168 M$="IN A N/E HALL":GOTO3
170 M$="IN A S/E HALL":GOTO3
172 M$="IN A ROOM WITH A LARGE ROUND TABLE":IFBO=0THENM$=M$+". YOU SEE A SMALL JEWELLED CASKET"
174 IFA(20,7)=2THENM$=M$+". ON THE TABLE IS A LOAF OF BREAD"
176 GOTO3
178 M$="ON N/S PATH THROUGH THE WOODS":GOTO3
180 M$="ON N/S PATH THROUGH DEEP WOODS. A TEMPLE IS NORTH":GOTO3
182 M$="IN DARK FOREST. BANDITS SURROUND YOU":GOSUB3:GOTO148
184 M$="IN A BEAUTIFUL TEMPLE":IFGR=0THENM$=M$+" ON THE ALTAR STANDS THE HOLY GRAIL"
186 GOTO3
188 A(27,7)=2:M$="BESIDE TM IN A DESERT":GOTO3
190 A(27,6)=21:GD=0:M$="IN A DESERT":GOTO3
192 M$="IN A SPANISH SETTLEMENT":IFA(27,7)=2THENM$=M$+". THERE'S A TABLE WITH BREAD ON IT"
194 IFGD=0THENM$=M$+", AND IN A CORNER-- A PILE OF GOLD"
196 GOTO3
198 KL$="SURROUNDED BY DESERT":ML$="CACTUS":DL$="NOTHING BUT TUMBLEWEED FOR MILES":TL$="A TREASURE CHEST OF GOLD":GOTO646
200 M$="AT 221B BAKER ST. SHERLOCK HOLMES STUDIES YOU & TM LACONICALLY AND REMARKS THAT YOU CAN GO ANY DIRECTION "
201 M$=M$+"IN THE CAB AT THE DOOR":GOTO3
202 M$="IN REGENTS PARK. A VENDOR SELLS BREAD AND WINE":IFBG=0THENM$=M$+". ON A BENCH YOU SEE A BAG"
204 A(30,7)=2:M$=M$+". THE CAB WAITS":GOTO3
206 M$="AT PROFESSOR MORIARITY'S HOUSE":IFA(31,6)=0THENM$=M$+". IT IS CLOSED UP TIGHT"
210 GOTO3
212 M$="AT SCOTLAND YARD":IFA(33,6)<1THEN3
213 M$=M$+" A SERGEANT TELLS YOU THERE'S A STRANGE ILLNESS IN THE CITY. HE CONFIDES HE THINKS A VAMPIRE IS RESPONSIBLE":GOTO3
216 M$="AT A LARGE HOUSE":GOTO3
218 A(35,7)=2:M$="ON AN AVENUE NEAR A POLICE STATION. TM WAITS FOR YOU"
220 M$=M$+". A MAN SIDLES UP TO YOU AND MUTTERS 'LOOKING FOR HOOCH?' AND NODS AT A NORTH DOOR":GOTO3
222 M$="IN A DARK SPEAKEASY"
223 IFA(X,7)=2THENM$=M$+". A WAITER BRINGS BREAD AND FOUL SMELLING BREW"
224 GOTO3
226 M$="IN A POLICE STATION. SERGEANT O'HARA GIVES YOU A LECTURE ON THE EVILS OF DEMON RUM":GOTO3
228 KL$="IN A MAZE OF STREETS":ML$="CRUST OF BREAD ON THE STREET":DL$="RAUCOUS NOISE EMINATES FROM A DINGY DOOR"
229 TL$="A DIAMOND STICKPIN":GOTO646
230 M$="IN A LARGE AUDITORIUM WITH TM. EAST IS A LEAD DOOR WITH A WINDOW SHOWING A DESOLATE LAND":GOSUB1
232 O=RND(3):IFO=1THENA(39,6)=27
233 IFO=2THENA(42,6)=27
234 IFO=3THENA(43,6)=27
235 A(41,6)=27:RETURN
236 M$="IN AN L-SHAPED ROOM. NORTH IS":GOSUB249:GOTO3
238 M$="IN LONG E/W HALL":GOTO3
240 M$="ON A ROCKET SHIP":IFA(41,7)>0THENM$=M$+". AGAINST THE WALL ARE FOOD TUBES"
242 IFA(41,6)=27THENM$=M$+". THE ROCKET SUDDENLY SHAKES TAKES OFF..."
244 IFA(41,6)=1THENM$=M$+". THROUGH THE WINDOWS YOU CAN SEE STARS"
246 IFA(41,6)=0THENM$=M$+". THE ROCKET HAS JUST RETURNED TO EARTH"
248 GOTO3
249 M$=M$+" A DOOR LABELLED danger radiation":RETURN
250 M$="IN A LAB. TO THE WEST IS":GOSUB249:GOTO3
252 M$="IN A HALL":GOTO3
254 M$="IN A radiation zone. YOU FEEL YOURSELVES BECOMING SICK.":GOSUB1:GOSUB8
255 M$="YOU SINK TO THE GROUND...":GOSUB8:GOTO348
256 M$="IN THE CAB. THE CABBIE SAYS YOU NEED A TOUR OF LONDON":GOSUB1:GOSUB8:IFRND(2)=1THENPRINT"HERE'S HYDE PARK":GOTO258
257 PRINT"HERE'S BUCKINGHAM PALACE"
258 GOSUB8:IFRND(2)=1THENPRINT"THERE'S THE TOWER OF LONDON":GOTO260
259 M$="THERE'S BEDLAM... ER... BETHLEHEM ASYLUM":GOSUB1
260 GOSUB8:IFRND(2)=1THENPRINT"AND THE BRITISH MUSEUM":GOTO262
261 PRINT"AND MADAME TUSSAUD'S"
262 GOSUB8:X=29:M$="AND HE BRINGS YOU BACK TO BAKER STREET":GOTO5
264 IFA(X,6)<15THENM$="THERE'S NOTHING TO FIGHT HERE":GOTO4
266 GOSUB8:IFSW=1THENJ=RND(7):GOTO270
268 J=RND(4)
270 IFJ=1THENPRINT"HE GOT YOU":GOTO286
272 IFJ=2THENPRINT"HE STRIKES A TELLING BLOW":GOTO266
274 IFJ=3THENPRINT"YOU HAVE WOUNDED HIM":GOTO266
276 IFX=33THENA(33,6)=0:M$="DRACULA TURNED INTO A BAT AND FLEW AWAY":GOTO5
278 IFX=31THENA(31,6)=0:M$="YOU KILLED THE ARCH CRIMINAL MORIARITY AND MAKE THE WORLD SAFER FOR GOOD MEN":GOTO5
280 IFX=41THENA(X,6)=1:M$="YOU GOT HIM":GOTO5
282 IFX=10THENA(10,6)=3:M$="YOU HAVE BESTED THEM. MAGNANIMOUSLY YOU SPARE THEM":GOTO5
284 A(X,6)=0:M$="YOU GOT HIM":GOTO5
286 IFX=33THENPRINT"NOW YOU ARE A VAMPIRE TOO.":GOTO348
288 IFX<>22THEN348
289 M$="THE SORCERER HAS TURNED YOU BOTH INTO FROGS...":GOSUB1:GOSUB8:PRINT"I HOPE YOU LIKE FLIES.":GOTO348
300 DATA2,7,4,7,1,,,7,1,3,7,,,,7,4,5,2,,,,3,7,,1,,,1,7,6,7,3,,,,,,,,,,,,,,,,,,9,13,11,12,1,2,,11,8,10,11,,,
308 DATA0,,,9,,,,,,,,,,,11,,8,,,3,,8,,14,,,,,14,14,14,13,,4,,,13,,8,,,2,17,23,23,23,1,,2,,16,,18,,2,
316 DATA19,,17,,,,,,18,20,,,,,21,,,19,,5,2,22,20,23,23,,22,,24,21,23,23,,23,,,,,,,,,,22,,,,6,,26,,28,,1,,,,25,27,,,,
324 DATA0,,,26,,21,2,28,28,28,25,,,,30,31,32,33,1,,,45,29,32,33,,3,2,29,45,32,33,,24,,30,31,45,29,,,
330 DATA30,31,29,45,,25,,,,37,36,1,26,,0,34,,,,1,2,,,34,,,,,37,37,37,34,,,,39,43,44,39,1,,,44,38,38,40,,,
338 DATA0,,39,41,,,,,42,40,,,27,2,41,,43,44,,9,,38,,,42,,,,,,,,,,,30,30,30,30,,,,DIAMOND,SWORD,COINS,SCROLL,CROSS
339 DATAHOLY GRAIL,GOLD,VACCINE,A CONQUISTADOR,A DRAGON,A SORCERER,PROF MORIARITY,DRACULA,A GANGSTER,A ROBOT
348 INPUT"ANOTHER GAME? (Y/N)";A$:IFA$="Y"THENRUN
350 END
358 PRINT"THE GLADIATORS APPEAR"
360 GOSUB6:IFA$="STAB"ORA$="FIGHT"ORA$="HIT"THEN266
364 GOSUB9:GOTO360
370 PRINT"THE LIONS APPEAR":T=RND(3):IFSW=1THEN376
374 T=RND(9)
376 GOSUB8:IFT=2THENA(10,6)=3:M$="YOU HAVE SLAIN THE LIONS":GOTO5
380 GOTO148
382 IFA(X,6)>19THENM$="YOUR FOE WILL NOT LET YOU GO":GOTO5
384 IFC$="HOU"ANDX=33THEN422
386 IFY<1THENA(33,6)=25
388 IFX=15ANDSL=0THENM$="CAESAR SHOUTS-- YOU CAN'T GO. EAT, DRINK, SING & CAROUSE":GOTO5
390 IFX=4ANDE=1THEN590
392 IFX=41ANDA(X,6)>0THENM$="YOU DON'T WANT TO LEAVE THE ROCKET. IT'S IN SPACE":GOTO5
394 IFC$="TM"ANDA(X,5)=0THENM$="IT ISN'T HERE":GOTO4
396 IFC$="TM"THEN770
398 D$=LEFT$(C$,1):FORCC=1TO4:IFD$=MID$("NSEW",CC,1)THENIFA(X,CC)=0THENCC=4:NEXT:GOTO422
400 NEXT:IFD$="N"ANDX=22THEN424
410 FORCC=1TO4:IFD$=MID$("NSEW",CC,1)THENX=A(X,CC):CC=4:NEXT:GOTO24
411 NEXT:IFC$="TEM"ANDX=8THENX=12:GOTO24
420 IFC$="TEM"ANDX=22THEN424
422 GOSUB9:GOTO36
424 IFL=0THENPRINT"THE DOOR IS LOCKED":GOTO36
426 X=24:GOTO24
428 IFC$="BER"AND(A(X,7)=1ORB=1)THEN444
432 IFC$="BRE"ANDA(X,7)=2THENM$="RATHER TASTY AND VERY FILLING":GOTO446
434 IFC$="BRE"ANDBR>0THENM$="RATHER TASTY":BR=BR-1:GOTO446
436 IFC$="BRE"THENM$="YOU DON'T HAVE ANY":GOTO4
440 IFC$="FOO"AND(A(X,7)>0ORF=1)THEN444
442 PRINT"YUCK":GOTO36
444 M$="MMM, NOT BAD"
446 H=H+15:GOTO4
448 IFC$="DR."THEN456
450 IFX=34ANDC$="FOR"ORC$="JOE"THEN454
452 PRINT"WHO?":GOTO36
454 X=35:M$="THE DOOR OPENS ON A DINGY BAR":GOTO5
456 IFX=15THENFORC1=1TO5:SOUND200,1:SOUNDC1,1:NEXT:GOTO36
458 IFX=22THENM$="ONE MUST GO WHERE ALL ROADS LEAD":GOTO4
460 IFX=5THENM$="DIAMONDS'RE A DROP IN THE BUCKET":GOTO4
462 IFX=34THENSOUND1,5:GOTO36
464 IFX=33ANDY<1THENM$="ALL MUST CROSS HIS PATH SOMETIME":GOTO4
466 IFX=41ORX=35ORX=43THENM$="SEEK AND YE SHALL FIND":GOTO4
468 ONRND(3)GOTO470,472:IFX>7THENM$="HALF A LOAF IS BETTER THAN NONE":GOTO4
469 M$="BEWARE THE DARK, DAMP OR MEAN":GOTO4
470 M$="COMMON SENSE IS NOT SO COMMON":GOTO4
472 M$="NOBODY CAN GIVE YOU WISER ADVICE THAN YOURSELF":GOTO4
478 IFC$="BOX"THEN490
480 IFC$="CAS"THEN492
482 IFC$="DOO"ANDX=22ANDL=0THENM$="IT'S LOCKED":GOTO4
484 IFC$="DOO"ANDX=22ANDL=1THENM$="IT'S OPEN":GOTO4
486 IFC$="BAG"ANDX=30THENM$="COINS FALL OUT":GOTO4
488 M$="IT CANNOT BE OPENED":GOTO4
490 GOSUB87:IFX=12ORBX=1THENM$="THE BOX IS OPEN":IFKY=0THENM$=M$+" AND INSIDE YOU SEE A KEY"
491 GOTO4
492 GOSUB87:IFX=20ORBO=1THENM$="THE CASKET IS OPEN":IFCR=0THENM$=M$+" AND INSIDE YOU SEE A SMALL CROSS"
494 GOTO4
496 IFKY<>1THEN504
498 IFX=22ANDL=1THEN508
499 INPUT"WITH WHAT";A$:IFX=22ANDA$="KEY"THEN506
502 IFX=22ANDA$="WITH KEY"THEN506
504 M$="YOU DON'T HAVE A KEY FOR THAT":GOTO4
506 SOUND150,1:L=1
508 M$="THE DOOR IS UNLOCKED":GOTO4
510 IFC$="WIN"THENM$="GAGGG, YOU ARE ABOUT TO BE SICK":H=H-6:GOTO4
512 IFC$="RUM"ORC$="BRE"THENM$="BARF, WHAT FOUL ROT THEY DRINK":H=H-5:GOTO4
514 IFC$="WAT"ANDX>7THENM$="MMM, MOST REFRESHING":H=H+5:GOTO4
516 PRINT"YOU WANT TO DRINK WHAT?":GOTO36
518 IFX=34ANDC$="THR"THEN454
520 IFX=34ANDC$="LOW"ORC$="JOE"THEN454
522 GOTO7
524 IFX=35ANDA(35,6)=1THENM$="HEY! THAT FLAPPER JUST DROPPED A DIAMOND ON OUR TABLE":GOTO4
526 IFX=42THENM$="ALONG THE WALLS ARE SHELVES FILLED WITH VARIOUS CONTAINERS":GOTO5
528 IFX=41ANDA(41,6)>0THENM$="THE ROBOT HAS A DIAMOND":GOTO4
530 IFX=3THENM$="PTERADACTYLS GRACE THE SKY":GOTO5
532 IFX=9THENM$="A GROUP OF SENATORS CONVERSE IN THE CORNER":GOTO5
534 GOTO24
536 IFX=42THENM$="DR.T EXCLAIMS 'WE'RE RICH-- A VACCINE FOR THE COMMON COLD'":GOTO4
537 IFNOT(C$="DOO"ANDX<38)THEN541
538 M$="IT'S A HEAVY WOODEN DOOR":IFX=15THENM$=M$+" WITH AN IRON BAR ACROSS IT":GOTO4
539 M$=M$+" WITH A BRASS HANDLE":GOTO4
541 IFC$="VOL"ANDX<8THENM$="IT EMITS WHIFFS OF SMOKE":GOTO4
542 IFC$="TEM"THENM$="BEAUTIFUL WHITE MARBLE GLEAMS IN THE SUNLIGHT":GOTO4
544 M$="YOU SEE NOTHING SPECIAL":GOTO4
546 IFC$="BER"ANDX=4THENB=1:W$="BERRIES":GOTO588
548 IFC$="BRE"ANDA(X,7)=2THENBR=BR+1:A(X,7)=0:W$="BREAD":GOTO588
550 IFX=41AND(C$="TUB"ORC$="FOO")THENW$="FOOD":F=1:A(X,7)=0:GOTO588
552 IFC$="DIA"ANDA(X,6)=1THEND=D+1:W$=T$(1):A(X,6)=0:GOTO588
554 IFC$="EGG"ANDX=4THENE=1:M$="YOU GOT THE EGG BUT MAMA IS VERY UPSET":GOTO4
556 IFC$="SWO"ANDA(X,6)=2THENSW=1:W$=T$(2):GOTO588
558 IFC$="BOX"ANDX=12THENW$="BOX":BX=1:GOTO588
560 IFC$="CAS"ANDX=20THENBO=1:W$="CASKET":GOTO588
562 IFC$="KEY"AND(X=12ORBX=1)THENKY=1:Z=Z+1:W$="KEY":GOTO588
566 IFC$="VAC"ANDX=42THENV=1:W$=T$(8):GOTO588
568 IFC$="BAG"ANDX=30ANDA(30,6)=3THENM$="COINS FELL OUT":BG=1:GOTO4
570 IFC$="BAG"THENM$="IT IS EMPTY":GOTO4
572 IFC$="COI"ANDA(X,6)=3THENZ=Z+1:W$=T$(3):GOTO588
574 IFC$="GOL"ANDX=27ANDGD=0THENG=G+1:GD=1:M$="YOU SCOOP SOME UP-- THERE'S MORE LEFT BUT NO TIME TO GET IT":GOTO5
576 IFC$="GOL"ANDX=27ANDGD=1THENM$="THERE'S NO TIME. MORE MEN ARE COMING":GOTO4
578 IFC$="CRO"AND(X=20ORBO=1)THENCR=1:Z=Z+1:W$=T$(5):GOTO588
582 IFC$="WIN"THENW$="WINE":GOTO588
584 IFC$="GRA"ORC$="HOL"ANDX=24THENGR=1:W$=T$(6):GOTO588
586 M$="YOU CANNOT "+A$:GOTO4
588 GOSUB721:GOTO24
590 IFB$="RUN"THEN398
592 O=RND(3)
594 IFO=1THENGOSUB901:GOTO36
596 IFO=2THENM$="MAMA IS ALMOST ON YOU. YOU DROP THE EGG":E=0:GOTO4
598 IFO=3THENM$="SHE'S GAINING":GOTO4
600 IFX=5ANDC$="BER"ANDB=1THENW$="BERRIES":B=0:GOTO632
602 IFX=5ANDC$="EGG"ANDE=1THENW$="EGG":E=0:GOTO632
604 IFC$="BER"ANDB=1THENPRINT"SQUISH":B=0:GOTO36
606 IFC$="SWO"ANDSW=1THEN642
608 IFC$="EGG"ANDE=1THENM$="YOU DROP THE EGG AND IT CRACKS":E=0:GOTO4
610 IFC$="DIA"ANDD>0THENW$=T$(1):D=0:GOTO632
612 IFC$="VAC"THENV=0:M$="IT SPILLS ACROSS THE GROUND":GOTO4
614 IFC$="BOX"ANDBX=1THENW$="BOX":BX=2:GOTO632
616 IFC$="CAS"ANDBO=1THENW$="CASKET":BO=2:GOTO632
618 IFC$="SCR"ANDSC=1THENW$=T$(4):SC=2:GOTO632
620 IFC$="GOL"ANDG>0THENW$=T$(7):G=0:GOTO632
622 IFC$="KEY"ANDKY=1THENW$="KEY":KY=2:Z=Z-1:GOTO632
624 IFC$="CRO"ANDCR=1THENW$=T$(5):CR=2:Z=Z-1:GOTO632
626 IFC$="GRA"ORC$="HOL"ANDGR=1THENA$=T$(6):GOSUB631:GOTO36
628 IFC$="BAG"ANDBG=1THENW$="BAG":BG=0:GOTO632
630 PRINT"YOU DON'T HAVE IT":GOTO36
631 M$="YOU DON'T WANT TO DROP THE "+A$:GOTO3
632 M$="YOU HAVE DROPPED THE "+W$
634 IFX=5THENA(5,6)=1:M$=M$+" INTO THE VOLCANO. IT ERUPTS BUT ALL THAT COMES OUT IS A DIAMOND":GOTO4
636 IFX<8THENM$=M$+" AND IT FALLS INTO A HOLE":GOTO5
638 IFX>37THENM$=M$+" AND A ROBOT GRABS IT AND DISAPPEARS":GOTO5
640 M$=M$+"A STRANGE MAN CRIES OUT IN GLEE, GRABS IT AND RUNS OFF":GOTO5
642 SW=0:IFX=5THEN632
643 IFA(X,6)=0THENA(X,6)=2:M$="YOU DROPPED THE SWORD":GOTO5
644 GOTO632
645 PRINT"HUNGER HAS TAKEN YOU BOTH":GOTO348
646 K=100
648 H=H-1:IFH=0THEN645
650 IFK=99THENX=A(X,4):GOTO24
652 M$="YOU ARE "+KL$:GOSUB3
654 IFK=303ORK=6THENM$="YOU SEE A "+ML$:GOSUB3
656 IFK=506ANDX=14ANDSC=1THEN662
658 IFK=506THENM$="YOU SEE "+TL$:GOSUB3
660 IFK=107ORK=204ORK=-103THENM$=DL$:GOSUB3
662 IFH<7THENGOSUB665
664 GOTO668
665 PRINT"DR.T GROANS 'I FEEL HUNGRY'":RETURN
668 PRINT:INPUT"WHAT WILL YOU DO";A$:B$=LEFT$(A$,4):C$=LEFT$(A$,3):D$=MID$(A$,5,3)
670 IFB$="GO N"THENK=K-100:GOTO648
672 IFB$="GO S"THENK=K+100:GOTO648
674 IFB$="GO E"THENK=K+1:GOTO648
676 IFB$="GO W"THENK=K-1:GOTO648
678 IFC$="EAT"THEN698
680 IFC$="HEL"THENPRINT"DON'T ASK ME ASK DR.T":GOTO668
682 IFA$="ASK DR.T"THENM$="EVERY ACTION HAS AN EQUAL AND OPPOSITE REACTION":GOSUB1:GOTO668
684 IFC$="GET"THEN710
686 IFC$="LOO"THENM$="IT LOOKS THE SAME IN EVERY DIRECTION":GOSUB1:GOTO668
688 IFC$="INV"THENGOSUB736:GOTO668
690 IFC$="FIG"ORC$="OPE"ORC$="GO "THENGOSUB9:GOTO668
692 IFC$="QUI"THEN348
694 IFB$="DROP"THENA$="ITEM":GOSUB631:GOTO668
696 GOSUB86:GOTO668
698 IFK=303ORK=6ORBR>0ORB=1THEN700
699 GOTO706
700 IFD$="CAC"ORD$="FOO"THENM$="UGH...":GOTO708
702 IFD$="BER"ANDB=1THENM$="SOUR":GOTO708
704 IFD$="BRE"ORD$="CRU"THENM$="A BIT MOLDY":GOTO708
706 PRINT"YUCK":GOTO668
708 M$=M$+" BUT YOU FEEL BETTER":GOSUB3:H=H+10:GOTO668
710 IFD$="SCR"ANDK=506THENSC=1:W$=T$(4):GOTO722
712 IF(D$="DIA"ORD$="STI")ANDK=506THEND=D+1:W$=T$(1):GOTO722
714 IFD$="GOL"ANDK=506THENW$="GOLD":G=G+1:GOTO722
716 IFD$="CHE"THENPRINT"IT IS TOO HEAVY":GOTO668
718 IFD$="BRE"THENBR=BR+1:W$="BREAD":GOTO722
720 GOSUB9:GOTO668
721 M$="YOU HAVE THE "+W$:GOTO3
722 GOSUB721:GOTO668
724 IFC$="LUL"ANDX=15THENSL=1:M$="EVERYONE IS LULLED TO SLEEP":GOTO4
726 IFC$="HYM"ANDX=33ANDA(33,6)=25THENA(33,6)=0:M$="DRACULA FLEES TO HIS HOME":GOTO4
728 PRINT"LA-LA-LA":SOUND233,5:GOTO36
730 IFC$="CRO"ANDCR=1ANDX=33THENM$="HE SHRIEKS AND FLEES FROM THE SACRED CROSS BORNE BY SIR GALAHAD!":GOSUB1:GOTO734
732 GOTO7
734 Y=2:GOSUB8:M$="IN GRATITUDE THE MAYOR GIVES YOU THE KEY TO THE CITY AND A 500 GRAND REWARD":Z=Z+500:A(33,6)=0:GOTO5
736 M$="YOU HAVE HOPE, ":IFSW=1THENM$=M$+"A SWORD, "
740 IFB=1THENM$=M$+"SOME BERRIES, "
742 IFBR>0THENM$=M$+"SOME BREAD, "
744 IFF=1THENM$=M$+"TUBES OF FOOD, "
746 IFE=1THENM$=M$+"A DINOSAUR EGG, "
748 IFG>0THENM$=M$+"GOLD WORTH $"+STR$(G*10000)+", "
750 IFD>0THENM$=M$+MID$(STR$(D),2)+" DIAMOND(S), "
752 IFV=1THENM$=M$+T$(8)+", "
754 IFKY=1THENM$=M$+"A KEY, "
756 IFBX=1THENM$=M$+"A BOX, "
758 IFCR=1THENM$=M$+"A CROSS, "
760 IFBO=1THENM$=M$+"A JEWELLED CASKET, "
762 IFGR=1THENM$=M$+T$(6)+", "
764 IFSC=1THENM$=M$+"A SACRED SCROLL, "
766 IFZ>0THENM$=M$+"TREASURE WORTH $"+STR$(Z*1000)+", "
768 M$=M$+"AND MUCH COURAGE":GOTO3
770 CLS:M$="YOU ARE IN THE TIME MACHINE. IT FLASHES THE TIMES ZONES:":GOSUB1:PRINT:O=0
772 IFX=1THEN778
774 IFRND(3)=1THENPRINT"AGE OF dINOSAURS":O=1
776 IFX=8THEN782
778 IFRND(3)=1THENPRINT"ANCIENT rOME":O=1
780 IFX=16THEN786
782 IFRND(3)=1THENPRINT"KING aRTHUR'S COURT":O=1
784 IFX=25THEN790
786 IFRND(3)=1THENPRINT"eLDORADO":O=1
788 IFX=29THEN794
790 IFRND(3)=1THENPRINT"sHERLOCK HOLMES' LONDON":O=1
792 IFX=34THEN798
794 IFRND(3)=1THENPRINT"ROARING tWENTIES":O=1
796 IFX=38THEN802
798 IFRND(3)=1THENPRINT"THE fUTURE":O=1
800 IFX=0THEN804
802 IFRND(5)=1THENPRINT"RETURN TO THE pRESENT"
804 IFO<1THEN772
806 GOSUB900:IFA$="D"THENX=1:DO=1
810 IFA$="R"THENX=8:R=1
812 IFA$="A"THENX=16:AR=1
814 IFA$="E"THENX=25:EL=1
816 IFA$="S"THENX=29:S=1:BG=0
818 IFA$="T"THENX=34:RT=1
820 IFA$="F"THENX=38:FU=1
822 IFA$="P"THEN828
824 IFA$="X"THEN770
826 P=P+1:IFX=0THEN770
827 GOTO24
828 M=Z+10*G+100*D+500*V+1000*GR:ZN=DO+R+AR+EL+S+RT+FU:IFSC=1THENM=M+1000
832 PRINT:M$="CONGRATULATIONS! YOU HAVE RETURNED ALIVE FROM YOUR JOURNEY BEYOND TIME HAVING MADE"+STR$(P)
833 M$=M$+" TIME JUMPS & VISITED"+STR$(ZN)+" TIME ZONES":GOSUB1
834 M$="YOU BROUGHT BACK ":IFE=1THENM$=M$+"A DINOSAUR EGG AND "
836 IFM=0THENM$=M$+"NO TREASURE":GOTO840
838 M$=M$+"TREASURE WORTH"+STR$(M*1000)
840 GOSUB3:GOTO348
900 A$=INKEY$:C1=RND(0):ON-(A$="")GOTO900:RETURN
901 PRINT"DR.T YELLS 'FASTER...FASTER!'":RETURN
