0 CLS:CLEAR500:DIMC1,CC,M$:GOTO22
2 GOSUB1500:C1=1:CC=32
4 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO4
6 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN4
8 RETURN
10 GOSUB2:GOTO42
12 GOSUB2:GOSUB16:GOTO52
16 FORCC=1TO11:C$(CC)="":NEXT:MO=MO-1:RETURN
18 FORCC=1TO32:PRINT"-";:NEXT:PRINT"SCORE:"S,"MOVES:"MO:RETURN
20 GOSUB2:RETURN
22 NO=9:R=5:ND=6:NR=27:MO=0:NI=2
24 CLS:DIMR$(NR),O$(NO),O2$(NO),O(NO),MA(28,6),C$(11)
26 PRINT@192,"THE NIGHT OF THE VAMPIRE BUNNIES":PRINT@298,"BY JASON DYER"
28 FORI=1TONR:FORJ=0TOND-1:READMA(I,J):NEXT:NEXT:FORI=0TONO-1:READO$(I),O2$(I),O(I):NEXT
34 CLS
36 GOSUB186
38 IFFFTHENFF=0:GOTO212
40 GOSUB182
42 IFVATHENVT=VT+1:IFVT=2THENM$="THE VAMPIRE DECIDES TO FANG YOU."GOSUB2:GOTO210
44 IFR=25ANDNOTDBTHENRA=RA+1:GOTO48
46 RA=0
48 IFR=25ANDNOTDBTHENIFRA=3THENM$="THE VAMPIRE BUNNY NIBBLES A LITTLE ON YOUR LEG.":GOSUB2:GOTO210
50 IFC$(1)<>""THENC$=C$(1):BF=0:GOTO78
52 GOSUB18:INPUTC$:IFC$=""THENPRINT"HUH?":GOTO52
54 FORI=1TOLEN(C$)
56 IFMID$(C$,I,4)="THE "THENG$=LEFT$(C$,I-1):GG$=MID$(C$,I+4):C$=G$+GG$:GOTO56
58 NEXT:C=0
60 Z=LEN(C$):L=0:FORI=1TOZ
62 L=L+1
64 IFMID$(C$,L,3)="AND"THENC=C+1:C$(C)=LEFT$(C$,L-2):G$=MID$(C$,L+4):C$=G$:L=1
66 IFMID$(C$,L,4)="THEN"THENC=C+1:C$(C)=LEFT$(C$,L-2):G$=MID$(C$,L+5):C$=G$:L=1
68 IFMID$(C$,L,1)="."THENIFLEN(C$)<>LTHENC=C+1:C$(C)=LEFT$(C$,L-1):G$=MID$(C$,L+1):C$=G$:L=1
70 IFMID$(C$,L,2)="  "THENG$=LEFT$(C$,L-1):GG$=MID$(C$,L+3):C$=G$+GG$:GOTO70
72 IFLEFT$(C$,1)=" "THENC$=RIGHT$(C$,LEN(C$)-1)
74 IFRIGHT$(C$,1)="."THENC$=LEFT$(C$,LEN(C$)-1)
76 NEXT:C$(C+1)=C$:IFC$(1)<>""THENC$=C$(1)
78 BF=0:FORI=2TO10:C$(I-1)=C$(I):C$(I)="":NEXT
80 C=0:V$="":N$="":W1$="":W2$=""
82 C=C+1:IFC>LEN(C$)THEN88
84 W$=MID$(C$,C,1):IFW$=" "THEN88
86 V$=V$+W$:GOTO82
88 C=C+1:IFC>LEN(C$)THEN94
90 W$=MID$(C$,C,1):IFW$=" "THEN94
92 N$=N$+W$:GOTO88
94 C=C+1:IFC>LEN(C$)THEN100
96 W$=MID$(C$,C,1):IFW$=" "THEN100
98 W1$=W1$+W$:GOTO94
100 C=C+1:IFC>LEN(C$)THEN106
102 W$=MID$(C$,C,1):IFW$=" "THEN106
104 W2$=W2$+W$:GOTO100
106 IFV$=""THENPRINT"HUH?":GOTO52
108 IFLEN(V$)>5THENV$=LEFT$(V$,5)
110 IFLEN(N$)>5THENN$=LEFT$(N$,5)
112 IFLEN(W1$)>5THENW1$=LEFT$(W1$,5)
114 IFLEN(W2$)>5THENW2$=LEFT$(W2$,5)
116 IFV$="G"ORV$="AGAIN"THENC$=V4$:V$=V1$:N$=VV$:W1$=V2$:W2$=V3$
118 IFN$="IT"THENN$=VV$
120 VV$=N$:V4$=C$:V1$=V$:V2$=W1$:V3$=W2$
122 IFN$="SILVE"THENN$="CROSS"
124 IFN$="SCRAP"THENN$="PAPER"
126 IFW1$="SCRAP"THENW1$="PAPER"
132 IFN$="GRAY"THENN$="OOZE"
134 IFN$="WOODE"THENN$="CRANK"
136 IFN$="WEBS"THENN$="COBWE"
138 IFW2$="GRAY"THENW2$="OOZE"
140 IFW1$="SILVE"THENS1$="CROSS"
142 MO=MO+1:GOTO144
144 IFR<11ANDR<>7THEN156
146 IFO(4)=-1ORO(4)=RTHEN156
148 IFNOTLATHEN152
150 IFO(1)=-1ORO(1)=RTHEN156
152 IFC$<>"LIGHTLAMP"ANDC$<>"TURN ON LAMP"THENM$="*IN TOTAL DARKNESS.":GOSUB2
154 FORTEMP=1TO11:C$(TEMP)="":NEXT:GOTO52
156 IFC$="TALK INTO WELL"ORC$="TALK TO WELL"ORC$="TALK WELL"THEN160
158 GOTO162
160 IFR=4THENM$="ECHO... ECHO... ECHO...":GOTO10
162 IFV$="PUT"ANDN$="ON"THENV$="WEAR":N$=W1$
164 IFV$="GET"ANDN$="ON"THENV$="WEAR":N$=W1$
166 IFV$="TAKE"ANDN$="OFF"THENV$="UNWEA":N$=W1$
168 IFV$="GET"ANDN$="OFF"THENV$="UNWEA":N$=W1$
170 IFV$="THROW"ANDN$="WATER"THENV$="POUR"
172 IFV$="THROW"ANDN$="BUCKE"THENV$="POUR"
174 IFVAANDV$="POUR"THENIFN$="WATER"ORN$="BUCKE"THENW1$="ON":W2$="VAMPI"
176 IFV$="TAKE"ANDW1$="OFF"THENV$="UNWEA"
178 IFV$="GET"ANDW1$="OFF"THENV$="UNWEA"
180 GOTO234
182 FORI=0TONO-1:IFO(I)=RTHENM$="YOU SEE "+O$(I)+" HERE.":GOSUB2
184 NEXT:RETURN
186 IFR<11ANDR<>7THEN196
188 IFO(4)=-1ORO(4)=RTHEN196
190 IFNOTLATHEN194
192 IFO(1)=-1ORO(1)=RTHEN196
194 DA=1:RETURN
196 IFR=100THENDA=0:GOTO878
198 ONRGOSUB916,934,940,946,954,960,966,972,980,990,996,1002,1006,1008,1014:IFR<=15THENRETURN
200 ONR-15GOSUB1018,1020,1022,1028,1036,1038,1040,1042,1050,1056,1066,1070:RETURN
202 IFNOTLATHEN206
204 IFO(1)=-1ORO(1)=RTHENRETURN
206 DA=1:M$="UH-OH, NOW YOU CAN'T SEE!":GOTO20
208 M$="YOU HAVE DIED.":GOTO20
210 GOSUB208:GOSUB886:GOTO476
212 GOSUB208:GOSUB886:M$="NEW GAME OR UNDO (Y/N/U)?":GOSUB2
214 I$=INKEY$:IFI$<>"Y"ANDI$<>"N"ANDI$<>"U"THEN214
216 IFI$="Y"THENRUN
218 IFI$="U"THENGOSUB16:GOTO36
220 END
222 FORI=0TONO-1:IFO2$(I)=N$THENFL=1:RO=O(I):RETURN
228 NEXT:FL=0:RETURN
234 IFV$<>"GO"ANDV$<>"RUN"ANDV$<>"CRAWL"ANDV$<>"N"ANDV$<>"S"ANDV$<>"CLIMB"ANDV$<>"E"ANDV$<>"W"ANDV$<>"U"ANDV$<>"D"THEN306
236 IFN$="NORTH"ORV$="N"THENDI=0:GOTO276
238 IFN$="SOUTH"ORV$="S"THENDI=1:GOTO276
240 IFN$="EAST"ORV$="E"THENDI=2:GOTO276
242 IFN$="WEST"ORV$="W"THENDI=3:GOTO276
244 IFN$="UP"ORV$="U"THENDI=4:GOTO276
246 IFN$="DOWN"ORV$="D"THENDI=5:GOTO276
248 IFR=1ANDN$="COFFI"THENM$="ARE YOU CRAZY?":GOTO10
250 IFR=3THENIFN$="STREA"THENV$="S":GOTO234
252 IFR=6THENIFN$="STREA"THENV$="N":GOTO234
254 IFR=9ANDN$="HOLE"THENV$="D":GOTO234
256 IFR=4ANDN$="WELL"THENV$="D":GOTO234
258 IFR=7ANDN$="CREVI"THENV$="S":GOTO234
260 IFR=8ANDN$="HOLE"THENV$="D":GOTO234
262 IFR=7ANDN$="ROPE"THENDI=4:GOTO276
264 IFR=4ANDN$="ROPE"ANDROTHENDI=5:GOTO276
266 IFR=27ANDN$="HOLE"THENV$="U":GOTO234
268 IFR=14ANDN$="HOLE"THENV$="U":GOTO234
270 IFR=2ANDN$="HOLE"THENV$="E":GOTO234
272 IFR=3ANDN$="HOLE"THENV$="W":GOTO234
274 M$="YOU CAN'T GO THAT WAY!":GOTO12
276 IFMA(R,DI)>0ANDMA(R,DI)<128THENR=MA(R,DI):GOTO36
278 IFMA(R,DI)=203THENM$="THE OOZE BLOCKS YOUR PATH.":GOTO10
280 IFMA(R,DI)=204THENM$="THE BUNNY WON'T LET YOU!":GOTO10
282 IFMA(R,DI)=200THENM$="YOU ENTER THE STREAM AND DROWN.":GOSUB2:GOTO212
284 IFMA(R,DI)=201ANDNOTROTHEN274
286 IFMA(R,DI)=201ANDNOTLATHENM$="IT'S TOO DARK DOWN THERE.":GOTO10
288 IFMA(R,DI)=201THENIFO(1)<>7ANDO(1)<>-1THENM$="IT'S TOO DARK DOWN THERE.":GOTO10
290 IFMA(R,DI)=240ANDNI>1THENM$="*TOO HEAVY TO CLIMB UP.":GOTO10
292 IFMA(R,DI)=240THENR=4:M$="YOU CLIMB UP THE ROPE.":GOSUB2:GOTO36
294 IFMA(R,DI)=201ANDNI>1THENM$="THE ROPE SNAPS AND YOU PLUNGE DOWN.":GOSUB2:GOTO212
296 IFMA(R,DI)=201THENR=7:M$="YOU CLIMB DOWN THE ROPE.":GOSUB2:GOTO36
298 IFMA(R,DI)=700THENM$="THE VAMPIRE, UNFORTUNATELY, GIVES YOU A NASTY BITE.":GOSUB2:GOTO210
300 IFMA(R,DI)=314ANDNOTWCTHENM$="THE SKELETON PUSHES YOU AWAY.":GOTO10
302 IFMA(R,DI)=314THENM$="THE SKELETON ALLOWS YOU TO ENTER.":GOSUB2:R=14:GOTO36
304 GOTO274
306 IFV$<>"GET"ANDV$<>"TAKE"THEN324
308 ZH=0:GOSUB222:IFN$="WATER"THENIFO(2)=-1THENV$="FILL":N$="BUCKE":GOTO234
310 IFFL=0ORRO<>RTHENM$="YOU CAN'T GET THAT!":GOTO12
312 IFRO=-1THENM$="YOU ALREADY HAVE THAT!":GOTO12
316 IFNI=5THENM$="*CARRYING TOO MUCH!":GOTO12
318 IFNOTCRANDI=3THENCR=-1:S=S+10
320 IFNOTCAANDI=0THENCA=-1:S=S+2
322 NI=NI+1:O(I)=-1:PRINT"TAKEN.":GOTO42
324 IFV$="HINT"ORV$="HELP"THENPRINT"SORRY."
326 IFV$="TURN"ANDW1$="OFF"THENV$="UNLIG"
328 IFV$="TURN"ANDW1$="ON"THENV$="LIGHT"
330 IFV$="TURN"ANDN$="OFF"THENV$="UNLIG":N$=W1$
332 IFV$="TURN"ANDN$="ON"THENV$="LIGHT":N$=W1$
334 IFV$="PUT"ANDN$="DOWN"THENV$="DROP":N$=W1$:GOTO306
336 IFV$="PICK"ANDN$="UP"THENV$="TAKE":N$=W1$:GOTO306
338 IFV$<>"DROP"THEN352
340 IFW1$<>""THENV$="PUT":GOTO234
342 GOSUB222
344 IFFL=0THEN695
346 IFRO=-2THENRO=-1
348 IFRO<>-1THEN695
350 NI=NI-1:O(I)=R:PRINT"DROPPED.":GOTO42
352 IFV$<>"INVEN"ANDV$<>"I"THEN382
354 FL=0:PRINT"CARRYING:"
356 IFWCTHENM$="WEARING A SILVER CROSS":GOSUB2:FL=1
358 IFBUANDO(2)=-1THENM$="A BUCKET WITH WATER":GOSUB2:FL=1
360 IFLAANDO(1)=-1THENM$="A LIT LAMP":GOSUB2:FL=1
362 IFO(4)=-1THENM$="A LIT TORCH":GOSUB2:FL=1
364 FORI=0TONO-1
366 IFWCANDI=3THEN376
368 IFBUANDI=2ANDO(I)=-1THEN376
370 IFLAANDI=1ANDO(I)=-1THEN376
372 IFI=4ANDO(I)=-1THEN376
374 IFO(I)=-1THENM$=O$(I):GOSUB2:FL=1
376 NEXT
378 IFFL=0THENPRINT"NOTHING"
380 GOTO42
382 IFV$<>"LOOK"ANDV$<>"L"THEN388
384 IFN$<>""THEN390
386 GOTO36
388 IFV$<>"EXAMI"ANDV$<>"SEARC"THEN466
392 IFN$="GROUN"ORN$="FLOOR"ORN$="FIELD"THENIFR=5ANDNOTCATHENCA=-1:O(0)=5:M$="YOU FIND A CARROT.":GOTO10
394 IFN$="GROUN"ORN$="FLOOR"THENM$="YOU SEE NOTHING UNUSUAL.":GOTO10
396 IFN$="COFFI"ANDR=1THENM$="IT IS CARVED OF MARBLE.":GOTO10
398 IFN$="SKELE"ANDR=9THENM$="IT LOOKS PRETTY BONY.":GOTO10
400 IFN$="WELL"ANDR=4THENM$="ROPE HANGS FROM A WOODEN CRANK. THE WELL IS DARK.":GOTO10
402 IFN$="ROPE"THENIFR=4ORR=7THENM$="IT DOESN'T LOOK VERY STURDY.":GOTO10
404 IFN$="WELL"ANDR=7THENM$="IT'S FULL OF CRACKS.":GOTO10
406 IFN$="OOZE"ANDR=19ANDNOTDGTHENM$="IT IS BIG, UGLY AND SLIMY.":GOTO10
408 IFN$="COBWE"ANDNOTBCTHENM$="THEY ARE BLOCKING THE E PASSAGE.":GOTO10
410 IFN$="LEVER"ANDR=26THENM$="IT'S RUSTY BUT USEABLE.":GOTO10
412 IFN$="BUNNY"ANDNOTDBANDR=25THENM$="IT LOOKS EXTREMELY VICIOUS.":GOTO10
414 IFN$="VAMPI"ANDVATHENM$="IT LOOKS AT YOU WITH AN UNNERVING GAZE.":GOTO10
416 IFN$="TOMBS"ORN$="GRAVE"THENIFR<11ANDR<>7THENM$="MANY ARE OLD AND ROTTED AND SOME ARE BLANK.":GOTO10
418 IFN$="CRANK"ANDR=4THENM$="IT IS CRACKING BUT USABLE.":GOTO10
420 IFN$="STREA"THENIFR=6ORR=3THENM$="IT LOOKS SHALLOW ENOUGH TO CROSS.":GOTO10
424 IFN$="MIRRO"ANDR=24THEN428
426 GOTO434
428 IFBRTHENM$="IT'S BROKEN. THERE'S A PASSAGE BEHIND IT.":GOTO10
430 M$="IT'S NOT WELL POLISHED.":GOTO10
432 IFN$="WALLS"ORN$="WALL"THENM$="THERE ARE ENOUGH OF THEM.":GOTO10
434 GOSUB222
436 IFRO<>RANDRO<>-1ANDO(I)<>(R-128)THEN629
438 IFN$="CARRO"THENM$="IT LOOKS NICE AND JUICY.":GOTO10
440 IFN$<>"LAMP"THEN446
442 IFLATHENM$="THE LAMP IS ON.":GOTO10
444 M$="THE LAMP IS OFF.":GOTO10
446 IFN$<>"BUCKE"THEN452
448 IFBUTHENM$="THE BUCKET IS FILLED.":GOTO10
450 M$="THE BUCKET IS EMPTY.":GOTO10
452 IFN$="CROSS"THENM$="IT IS ROUGH AND TARNISHED.":GOTO10
454 IFN$="PAPER"THENM$="IT HAS SOME WRITING ON IT.":GOTO10
456 IFN$="KEY"THENM$="IT IS FAIRLY SHINY.":GOTO10
458 IFN$="TORCH"THENM$="IT IS BURNING SLOWLY.":GOTO10
460 IFN$="PEN"THENM$="IT DOES NOT HAVE ANY INK.":GOTO10
462 IFN$="APPLE"THENM$="IT LOOKS A LITTLE WORMY.":GOTO10
464 M$="YOU SEE NOTHING UNUSUAL.":GOTO10
466 IFV$<>"QUIT"ANDV$<>"Q"ANDV$<>"END"THEN484
468 GOSUB886
470 M$="SURE YOU WANT TO QUIT (Y/N)":GOSUB2:INPUTM$:IFM$="N"THENGOSUB16:GOTO52
474 IFM$<>"Y"THEN470
476 M$="PLAY AGAIN (Y/N)?":GOSUB2
478 I$=INKEY$:IFI$<>"Y"ANDI$<>"N"THEN478
480 IFI$="Y"ORI$="Y"THENRUN
482 END
484 IFV$<>"ENTER"THEN488
486 V$="GO":GOTO234
488 IFV$<>"JUMP"THEN508
490 IFN$=""THENPRINT"WHEEEEEE!":GOTO42
492 IFN$<>"OVER"THENM$="THAT WON'T DO ANY GOOD.":GOTO12
494 IFR=19ANDNOTDGANDW1$="OOZE"ORW1$="GREY"THENM$="THE OOZE EXPANDS AND PREVENTS YOUR JUMP.":GOTO12
496 IFR=25ANDNOTDBANDW1$="BUNNY"ORW1$="RABBI"THENM$="THE BUNNY BITES YOUR LEG.":GOSUB2:GOTO212
500 IFW1$<>"STREA"OR(R<>3ANDR<>6)THENM$="THAT WON'T DO ANYTHING!":GOTO10
502 IFR=3THENR=6:GOTO506
504 R=3
506 M$="YOU JUMP OVER THE STREAM.":GOSUB2:GOTO36
508 IFV$<>"READ"THEN516
510 IFN$<>"PAPER"THEN699
512 IFO(6)<>-1THEN695
514 M$="IT SAYS, 'PUSHING IS BETTER THAN PULLING'":GOTO10
516 IFV$<>"LIGHT"THEN542
518 IFN$="COBWE"THEN526
520 GOSUB222:IFFL=0ORO(I)<>-1THEN695
524 IFN$<>"TORCH"ANDN$<>"COBWE"ANDN$<>"LAMP"THEN699
526 IFN$="COBWE"ANDR=23ANDBC=0THENS=S+5:BC=-1:MA(23,2)=24:M$="THE COBWEBS BURN.":GOTO10
528 IFN$="COBWE"THEN699
530 IF(N$="TORCH"ANDO(4)=-1)OR(N$="LAMP"ANDO(1)=-1ANDLA)THENM$="IT'S ALREADY LIT!":GOTO10
534 IFN$="LAMP"ANDNOTLATHEN538
536 GOTO542
538 M$="THE LAMP LETS OUT A WARM GLOW.":GOSUB2:LA=-1:IFDATHEN36
540 GOTO42
542 IFV$<>"LOWER"THEN552
544 IFN$<>"ROPE"ANDN$<>"WELL"THEN699
546 IFR<>4THEN699
548 IFROTHENM$="IT ALREADY IS!":GOTO12
550 RO=-1:M$="THE ROPE GOES DOWN.":GOTO10
552 IFV$<>"RAISE"ANDV$<>"LIFT"THEN562
554 IFN$<>"ROPE"ANDN$<>"WELL"THEN699
556 IFR<>4THEN699
558 IFNOTROTHENM$="IT ALREADY IS!":GOTO12
560 RO=0:M$="THE ROPE RISES.":GOTO10
562 IFV$<>"FILL"THEN570
564 IFN$<>"BUCKE"ORO(2)<>-1OR(R<>3ANDR<>6)THEN699
566 IFBUTHENM$="IT'S ALREADY FULL!":GOTO12
568 BU=-1:M$="THE BUCKET FILLS WITH WATER.":GOTO10
570 IFV$<>"UNLIG"THEN584
572 GOSUB222:IFFL=0THEN695
574 IFO(I)<>-1THEN695
576 IFN$<>"TORCH"ANDN$<>"LAMP"THEN699
578 IFN$="TORCH"ANDO(4)=-1THENM$="IT REFUSES TO GO OUT.":GOTO10
580 IFN$="LAMP"ANDO(1)=-1ANDNOTLATHENM$="IT ALREADY IS!":GOTO10
582 IFN$="LAMP"ANDO(1)=-1THENLA=0:M$="THE LAMP TURNS OFF.":GOTO10
584 IFV$<>"OPEN"THEN594
586 IFN$<>"COFFI"THEN699
588 IFR<>1THEN699
590 IFOCTHENM$="IT'S ALREADY OPEN!":GOTO12
592 OC=-1:VA=-1:MA(1,1)=700:M$="AS YOU OPEN THE COFFIN, A VAMPIRE REACHES OUT!":GOTO10
594 IFV$<>"CLOSE"THEN606
596 IFN$<>"COFFI"THEN699
598 IFR<>1THEN699
600 IFNOTOCTHENM$="IT'S NOT OPEN!":GOTO12
602 IFVATHENVT=0:VA=0:OC=0:MA(1,1)=4:M$="THE COFFIN SLAMS SHUT IN THE VAMPIRE'S FACE.":GOTO10
604 OC=0:M$="THE COFFIN CLOSES.":GOTO10
606 IFV$<>"POUR"ANDV$<>"EMPTY"ANDV$<>"SPLAS"THEN630
608 IFN$<>"WATER"ANDN$<>"BUCKE"THEN699
610 IFO(2)<>-1THEN695
612 IFNOTBUTHEN699
614 IFW2$=""THENBU=0:M$="YOU EMPTY THE BUCKET.":GOTO10
616 IFW2$="OOZE"ORW2$="GREY"ANDR=19ANDDG=0THENM$="THE OOZE GROWS AND SPLATTERS YOU WITH DEADLY FORCE.":GOSUB2:GOTO212
618 IFW2$="RABBI"ORW2$="BUNNY"ANDR=24ANDDB=0THENBU=0:M$="THE BUNNY MOVES, AVOIDING THE WATER.":GOTO10
620 IFW2$="APPLE"THENIFO(8)=-1ORO(8)=RTHENBU=0:AP=-1:M$="THE APPLE IS SPLASHED WITH WATER.":GOTO10
622 IFW2$="CARRO"THENIFO(0)=-1ORO(0)=RTHENS=S+10:BU=0:CS=-1:M$="THE CARROT IS SPLASHED.":GOTO10
624 IFW2$="SKELE"ANDR=9THENBU=0:M$="THE WATER FLIES THROUGH THE SKELTAL BODY.":GOTO10
626 IFW2$="VAMPI"ANDVATHENMA(1,1)=4:BU=0:VA=0:O(3)=1:M$="THE VAMPIRE SCREAMS AND DISSOLVES.":GOTO10
628 GOTO856
629 M$="THAT'S NOT HERE!":GOTO12
630 IFV$<>"WEAR"THEN640
632 IFN$<>"CROSS"THEN699
634 IFO(3)<>-1THEN695
636 IFWCTHENM$="*ALREADY WEARING IT!":GOTO12
638 WC=-1:M$="IT FITS RATHER NICE.":GOTO10
640 IFV$="PUT"ANDW1$="ON"THENIFN$="CROSS"THENV$="WEAR":GOTO630
642 IFV$="GET"ANDW1$="ON"THENIFN$="CROSS"THENV$="WEAR":GOTO630
644 IFV$="GET"ANDW1$="OFF"THENV$="UNWEAR":GOTO234
646 IFV$="TAKE"ANDW1$="OFF"THENV$="UNWEAR":GOTO234
648 IFV$<>"PUT"THEN716
650 IFW1$<>"ON"ANDW1$<>"ONTO"THEN664
652 IFN$="WATER"ORN$="BUCKE"THENV$="POUR":GOTO234
654 IFN$<>"TORCH"THEN662
656 IFO(4)<>-1THEN695
658 IFW2$="GREY"ORW2$="OOZE"THEN864
660 IFW2$="COBWE"ANDR=23ANDNOTBCTHEN872
662 M$="THAT PROBABLY WON'T DO MUCH.":GOTO12
664 IFW1$<>"IN"ANDW1$<>"INTO"THEN714
666 IFN$="WATER"ANDW2$="BUCKE"THENV$="FILL":N$="BUCKE":GOTO234
668 IFW2$="OOZE"ORW2$="GREY"THEN658
670 IFW2$="COBWE"THEN658
672 IFW2$<>"WELL"THEN682
674 IFR<>4THEN629
676 GOSUB222:IFFL=0THEN695
678 IFRO<>-1THEN695
680 O(I)=7:NI=NI-1:PRINT"ENUOOOOOAUUUOOOCLUNK!":GOTO42
682 IFW2$<>"STREA"ANDW2$<>"WATER"ANDW2$<>"BUCKE"THEN714
684 IFW2$="BUCKE"THENIF((O(2)<>-1)AND(O(2)<>R))THEN695
686 IFW2$="BUCKE"ANDNOTBUANDN$="WATER"THENV$="GET":GOTO234
688 IFW2$="BUCKE"ANDNOTBUTHEN856
690 IFW2$="WATER"THENIFR=3ORR=6THEN700
692 IFW2$="WATER"THEN696
694 GOTO700
695 M$="YOU DON'T HAVE THAT!":GOTO12
696 IFO(2)=-1ORBUTHEN700
698 M$="THAT'S NOT HERE!":GOSUB2:GOSUB16:GOTO42
699 M$="YOU CAN'T DO THAT!":GOTO12
700 IFW2$="STREA"THENIFR<>3ANDR<>6THENM$="YOU DON'T SEE ANY!":GOTO12
702 GOSUB222:IFFL=0THEN695
704 IFO(I)<>-1THEN695
706 IFN$="CARRO"THENIFR=3ORR=6OR(O(2)=-1ANDBU)THENS=S+10:CS=-1:M$="THE CARROT SOAKS UP THE WATER.":GOTO10
710 IFN$="APPLE"THENIFR=3ORR=6OR(O(2)=-1ANDBU)THENAP=-1:M$="THE APPLE SOAKS UP THE WATER.":GOTO10
714 GOTO856
716 IFV$<>"PUSH"THEN730
718 IFN$="COFFI"ANDR=1THENM$="IT IS TOO HEAVY.":GOTO10
720 IFN$="MIRRO"ANDR=24ANDNOTBRTHENM$="IT IS ATTACHED TO THE WALL.":GOTO10
722 IFN$="THRON"ANDR=25THENM$="IT IS BOLTED TO THE WALL.":GOTO10
724 IFN$<>"LEVER"ORR<>26THENM$="YOU DON'T NEED TO DO THAT.":GOTO12
726 IFPLTHENM$="NOTHING HAPPENS.":GOTO10
728 MA(8,5)=27:PL=-1:MA(27,4)=8:M$="YOU HEAR A LOUD 'CLUNK' TO THE S.":GOTO10
730 IFV$<>"PULL"THEN738
732 IFN$<>"LEVER"THEN718
734 IFR<>26THEN699
736 M$="THE ROOF CAVES.":GOSUB2:GOTO212
738 IFV$<>"TURN"THEN748
740 IFN$<>"CRANK"THEN699
742 IFR<>4THEN699
744 IFROTHENRO=0:M$="THE ROPE RISES.":GOTO10
746 RO=-1:M$="THE ROPE GOES DOWN.":GOTO10
748 IFV$="WAIT"ORV$="Z"THENM$="TIME PASSES...":GOTO10
750 IFV$<>"EAT"ANDV$<>"TASTE"THEN766
752 GOSUB222:IFFL=0ORO(I)<>-1THENM$="YOU CAN'T DO THAT NOW!":GOSUB2:GOSUB16:GOTO42
756 IFI<>0ANDI<>8THENM$="YOU CAN'T EAT THAT!":GOSUB2:GOSUB16:GOTO42
758 IFI=8ANDAPTHENM$="YOU TAKE A BITE & FALL DOWN. THE APPLE FALLS FROM YOUR LIMP HAND.":GOSUB2:GOTO212
760 IFI=0ANDCSTHENM$="YOU MUNCH A LITTLE & COLLAPSE.":GOSUB2:GOTO212
762 IFI=8THENM$="YOU TAKE A BITE AND SPIT IT OUT.":GOTO10
764 O(0)=0:M$="MMM... IT IS DELICIOUS.":GOTO10
766 IFV$="SOAK"ANDW1$=""THENV$="PUT":W1$="IN":W2$="WATER":GOTO234
768 IFV$="SOAK"THENV$="PUT":GOTO234
770 IFV$<>"BREAK"ANDV$<>"SMASH"THEN780
772 IFN$<>"MIRRO"ORR<>24THEN629
776 IFBRTHENM$="IT'S ALREADY BROKEN!":GOTO12
778 BR=-1:S=S+8:MA(24,2)=25:M$="IT BREAKS.":GOTO10
780 IFV$<>"GIVE"THEN800
782 IFW1$="TO"THENW1$=N$:N$=W2$
784 IFN$<>"BUNNY"THEN798
786 IFR<>25ORDBTHENM$="I DON'T SEE A BUNNY-- DO YOU?":GOTO12
788 N$=W1$:GOSUB222:IFFL=0ORO(I)<>-1THEN695
792 IFN$<>"CARRO"THENM$="THE BUNNY REFUSES YOUR OFFER.":GOTO12
794 IFNOTCSTHENO(0)=0:M$="THE BUNNY GOBBLES UP THE CARROT BUT IS NOT SATISFIED.":GOTO10
796 IFCSTHENS=S+10:DB=-1:M$="THE BUNNY GOBBLES THE CARROT UP, GASPS & DIES.":GOTO10
798 M$="I'M NOT SURE IT WILL ACCEPT YOUR OFFER.":GOTO12
800 IFV$<>"SIT"THEN810
802 IFN$="ON"THENN$=W1$
804 IFN$<>"THRON"ORR<>25THEN856
806 IFSTTHENM$="NOTHING HAPPENS SO YOU STAND UP AGAIN.":GOTO10
808 S=S+2:ST=-1:MA(25,2)=100:M$="AS YOU SIT, A PASSAGE OPENS TO THE E!":GOTO10
810 IFV$="BURN"THENV$="LIGHT":GOTO234
812 IFV$="NORTH"THENV$="N":GOTO234
814 IFV$="SOUTH"THENV$="S":GOTO234
816 IFV$="EAST"THENV$="E":GOTO234
818 IFV$="WEST"THENV$="W":GOTO234
820 IFV$="UP"THENV$="U":GOTO234
822 IFV$="DOWN"THENV$="D":GOTO234
824 IFV$<>"UNWEA"THEN832
826 IFN$<>"CROSS"THEN845
828 IFNOTWCTHENM$="*NOT WEARING IT!":GOTO12
830 WC=0:M$="YOU SLIDE THE CROSS OFF YOUR NECK.":GOTO10
832 IFV$="DESTR"ANDN$="MIRRO"THENV$="BREAK":GOTO234
834 IFV$<>"DESTR"ANDV$<>"KILL"ANDV$<>"ATTAC"THEN846
836 IFN$="SKELE"ANDR=9THENM$="IT REPELS YOU.":GOTO10
838 IFN$="VAMPI"ANDVATHENM$="HE IGNORES YOU.":GOTO10
840 IFN$="OOZE"ANDNOTDGANDR=19THENM$="THE OOZE SPLATTERS YOU WITH KILLING FORCE.":GOSUB2:GOTO212
842 IFN$="BUNNY"ANDNOTDBANDR=25THENM$="THE BUNNY BITES.":GOSUB2:GOTO212
844 GOTO856
845 M$="I DON'T UNDERTAND.":GOTO12
846 IFV$="TALK"THEN856
848 IFV$<>"DRINK"ANDV$<>"SIP"THEN860
850 IFN$<>"WATER"THEN856
852 IFR=3ORR=6THEN858
854 IFO(2)=-1ORO(2)=RTHENIFBUTHEN858
856 M$="THAT WON'T DO MUCH.":GOTO12
858 M$="YOU SIP THE WATER AND FEEL SICK. YOU FALL TO THE FLOOR, DEAD.":GOSUB2:GOTO212
860 IFV$="SCORE"THENGOSUB886:GOSUB16:GOTO50
862 M$="I DON'T KNOW THAT VERB.":GOTO12
864 IFR=19ANDDG=0THEN868
866 GOTO660
868 DG=-1:MA(19,3)=18:O(4)=22:NI=NI-1
870 M$="THE OOZE LIGHTS & MELTS AWAY.":GOSUB2:GOSUB202:GOTO42
872 BC=-1:MA(23,2)=24:S=S+5:M$="THE COBWEBS LIGHT AND BURN AWAY INTO DUST.":GOTO10
878 M$="YOU GO THROUGH THE TUNNEL AND END UP OUTSIDE THE GRAVEYARD. YOU HURRY ALONG "
880 M$=M$+"THE PATH BACK INTO TOWN SHOUTING THE NEWS. PEOPLE RUSH OUT TO GREET YOU.":GOSUB2
884 GOSUB886:GOTO476
886 M$="YOU HAVE SCORED"+STR$(S)+" OUT OF 60 POINTS.":GOTO20
916 DA=0:M$="*AT A COFFIN. WALLS N, E & W. A BLUE MIST HANGS OVER THE GRAVEYARD. "
922 IFVATHENM=M$+"THE COFFIN IS OPEN WITH A VAMPIRE IN IT!":GOTO20
924 IFOCTHENM$=M$+"THE COFFIN IS OPEN.":GOSUB2:GOTO928
926 M$=M$+"THE COFFIN IS CLOSED.":GOSUB2
928 IFNOTOCTHENRETURN
930 FORI=0TO8:IFO(I)=.1THENM$="THERE IS "+O$(I)+" IN THE COFFIN.":GOSUB2
932 NEXT:RETURN
933 M$="*IN A GLOOMY WALLED GRAVEYARD. ":RETURN
934 DA=0:GOSUB933:M$=M$+"A HOLE IN THE E WALL ALLOWS EXIT.":GOTO20
940 DA=0:IFNOTJRTHENJR=-1:S=S+3
942 GOSUB933:M$=M$+"A STREAM IS TO THE S. A HOLE IN THE W WALL ALLOWS PASSAGE.":GOTO20
946 DA=0:GOSUB933:M$=M$+"A MOSS COVERED WALL IS W, A COFFIN N AND A WELL IN THE CENTRE.":GOSUB2
952 IFROTHENM$="A ROPE LEADS DOWN.":GOTO20
954 DA=0:M$="*IN A MISTY FIELD. A WALL IS TO THE N.":GOTO20
960 DA=0:GOSUB933:M$=M$+"A SHALLOW STREAM IS N.":GOTO20
966 DA=0:IFNOTWETHENWE=-1:S=S+5
968 M$="*AT THE BOTTOM OF A WELL WITH A LARGE CRACK S. A ROPE HANGS DOWN.":GOTO20
972 DA=0:GOSUB933:M$=M$+"A BLACK WALL S. "
976 IFPLTHENM$=M$+"A GAPING HOLE LIES AMONGST THE GRAVESTONES.":GOTO20
978 M$=M$+"MANY TOMBSTONES ARE HERE.":GOTO20
980 DA=0:GOSUB933:M$=M$+"A WALL IS S. A LARGE HOLE IS HERE.":GOSUB2
986 IFWCTHENM$="A SKELETON FLEES WHEN HE SEES YOUR CROSS.":GOTO20
988 M$="A SKELETON GUARDS THE HOLE.":GOTO20
990 DA=0:GOSUB933:M$=M$+"WALLS CONNECT TO THE S & E.":GOTO20
996 DA=0:M$="*IN A LARGE CREVICE IN A WELL. WATER TRICKLES DOWN THE SIDES. EXIT N.":GOTO20
1002 DA=0:M$="*NERVOUS ABOUT THIS PASSAGE, BUT YOU TRUDGE ON AND ARE KILLED IN A CAVE-IN."
1004 R=13:FF=-1:GOTO20
1006 DA=0:M$="*IN A DARK MAZE.":GOTO20
1008 DA=0:M$="*IN A DARK E/W TUNNEL, WITH A HOLE ABOVE.":GOTO20
1014 DA=0:M$="*IN A DARK TUNNEL WITH N, S, E & W EXITS.":GOTO20
1018 DA=0:M$="*IN A DAMP MAZE.":GOTO20
1020 DA=0:M$="*IN A DANK MAZE.":GOTO20
1022 DA=0:IFNOTSLTHENSL=-1:S=S+5
1026 M$="*IN A LONG E/W TUNNEL. YOUR FEET ACHE AS YOU WALK.":GOTO20
1028 DA=0:M$="*IN A SLIME COVERED ROOM. YOUR FEET STICK TO THE GROUND.":GOSUB2
1032 IFNOTDGTHENM$="GREY OOZE GUARDS THE WEST CORRIDOR.":GOSUB2
1034 RETURN
1036 DA=0:M$="*IN A DREARY MAZE.":GOTO20
1038 DA=0:M$="*IN A MAZE.":GOTO20
1040 DA=0:M$="*IN A DEVLISH MAZE.":GOTO20
1042 DA=0:M$="*IN A LARGE MARBLE ROOM WITH TORCH HOLDERS. MOST ARE EMPTY. " 
1046 IFBCTHENM$="EXITS N, S & E.":GOTO20
1048 M$="EXITS N & S, BUT THE PASSAGE E IS BLOCKED BY COBWEBS.":GOTO20
1050 DA=0:M$="*IN A DEAD END.":IFBRTHENM$=M$+"IT EXITS W, BUT A SHATTERED MIRROR SHOWS A WAY E.":GOTO20
1054 M$=M$+"A LARGE MIRROR COVERS THE E WALL. AN EXIT IS W.":GOTO20
1056 DA=0:M$="*IN A LARGE CHAMBER WITH A THRONE. ":IFNOTDBTHENM$=M$+"A BUNNY IS NEXT TO IT. ":GOSUB2
1060 IFNOTDBTHENM$="THE VAMPIRE BUNNY! A PASSAGE EXITS W.":GOTO20
1062 IFNOTSTTHENM$=M$+"A PASSAGE EXITS W.":GOTO20
1064 M$=M$+"PASSAGES EXIT E&W.":GOTO20
1066 DA=0:M$="*IN A CRAMPED CORRIDOR GOING N/S. A WORN LEVER IS SET INTO THE W WALL.":GOTO20
1070 M$="*IN A SANDY ROOM WITH AN EXIT N. ":DA=0:IFNOTPLTHENM$=M$+"THE CEILING LOOKS WEAK.":GOTO20
1074 M$=M$+"THERE IS A HOLE IN THE ROOF.":GOTO20
1500 IFLEFT$(M$,1)="*"THENM$="YOU'RE "+MID$(M$,2)
1501 RETURN
2100 DATA0,4,,,,,,,3,,,,,200,,2,,,1,8,5,,,201,,8,6,4,,,200,9,10,5,,,,11,,,240,,5,,9,4,,,6,,10,8,,314
2190 DATA6,,,9,,,7,,,,,,,,,,,,16,12,17,17,,,,,15,,9,,15,19,15,14,,,13,22,20,21,,,13,20,16,16,,,,,19,17,,0
2280 DATA15,,,203,,,13,17,17,16,,,16,23,13,20,,,17,21,20,16,,,21,26,,,,,,,,23,,,,,,24,,,23,27,,,,,26,,,,,0
2600 DATA"A CARROT",CARRO,0,"A LAMP",LAMP,2,"A BUCKET",BUCKE,11,"A SILVER CROSS",CROSS,0,"A TORCH",TORCH,14
2650 DATA"A KEY",KEY,18,"A SCRAP OF PAPER",PAPER,8,"A QUILL PEN",PEN,-1,"AN APPLE",APPLE,-1
