0 CLS:CLEAR1000:DIMC1,CC,M$:GOTO22
2 GOSUB1500:C1=1:CC=32
4 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO4
6 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN4
8 RETURN
10 GOSUB2:GOTO42
12 GOSUB2:GOSUB16:GOTO52
14 M$=L$:GOTO20
16 FORCC=1TO11:C$(CC)="":NEXT:MO=MO-1:RETURN
18 FORCC=1TO32:PRINT"-";:NEXT:PRINT"SCORE:"SC,"MOVES:"MO:RETURN
20 GOSUB2:RETURN
22 NO=9:R=5:ND=6:NR=27:MO=0:NI=2
24 CLS:DIMR$(NR),O$(NO),O2$(NO),O(NO),MA(28,6),C$(11)
26 PRINT@6*32,"THE NIGHT OF THE VAMPIRE BUNNIES":PRINT@9*32,TAB(10)"BY JASON DYER"
28 FORCC=1TO7:READM$:NEXT:FORI=1TONR:FORJ=0TOND-1:READMA(I,J):NEXT:NEXT:FORI=0TONO-1:READO$(I),O2$(I),O(I):NEXT
34 CLS
36 GOSUB186
38 IFFFTHENFF=0:GOTO212
40 GOSUB182
42 IFVATHENVT=VT+1:IFVT=2THENM$="THE VAMPIRE DECIDES TO FANG YOU."GOSUB2:GOTO210
44 IFR=25ANDNOTDBTHENRA=RA+1:GOTO48
46 RA=0
48 IFR=25ANDNOTDBTHENIFRA=3THENM$="THE VAMPIRE BUNNY NIBBLES A LITTLE ON YOUR LEG.":GOSUB2:GOTO210
50 IFC$(1)<>""THENC$=C$(1):BF=0:GOTO78
52 GOSUB18:INPUTC$:IFC$=""THENPRINT"HUH?":GOTO52
54 FORI=1TOLEN(C$)
56 IFMID$(C$,I,4)="THE "THENG$=LEFT$(C$,I-1):GG$=MID$(C$,I+4):C$=G$+GG$:GOTO56
58 NEXT:C=0
60 Z=LEN(C$):L=0:FORI=1TOZ
62 L=L+1
64 IFMID$(C$,L,3)="AND"THENC=C+1:C$(C)=LEFT$(C$,L-2):G$=MID$(C$,L+4):C$=G$:L=1
66 IFMID$(C$,L,4)="THEN"THENC=C+1:C$(C)=LEFT$(C$,L-2):G$=MID$(C$,L+5):C$=G$:L=1
68 IFMID$(C$,L,1)="."THENIFLEN(C$)<>LTHENC=C+1:C$(C)=LEFT$(C$,L-1):G$=MID$(C$,L+1):C$=G$:L=1
70 IFMID$(C$,L,2)="  "THENG$=LEFT$(C$,L-1):GG$=MID$(C$,L+3):C$=G$+GG$:GOTO70
72 IFLEFT$(C$,1)=" "THENC$=RIGHT$(C$,LEN(C$)-1)
74 IFRIGHT$(C$,1)="."THENC$=LEFT$(C$,LEN(C$)-1)
76 NEXT:C$(C+1)=C$:IFC$(1)<>""THENC$=C$(1)
78 BF=0:FORI=2TO10:C$(I-1)=C$(I):C$(I)="":NEXT
80 C=0:V$="":N$="":W1$="":W2$=""
82 C=C+1:IFC>LEN(C$)THEN88
84 W$=MID$(C$,C,1):IFW$=" "THEN88
86 V$=V$+W$:GOTO82
88 C=C+1:IFC>LEN(C$)THEN94
90 W$=MID$(C$,C,1):IFW$=" "THEN94
92 N$=N$+W$:GOTO88
94 C=C+1:IFC>LEN(C$)THEN100
96 W$=MID$(C$,C,1):IFW$=" "THEN100
98 W1$=W1$+W$:GOTO94
100 C=C+1:IFC>LEN(C$)THEN106
102 W$=MID$(C$,C,1):IFW$=" "THEN106
104 W2$=W2$+W$:GOTO100
106 IFV$=""THENPRINT"HUH?":GOTO52
108 IFLEN(V$)>5THENV$=LEFT$(V$,5)
110 IFLEN(N$)>5THENN$=LEFT$(N$,5)
112 IFLEN(W1$)>5THENW1$=LEFT$(W1$,5)
114 IFLEN(W2$)>5THENW2$=LEFT$(W2$,5)
116 IFV$="G"ORV$="AGAIN"THENC$=V4$:V$=V1$:N$=VV$:W1$=V2$:W2$=V3$
118 IFN$="IT"THENN$=VV$
120 VV$=N$:V4$=C$:V1$=V$:V2$=W1$:V3$=W2$
122 IFN$="SILVE"THENN$="CROSS"
124 IFN$="SCRAP"ORN$="SHEET"THENN$="PAPER"
126 IFW1$="SCRAP"ORW1$="SHEET"THENW1$="PAPER"
128 IFN$="QUILL"THENN$="PEN"
130 IFW1$="QUILL"THENW1$="PEN"
132 IFN$="GRAY"THENN$="OOZE"
134 IFN$="WOODE"THENN$="CRANK"
136 IFN$="WEBS"THENN$="COBWE"
138 IFW2$="GRAY"THENW2$="OOZE"
140 IFW1$="SILVE"THENS1$="CROSS"
142 MO=MO+1:GOTO144
144 IFR<11ANDR<>7THEN156
146 IFO(4)=-1ORO(4)=RTHEN156
148 IFNOTLATHEN152
150 IFO(1)=-1ORO(1)=RTHEN156
152 IFC$<>"LIGHTLAMP"ANDC$<>"TURN ON LAMP"THENM$="*IN TOTAL DARKNESS.":GOSUB2
154 FORTEMP=1TO11:C$(TEMP)="":NEXT:GOTO52
156 IFC$="TALK INTO WELL"ORC$="TALK TO WELL"ORC$="TALK WELL"THEN160
158 GOTO162
160 IFR=4THENM$="ECHO... ECHO... ECHO...":GOTO10
162 IFV$="PUT"ANDN$="ON"THENV$="WEAR":N$=W1$
164 IFV$="GET"ANDN$="ON"THENV$="WEAR":N$=W1$
166 IFV$="TAKE"ANDN$="OFF"THENV$="UNWEA":N$=W1$
168 IFV$="GET"ANDN$="OFF"THENV$="UNWEA":N$=W1$
170 IFV$="THROW"ANDN$="WATER"THENV$="POUR"
172 IFV$="THROW"ANDN$="BUCKE"THENV$="POUR"
174 IFVAANDV$="POUR"THENIFN$="WATER"ORN$="BUCKE"THENW1$="ON":W2$="VAMPI"
176 IFV$="TAKE"ANDW1$="OFF"THENV$="UNWEA"
178 IFV$="GET"ANDW1$="OFF"THENV$="UNWEA"
180 GOTO234
182 FORI=0TONO-1:IFO(I)=RTHENM$="YOU SEE "+O$(I)+" HERE.":GOSUB2
184 NEXT:RETURN
186 IFR<11ANDR<>7THEN196
188 IFO(4)=-1ORO(4)=RTHEN196
190 IFNOTLATHEN194
192 IFO(1)=-1ORO(1)=RTHEN196
194 L$="DARKNESS":RETURN
196 IFR=100THENL$="OUTSIDE GRAVEYARD":GOTO878
198 ONRGOSUB916,934,940,946,954,960,966,972,980,990,996,1002,1006,1008,1014:IFR<=15THENRETURN
200 ONR-15GOSUB1018,1020,1022,1028,1036,1038,1040,1042,1050,1056,1066,1070:RETURN
202 IFNOTLATHEN206
204 IFO(1)=-1ORO(1)=RTHENRETURN
206 L$="DARKNESS":M$="UH-OH, NOW YOU CAN'T SEE!":GOTO20
208 M$=" *** YOU HAVE DIED ***":GOTO20
210 GOSUB208:GOSUB886:GOTO476
212 GOSUB208:GOSUB886:M$="NEW GAME OR UNDO (Y/N/U)?":GOSUB2
214 I$=INKEY$:IFI$<>"Y"ANDI$<>"N"ANDI$<>"U"THEN214
216 IFI$="Y"THENRUN
218 IFI$="U"THENGOSUB16:GOTO36
220 END
222 FORI=0TONO-1:IFO2$(I)=N$THENFL=1:RO=O(I):RETURN
228 NEXT:FL=0:RETURN
234 IFV$<>"GO"ANDV$<>"RUN"ANDV$<>"CRAWL"ANDV$<>"N"ANDV$<>"S"ANDV$<>"CLIMB"ANDV$<>"E"ANDV$<>"W"ANDV$<>"U"ANDV$<>"D"THEN306
236 IFN$="NORTH"ORV$="N"THENDI=0:GOTO276
238 IFN$="SOUTH"ORV$="S"THENDI=1:GOTO276
240 IFN$="EAST"ORV$="E"THENDI=2:GOTO276
242 IFN$="WEST"ORV$="W"THENDI=3:GOTO276
244 IFN$="UP"ORV$="U"THENDI=4:GOTO276
246 IFN$="DOWN"ORV$="D"THENDI=5:GOTO276
248 IFR=1ANDN$="COFFI"THENM$="ARE YOU CRAZY?":GOTO10
250 IFR=3THENIFN$="STREA"ORN$="RIVER"THENV$="S":GOTO234
252 IFR=6THENIFN$="STREA"ORN$="RIVER"THENV$="N":GOTO234
254 IFR=9ANDN$="HOLE"THENV$="D":GOTO234
256 IFR=4ANDN$="WELL"THENV$="D":GOTO234
258 IFR=7ANDN$="CREVI"THENV$="S":GOTO234
260 IFR=8ANDN$="HOLE"THENV$="D":GOTO234
262 IFR=7ANDN$="ROPE"THENDI=4:GOTO276
264 IFR=4ANDN$="ROPE"ANDROTHENDI=5:GOTO276
266 IFR=27ANDN$="HOLE"THENV$="U":GOTO234
268 IFR=14ANDN$="HOLE"THENV$="U":GOTO234
270 IFR=2ANDN$="HOLE"THENV$="E":GOTO234
272 IFR=3ANDN$="HOLE"THENV$="W":GOTO234
274 M$="YOU CAN'T GO THAT WAY!":GOTO12
276 IFMA(R,DI)>0ANDMA(R,DI)<128THENR=MA(R,DI):GOTO36
278 IFMA(R,DI)=203THENM$="THE OOZE BLOCKS YOUR PATH.":GOTO10
280 IFMA(R,DI)=204THENM$="THE BUNNY WON'T LET YOU!":GOTO10
282 IFMA(R,DI)=200THENM$="YOU ENTER THE STREAM. YOU CHOKE AND DROWN.":GOSUB2:GOTO212
284 IFMA(R,DI)=201ANDNOTROTHEN274
286 IFMA(R,DI)=201ANDNOTLATHENM$="IT'S TOO DARK DOWN THERE.":GOTO10
288 IFMA(R,DI)=201THENIFO(1)<>7ANDO(1)<>-1THENM$="IT'S TOO DARK DOWN THERE.":GOTO10
290 IFMA(R,DI)=240ANDNI>1THENM$="*TOO HEAVY TO CLIMB UP.":GOTO10
292 IFMA(R,DI)=240THENR=4:M$="YOU CLIMB UP THE ROPE.":GOSUB2:GOTO36
294 IFMA(R,DI)=201ANDNI>1THENM$="THE ROPE SNAPS AND YOU PLUNGE DOWN.":GOSUB2:GOTO212
296 IFMA(R,DI)=201THENR=7:M$="YOU MAKE YOUR WAY DOWN THE ROPE.":GOSUB2:GOTO36
298 IFMA(R,DI)=700THENM$="THE VAMPIRE, UNFORTUNATELY, GIVES YOU A NASTY BITE.":GOSUB2:GOTO210
300 IFMA(R,DI)=314ANDNOTWCTHENM$="THE SKELETON PUSHES YOU AWAY FROM THE HOLE.":GOTO10
302 IFMA(R,DI)=314THENM$="THE SKELETON ALLOWS YOU TO ENTER.":GOSUB2:R=14:GOTO36
304 GOTO274
306 IFV$<>"GET"ANDV$<>"TAKE"THEN324
308 ZH=0:GOSUB222:IFN$="WATER"THENIFO(2)=-1THENV$="FILL":N$="BUCKE":GOTO234
310 IFFL=0THENM$="YOU CAN'T GET THAT!":GOTO12
312 IFRO=-1THENM$="YOU ALREADY HAVE THAT!":GOTO12
314 IFRO<>RTHENM$="YOU CAN'T GET THAT!":GOTO12
316 IFNI=5THENM$="*CARRYING TOO MUCH!":GOTO12
318 IFNOTCRANDI=3THENCR=-1:SC=SC+10
320 IFNOTCAANDI=0THENCA=-1:SC=SC+2
322 NI=NI+1:O(I)=-1:PRINT"TAKEN.":GOTO42
324 IFV$="HINT"ORV$="HELP"THENPRINT"SORRY."
326 IFV$="TURN"ANDW1$="OFF"THENV$="UNLIG"
328 IFV$="TURN"ANDW1$="ON"THENV$="LIGHT"
330 IFV$="TURN"ANDN$="OFF"THENV$="UNLIG":N$=W1$
332 IFV$="TURN"ANDN$="ON"THENV$="LIGHT":N$=W1$
334 IFV$="PUT"ANDN$="DOWN"THENV$="DROP":N$=W1$:GOTO306
336 IFV$="PICK"ANDN$="UP"THENV$="TAKE":N$=W1$:GOTO306
338 IFV$<>"DROP"THEN352
340 IFW1$<>""THENV$="PUT":GOTO234
342 GOSUB222
344 IFFL=0THENM$="YOU DON'T HAVE THAT!":GOTO12
346 IFRO=-2THENRO=-1
348 IFRO<>-1THENM$="YOU DON'T HAVE THAT!":GOTO12
350 NI=NI-1:O(I)=R:PRINT"DROPPED.":GOTO42
352 IFV$<>"INVEN"ANDV$<>"I"THEN382
354 FL=0:PRINT"CARRYING:"
356 IFWCTHENM$="A SILVER CROSS (BEING WORN)":GOSUB2:FL=1
358 IFBUANDO(2)=-1THENM$="A BUCKET (WITH WATER)":GOSUB2:FL=1
360 IFLAANDO(1)=-1THENM$="A LAMP (PROVIDING LIGHT)":GOSUB2:FL=1
362 IFO(4)=-1THENM$="A TORCH (PROVIDING LIGHT)":GOSUB2:FL=1
364 FORI=0TONO-1
366 IFWCANDI=3THEN376
368 IFBUANDI=2ANDO(I)=-1THEN376
370 IFLAANDI=1ANDO(I)=-1THEN376
372 IFI=4ANDO(I)=-1THEN376
374 IFO(I)=-1THENM$=O$(I):GOSUB2:FL=1
376 NEXT
378 IFFL=0THENPRINT"NOTHING"
380 GOTO42
382 IFV$<>"LOOK"ANDV$<>"L"THEN388
384 IFN$<>""THEN390
386 GOTO36
388 IFV$<>"EXAMI"ANDV$<>"SEARC"THEN466
390 IFN$="FIELD"ANDR<>5THENM$="THERE ISN'T A FIELD HERE!":GOTO12
392 IFN$="GROUN"ORN$="FLOOR"ORN$="FIELD"THENIFR=5ANDNOTCATHENCA=-1:O(0)=5:M$="YOU FIND A CARROT.":GOTO10
394 IFN$="GROUN"ORN$="FLOOR"ORN$="FIELD"THENM$="YOU SEE NOTHING UNUSUAL.":GOTO10
396 IFN$="COFFI"ANDR=1THENM$="IT IS CARVED OF MARBLE.":GOTO10
398 IFN$="SKELE"ANDR=9THENM$="IT LOOKS PRETTY BONY.":GOTO10
400 IFN$="WELL"ANDR=4THENM$="IT HAS A POLE CONNECTED TO SOME ROPE AND A WOODEN CRANK. THE WELL IS DARK.":GOTO10
402 IFN$="ROPE"THENIFR=4ORR=7THENM$="IT DOESN'T LOOK VERY STURDY.":GOTO10
404 IFN$="WELL"ANDR=7THENM$="IT IS FULL OF CRACKS.":GOTO10
406 IFN$="OOZE"ANDR=19ANDNOTDGTHENM$="IT IS BIG, UGLY, AND SLIMY.":GOTO10
408 IFN$="COBWE"ANDNOTBCTHENM$="THE ARE BLOCKING THE EAST PASSAGE.":GOTO10
410 IFN$="LEVER"ANDR=26THENM$="IT IS RUSTY, BUT USEABLE.":GOTO10
412 IFN$="BUNNY"ANDNOTDBANDR=25THENM$="IT LOOKS EXTREMELY VICIOUS FOR ITS SMALL SIZE.":GOTO10
414 IFN$="VAMPI"ANDVATHENM$="IT LOOKS AT YOU WITH AN UNNERVING GAZE.":GOTO10
416 IFN$="TOMBS"ORN$="GRAVE"THENIFR<11ANDR<>7THENM$="MANY ARE OLD AND ROTTED, AND SOME ARE BLANK.":GOTO10
418 IFN$="CRANK"ANDR=4THENM$="IT IS CRACKING, BUT USABLE.":GOTO10
420 IFN$="STREA"ORN$="RIVER"THENIFR=6ORR=3THENM$="IT LOOKS LIFELESS AND SHALLOW ENOUGH TO CROSS.":GOTO10
422 IFN$="POLE"ANDR=4THENM$="IT HAS MANY HOLES IN IT.":GOTO10
424 IFN$="MIRRO"ANDR=24THEN428
426 GOTO434
428 IFBRTHENM$="IT IS BROKEN INTO SHARDS, AND THERE IS A PASSAGE BEHIND IT.":GOTO10
430 M$="IT IS NOT WELL POLISHED.":GOTO10
432 IFN$="WALLS"ORN$="WALL"THENM$="THERE ARE ENOUGH OF THEM.":GOTO10
434 GOSUB222
436 IFRO<>RANDRO<>-1ANDO(I)<>(R-128)THENM$="THAT'S NOT HERE!":GOTO12
438 IFN$="CARRO"THENM$="IT LOOKS NICE AND JUICY.":GOTO10
440 IFN$<>"LAMP"THEN446
442 IFLATHENM$="THE LAMP IS ON.":GOTO10
444 M$="THE LAMP IS OFF.":GOTO10
446 IFN$<>"BUCKE"THEN452
448 IFBUTHENM$="THE BUCKET IS FILLED.":GOTO10
450 M$="THE BUCKET IS EMPTY.":GOTO10
452 IFN$="CROSS"THENM$="IT IS ROUGH AND TARNISHED.":GOTO10
454 IFN$="PAPER"THENM$="IT HAS SOME WRITING ON IT.":GOTO10
456 IFN$="KEY"THENM$="IT IS FAIRLY SHINY.":GOTO10
458 IFN$="TORCH"THENM$="IT IS BURNING FAIRLY SLOWLY.":GOTO10
460 IFN$="PEN"THENM$="IT DOES NOT HAVE ANY INK.":GOTO10
462 IFN$="APPLE"THENM$="IT LOOKS A LITTLE TOO WORMY TO BE EDIBLE.":GOTO10
464 M$="YOU SEE NOTHING UNUSUAL.":GOTO10
466 IFV$<>"QUIT"ANDV$<>"Q"ANDV$<>"END"THEN484
468 GOSUB886
470 M$="SURE YOU WANT TO QUIT (Y/N)":GOSUB2:INPUTM$
472 IFM$="N"THENGOSUB16:GOTO52
474 IFM$<>"Y"THEN470
476 M$="PLAY AGAIN (Y/N)?":GOSUB2
478 I$=INKEY$:IFI$<>"Y"ANDI$<>"N"THEN478
480 IFI$="Y"ORI$="Y"THENRUN
482 END
484 IFV$<>"ENTER"THEN488
486 V$="GO":GOTO234
488 IFV$<>"JUMP"ANDV$<>"LEAP"THEN508
490 IFN$=""THENPRINT"WHEEEEEE!":GOTO42
492 IFN$<>"OVER"THENM$="THAT WON'T DO ANY GOOD.":GOTO12
494 IFR=19ANDNOTDGANDW1$="OOZE"ORW1$="GREY"THENM$="THE OOZE EXPANDS AND PREVENTS YOUR JUMP.":GOTO12
496 IFR=25ANDNOTDBANDW1$="BUNNY"ORW1$="RABBI"THENM$="THE BUNNY MANAGES TO BITE YOUR LEG.":GOSUB2:GOTO212
500 IF(W1$<>"STREA"ANDW1$<>"RIVER")OR(R<>3ANDR<>6)THENM$="THAT WON'T DO ANYTHING!":GOTO10
502 IFR=3THENR=6:GOTO506
504 R=3
506 M$="YOU LEAP OVER THE STREAM.":GOSUB2:GOTO36
508 IFV$<>"READ"THEN516
510 IFN$<>"PAPER"THEN699
512 IFO(6)<>-1THENM$="YOU DON'T HAVE THAT!":GOTO12
514 M$="IT SAYS, 'PUSHING IS BETTER THAN PULLING'":GOTO10
516 IFV$<>"LIGHT"THEN542
518 IFN$="COBWE"THEN526
520 GOSUB222:IFFL=0THENM$="YOU DON'T HAVE THAT!":GOTO12
522 IFO(I)<>-1THENM$="YOU DON'T HAVE THAT!":GOTO12
524 IFN$<>"TORCH"ANDN$<>"COBWE"ANDN$<>"LAMP"THEN699
526 IFN$="COBWE"ANDR=23ANDBC=0THENSC=SC+5:BC=-1:MA(23,2)=24:M$="THE COBWEBS LIGHT AND BURN.":GOTO10
528 IFN$="COBWE"THEN699
530 IFN$="TORCH"ANDO(4)=-1THENM$="IT'S ALREADY LIT!":GOTO10
532 IFN$="LAMP"ANDO(1)=-1ANDLATHENM$="IT'S ALREADY LIT!":GOTO10
534 IFN$="LAMP"ANDNOTLATHEN538
536 GOTO542
538 M$="THE LAMP LETS OUT A WARM GLOW.":GOSUB2:LA=-1:IFL$="DARKNESS"THEN36
540 GOTO42
542 IFV$<>"LOWER"THEN552
544 IFN$<>"ROPE"ANDN$<>"WELL"THEN699
546 IFR<>4THEN699
548 IFROTHENM$="IT ALREADY IS!":GOTO12
550 RO=-1:M$="THE ROPE GOES DOWN INTO THE WELL.":GOTO10
552 IFV$<>"RAISE"ANDV$<>"LIFT"THEN562
554 IFN$<>"ROPE"ANDN$<>"WELL"THEN699
556 IFR<>4THEN699
558 IFNOTROTHENM$="IT ALREADY IS!":GOTO12
560 RO=0:M$="THE ROPE RISES FROM THE WELL.":GOTO10
562 IFV$<>"FILL"THEN570
564 IFN$<>"BUCKE"ORO(2)<>-1OR(R<>3ANDR<>6)THEN699
566 IFBUTHENM$="IT'S ALREADY FULL!":GOTO12
568 BU=-1:M$="THE BUCKET FILLS WITH WATER.":GOTO10
570 IFV$<>"UNLIG"THEN584
572 GOSUB222:IFFL=0THENM$="YOU DON'T HAVE THAT!":GOTO12
574 IFO(I)<>-1THENM$="YOU DON'T HAVE THAT!":GOTO12
576 IFN$<>"TORCH"ANDN$<>"LAMP"THEN699
578 IFN$="TORCH"ANDO(4)=-1THENM$="IT REFUSES TO GO OUT.":GOTO10
580 IFN$="LAMP"ANDO(1)=-1ANDNOTLATHENM$="IT ALREADY IS!":GOTO10
582 IFN$="LAMP"ANDO(1)=-1THENLA=0:M$="THE LAMP TURNS OFF WITH A CLICK.":GOTO10
584 IFV$<>"OPEN"THEN594
586 IFN$<>"COFFI"THEN699
588 IFR<>1THEN699
590 IFOCTHENM$="IT'S ALREADY OPEN!":GOTO12
592 OC=-1:VA=-1:MA(1,1)=700:M$="AS YOU OPEN THE COFFIN, A VAMPIRE REACHES OUT!":GOTO10
594 IFV$<>"CLOSE"THEN606
596 IFN$<>"COFFI"THEN699
598 IFR<>1THEN699
600 IFNOTOCTHENM$="IT'S NOT OPEN!":GOTO12
602 IFVATHENVT=0:VA=0:OC=0:MA(1,1)=4:M$="THE COFFIN SLAMS SHUT IN THE VAMPIRE'S FACE.":GOTO10
604 OC=0:M$="THE COFFIN CLOSES.":GOTO10
606 IFV$<>"POUR"ANDV$<>"EMPTY"ANDV$<>"SPLAS"THEN630
608 IFN$<>"WATER"ANDN$<>"BUCKE"THEN699
610 IFO(2)<>-1THENM$="YOU DON'T HAVE THAT!":GOTO12
612 IFNOTBUTHEN699
614 IFW2$=""THENBU=0:M$="YOU EMPTY THE BUCKET.":GOTO10
616 IFW2$="OOZE"ORW2$="GREY"ANDR=19ANDDG=0THENM$="THE OOZE GROWS AND SPLATTERS YOU WITH A DEADLY FORCE.":GOSUB2:GOTO212
618 IFW2$="RABBI"ORW2$="BUNNY"ANDR=24ANDDB=0THENBU=0:M$="THE BUNNY MOVES, AVOIDING THE SPLASH OF WATER.":GOTO10
620 IFW2$="APPLE"THENIFO(8)=-1ORO(8)=RTHENBU=0:AP=-1:M$="THE APPLE IS SPLASHED WITH WATER.":GOTO10
622 IFW2$="CARRO"THENIFO(0)=-1ORO(0)=RTHENSC=SC+10:BU=0:CS=-1:M$="THE CARROT IS SPLASHED.":GOTO10
624 IFW2$="SKELE"ANDR=9THENBU=0:M$="THE WATER FLIES THROUGH THE SKELTAL BODY.":GOTO10
626 IFW2$="VAMPI"ANDVATHENMA(1,1)=4:BU=0:VA=0:O(3)=1:M$="THE VAMPIRE SCREAMS AND DISSOLVES.":GOTO10
628 M$="I'M NOT SO SURE ABOUT THAT.":GOTO12
630 IFV$<>"WEAR"THEN640
632 IFN$<>"CROSS"THEN699
634 IFO(3)<>-1THENM$="YOU DON'T HAVE THAT!":GOTO12
636 IFWCTHENM$="*ALREADY WEARING THE CROSS!":GOTO12
638 WC=-1:M$="IT FITS RATHER NICE.":GOTO10
640 IFV$="PUT"ANDW1$="ON"THENIFN$="CROSS"THENV$="WEAR":GOTO630
642 IFV$="GET"ANDW1$="ON"THENIFN$="CROSS"THENV$="WEAR":GOTO630
644 IFV$="GET"ANDW1$="OFF"THENV$="UNWEAR":GOTO234
646 IFV$="TAKE"ANDW1$="OFF"THENV$="UNWEAR":GOTO234
648 IFV$<>"PUT"THEN716
650 IFW1$<>"ON"ANDW1$<>"ONTO"THEN664
652 IFN$="WATER"ORN$="BUCKE"THENV$="POUR":GOTO234
654 IFN$<>"TORCH"THEN662
656 IFO(4)<>-1THENM$="YOU DON'T HAVE THAT!":GOTO12
658 IFW2$="GREY"ORW2$="OOZE"THEN864
660 IFW2$="COBWE"ANDR=23ANDNOTBCTHEN872
662 M$="THAT PROBABLY WON'T DO MUCH.":GOTO12
664 IFW1$<>"IN"ANDW1$<>"INTO"THEN714
666 IFN$="WATER"ANDW2$="BUCKE"THENV$="FILL":N$="BUCKE":GOTO234
668 IFW2$="OOZE"ORW2$="GREY"THEN658
670 IFW2$="COBWE"THEN658
672 IFW2$<>"WELL"THEN682
674 IFR<>4THENM$="THAT'S NOT HERE!":GOTO12
676 GOSUB222:IFFL=0THENM$="YOU DON'T HAVE THAT!":GOTO12
678 IFRO<>-1THENM$="YOU DON'T HAVE THAT!":GOTO12
680 O(I)=7:NI=NI-1:PRINT"ENUOOOOOAUUUOOOCLUNK!":GOTO42
682 IFW2$<>"STREA"ANDW2$<>"WATER"ANDW2$<>"BUCKE"ANDW2$<>"RIVER"THEN714
684 IFW2$="BUCKE"THENIF((O(2)<>-1)AND(O(2)<>R))THENM$="YOU CAN'T SEE ANY BUCKET HERE!":GOTO12
686 IFW2$="BUCKE"ANDNOTBUANDN$="WATER"THENV$="GET":GOTO234
688 IFW2$="BUCKE"ANDNOTBUTHENM$="UNFORTUNATELY, THAT'S NOT GOING TO BE VERY HELPFUL.":GOTO12
690 IFW2$="WATER"THENIFR=3ORR=6THEN700
692 IFW2$="WATER"THEN696
694 GOTO700
696 IFO(2)=-1ORBUTHEN700
698 M$="THAT'S NOT HERE!":GOSUB2:GOSUB16:GOTO42
699 M$="YOU CAN'T DO THAT NOW!":GOTO12
700 IFW2$="STREA"ORW2$="RIVER"THENIFR<>3ANDR<>6THENM$="YOU DON'T SEE ANY!":GOTO12
702 GOSUB222:IFFL=0THENM$="YOU DON'T HAVE THAT!":GOTO12
704 IFO(I)<>-1THENM$="YOU DON'T HAVE THAT!":GOTO12
706 IFN$="CARRO"THENIFR=3ORR=6OR(O(2)=-1ANDBU)THENSC=SC+10:CS=-1:M$="THE CARROT SOAKS UP THE WATER.":GOTO10
710 IFN$="APPLE"THENIFR=3ORR=6OR(O(2)=-1ANDBU)THENAP=-1:M$="THE APPLE SOAKS UP THE WATER.":GOTO10
714 M$="THAT'S NOT GOING TO DO MUCH FOR THIS ADVENTURE.":GOTO12
716 IFV$<>"PUSH"THEN730
718 IFN$="COFFI"ANDR=1THENM$="IT IS MUCH TOO HEAVY.":GOTO10
720 IFN$="MIRRO"ANDR=24ANDNOTBRTHENM$="IT IS ATTACHED TO THE WALL.":GOTO10
722 IFN$="THRON"ANDR=25THENM$="IT IS BOLTED TO THE WALL.":GOTO10
724 IFN$<>"LEVER"ORR<>26THENM$="YOU DON'T NEED TO DO THAT.":GOTO12
726 IFPLTHENM$="NOTHING HAPPENS.":GOTO10
728 MA(8,5)=27:PL=-1:MA(27,4)=8:M$="YOU HEAR A LOUD 'CLUNK' TO THE SOUTH.":GOTO10
730 IFV$<>"PULL"THEN738
732 IFN$<>"LEVER"THEN718
734 IFR<>26THEN699
736 M$="THE ROOF CAVES IN ON YOU.":GOSUB2:GOTO212
738 IFV$<>"TURN"THEN748
740 IFN$<>"CRANK"THEN699
742 IFR<>4THEN699
744 IFROTHENRO=0:M$="THE ROPE RISES FROM THE WELL.":GOTO10
746 RO=-1:M$="THE POLE TURNS, AND THE ROPE GOES DOWN INTO THE WELL.":GOTO10
748 IFV$="WAIT"ORV$="Z"THENM$="TIME PASSES...":GOTO10
750 IFV$<>"EAT"ANDV$<>"TASTE"THEN766
752 GOSUB222:IFFL=0THENM$="YOU CAN'T DO THAT NOW!":GOSUB2:GOSUB16:GOTO42
754 IFO(I)<>-1THENM$="YOU CAN'T DO THAT NOW!":GOSUB2:GOSUB16:GOTO42
756 IFI<>0ANDI<>8THENM$="YOU CAN'T EAT THAT!":GOSUB2:GOSUB16:GOTO42
758 IFI=8ANDAPTHENM$="YOU TAKE A BITE. YOU GASP, FALL DOWN AND THE APPLE FALLS FROM YOUR LIMP HAND.":GOSUB2:GOTO212
760 IFI=0ANDCSTHENM$="YOU MUNCH A LITTLE. YOU COLLAPSE WITH THE CARROT CLUTCHED IN YOUR HAND.":GOSUB2:GOTO212
762 IFI=8THENM$="YOU TAKE A BITE OUT OF THE APPLE AND SPIT IT OUT.":GOTO10
764 O(0)=0:M$="MMMMMNN... THE CARROT IS DELICIOUS. YOU GOBBLE IT UP.":GOTO10
766 IFV$="SOAK"ANDW1$=""THENV$="PUT":W1$="IN":W2$="WATER":GOTO234
768 IFV$="SOAK"THENV$="PUT":GOTO234
770 IFV$<>"BREAK"ANDV$<>"SMASH"THEN780
772 IFN$<>"MIRRO"THENM$="THERE'S EITHER NO POINT OR YOU DON'T HAVE THE OBJECT.":GOTO12
774 IFR<>24THENM$="THERE'S EITHER NO POINT OR YOU DON'T HAVE THE OBJECT.":GOTO12
776 IFBRTHENM$="IT'S ALREADY BROKEN!":GOTO12
778 BR=-1:SC=SC+8:MA(24,2)=25:M$="THE MIRROR BREAKS, REVEALING A PASSAGE TO THE EAST.":GOTO10
780 IFV$<>"GIVE"THEN800
782 IFW1$="TO"THENW1$=N$:N$=W2$
784 IFN$<>"BUNNY"THEN798
786 IFR<>25ORDBTHENM$="I DON'T SEE A BUNNY-- DO YOU?":GOTO12
788 N$=W1$:GOSUB222:IFFL=0THENM$="YOU DON'T HAVE THAT!":GOTO12
790 IFO(I)<>-1THENM$="YOU DON'T HAVE THAT!":GOTO12
792 IFN$<>"CARRO"THENM$="THE BUNNY REFUSES YOUR OFFER.":GOTO12
794 IFNOTCSTHENO(0)=0:M$="THE BUNNY GOBBLES UP THE CARROT BUT IS NOT SATISFIED.":GOTO10
796 IFCSTHENSC=SC+10:DB=-1:M$="THE BUNNY GOBBLES THE CARROT UP, GASPS, DIES AND THEN DISAPPEARS.":GOTO10
798 M$="I'M NOT SURE IT WILL ACCEPT YOUR OFFER.":GOTO12
800 IFV$<>"SIT"THEN810
802 IFN$="ON"THENN$=W1$
804 IFN$<>"THRON"ORR<>25THENM$="THAT PROBABLY WON'T WORK.":GOTO12
806 IFSTTHENM$="YOU SIT ON THE THRONE AND NOTHING HAPPENS SO YOU STAND UP AGAIN.":GOTO10
808 SC=SC+2:ST=-1:MA(25,2)=100:M$="AS YOU SIT ON THE THRONE, A PASSAGE OPENS TO THE EAST!":GOTO10
810 IFV$="BURN"THENV$="LIGHT":GOTO234
812 IFV$="NORTH"THENV$="N":GOTO234
814 IFV$="SOUTH"THENV$="S":GOTO234
816 IFV$="EAST"THENV$="E":GOTO234
818 IFV$="WEST"THENV$="W":GOTO234
820 IFV$="UP"THENV$="U":GOTO234
822 IFV$="DOWN"THENV$="D":GOTO234
824 IFV$<>"UNWEA"THEN832
826 IFN$<>"CROSS"THENM$="THAT DOESN'T MAKE SENSE!":GOTO12
828 IFNOTWCTHENM$="*NOT WEARING IT!":GOTO12
830 WC=0:M$="YOU SLIDE THE CROSS OFF YOUR NECK.":GOTO10
832 IFV$="DESTR"ANDN$="MIRRO"THENV$="BREAK":GOTO234
834 IFV$<>"DESTR"ANDV$<>"KILL"ANDV$<>"ATTAC"THEN846
836 IFN$="SKELE"ANDR=9THENM$="THE SKELETON REPELS YOUR ATTACK.":GOTO10
838 IFN$="VAMPI"ANDVATHENM$="THE VAMPIRE IGNORES YOUR ATTACK.":GOTO10
840 IFN$="OOZE"ANDNOTDGANDR=19THENM$="THE OOZE LETS OUT A SPLATTER WITH KILLING FORCE.":GOSUB2:GOTO212
842 IFN$="BUNNY"ANDNOTDBANDR=25THENM$="THE BUNNY BITES YOU IN DEFENSE.":GOSUB2:GOTO212
844 M$="THAT'S NOT IMPORTANT TO THIS ADVENTURE.":GOTO12
846 IFV$="TALK"THENM$="THAT WON'T DO MUCH.":GOTO12
848 IFV$<>"DRINK"ANDV$<>"SIP"THEN860
850 IFN$<>"WATER"THENM$="THAT WON'T DO MUCH.":GOTO12
852 IFR=3ORR=6THEN858
854 IFO(2)=-1ORO(2)=RTHENIFBUTHEN858
856 M$="THAT WON'T DO MUCH.":GOTO12
858 M$="YOU SIP THE WATER, AND FEEL SICK. YOU LUNGS TURN INSIDE OUT AND YOU FALL ON THE FLOOR, DEAD.":GOSUB2:GOTO212
860 IFV$="SCORE"THENGOSUB886:GOSUB16:GOTO50
862 M$="SORRY, BUT I DON'T KNOW THAT VERB.":GOTO12
864 IFR=19ANDDG=0THEN868
866 GOTO660
868 DG=-1:MA(19,3)=18:O(4)=22:NI=NI-1
870 M$="THE OOZE DISSOLVES THE TORCH, LIGHTING IT INSTANTLY. THE OOZE MELTS AWAY INTO NOTHING.":GOSUB2:GOSUB202:GOTO42
872 BC=-1:MA(23,2)=24:SC=SC+5:M$="THE COBWEBS LIGHT AND BURN AWAY INTO DUST.":GOTO10
878 M$="YOU GO THROUGH THE TUNNEL AND END UP OUTSIDE THE GRAVEYARD. WHEN YOU REALIZE WHERE YOU ARE, YOU HURRY ALONG "
880 M$=M$+"THE PATH BACK INTO TOWN, OVERCOME WITH JOY, SHOUTING THE NEWS.":GOSUB2
882 M$="PEOPLE RUSH OUT OF THEIR HOUSES TO GREET YOU WITH A HERO'S WELCOME.":GOSUB2
884 GOSUB886:GOTO476
886 M$="YOU HAVE SCORED"+STR$(SC)+" OUT OF 60 POINTS.":GOSUB2
888 RESTORE:FORCC=1TO7:READM$:IFSC<CC*10+1THENCC=7
890 NEXT:M$="YOU HAVE REACHED THE RANK OF "+M$+".":GOTO20
916 L$="AT COFFIN":GOSUB14
918 M$="*AT A COFFIN IN A GRAVEYARD, WITH WALLS TO THE NORTH, EAST, AND WEST.":GOSUB2
920 E$="A BLUE MIST HANGS OVER THE GRAVEYARD. "
922 IFVATHENM=E$+"THE COFFIN IS OPEN WITH A VAMPIRE IN IT!":GOTO20
924 IFOCTHENM$=E$+"THE COFFIN IS OPEN.":GOSUB2:GOTO928
926 M$=E$+"THE COFFIN IS CLOSED.":GOSUB2
928 IFNOTOCTHENRETURN
930 FORI=0TO8:IFO(I)=.1THENM$="THERE IS "+O$(I)+" IN THE COFFIN.":GOSUB2
932 NEXT:RETURN
934 L$="GRAVEYARD":GOSUB14
936 M$="*IN A GRAVEYARD SURROUNDED BY WALLS. THERE IS FOG HERE SO DENSE YOU CAN BARELY SEE TWO METERS IN FRONT "
938 M$=M$+"OF YOU. A HOLE IN THE EAST WALL ALLOWS EXIT FROM THIS GLOOMY PLACE.":GOTO20
940 L$="GRAVEYARD":GOSUB14:IFNOTJRTHENJR=-1:SC=SC+3
942 M$="*IN A GRAVEYARD WITH A STREAM TO THE SOUTH. WALLS SURROUND THE OTHER DIRECTIONS, BUT A HOLE IN THE WEST "
944 M$=M$+"WALL ALLOWS PASSAGE. IT IS VERY COOL HERE.":GOTO20
946 L$="GRAVEYARD":GOSUB14
948 M$="*IN A DARK GRAVEYARD. A WALL COVERED WITH MOSS IS TO THE WEST, A COFFIN IS IN SIGHT TO THE NORTH, AND AN "
950 M$=M$+"EMPTY WELL LIES IN THE MIDDLE OF ALL THE FOG.":GOSUB2
952 IFROTHENM$="A ROPE LEADS DOWN THE DAMP WELL.":GOTO20
954 L$="CARROT FIELD":GOSUB14
956 M$="*IN A CARROT FIELD IN THE MIDDLE OF A GRAVEYARD. FOG IS ALL AROUND YOU. "
958 M$=M$+"A WALL IS TO THE NORTH, TOO HIGH TO CLIMB.":GOTO20
960 L$="GRAVEYARD":GOSUB14
962 M$="*IN A FOGGY GRAVEYARD. A STREAM IS TO THE NORTH AND A FIELD CAN BE SEEN TO THE WEST. "
964 M$=M$+"IN ALL OTHER DIRECTIONS ONLY FOG CAN BE SEEN. THE STREAM LOOKS SHALLOW ENOUGH TO CROSS.":GOTO20
966 L$="BOTTOM OF WELL":GOSUB14:IFNOTWETHENWE=-1:SC=SC+5
968 M$="*AT THE BOTTOM OF AN EMPTY WELL. THE SURROUNDING CRACKS ARE RATHER BIG, "
970 M$=M$+"AND ONE TO THE SOUTH IS BIG ENOUGH TO GO THROUGH. A ROPE HANGS DOWN ALLOWING YOU TO CLIMB UP.":GOTO20
972 L$="GRAVEYARD":GOSUB14
974 M$="*IN A WALLED GRAVEYARD. A BLACK WALL IS TO THE SOUTH. ":GOSUB2
976 IFPLTHENM$="A GAPING HOLE IS IN THE MIDDLE OF ALL THE GRAVESTONES.":GOTO20
978 M$="MANY TOMBSTONES ARE HERE-- SO MANY, IN FACT, YOU HAVE TO CLIMB OVER SOME OF THEM.":GOTO20
980 L$="AT HOLE":GOSUB14
982 M$="*IN A GLOOMY GRAVEYARD, WITH A WALL TO THE SOUTH, AND PLENTY OF FOG ALL AROUND YOU. "
984 M$=M$+"A LARGE HOLE SURROUNDED BY GRAVESTONES LEADS DOWN INTO GLOOM.":GOSUB2
986 IFWCTHENM$="A SKELETON GUARDING THE HOLE FLEES WHEN HE SEES THE CROSS YOU ARE WEARING.":GOTO20
988 M$="A SKELETON GUARDS THE HOLE AND DOES NOT ALLOW YOU TO ENTER.":GOTO20
990 L$="GRAVEYARD":GOSUB14
992 M$="*AT THE CORNER OF A WALLED GRAVEYARD. WALLS CONNECT TO THE SOUTH AND EAST, AND MIST HANGS OVER THE "
994 M$=M$+"SKY LIKE A PROWLER. A CHILL RUNS THROUGH YOUR BACK.":GOTO20
996 L$="CREVICE":GOSUB14
998 M$="*IN A LARGE CREVICE IN A WELL. SMALL AMOUNTS OF WATER TRICKLE DOWN THE SIDES. "
1000 M$=M$+"EXIT CAN BE REACHED TO THE NORTH.":GOTO20
1002 L$="MAZE":M$="YOU HAVE A NERVOUSNESS ABOUT THIS PASSAGE, BUT YOU TRUDGE ON, AND END UP GETTING KILLED IN A CAVE-IN."
1004 R=13:FF=-1:GOTO20
1006 L$="MAZE":M$="*IN A DARK MAZE.":GOTO20
1008 L$="DARK TUNNEL":GOSUB14
1010 M$="*IN A VERY DARK TUNNEL. THE TUNNEL HEADS TO THE EAST, WHILE A HOLE IN THE CEILING ALLOWS EXIT FROM "
1012 M$=M$+"THE DARKNESS.":GOTO20
1014 L$="DARK TUNNEL":GOSUB14
1016 M$="*IN A DARK TUNNEL. YOU CAN BARELY MAKE OUT EXITS TO ALL DIRECTIONS EXCEPT UP AND DOWN.":GOTO20
1018 L$="MAZE":M$="*IN A DAMP MAZE.":GOTO20
1020 L$="MAZE":M$="*IN A DANK MAZE.":GOTO20
1022 L$="LONG CORRIDOR":GOSUB14:IFNOTSLTHENSL=-1:SC=SC+5
1026 M$="*IN A LONG TUNNEL STREACHING TO THE EAST AND WEST. YOUR FEET ACHE AS YOU WALK DOWN IT.":GOTO20
1028 L$="SLIMY ROOM":GOSUB14
1030 M$="*IN A SLIME COVERED MEDIUM-SIZE ROOM. YOUR FEET STICK TO THE GROUND.":GOSUB2
1032 IFNOTDGTHENM$="SOME GREY OOZE GUARDS THE WESTWARD CORRIDOR.":GOSUB2
1034 RETURN
1036 L$="MAZE":M$="*IN A DREARY MAZE.":GOTO20
1038 L$="MAZE":M$="*IN A MAZE.":GOTO20
1040 L$="MAZE":M$="*IN A DEVLISH MAZE.":GOTO20
1042 L$="TORCH ROOM":GOSUB14
1044 M$="*IN A LARGE MARBLE ROOM WITH TORCH HOLDERS EVERYWHERE. HOWEVER, ":GOSUB2
1046 IFBCTHENM$="MOST OF THEM ARE EMPTY. YOU CAN LEAVE TO THE NORTH, SOUTH, OR EAST.":GOTO20
1048 M$="MOST OF THEM ARE EMPTY. YOU CAN GO SOUTH AND NORTH, BUT THE EAST PASSAGE IS BLOCKED BY COBWEBS.":GOTO20
1050 L$="DEAD END":GOSUB14
1052 IFBRTHENM$="*IN A DEAD END THAT EXITS WEST, BUT A SHATTERED MIRROR SHOWS A PASSAGE EAST.":GOTO20
1054 M$="*IN A DEAD END, WITH A LARGE MIRROR COVERING THE EAST WALL AND AN EXIT TO THE WEST.":GOTO20
1056 L$="BUNNY ROOM":GOSUB14
1058 IFNOTDBTHENM$="*IN A LARGE SPARSELY FURNISHED CHAMBER EXCEPT FOR A THRONE WITH A BUNNY NEXT TO IT.":GOSUB2
1060 IFNOTDBTHENM$="IT LOOKS LIKE THE VAMPIRE BUNNY! A PASSAGE EXITS WEST FROM THIS UNFRIENDLY PLACE.":GOTO20
1062 IFNOTSTTHENM$="*IN A LARGE SPARSELY FURNISHED CHAMBER EXCEPT FOR A THRONE. A PASSAGE EXITS WEST.":GOTO20
1064 M$="*IN A LARGE SPARSELY FURNISHED CHAMBER EXCEPT FOR A THRONE. PASSAGES EXIT EAST AND WEST.":GOTO20
1066 L$="CRAMPED CORRIDOR":GOSUB14
1068 M$="*IN A CRAMPED CORRIDOR GOING NORTH/SOUTH. A WORN LEVER IS SET INTO THE WEST WALL.":GOTO20
1070 L$="SANDY ROOM":GOSUB14
1072 IFNOTPLTHENM$="*IN A SANDY ROOM WITH AN EXIT NORTH. THE CEILING LOOKS LIKE IT COULD CAVE IN.":GOTO20
1074 M$="*IN A SANDY ROOM WITH AN EXIT NORTH AND A HOLE IN THE ROOF. THE CEILING LOOKS WEAK.":GOTO20
1500 IFLEFT$(M$,1)="*"THENM$="YOU'RE "+MID$(M$,2)
1501 RETURN
2000 DATA"NOVICE ADVENTURER","GRAVEYARD EXPLORER","ADVENTURER","MONSTER BASHER","EXPERT ADVENTURER"
2010 DATA"EXTERMINATOR","BUNNY SLAYER"
2100 DATA0,4,,,,,,,3,,,,,200,,2,,,1,8,5,,,201,,8,6,4,,,200,9,10,5,,,,11,,,240,,5,,9,4,,,6,,10,8,,314
2190 DATA6,,,9,,,7,,,,,,,,,,,,16,12,17,17,,,,,15,,9,,15,19,15,14,,,13,22,20,21,,,13,20,16,16,,,,,19,17,,0
2280 DATA15,,,203,,,13,17,17,16,,,16,23,13,20,,,17,21,20,16,,,21,26,,,,,,,,23,,,,,,24,,,23,27,,,,,26,,,,,0
2600 DATA"A CARROT",CARRO,0,"A LAMP",LAMP,2,"A BUCKET",BUCKE,11,"A SILVER CROSS",CROSS,0,"A TORCH",TORCH,14
2650 DATA"A KEY",KEY,18,"A SCRAP OF PAPER",PAPER,8,"A QUILL PEN",PEN,-1,"AN APPLE",APPLE,-1
