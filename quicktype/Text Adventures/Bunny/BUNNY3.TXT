0 CLS:CLEAR500:DIMC1,CC,M$:GOTO20
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
5 GOSUB1:GOTO100
6 GOSUB1:GOSUB8:GOTO109
7 M$=LO$:GOTO10
8 FORCC=1TO11:C$(CC)="":NEXT:MO=MO-1
9 FORCC=1TO32:PRINT"-";:NEXT:PRINT"SCORE:"SC,"MOVES:"MO:RETURN
10 GOSUB1:RETURN
20 NO=9:R=5:ND=6:NR=27:MO=0:NI=2
30 DIMR$(NR),O$(NO),O2$(NO),O(NO),MA(28,6),C$(11)
35 CLS:GOSUB27000:CLS
40 IFRND(0)>.5THENPRINT"I'LL BE BACK IN A SEC...":GOTO50
41 PRINT"PLEASE WAIT..."
50 GOSUB25000:GOSUB26000
55 CLS
90 GOSUB700
91 IFFFTHENFF=0:GOTO820
92 GOSUB600:GOSUB9
100 IFVATHENVT=VT+1:IFVT=2THENM$="THE VAMPIRE DECIDES TO FANG YOU."GOSUB1:GOTO810
102 IFR=25ANDNOTDBTHENRA=RA+1:GOTO104
103 RA=0
104 IFR=25ANDNOTDBTHENIFRA=3THENM$="THE VAMPIRE BUNNY NIBBLES A LITTLE ON YOUR LEG.":GOSUB1:GOTO810
108 IFC$(1)<>""THENC$=C$(1):BF=0:GOTO126
109 INPUTC$:IFC$=""THENPRINT"WHAT'S THAT?":GOTO109
114 FORI=1TOLEN(C$)
115 IFMID$(C$,I,4)="THE "THENG$=LEFT$(C$,I-1):GG$=MID$(C$,I+4):C$=G$+GG$:GOTO115
116 NEXT:C=0
117 Z=LEN(C$):L=0:FORI=1TOZ
118 L=L+1
119 IFMID$(C$,L,3)="AND"THENC=C+1:C$(C)=LEFT$(C$, L-2):G$=MID$(C$,L+4):C$=G$:L=1
120 IFMID$(C$,L,4)="THEN"THENC=C+1:C$(C)=LEFT$(C$, L-2):G$=MID$(C$,L+5):C$=G$:L=1
121 IFMID$(C$,L,1)="."THENIFLEN(C$)<>LTHENC=C+1:C$(C)=LEFT$(C$,L-1):G$=MID$(C$,L+1):C$=G$:L=1
122 IFMID$(C$,L,2)="  "THENG$=LEFT$(C$,L-1):GG$=MID$(C$,L+3):C$=G$+GG$:GOTO122
123 IFLEFT$(C$,1)=" "THENC$=RIGHT$(C$,LEN(C$)-1)
124 IFRIGHT$(C$,1)="."THENC$=LEFT$(C$,LEN(C$)-1)
125 NEXT:C$(C+1)=C$:IFC$(1)<>""THENC$=C$(1)
126 BF=0:FORI=2TO10:C$(I-1)=C$(I):C$(I)="":NEXT
200 C=0:V$="":N$="":W1$="":W2$=""
202 C=C+1:IFC>LEN(C$)THEN208
204 W$=MID$(C$,C,1):IFW$=" "THEN208
206 V$=V$+W$:GOTO202
208 C=C+1:IFC>LEN(C$)THEN214
210 W$=MID$(C$,C,1):IFW$=" "THEN214
212 N$=N$+W$:GOTO208
214 C=C+1:IFC>LEN(C$)THEN220
216 W$=MID$(C$,C,1):IFW$=" "THEN220
218 W1$=W1$+W$:GOTO214
220 C=C+1:IFC>LEN(C$)THEN226
222 W$=MID$(C$,C,1):IFW$=" "THEN226
224 W2$=W2$+W$:GOTO220
226 IFV$=""THENPRINT"COME AGAIN?":GOTO109
227 IFLEN(V$)>5THENV$=LEFT$(V$,5)
228 IFLEN(N$)>5THENN$=LEFT$(N$,5)
229 IFLEN(W1$)>5THENW1$=LEFT$(W1$,5)
230 IFLEN(W2$)>5THENW2$=LEFT$(W2$,5)
234 IFV$="G"ORV$="AGAIN"THENC$=VVV$:V$=V1$:N$=VV$:W1$=V2$:W2$=V3$
235 IFN$="IT"THENN$=VV$
236 VV$=N$:VVV$=C$:V1$=V$:V2$=W1$:V3$=W2$
240 IFN$="SILVE"THENN$="CROSS"
250 IFN$="SCRAP"ORN$="SHEET"THENN$="PAPER"
255 IFW1$="SCRAP"ORW1$="SHEET"THENW1$="PAPER"
260 IFN$="QUILL"THENN$="PEN"
262 IFW1$="QUILL"THENW1$="PEN"
265 IFN$="GRAY"THENN$="OOZE"
266 IFN$="WOODE"THENN$="CRANK"
267 IFN$="WEBS"THENN$="COBWE"
268 IFW2$="GRAY"THENW2$="OOZE"
269 IFW1$="SILVE"THENS1$="CROSS"
290 MO=MO+1:GOSUB9:GOTO320
320 IFR<11ANDR<>7THEN340
325 IFO(4)=-1ORO(4)=RTHEN340
327 IFNOTLATHEN330
328 IFO(1)=-1ORO(1)=RTHEN340
330 IFC$<>"LIGHTLAMP"ANDC$<>"TURN ON LAMP"THENM$="YOU ARE IN TOTAL DARKNESS.":GOSUB1:GOSUB9
335 FORTEMP=1TO11:C$(TEMP)="":NEXT:GOTO109
340 IFC$="TALK INTO WELL"ORC$="TALK TO WELL"ORC$="TALK WELL"THEN345
341 GOTO400
345 IFR=4THENM$="ECHO... ECHO... ECHO...":GOTO5
400 IFV$="PUT"ANDN$="ON"THENV$="WEAR":N$=W1$
402 IFV$="GET"ANDN$="ON"THENV$="WEAR":N$=W1$
404 IFV$="TAKE"ANDN$="OFF"THENV$="UNWEA":N$=W1$
405 IFV$="GET"ANDN$="OFF"THENV$="UNWEA":N$=W1$
407 IFV$="THROW"ANDN$="WATER"THENV$="POUR"
408 IFV$="THROW"ANDN$="BUCKE"THENV$="POUR"
409 IFVAANDV$="POUR"THENIFN$="WATER"ORN$="BUCKE"THENW1$="ON":W2$="VAMPI"
410 IFV$="TAKE"ANDW1$="OFF"THENV$="UNWEA"
420 IFV$="GET"ANDW1$="OFF"THENV$="UNWEA"
590 GOTO2000
600 FORI=0TONO-1:IFO(I)=RTHENM$="YOU SEE "+O$(I)+" HERE.":GOSUB1
610 NEXT:RETURN
700 IFR<11ANDR<>7THEN710
701 IFO(4)=-1ORO(4)=RTHEN710
702 IFNOTLATHEN704
703 IFO(1)=-1ORO(1)=RTHEN710
704 LO$="DARKNESS":GOSUB9:RETURN
710 IFR=100THENLO$="OUTSIDE GRAVEYARD":GOSUB9:GOTO25400
715 ONRGOSUB40000,40060,40080,40100,40140,40160,40180,40200,40220,40250,40270,40290,40300,40310,40330:IFR<=15THENRETURN
716 ONR-15GOSUB40350,40360,40370,40390,40410,40420,40430,40440,40450,40470,40490,40510:RETURN
750 IFNOTLATHEN770
760 IFO(1)=-1ORO(1)=RTHENRETURN
770 LO$="DARKNESS":GOSUB9:M$="UH-OH, NOW YOU CAN'T SEE!":GOTO10
805 M$="*** YOU HAVE DIED ***":GOTO10
810 GOSUB805:GOSUB25420:GOTO3440
820 GOSUB805:GOSUB25420:PRINT"NEW GAME OR UNDO (Y/N/U)?"
830 I$=INKEY$:IFI$<>"Y"ANDI$<>"N"ANDI$<>"U"THEN830
840 IFI$="Y"THENRUN
841 IFI$="U"THENGOSUB8:GOTO90
850 END
1000 IFNO=0THENRETURN
1010 FORI=0TONO-1:IFO2$(I)=N$THENFL=1:O(I)=R:RETURN
1030 NEXT:FL=0:RETURN
2000 IFV$<>"GO"ANDV$<>"RUN"ANDV$<>"CRAWL"ANDV$<>"N"ANDV$<>"S"ANDV$<>"CLIMB"ANDV$<>"E"ANDV$<>"W"ANDV$<>"U"ANDV$<>"D"THEN2500
2010 IFN$="NORTH"ORV$="N"THENDI=0:GOTO2400
2020 IFN$="SOUTH"ORV$="S"THENDI=1:GOTO2400
2030 IFN$="EAST"ORV$="E"THENDI=2:GOTO2400
2040 IFN$="WEST"ORV$="W"THENDI=3:GOTO2400
2050 IFN$="UP"ORV$="U"THENDI=4:GOTO2400
2060 IFN$="DOWN"ORV$="D"THENDI=5:GOTO2400
2070 IFR=1ANDN$="COFFI"THENM$="ARE YOU CRAZY?":GOTO5
2080 IFR=3THENIFN$="STREA"ORN$="RIVER" THEN V$="S":GOTO2000
2090 IFR=6THENIFN$="STREA"ORN$="RIVER" THEN V$="N":GOTO2000
2100 IFR=9ANDN$="HOLE"THENV$="D":GOTO2000
2110 IFR=4ANDN$="WELL"THENV$="D":GOTO2000
2120 IFR=7ANDN$="CREVI"THENV$="S":GOTO2000
2130 IFR=8ANDN$="HOLE"THENV$="D":GOTO2000
2135 IFR=7ANDN$="ROPE"THENDI=4:GOTO2400
2136 IFR=4ANDN$="ROPE"ANDROTHEN DI=5:GOTO2400
2140 IFR=27ANDN$="HOLE"THENV$="U":GOTO2000
2145 IFR=14ANDN$="HOLE"THENV$="U":GOTO2000
2148 IFR=2ANDN$="HOLE"THENV$="E":GOTO2000
2200 IFR=3ANDN$="HOLE"THENV$="W":GOTO2000
2390 M$="YOU CAN'T GO THAT WAY!":GOTO6
2400 IFMA(R,DI)>0ANDMA(R,DI)<128THENR=MA(R,DI):GOTO90
2402 IFMA(R,DI)=203 THENM$="THE OOZE BLOCKS YOUR PATH.":GOTO5
2404 IFMA(R,DI)=204 THENM$="THE BUNNY WON'T LET YOU!":GOTO5
2405 IFMA(R,DI)=200THENM$="YOU ENTER THE STREAM. A LITTLE WATER GETS IN YOUR MOUTH. YOU CHOKE AND DROWN.":GOSUB1:GOTO820
2408 IFMA(R,DI)=201ANDNOTROTHEN2390
2409 IFMA(R,DI)=201ANDNOTLATHENM$="IT'S TOO DARK DOWN THERE.":GOTO5
2410 IFMA(R,DI)=201THENIFO(1)<>7ANDO(1)<>-1THEN M$="IT'S TOO DARK DOWN THERE.":GOTO5
2411 IFMA(R,DI)=240ANDNI>1THENM$="YOU'RE TOO HEAVY TO CLIMB UP THE WELL.":GOTO5
2412 IFMA(R,DI)=240THENPRINT"YOU CLIMB UP THE ROPE.":R=4:GOTO90
2413 IFMA(R,DI)=201ANDNI>1THENM$="AS YOU ATTEMPT TO CLIMB DOWN THE ROPE SNAPS AND YOU PLUNGE DOWN THE WELL.":GOSUB1:GOTO820
2415 IFMA(R,DI)=201THENM$="YOU MAKE YOUR WAY DOWN THE ROPE.":GOSUB1:R=7:GOTO90
2419 IFMA(R,DI)=700THENM$="THE VAMPIRE, UNFORTUNATELY, GIVES YOU A NASTY BITE.":GOSUB1:GOTO810
2420 IFMA(R,DI)=314ANDNOTWCTHEN M$="THE SKELETON PUSHES YOU AWAY FROM THE HOLE.":GOTO5
2430 IFMA(R,DI)=314THENM$="THE SKELETON ALLOWS YOU TO ENTER.":GOSUB1:R=14:GOTO90
2480 GOTO2390
2500 IFV$<>"GET"ANDV$<>"TAKE"THEN2580
2510 ZH=0:GOSUB1000:IFN$="WATER"THENIFO(2)=-1THENV$="FILL":N$="BUCKE":GOTO2000
2520 IFFL=0THENM$="YOU CAN'T GET THAT!":GOTO6
2530 IFRO=-1THENM$="YOU ALREADY HAVE THAT!":GOTO6
2550 IFRO<>RTHENM$="YOU CAN'T GET THAT!":GOTO6
2560 IFNI=5THENM$="YOU'RE CARRYING TOO MUCH!":GOTO6
2565 IFNOT CROS AND I=3 THEN CROS=-1:SC=SC+10:GOSUB9
2566 IFNOT CARP AND I=0 THEN CARP=-1:SC=SC+2:GOSUB9
2570 NI=NI+1:O(I)=-1:PRINT"TAKEN.":GOTO100
2580 IFV$="HINT"ORV$="HELP"THENPRINT"SORRY."
2581 IFV$="TURN"ANDW1$="OFF"THENV$="UNLIG"
2582 IFV$="TURN"ANDW1$="ON"THENV$="LIGHT"
2585 IFV$="TURN"ANDN$="OFF"THENV$="UNLIG":N$=W1$
2586 IFV$="TURN"ANDN$="ON"THENV$="LIGHT":N$=W1$
2588 IFV$="PUT"ANDN$="DOWN"THENV$="DROP":N$=W1$:GOTO2500
2590 IFV$="PICK"ANDN$="UP"THENV$="TAKE":N$=W1$:GOTO2500
2600 IFV$<>"DROP"THEN2700
2605 IFW1$<>""THENV$="PUT":GOTO2000
2610 GOSUB1000
2620 IFFL=0THENM$="YOU DON'T HAVE THAT!":GOTO6
2635 IFRO=-2THENRO=-1
2640 IFRO<>-1THENM$="YOU DON'T HAVE THAT!":GOTO6
2650 NI=NI-1:O(I)=R:PRINT"DROPPED.":GOTO100
2700 IFV$<>"INVEN"ANDV$<>"I"THEN2800
2710 FL=0:PRINT"YOU HAVE:"
2715 IFWCTHENM$="A SILVER CROSS (BEING WORN)":GOSUB1:FL=1
2716 IFBUANDO(2)=-1THENM$="A BUCKET (WITH WATER)":GOSUB1:FL=1
2717 IFLAANDO(1)=-1THENM$="A LAMP (PROVIDING LIGHT)":GOSUB1:FL=1
2718 IFO(4)=-1THENM$="A TORCH (PROVIDING LIGHT)":GOSUB1:FL=1
2720 FORI=0TONO-1
2725 IFWCANDI=3THEN2740
2726 IFBUANDI=2ANDO(I)=-1THEN2740
2727 IFLAANDI=1ANDO(I)=-1THEN2740
2728 IFI=4ANDO(I)=-1THEN2740
2730 IFO(I)=-1THENM$=O$(I):GOSUB1:FL=1
2740 NEXT
2745 IFFL=0THENPRINT"NOTHING"
2760 GOTO100
2800 IFV$<>"LOOK"ANDV$<>"L"THEN2900
2810 IFN$<>""THEN2901
2820 GOTO90
2900 IFV$<>"EXAMI"ANDV$<>"SEARC"THEN3400
2901 IFN$="FIELD"ANDR<>5THENM$="THERE ISN'T A FIELD HERE!":GOTO6
2904 IFN$="GROUN"ORN$="FLOOR"ORN$="FIELD"THENIFR=5ANDNOTCATHENCA=-1:PRINT"YOU FIND A CARROT.":O(0)=5:GOTO100
2906 IFN$="GROUN"ORN$="FLOOR"ORN$="FIELD"THENM$="YOU SEE NOTHING UNUSUAL.":GOTO5
2908 IFN$="COFFI"ANDR=1THENM$="IT IS CARVED OF MARBLE.":GOTO5
2909 IFN$="SKELE"ANDR=9THENM$="IT LOOKS PRETTY BONY.":GOTO5
3000 IFN$="WELL"ANDR=4THENM$="IT HAS A POLE CONNECTED TO SOME ROPE AND A WOODEN CRANK. THE WELL IS DARK.":GOTO5
3001 IFN$="ROPE"THENIFR=4ORR=7THENM$="IT DOESN'T LOOK VERY STURDY.":GOTO5
3002 IFN$="WELL"ANDR=7THENM$="IT IS FULL OF CRACKS.":GOTO5
3004 IFN$="OOZE"ANDR=19ANDNOTDGTHENM$="IT IS BIG, UGLY, AND SLIMY.":GOTO5
3005 IFN$="COBWE"ANDNOTBCTHENM$="THE ARE BLOCKING THE EAST PASSAGE.":GOTO5
3006 IFN$="LEVER"ANDR=26THENM$="IT IS RUSTY, BUT USEABLE.":GOTO5
3008 IFN$="BUNNY"ANDNOTDBANDR=25THENM$="IT LOOKS EXTREMELY VICIOUS FOR ITS SMALL SIZE.":GOTO5
3009 IFN$="VAMPI"ANDVATHENM$="IT LOOKS AT YOU WITH AN UNNERVING GAZE.":GOTO5
3010 IFN$="TOMBS"ORN$="GRAVE"THENIFR<11ANDR<>7THENM$="MANY ARE OLD AND ROTTED, AND SOME ARE BLANK.":GOTO5
3011 IFN$="CRANK"ANDR=4THENM$="IT IS CRACKING, BUT USABLE.":GOTO5
3012 IFN$="STREA"ORN$="RIVER"THENIFR=6ORR=3THENM$="IT LOOKS LIFELESS AND SHALLOW ENOUGH TO CROSS.":GOTO5
3013 IFN$="POLE"ANDR=4THENM$="IT HAS MANY HOLES IN IT.":GOTO5
3014 IFN$="MIRRO"ANDR=24THEN3020
3015 GOTO3200
3020 IFBRTHENM$="IT IS BROKEN INTO SHARDS, AND THERE IS A PASSAGE BEHIND IT.":GOTO5
3025 M$="IT IS NOT WELL POLISHED.":GOTO5
3027 IFN$="WALLS"ORN$="WALL"THENM$="THERE ARE ENOUGH OF THEM.":GOTO5
3200 GOSUB1000
3202 IFRO<>RANDRO<>-1ANDO(I)<>(R-128)THENM$="THAT'S NOT HERE!":GOTO6
3204 IFN$="CARRO"THENM$="IT LOOKS NICE AND JUICY.":GOTO5
3208 IFN$<>"LAMP"THEN3215
3209 IFLATHENM$="THE LAMP IS ON.":GOTO5
3210 M$="THE LAMP IS OFF.":GOTO5
3215 IFN$<>"BUCKE"THEN3220
3216 IFBUTHENM$="THE BUCKET IS FILLED.":GOTO5
3217 M$="THE BUCKET IS EMPTY.":GOTO5
3220 IFN$="CROSS"THENM$="IT IS ROUGH AND TARNISHED.":GOTO5
3300 IFN$="PAPER"THENM$="IT HAS SOME WRITING ON IT.":GOTO5
3310 IFN$="KEY"THENM$="IT IS FAIRLY SHINY.":GOTO5
3312 IFN$="TORCH"THENM$="IT IS BURNING FAIRLY SLOWLY.":GOTO5
3314 IFN$="PEN"THENM$="IT DOES NOT HAVE ANY INK.":GOTO5
3316 IFN$="APPLE"THENM$="IT LOOKS A LITTLE TOO WORMY TO BE EDIBLE.":GOTO5
3318 M$="YOU SEE NOTHING UNUSUAL.":GOTO5
3400 IFV$<>"QUIT"ANDV$<>"Q"ANDV$<>"END"THEN3500
3405 GOSUB25420
3410 INPUT"SURE YOU WANT TO QUIT <Y/N>";QU$
3420 IFQU$="N"THENGOSUB8:GOTO109
3430 IFQU$<>"Y"THEN3410
3440 PRINT"PLAY AGAIN <Y/N>?"
3450 I$=INKEY$:IFI$<>"Y"ANDI$<>"N"THEN3450
3455 IFI$="Y"ORI$="Y"THENRUN
3456 END
3500 IFV$<>"ENTER"THEN3510
3505 V$="GO":GOTO2000
3510 IFV$<>"JUMP"ANDV$<>"LEAP"THEN3640
3520 IFN$=""THENPRINT"WHEEEEEE!":GOTO100
3530 IFN$<>"OVER"THENM$="THAT WON'T DO ANY GOOD.":GOTO6
3535 IFR=19ANDNOTDGANDW1$="OOZE"ORW1$="GREY"THENM$="THE OOZE EXPANDS AND PREVENTS YOUR JUMP.":GOTO6
3538 IFR=25ANDNOTDBANDW1$="BUNNY"ORW1$="RABBI"THENM$="THE BUNNY MANAGES TO BITE YOUR LEG.":GOSUB1:GOTO820
3600 IFW1$<>"STREA"ANDW1$<>"RIVER"THENM$="THAT WON'T DO ANYTHING!":GOTO5
3610 IFR<>3ANDR<>6THENM$="THAT WON'T DO ANYTHING!":GOTO5
3620 IFR=3THENR=6:GOTO3630
3621 R=3
3630 M$="YOU LEAP OVER THE STREAM.":GOSUB1:GOTO90
3640 IFV$<>"READ"THEN3680
3650 IFN$<>"PAPER"THENM$="YOU CAN'T DO THAT!":GOTO6
3660 IFO(6)<>-1THENM$="YOU DON'T HAVE THAT!":GOTO6
3670 M$="IT SAYS, 'PUSHING IS BETTER THAN PULLING'":GOTO5
3680 IFV$<>"LIGHT"THEN3730
3685 IFN$="COBWE"THEN3697
3690 GOSUB1000:IFFL=0THENM$="YOU DON'T HAVE THAT!":GOTO6
3692 IFO(I)<>-1THENM$="YOU DON'T HAVE THAT!":GOTO6
3695 IFN$<>"TORCH"ANDN$<>"COBWE"ANDN$<>"LAMP"THENM$="YOU CAN'T DO THAT!":GOTO6
3697 IFN$="COBWE"ANDR=23ANDBC=0THENSC=SC+5:GOSUB9:BC=-1:MA(23,2)=24:M$="THE COBWEBS LIGHT AND BURN.":GOTO5
3699 IFN$="COBWE"THENM$="YOU CAN'T DO THAT NOW!":GOTO6
3700 IFN$="TORCH"ANDO(4)=-1THENM$="IT'S ALREADY LIT!":GOTO5
3710 IFN$="LAMP"ANDO(1)=-1ANDLATHENM$="IT'S ALREADY LIT!":GOTO5
3720 IFN$="LAMP"ANDNOTLATHEN3725
3721 GOTO3730
3725 M$="THE LAMP LETS OUT A WARM GLOW.":GOSUB1:LA=-1:IFLO$="DARKNESS"THEN90
3726 GOTO100
3730 IFV$<>"LOWER"THEN3780
3740 IFN$<>"ROPE"ANDN$<>"WELL"THENM$="YOU CAN'T DO THAT NOW!":GOTO6
3750 IFR<>4THENM$="YOU CAN'T DO THAT NOW!":GOTO6
3760 IFROTHENM$="IT ALREADY IS!":GOTO6
3770 RO=-1:M$="THE ROPE GOES DOWN INTO THE WELL.":GOTO5
3780 IFV$<>"RAISE"ANDV$<>"LIFT"THEN3830
3790 IFN$<>"ROPE"ANDN$<>"WELL"THENM$="YOU CAN'T DO THAT NOW!":GOTO6
3800 IFR<>4THENM$="YOU CAN'T DO THAT NOW!":GOTO6
3810 IFNOTROTHENM$="IT ALREADY IS!":GOTO6
3820 RO=0:M$="THE ROPE RISES FROM THE WELL.":GOTO5
3830 IFV$<>"FILL"THEN3880
3840 IFN$<>"BUCKE"ORO(2)<>-1OR(R<>3ANDR<>6)THENM$="YOU CAN'T DO THAT NOW!":GOTO6
3860 IFBUTHENM$="IT'S ALREADY FULL!":GOTO6
3870 BU=-1:M$="THE BUCKET FILLS WITH WATER.":GOTO5
3880 IFV$<>"UNLIG"THEN3940
3890 GOSUB1000:IFFL=0THENM$="YOU DON'T HAVE THAT!":GOTO6
3892 IFO(I)<>-1THENM$="YOU DON'T HAVE THAT!":GOTO6
3900 IFN$<>"TORCH"ANDN$<>"LAMP"THENM$="YOU CAN'T DO THAT!":GOTO6
3910 IFN$="TORCH"ANDO(4)=-1THENM$="IT REFUSES TO GO OUT.":GOTO5
3920 IFN$="LAMP"ANDO(1)=-1ANDNOTLATHENM$="IT ALREADY IS!":GOTO5
3930 IFN$="LAMP"ANDO(1)=-1THENLA=0:M$="THE LAMP TURNS OFF WITH A CLICK.":GOTO5
3940 IFV$<>"OPEN"THEN4000
3950 IFN$<>"COFFI"THENM$="YOU CAN'T DO THAT NOW!":GOTO6
3960 IFR<>1THENM$="YOU CAN'T DO THAT NOW!":GOTO6
3970 IFOCTHENM$="IT'S ALREADY OPEN!":GOTO6
3990 OC=-1:VA=-1:MA(1,1)=700:M$="AS YOU OPEN THE COFFIN, A VAMPIRE REACHES OUT!":GOTO5
4000 IFV$<>"CLOSE"THEN4100
4010 IFN$<>"COFFI"THENM$="YOU CAN'T DO THAT NOW!":GOTO6
4020 IFR<>1THENM$="YOU CAN'T DO THAT NOW!":GOTO6
4030 IFNOTOCTHENM$="IT'S NOT OPEN!":GOTO6
4032 IFVATHENVT=0:VA=0:OC=0:MA(1,1)=4:M$="THE COFFIN SLAMS SHUT IN THE VAMPIRE'S FACE.":GOTO5
4034 OC=0:M$="THE COFFIN CLOSES.":GOTO5
4100 IFV$<>"POUR"ANDV$<>"EMPTY"ANDV$<>"SPLAS"THEN4130
4110 IFN$<>"WATER"ANDN$<>"BUCKE"THENM$="YOU CAN'T POUR THAT!":GOTO6
4112 IFO(2)<>-1THENM$="YOU DON'T HAVE THAT!":GOTO6
4114 IFNOTBUTHENM$="YOU CAN'T DO THAT NOW!":GOTO6
4116 IFW2$=""THENBU=0:M$="YOU EMPTY THE BUCKET.":GOTO5
4118 IFW2$="OOZE"ORW2$="GREY"ANDR=19ANDDG=0THENM$="THE OOZE GROWS AND SPLATTERS YOU WITH A DEADLY FORCE.":GOSUB1:GOTO820
4120 IFW2$="RABBI"ORW2$="BUNNY"ANDR=24ANDDB=0THENBU=0:M$="THE BUNNY MOVES, AVOIDING THE SPLASH OF WATER.":GOTO5
4122 IFW2$="APPLE"THENIFO(8)=-1ORO(8)=RTHENBU=0:AP=-1:M$="THE APPLE IS SPLASHED WITH WATER.":GOTO5
4123 IFW2$="CARRO"THENIFO(0)=-1ORO(0)=RTHENSC=SC+10:GOSUB9:BU=0:CS=-1:M$="THE CARROT IS SPLASHED.":GOTO5
4124 IFW2$="SKELE"ANDR=9THENBU=0:M$="THE WATER FLIES THROUGH THE SKELTAL BODY.":GOTO5
4125 IFW2$="VAMPI"ANDVATHENMA(1,1)=4:BU=0:VA=0:O(3)=1:M$="THE VAMPIRE SCREAMS AND DISSOLVES.":GOTO5
4128 M$="I'M NOT SO SURE ABOUT THAT.":GOTO6
4130 IFV$<>"WEAR"THEN4150
4140 IFN$<>"CROSS"THENM$="YOU CAN'T DO THAT!":GOTO6
4142 IFO(3)<>-1THENM$="YOU DON'T HAVE THAT!":GOTO6
4144 IFWCTHENM$="YOU ARE ALREADY WEARING THE CROSS!":GOTO6
4146 WC=-1:M$="IT FITS RATHER NICE.":GOTO5
4150 IFV$="PUT"ANDW1$="ON"THENIFN$="CROSS"THENV$="WEAR":GOTO4130
4152 IFV$="GET"ANDW1$="ON"THENIFN$="CROSS"THENV$="WEAR":GOTO4130
4154 IFV$="GET"ANDW1$="OFF"THENV$="UNWEAR":GOTO2000
4156 IFV$="TAKE"ANDW1$="OFF"THENV$="UNWEAR":GOTO2000
4200 IFV$<>"PUT"THEN4310
4205 IFW1$<>"ON"ANDW1$<>"ONTO"THEN4220
4211 IFN$="WATER"ORN$="BUCKE"THENV$="POUR":GOTO2000
4212 IFN$<>"TORCH"THEN4217
4213 IFO(4)<>-1THENM$="YOU DON'T HAVE THAT!":GOTO6
4215 IFW2$="GREY"ORW2$="OOZE"THEN5000
4216 IFW2$="COBWE"ANDR=23ANDNOTBCTHEN5010
4217 M$="THAT PROBABLY WON'T DO MUCH.":GOTO6
4220 IFW1$<>"IN" AND W1$<>"INTO" THEN 4300
4221 IFN$="WATER" AND W2$="BUCKE" THEN V$="FILL":N$="BUCKE":GOTO2000
4222 IFW2$="OOZE" OR W2$="GREY" THEN 4215
4224 IFW2$="COBWE"THEN4215
4230 IFW2$<>"WELL"THEN4242
4235 IFR<>4THENM$="THAT'S NOT HERE!":GOTO6
4238 GOSUB1000:IFFL=0THENM$="YOU DON'T HAVE THAT!":GOTO6
4239 IFRO<>-1THENM$="YOU DON'T HAVE THAT!":GOTO6
4240 O(I)=7:NI=NI-1:PRINT"ENUOOOOOAUUUOOOCLUNK!":GOTO100
4242 IFW2$<>"STREA"ANDW2$<>"WATER"ANDW2$<>"BUCKE"ANDW2$<>"RIVER"THEN4300
4244 IFW2$="BUCKE"THENIF((O(2)<>-1)AND(O(2)<>R))THENM$="YOU CAN'T SEE ANY BUCKET HERE!":GOTO6
4245 IFW2$="BUCKE"ANDNOTBUCK AND N$="WATER"THENV$="GET":GOTO2000
4246 IFW2$="BUCKE"ANDNOTBUTHENM$="UNFORTUNATELY, THAT'S NOT GOING TO BE VERY HELPFUL.":GOTO6
4248 IFW2$="WATER"THENIFR=3ORR=6THEN4254
4249 IFW2$="WATER"THEN4252
4250 GOTO4254
4252 IFO(2)=-1ORBUTHEN4254
4253 M$="THAT'S NOT HERE!":GOSUB1:GOSUB8:GOTO100
4254 IFW2$="STREA"ORW2$="RIVER"THENIFR<>3ANDR<>6THENM$="YOU DON'T SEE ANY!":GOTO6
4255 GOSUB1000:IFFL=0THENM$="YOU DON'T HAVE THAT!":GOTO6
4258 IFO(I)<>-1THENM$="YOU DON'T HAVE THAT!":GOTO6
4260 IFN$="CARRO"THENIFR=3ORR=6THENSC=SC+10:GOSUB9:CS=-1:M$="THE CARROT SOAKS UP THE WATER.":GOTO5
4262 IFN$="CARRO"ANDO(2)=-1ANDBUTHENSC=SC+10:GOSUB9:CS=-1:M$="THE CARROT SOAKS UP THE WATER.":GOTO5
4265 IFN$="APPLE"THENIFR=3ORR=6THENAP=-1:M$="THE APPLE SOAKS UP THE WATER.":GOTO5
4268 IFN$="APPLE"ANDO(2)=-1ANDBUTHENAP=-1:M$="THE APPLE SOAKS UP THE WATER.":GOTO5
4300 M$="THAT'S NOT GOING TO DO MUCH FOR THIS ADVENTURE.":GOTO6
4310 IFV$<>"PUSH"THEN4350
4315 IFN$="COFFI"ANDR=1THENM$="IT IS MUCH TOO HEAVY.":GOTO5
4317 IFN$="MIRRO"ANDR=24ANDNOTBRTHENM$="IT IS ATTACHED TO THE WALL.":GOTO5
4319 IFN$="THRON"ANDR=25THENM$="IT IS BOLTED TO THE WALL.":GOTO5
4320 IFN$<>"LEVER"ORR<>26THENM$="YOU DON'T NEED TO DO THAT.":GOTO6
4330 IFPLTHENM$="NOTHING HAPPENS.":GOTO5
4340 MA(8,5)=27:PL=-1:MA(27,4)=8:M$="YOU HEAR A LOUD 'CLUNK' TO THE SOUTH.":GOTO5
4350 IFV$<>"PULL"THEN4375
4355 IFN$<>"LEVER"THEN4315
4360 IFR<>26THENM$="YOU CAN'T DO THAT NOW!":GOTO6
4370 M$="THE ROOF CAVES IN ON YOU.":GOSUB1:GOTO820
4375 IFV$<>"TURN"THEN4400
4380 IFN$<>"CRANK"THENM$="YOU CAN'T DO THAT NOW!":GOTO6
4390 IFR<>4THENM$="YOU CAN'T DO THAT NOW!":GOTO6
4395 IFROTHENRO=0:M$="THE ROPE RISES FROM THE WELL.":GOTO5
4398 RO=-1:M$="THE POLE TURNS, AND THE ROPE GOES DOWN INTO THE WELL.":GOTO5
4400 IFV$="WAIT"ORV$="Z"THENM$="TIME PASSES...":GOTO5
4410 IFV$<>"EAT"ANDV$<>"TASTE"THEN4450
4415 GOSUB1000:IFFL=0THENM$="YOU CAN'T DO THAT NOW!":GOSUB1:GOSUB8:GOTO100
4418 IFO(I)<>-1THENM$="YOU CAN'T DO THAT NOW!":GOSUB1:GOSUB8:GOTO100
4420 IFI<>0ANDI<>8THENM$="YOU CAN'T EAT THAT!":GOSUB1:GOSUB8:GOTO100
4430 IFI=8ANDAPTHENM$="YOU TAKE A BITE. YOU GASP, FALL DOWN AND THE APPLE FALLS FROM YOUR LIMP HAND.":GOSUB1:GOTO820
4435 IFI=0ANDCSTHENM$="YOU MUNCH A LITTLE. YOU COLLAPSE WITH THE CARROT CLUTCHED IN YOUR HAND.":GOSUB1:GOTO820
4440 IFI=8THENM$="YOU TAKE A BITE OUT OF THE APPLE AND SPIT IT OUT.":GOTO5
4442 O(0)=0:M$="MMMMMNN... THE CARROT IS DELICIOUS. YOU GOBBLE IT UP.":GOTO5
4450 IFV$="SOAK"ANDW1$=""THENV$="PUT":W1$="IN":W2$="WATER":GOTO2000
4452 IFV$="SOAK"THENV$="PUT":GOTO2000
4460 IFV$<>"BREAK"ANDV$<>"SMASH"THEN4490
4465 IFN$<>"MIRRO"THENM$="THERE'S EITHER NO POINT OR YOU DON'T HAVE THE OBJECT.":GOTO6
4470 IFR<>24THENM$="THERE'S EITHER NO POINT OR YOU DON'T HAVE THE OBJECT.":GOTO6
4480 IFBRTHENM$="IT'S ALREADY BROKEN!":GOTO6
4485 BR=-1:SC=SC+8:GOSUB9:MA(24,2)=25:M$="THE MIRROR BREAKS, REVEALING A PASSAGE TO THE EAST.":GOTO5
4490 IFV$<>"GIVE"THEN4520
4492 IFW1$="TO"THENW1$=N$:N$=W2$
4495 IFN$<>"BUNNY"THEN4510
4498 IFR<>25ORDBTHENM$="I DON'T SEE A BUNNY-- DO YOU?":GOTO6
4500 N$=W1$:GOSUB1000:IFFL=0THENM$="YOU DON'T HAVE THAT!":GOTO6
4502 IFO(I)<>-1THENM$="YOU DON'T HAVE THAT!":GOTO6
4505 IFN$<>"CARRO"THENM$="THE BUNNY REFUSES YOUR OFFER.":GOTO6
4507 IFNOTCSTHENO(0)=0:M$="THE BUNNY GOBBLES UP THE CARROT BUT IS NOT SATISFIED.":GOTO5
4509 IFCSTHENSC=SC+10:GOSUB9:DB=-1:M$="THE BUNNY GOBBLES THE CARROT UP, GASPS, DIES AND THEN DISAPPEARS.":GOTO5
4510 M$="I'M NOT SURE IT WILL ACCEPT YOUR OFFER.":GOTO6
4520 IFV$<>"SIT"THEN4540
4530 IFN$="ON"THENN$=W1$
4532 IFN$<>"THRON"ORR<>25THENM$="THAT PROBABLY WON'T WORK.":GOTO6
4535 IFSTTHENM$="YOU SIT ON THE THRONE AND NOTHING HAPPENS SO YOU STAND UP AGAIN.":GOTO5
4538 SC=SC+2:ST=-1:MA(25,2)=100:M$="AS YOU SIT ON THE THRONE, A PASSAGE OPENS TO THE EAST!":GOTO5
4540 IFV$="BURN"THENV$="LIGHT":GOTO2000
4542 IFV$="NORTH"THENV$="N":GOTO2000
4544 IFV$="SOUTH"THENV$="S":GOTO2000
4546 IFV$="EAST"THENV$="E":GOTO2000
4547 IFV$="WEST"THENV$="W":GOTO2000
4548 IFV$="UP"THENV$="U":GOTO2000
4549 IFV$="DOWN"THENV$="D":GOTO2000
4550 IFV$<>"UNWEA"THEN4565
4555 IFN$<>"CROSS"THENM$="THAT DOESN'T MAKE SENSE!":GOTO6
4556 IFNOTWCTHENM$="YOU'RE NOT WEARING IT!":GOTO6
4560 WC=0:M$="YOU SLIDE THE CROSS OFF YOUR NECK.":GOTO5
4565 IFV$="DESTR" AND N$="MIRRO" THEN V$="BREAK":GOTO2000
4570 IFV$<>"DESTR"ANDV$<>"KILL"ANDV$<>"ATTAC"THEN4590
4575 IFN$="SKELE"ANDR=9THENM$="THE SKELETON REPELS YOUR ATTACK.":GOTO5
4580 IFN$="VAMPI"ANDVATHENM$="THE VAMPIRE IGNORES YOUR ATTACK.":GOTO5
4585 IFN$="OOZE"ANDNOTDGANDR=19THENM$="THE OOZE LETS OUT A SPLATTER WITH KILLING FORCE.":GOSUB1:GOTO820
4588 IFN$="BUNNY"ANDNOTDBANDR=25THENM$="THE BUNNY BITES YOU IN DEFENSE.":GOSUB1:GOTO820
4589 M$="THAT'S NOT IMPORTANT TO THIS ADVENTURE.":GOTO6
4590 IFV$="TALK"THENM$="THAT WON'T DO MUCH.":GOTO6
4595 IFV$<>"DRINK"ANDV$<>"SIP"THEN4620
4600 IFN$<>"WATER"THENM$="THAT WON'T DO MUCH.":GOTO6
4605 IFR=3ORR=6THEN4615
4608 IFO(2)=-1ORO(2)=RTHENIFBUTHEN4615
4610 M$="THAT WON'T DO MUCH.":GOTO6
4615 M$="YOU SIP THE WATER, AND FEEL SICK. YOU LUNGS TURN INSIDE OUT AND YOU FALL ON THE FLOOR, DEAD.":GOSUB1:GOTO820
4620 IFV$="SCORE"THENGOSUB25420:GOSUB8:GOTO108
4630 M$="SORRY, BUT I DON'T KNOW THAT VERB.":GOTO6
5000 IFR=19ANDDG=0THEN5005
5001 GOTO4216
5005 DG=-1:MA(19,3)=18:O(4)=22:NI=NI-1
5006 M$="THE OOZE DISSOLVES THE TORCH, LIGHTING IT INSTANTLY. THE OOZE MELTS AWAY INTO NOTHING.":GOSUB1:GOSUB750:GOTO100
5010 BC=-1:MA(23,2)=24:SC=SC+5:GOSUB9:M$="THE COBWEBS LIGHT AND BURN AWAY INTO DUST.":GOTO5
25000 IFNR=0THENRETURN
25030 FORI=1TONR:FORJ=0TOND-1:READMA(I,J):NEXT:NEXT:RETURN
25100 DATA0,4,,,,,,,3,,,,,200,,2,,,1,8,5,,,201,,8,6,4,,,200,9,10,5,,,,11,,,240,,5,,9,4,,,6,,10,8,,314
25190 DATA6,,,9,,,7,,,,,,,,,,,,16,12,17,17,,,,,15,,9,,15,19,15,14,,,13,22,20,21,,,13,20,16,16,,,,,19,17,,0
25280 DATA15,,,203,,,13,17,17,16,,,16,23,13,20,,,17,21,20,16,,,21,26,,,,,,,,23,,,,,,24,,,23,27,,,,,26,,,,,0
25400 M$="YOU GO THROUGH THE TUNNEL AND END UP OUTSIDE THE GRAVEYARD. WHEN YOU REALIZE WHERE YOU ARE, YOU HURRY ALONG "
25405 M$=M$+"THE PATH BACK INTO TOWN, OVERCOME WITH JOY, SHOUTING THE NEWS.":GOSUB1
25406 M$="PEOPLE RUSH OUT OF THEIR HOUSES TO GREET YOU WITH A HERO'S WELCOME.":GOSUB1
25410 GOSUB25420:GOTO3440
25420 M$="YOU HAVE SCORED"+STR$(SC)+" OUT OF 60 POINTS.":GOSUB1
25422 IFSC<11THENRA$="NOVICE ADVENTURER"
25430 IFSC<21THENRA$="GRAVEYARD EXPLORER"
25440 IFSC<31THENRA$="ADVENTURER"
25450 IFSC<41THENRA$="MONSTER BASHER"
25460 IFSC<51THENRA$="EXPERT ADVENTURER"
25470 IFSC<60THENRA$="EXTERMINATOR"
25480 IFSC>=60THENRA$="BUNNY SLAYER"
25490 M$="YOU HAVE REACHED THE RANK OF "+RA$+".":GOTO10
26000 IFNO=0THENRETURN
26010 FORI=0TONO-1
26020 READO$(I),O2$(I),O(I):NEXT:RETURN
26100 DATA"A CARROT",CARRO,0,"A LAMP",LAMP,2,"A BUCKET",BUCKE,11,"A SILVER CROSS",CROSS,0,"A TORCH",TORCH,14
26150 DATA"A KEY",KEY,18,"A SCRAP OF PAPER",PAPER,8,"A QUILL PEN",PEN,-1,"AN APPLE",APPLE,-1
27000 PRINT@6*32,"the night of the vampire bunnies":PRINT@9*32,TAB(10)"BY JASON DYER":PRINT@480,"PRESS ANY KEY TO CONTINUE";
27070 I$=INKEY$:IFI$=""THEN27070
27080 CLS:RETURN
40000 LO$="AT COFFIN":GOSUB7
40010 M$="YOU ARE AT A COFFIN IN A GRAVEYARD, WITH WALLS TO THE NORTH, EAST, AND WEST.":GOSUB1
40011 MES$="A BLUE MIST HANGS OVER THE GRAVEYARD. "
40015 IFVATHENM=MES$+"THE COFFIN IS OPEN WITH A VAMPIRE IN IT!":GOTO10
40020 IFOC THENM$=MES$+"THE COFFIN IS OPEN.":GOSUB1:GOTO40030
40021 M$=MES$+"THE COFFIN IS CLOSED.":GOSUB1
40030 IFNOTOCTHENRETURN
40040 FORI=0TO8:IFO(I)=.1THENM$="THERE IS "+O$(I)+" IN THE COFFIN.":GOSUB1
40050 NEXT:RETURN
40060 LO$="GRAVEYARD":PRINTLO$
40070 M$="YOU ARE IN A GRAVEYARD SURROUNDED BY WALLS. THERE IS FOG HERE SO DENSE YOU CAN BARELY SEE 2 METERS IN FRONT "
40071 M$=M$+"OF YOU. A HOLE IN THE EAST WALL ALLOWS EXIT FROM THIS GLOOMY PLACE.":GOTO10
40080 LO$="GRAVEYARD":GOSUB7:IFNOTJRTHENJR=-1:SC=SC+3:GOSUB9
40090 M$="YOU ARE IN A GRAVEYARD WITH A STREAM TO THE SOUTH. WALLS SURROUND THE OTHER DIRECTIONS, BUT A HOLE IN THE WEST "
40091 M$=M$+"WALL ALLOWS PASSAGE. IT IS VERY COOL HERE.":GOTO10
40100 LO$="GRAVEYARD":GOSUB7
40110 M$="YOU ARE IN A DARK GRAVEYARD. A WALL COVERED WITH MOSS IS TO THE WEST, A COFFIN IS IN SIGHT TO THE NORTH, AND AN "
40111 M$=M$+"EMPTY WELL LIES IN THE MIDDLE OF ALL THE FOG.":GOSUB1
40112 IFRO THEN M$="A ROPE LEADS DOWN THE DAMP WELL.":GOTO10
40140 LO$="CARROT FIELD":GOSUB7
40150 M$="YOU ARE IN A CARROT FIELD IN THE MIDDLE OF A GRAVEYARD. FOG IS ALL AROUND YOU. "
40151 M$=M$+"A WALL IS TO THE NORTH, TOO HIGH TO CLIMB.":GOTO10
40160 LO$="GRAVEYARD":GOSUB7
40170 M$="YOU ARE IN A FOGGY GRAVEYARD. A STREAM IS TO THE NORTH AND A FIELD CAN BE SEEN TO THE WEST.":GOSUB1
40171 M$="IN ALL OTHER DIRECTIONS ONLY FOG CAN BE SEEN. THE STREAM LOOKS SHALLOW ENOUGH TO CROSS.":GOTO10
40180 LO$="BOTTOM OF WELL":GOSUB7:IFNOT WELL THEN WELL=-1:SC=SC+5:GOSUB9
40190 M$="YOU ARE AT THE BOTTOM OF AN EMPTY WELL. THE SURROUNDING CRACKS ARE RATHER BIG, "
40191 M$=M$+"AND ONE TO THE SOUTH IS BIG ENOUGH TO GO THROUGH. A ROPE HANGS DOWN ALLOWING YOU TO CLIMB UP.":GOTO10
40200 LO$="GRAVEYARD":GOSUB7
40205 M$="YOU ARE IN A WALLED GRAVEYARD. A BLACK WALL IS TO THE SOUTH. ":GOSUB1
40206 IFPL THEN M$="A GAPING HOLE IS IN THE MIDDLE OF ALL THE GRAVESTONES.":GOTO10
40207 M$="MANY TOMBSTONES ARE HERE-- SO MANY, IN FACT, YOU HAVE TO CLIMB OVER SOME OF THEM.":GOTO10
40220 LO$="AT HOLE":GOSUB7
40230 M$="YOU ARE IN A GLOOMY GRAVEYARD, WITH A WALL TO THE SOUTH, AND PLENTY OF FOG ALL AROUND YOU. "
40231 M$=M$+"A LARGE HOLE SURROUNDED BY GRAVESTONES LEADS DOWN INTO GLOOM.":GOSUB1
40240 IFWCTHENM$="A SKELETON GUARDING THE HOLE FLEES WHEN HE SEES THE CROSS YOU ARE WEARING.":GOTO10
40245 M$="A SKELETON GUARDS THE HOLE AND DOES NOT ALLOW YOU TO ENTER.":GOTO10
40250 LO$="GRAVEYARD":GOSUB7
40260 M$="YOU ARE AT THE CORNER OF A WALLED GRAVEYARD. WALLS CONNECT TO THE SOUTH AND EAST, AND MIST HANGS OVER THE "
40261 M$=M$+"SKY LIKE A PROWLER. A CHILL RUNS THROUGH YOUR BACK.":GOTO10
40270 LO$="CREVICE":GOSUB7
40280 M$="YOU ARE IN A LARGE CREVICE IN A WELL. SMALL AMOUNTS OF WATER TRICKLE DOWN THE SIDES. "
40281 M$=M$+"EXIT CAN BE REACHED TO THE NORTH.":GOTO10
40290 LO$="MAZE":M$="YOU HAVE A NERVOUSNESS ABOUT THIS PASSAGE, BUT YOU TRUDGE ON, AND END UP GETTING "
40291 M$=M$+"KILLED IN A CAVE-IN.":GOSUB1:R=13:FF=-1:RETURN
40300 LO$="MAZE":M$="YOU ARE IN A DARK MAZE.":GOTO10
40310 LO$="DARK TUNNEL":GOSUB7
40320 M$="YOU ARE IN A VERY DARK TUNNEL. THE TUNNEL HEADS TO THE EAST, WHILE A HOLE IN THE CEILING ALLOWS EXIT FROM "
40321 M$=M$+"THE DARKNESS.":GOTO10
40330 LO$="DARK TUNNEL":GOSUB7
40340 M$="YOU ARE IN A DARK TUNNEL. YOU CAN BARELY MAKE OUT EXITS TO ALL DIRECTIONS EXCEPT UP AND DOWN.":GOTO10
40350 LO$="MAZE":PRINT"YOU ARE IN A DAMP MAZE.":RETURN
40360 LO$="MAZE":PRINT"YOU ARE IN A DANK MAZE.":RETURN
40370 LO$="LONG CORRIDOR":GOSUB7
40375 IFNOT SL THEN SL=-1:SC=SC+5:GOSUB9
40380 M$="YOU ARE IN A LONG TUNNEL STREACHING TO THE EAST AND WEST. YOUR FEET ACHE AS YOU WALK DOWN IT.":GOTO10
40390 LO$="SLIMY ROOM":GOSUB7
40400 M$="YOU ARE IN A SLIME COVERED MEDIUM-SIZE ROOM. YOUR FEET STICK TO THE GROUND.":GOSUB1
40401 IFNOTDGTHENM$="SOME GREY OOZE GUARDS THE WESTWARD CORRIDOR.":GOSUB1
40405 RETURN
40410 LO$="MAZE":PRINT"YOU ARE IN A DREARY MAZE.":RETURN
40420 LO$="MAZE":PRINT"YOU ARE IN A MAZE.":RETURN
40430 LO$="MAZE":PRINT"YOU ARE IN A DEVLISH MAZE.":RETURN
40440 LO$="TORCH ROOM":GOSUB7
40445 M$="YOU ARE IN A LARGE MARBLE ROOM WITH TORCH HOLDERS EVERYWHERE. HOWEVER, ":GOSUB1
40446 IFBCTHENM$="MOST OF THEM ARE EMPTY. YOU CAN LEAVE TO THE NORTH, SOUTH, OR EAST.":GOTO10
40447 M$="MOST OF THEM ARE EMPTY. YOU CAN GO SOUTH AND NORTH, BUT THE EAST PASSAGE IS BLOCKED BY COBWEBS.":GOTO10
40450 LO$="DEAD END":GOSUB7
40460 IFBRTHENM$="YOU ARE IN A DEAD END THAT EXITS WEST, BUT A SHATTERED MIRROR SHOWS A PASSAGE EAST.":GOTO10
40465 M$="YOU ARE IN A DEAD END, WITH A LARGE MIRROR COVERING THE EAST WALL AND AN EXIT TO THE WEST.":GOTO10
40470 LO$="BUNNY ROOM":GOSUB7
40472 IFNOTDBTHENM$="YOU'RE IN A LARGE SPARSELY FURNISHED CHAMBER EXCEPT FOR A THRONE WITH A BUNNY NEXT TO IT.":GOSUB1
40475 IFNOTDBTHENM$="IT LOOKS LIKE THE VAMPIRE BUNNY! A PASSAGE EXITS WEST FROM THIS UNFRIENDLY PLACE.":GOTO10
40480 IFNOTSTTHENM$="YOU'RE IN A LARGE SPARSELY FURNISHED CHAMBER EXCEPT FOR A THRONE. A PASSAGE EXITS WEST.":GOTO10
40485 M$="YOU ARE IN A LARGE SPARSELY FURNISHED CHAMBER EXCEPT FOR A THRONE. PASSAGES EXIT EAST AND WEST.":GOTO10
40490 LO$="CRAMPED CORRIDOR":GOSUB7
40500 M$="YOU ARE IN A CRAMPED CORRIDOR GOING NORTH/SOUTH. A WORN LEVER IS SET INTO THE WEST WALL.":GOTO10
40510 LO$="SANDY ROOM":GOSUB7
40520 IFNOTPLTHENM$="YOU ARE IN A SANDY ROOM WITH AN EXIT NORTH. THE CEILING LOOKS LIKE IT COULD CAVE IN.":GOTO10
40530 M$="YOU ARE IN A SANDY ROOM WITH AN EXIT NORTH AND A HOLE IN THE ROOF. THE CEILING LOOKS WEAK.":GOTO10
