0 CLS:CLEAR500:DIMC1,CC,M$:GOTO20
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
20 NO=9:R=5:ND=6:NR=27:MO=0:NI=2
30 DIMR$(NR),OB$(NO),O2$(NO),OB(NO),MA(28,6),C$(11)
35 CLS:GOSUB27000:CLS
40 IFRND(0)>.5THENPRINT"I'LL BE BACK IN A SEC...":GOTO50
41 PRINT"PLEASE WAIT..."
50 GOSUB25000:GOSUB26000
55 CLS
90 GOSUB700
91 IFFFTHENFF=0:GOTO820
92 GOSUB600:GOSUB300
100 IF VA THEN VT=VT+1:IF VT=2 THEN M$="THE VAMPIRE DECIDES TO FANG YOU."GOSUB1:GOTO810
102 IFR=25ANDNOTDBTHENRA=RA+1:GOTO104
103 RA=0
104 IF R=25 AND NOT DB THEN IF RA=3 THEN M$="THE VAMPIRE BUNNY NIBBLES A LITTLE ON YOUR LEG.":GOSUB1:GOTO810
108 IFC$(1)<>"" THEN CM$=C$(1):BF=0:GOTO126
109 INPUTCM$:IFCM$=""THENPRINT"WHAT'S THAT?":GOTO109
110 FOR I=1 TO LEN(CM$):G$=MID$(CM$, I, 1):IF ASC(G$) > 96 AND ASC(G$)<123 THEN G=ASC(G$)-32:MID$(CM$, I)=CHR$(G)
111 NEXT
114 FOR I=1 TO LEN(CM$)
115 IFMID$(CM$, I, 4)="THE "THENG$=LEFT$(CM$, I-1):GG$=MID$(CM$,I+4):CM$=G$+GG$:GOTO115
116 NEXT:C=0
117 Z=LEN(CM$):L=0:FORI=1TOZ
118 L=L+1
119 IFMID$(CM$,L,3)="AND"THEN C=C+1:C$(C)=LEFT$(CM$, L-2):G$=MID$(CM$, L+4):CM$=G$:L=1
120 IFMID$(CM$,L,4)="THEN"THEN C=C+1:C$(C)=LEFT$(CM$, L-2):G$=MID$(CM$, L+5):CM$=G$:L=1
121 IFMID$(CM$,L,1)="."THENIF LEN(CM$)<>L THEN C=C+1:C$(C)=LEFT$(CM$, L-1):G$=MID$(CM$, L+1):CM$=G$:L=1
122 IFMID$(CM$,L,2)="  "THENG$=LEFT$(CM$, L-1):GG$=MID$(CM$, L+3):CM$=G$+GG$:GOTO122
123 IFLEFT$(CM$,1)=" "THENCM$=RIGHT$(CM$, LEN(CM$)-1)
124 IFRIGHT$(CM$,1)="."THENCM$=LEFT$(CM$, LEN(CM$)-1)
125 NEXT:C$(C+1)=CM$:IF C$(1)<>"" THEN CM$=C$(1)
126 BF=0:FOR I=2 TO 10:C$(I-1)=C$(I):C$(I)="":NEXT
200 C=0:V$="":N$="":W1$="":W2$=""
202 C=C+1:IFC>LEN(CM$)THEN208
204 W$=MID$(CM$,C,1):IFW$=" "THEN208
206 V$=V$+W$:GOTO202
208 C=C+1:IF C > LEN(CM$) THEN 214
210 W$=MID$(CM$, C, 1):IF W$=" " THEN 214
212 N$=N$+W$:GOTO208
214 C=C+1:IF C > LEN(CM$) THEN 220
216 W$=MID$(CM$, C, 1):IF W$=" " THEN 220
218 W1$=W1$+W$:GOTO214
220 C=C+1:IF C > LEN(CM$) THEN 226
222 W$=MID$(CM$, C, 1):IF W$=" " THEN 226
224 W2$=W2$+W$:GOTO220
226 IF V$="" THEN PRINT"COME AGAIN?":GOTO109
227 IF LEN(V$) > 5 THEN V$=LEFT$(V$, 5)
228 IF LEN(N$) > 5 THEN N$=LEFT$(N$, 5)
229 IF LEN(W1$) > 5 THEN W1$=LEFT$(W1$, 5)
230 IF LEN(W2$) > 5 THEN W2$=LEFT$(W2$, 5)
234 IF V$="G" OR V$="AGAIN" THEN CM$=VVV$:V$=V1$:N$=VV$:W1$=V2$:W2$=V3$
235 IF N$="IT" THEN N$=VV$
236 VV$=N$:VVV$=CM$:V1$=V$:V2$=W1$:V3$=W2$
240 IF N$="SILVE" THEN N$="CROSS"
250 IF N$="SCRAP" OR N$="SHEET" THEN N$="PAPER"
255 IF W1$="SCRAP" OR W1$="SHEET" THEN W1$="PAPER"
260 IF N$="QUILL" THEN N$="PEN"
262 IF W1$="QUILL" THEN W1$="PEN"
265 IF N$="GRAY" THEN N$="OOZE"
266 IF N$="WOODE" THEN N$="CRANK"
267 IF N$="WEBS" THEN N$="COBWE"
268 IF W2$="GRAY" THEN W2$="OOZE"
269 IF W1$="SILVE" THEN S1$="CROSS"
290 MO=MO+1:GOSUB300:GOTO320
300 PRINT"-------------------------------":PRINT"SCORE:"SC,"MOVES:"MO:RETURN
320 IFR<11 AND R<>7 THEN 340
325 IFOB(4)=-1 OR OB(4)=R THEN 340
327 IFNOTLATHEN330
328 IFOB(1)=-1 OR OB(1)=R THEN 340
330 IFCM$<>"LIGHT LAMP" AND CM$<>"TURN ON LAMP" THEN M$="YOU ARE IN TOTAL DARKNESS.":GOSUB1:GOSUB300
335 FORTEMP=1TO11:C$(TEMP)="":NEXT:GOTO109
340 IFCM$="TALK INTO WELL"ORCM$="TALK TO WELL"ORCM$="TALK WELL"THEN345
341 GOTO400
345 IFR=4THENPRINT"ECHO...ECHO....ECHO......ECHO...":GOTO100
400 IF V$="PUT" AND N$="ON" THEN V$="WEAR":N$=W1$
402 IF V$="GET" AND N$="ON" THEN V$="WEAR":N$=W1$
404 IF V$="TAKE" AND N$="OFF" THEN V$="UNWEA":N$=W1$
405 IF V$="GET" AND N$="OFF" THEN V$="UNWEA":N$=W1$
407 IF V$="THROW" AND N$="WATER" THEN V$="POUR"
408 IF V$="THROW" AND N$="BUCKE" THEN V$="POUR"
409 IF VA AND V$="POUR" THEN IF N$="WATER" OR N$="BUCKE" THEN W1$="ON":W2$="VAMPI"
410 IF V$="TAKE" AND W1$="OFF" THEN V$="UNWEA"
420 IF V$="GET" AND W1$="OFF" THEN V$="UNWEA"
590 GOTO2000
600 FOR I=0 TO NO-1:IF OB(I)=R THEN M$="YOU SEE "+OB$(I)+" HERE.":GOSUB1
610 NEXT:RETURN
700 IF R<11 AND R<>7 THEN 710
701 IF OB(4)=-1 OR OB(4)=R THEN 710
702 IFNOTLATHEN704
703 IF OB(1)=-1 OR OB(1)=R THEN 710
704 LO$="DARKNESS":GOSUB300:RETURN
710 IFR=100THENLO$="OUTSIDE GRAVEYARD":GOSUB300:GOTO25400
715 ONRGOSUB40000,40060,40080,40100,40140,40160,40180,40200,40220,40250,40270,40290,40300,40310,40330:IFR<=15THENRETURN
716 ONR-15GOSUB40350,40360,40370,40390,40410,40420,40430,40440,40450,40470,40490,40510:RETURN
750 IF NOT LA THEN 770
760 IF OB(1)=-1 OR OB(1)=R THEN RETURN
770 LO$="DARKNESS":GOSUB300:PRINT"UH-OH, NOW YOU CAN'T SEE!":RETURN
800 FOR TEMP=1 TO 11:C$(TEMP)="":NEXT:MO=MO-1:GOSUB300:RETURN
810 PRINT"***** YOU HAVE DIED *****":GOSUB25420:GOTO3440
820 PRINT"***** YOU HAVE DIED *****":GOSUB25420:PRINT"NEW GAME OR UNDO(Y/N/U)?"
830 I$=INKEY$:IFI$<>"Y"ANDI$<>"N"ANDI$<>"U"THEN830
840 IFI$="Y"THENRUN
841 IFI$="U"THENGOSUB800:GOTO90
850 END
1000 IF NO=0 THEN RETURN
1010 FOR I=0 TO NO-1
1020 IF O2$(I)=N$ THEN FL=1:RO=OB(I):GOTO1050
1030 NEXT
1040 FL=0:RETURN
1050 RETURN
2000 IFV$<>"GO"ANDV$<>"RUN"ANDV$<>"CRAWL"ANDV$<>"N"ANDV$<>"S"ANDV$<>"CLIMB"ANDV$<>"E"ANDV$<>"W"ANDV$<>"U"ANDV$<>"D"THEN2500
2010 IFN$="NORTH"ORV$="N"THENDI=0:GOTO2400
2020 IF N$="SOUTH" OR V$="S" THEN DI=1:GOTO2400
2030 IF N$="EAST" OR V$="E" THEN DI=2:GOTO2400
2040 IF N$="WEST" OR V$="W" THEN DI=3:GOTO2400
2050 IF N$="UP" OR N$="UPWAR" OR V$="U" THEN DI=4:GOTO2400
2060 IF N$="DOWN" OR N$="DOWNW" OR V$="D" THEN DI=5:GOTO2400
2070 IF R=1 AND N$="COFFI" THEN PRINT"ARE YOU CRAZY?":GOTO100
2080 IF R=3 THEN IF N$="STREA" OR N$="RIVER" THEN V$="S":GOTO2000
2090 IF R=6 THEN IF N$="STREA" OR N$="RIVER" THEN V$="N":GOTO2000
2100 IF R=9 AND N$="HOLE" THEN V$="D":GOTO2000
2110 IF R=4 AND N$="WELL" THEN V$="D":GOTO2000
2120 IF R=7 AND N$="CREVI" THEN V$="S":GOTO2000
2130 IF R=8 AND N$="HOLE" THEN V$="D":GOTO2000
2135 IF R=7 AND N$="ROPE" THEN DI=4:GOTO2400
2136 IF R=4 AND N$="ROPE" AND RO THEN DI=5:GOTO2400
2140 IF R=27 AND N$="HOLE" THEN V$="U":GOTO2000
2145 IF R=14 AND N$="HOLE" THEN V$="U":GOTO2000
2148 IF R=2 AND N$="HOLE" THEN V$="E":GOTO2000
2200 IF R=3 AND N$="HOLE" THEN V$="W":GOTO2000
2390 PRINT"YOU CAN'T GO THAT WAY!":GOSUB800:GOTO109
2400 IF MA(R, DI) > 0 AND MA(R, DI)<128 THEN R=MA(R, DI):GOTO90
2402 IF MA(R, DI)=203 THENM$="THE OOZE BLOCKS YOUR PATH.":GOSUB1:GOTO100
2404 IF MA(R, DI)=204 THENM$="THE BUNNY WON'T LET YOU!":GOSUB1:GOTO100
2405 IFMA(R,DI)=200THENM$="YOU ENTER THE STREAM. A LITTLE WATER GETS IN YOUR MOUTH. YOU CHOKE AND DROWN.":GOSUB1:GOTO820
2408 IF MA(R, DI)=201 AND NOT RO THEN 2390
2409 IF MA(R, DI)=201 AND NOT LA THEN M$="IT'S TOO DARK DOWN THERE.":GOSUB1:GOTO100
2410 IF MA(R, DI)=201 THEN IF OB(1)<>7 AND OB(1)<>-1 THEN M$="IT'S TOO DARK DOWN THERE.":GOSUB1:GOTO100
2411 IF MA(R, DI)=240 AND NI > 1 THEN M$="YOU'RE TOO HEAVY TO CLIMB UP THE WELL.":GOSUB1:GOTO100
2412 IF MA(R, DI)=240 THEN PRINT"YOU CLIMB UP THE ROPE.":R=4:GOTO90
2413 IFMA(R,DI)=201ANDNI>1THENM$="AS YOU ATTEMPT TO CLIMB DOWN THE ROPE SNAPS AND YOU PLUNGE DOWN THE WELL.":GOSUB1:GOTO820
2415 IF MA(R, DI)=201 THEN M$="YOU MAKE YOUR WAY DOWN THE ROPE.":GOSUB1:R=7:GOTO90
2419 IF MA(R, DI)=700 THEN M$="THE VAMPIRE, UNFORTUNATELY, GIVES YOU A NASTY BITE.":GOSUB1:GOTO810
2420 IF MA(R, DI)=314 AND NOT WC THEN M$="THE SKELETON PUSHES YOU AWAY FROM THE HOLE.":GOSUB1:GOTO100
2430 IF MA(R, DI)=314 THEN M$="THE SKELETON ALLOWS YOU TO ENTER.":GOSUB1:R=14:GOTO90
2480 GOTO2390
2500 IF V$<>"GET" AND V$<>"TAKE" THEN 2580
2510 ZH=0:GOSUB1000:IF N$="WATER" THEN IF OB(2)=-1 THEN V$="FILL":N$="BUCKE":GOTO2000
2520 IF FL=0 THEN PRINT"YOU CAN'T GET THAT!":GOSUB800:GOTO109
2530 IF RO=-1 THEN PRINT"YOU ALREADY HAVE THAT!":GOSUB800:GOTO109
2550 IF RO<>R THEN PRINT"YOU CAN'T GET THAT!":GOSUB800:GOTO109
2560 IF NI=5 THEN PRINT"YOU'RE CARRYING TOO MUCH!":GOSUB800:GOTO109
2565 IF NOT CROS AND I=3 THEN CROS=-1:SC=SC+10:GOSUB300
2566 IF NOT CARP AND I=0 THEN CARP=-1:SC=SC+2:GOSUB300
2570 NI=NI+1:OB(I)=-1:PRINT"TAKEN.":GOTO100
2580 IF V$="HINT" OR V$="HELP" THEN PRINT"SORRY."
2581 IF V$="TURN" AND W1$="OFF" THEN V$="UNLIG"
2582 IF V$="TURN" AND W1$="ON" THEN V$="LIGHT"
2585 IF V$="TURN" AND N$="OFF" THEN V$="UNLIG":N$=W1$
2586 IF V$="TURN" AND N$="ON" THEN V$="LIGHT":N$=W1$
2588 IF V$="PUT" AND N$="DOWN" THEN V$="DROP":N$=W1$:GOTO2500
2590 IF V$="PICK" AND N$="UP" THEN V$="TAKE":N$=W1$:GOTO2500
2600 IF V$<>"DROP" THEN 2700
2605 IF W1$<>"" THEN V$="PUT":GOTO2000
2610 GOSUB1000
2620 IF FL=0 THEN PRINT"YOU DON'T HAVE THAT!":GOSUB800:GOTO109
2635 IF RO=-2 THEN RO=-1
2640 IF RO<>-1 THEN PRINT"YOU DON'T HAVE THAT!":GOSUB800:GOTO109
2650 NI=NI-1:OB(I)=R:PRINT"DROPPED.":GOTO100
2700 IF V$<>"INVEN" AND V$<>"I" THEN 2800
2710 FL=0:PRINT"YOU HAVE:"
2715 IF WC THEN PRINT" A SILVER CROSS (BEING WORN)":FL=1
2716 IF BU AND OB(2)=-1 THEN PRINT" A BUCKET (WITH WATER)":FL=1
2717 IF LA AND OB(1)=-1 THEN PRINT" A LAMP (PROVIDING LIGHT)":FL=1
2718 IF OB(4)=-1 THEN PRINT" A TORCH (PROVIDING LIGHT)":FL=1
2720 FOR I=0 TO NO-1
2725 IF WC AND I=3 THEN 2740
2726 IF BU AND I=2 AND OB(I)=-1 THEN 2740
2727 IF LA AND I=1 AND OB(I)=-1 THEN 2740
2728 IF I=4 AND OB(I)=-1 THEN 2740
2730 IF OB(I)=-1 THEN PRINT" "; OB$(I):FL=1
2740 NEXT
2745 IF FL=0 THEN PRINT" NOTHING"
2760 GOTO100
2800 IF V$<>"LOOK" AND V$<>"L" THEN 2900
2810 IF N$<>"" THEN 2901
2820 GOTO90
2900 IF V$<>"EXAMI" AND V$<>"SEARC" THEN 3400
2901 IF N$="FIELD" AND R<>5 THEN PRINT"THERE ISN'T A FIELD HERE!":GOSUB800:GOTO109
2904 IFN$="GROUN"ORN$="FLOOR"ORN$="FIELD"THENIFR=5ANDNOTCATHENCA=-1:PRINT"YOU FIND A CARROT.":OB(0)=5:GOTO100
2906 IF N$="GROUN" OR N$="FLOOR" OR N$="FIELD" THEN PRINT"YOU SEE NOTHING UNUSUAL.":GOTO100
2908 IF N$="COFFI" AND R=1 THEN PRINT"IT IS CARVED OF MARBLE.":GOTO100
2909 IF N$="SKELE" AND R=9 THEN PRINT"IT LOOKS PRETTY BONY.":GOTO100
3000 IFN$="WELL"ANDR=4THENM$="IT HAS A POLE CONNECTED TO SOME ROPE AND A WOODEN CRANK. THE WELL IS DARK.":GOSUB1:GOTO100
3001 IF N$="ROPE" THEN IF R=4 OR R=7 THEN M$="IT DOESN'T LOOK VERY STURDY.":GOSUB1:GOTO100
3002 IF N$="WELL" AND R=7 THEN PRINT"IT IS FULL OF CRACKS.":GOTO100
3004 IF N$="OOZE" AND R=19 AND NOT DG THEN M$="IT IS BIG, UGLY, AND SLIMY.":GOSUB1:GOTO100
3005 IF N$="COBWE" AND NOT BC THEN M$="THE ARE BLOCKING THE EAST PASSAGE.":GOSUB1:GOTO100
3006 IF N$="LEVER" AND R=26 THEN PRINT"IT IS RUSTY, BUT USEABLE.":GOTO100
3008 IF N$="BUNNY" AND NOT DB AND R=25 THEN M$="IT LOOKS EXTREMELY VICIOUS FOR ITS SMALL SIZE.":GOSUB1:GOTO100
3009 IF N$="VAMPI" AND VA THEN PRINT"IT LOOKS AT YOU WITH AN UNNERVING GAZE.":GOTO100
3010 IFN$="TOMBS"ORN$="GRAVE"THENIFR<11ANDR<>7THENM$="MANY ARE OLD AND ROTTED, AND SOME ARE BLANK.":GOSUB1:GOTO100
3011 IF N$="CRANK" AND R=4 THEN M$="IT IS CRACKING, BUT USABLE.":GOSUB1:GOTO100
3012 IFN$="STREA"ORN$="RIVER"THENIFR=6ORR=3THENM$="IT LOOKS LIFELESS AND SHALLOW ENOUGH TO CROSS.":GOSUB1:GOTO100
3013 IF N$="POLE" AND R=4 THEN PRINT"IT HAS MANY HOLES IN IT.":GOTO100
3014 IFN$="MIRRO"ANDR=24THEN3020
3015 GOTO3200
3020 IFBRTHENM$="IT IS BROKEN INTO SHARDS, AND THERE IS A PASSAGE BEHIND IT.":GOSUB1:GOTO100
3025 PRINT"IT IS NOT WELL POLISHED.":GOTO100
3027 IFN$="WALLS"ORN$="WALL"THENM$="THERE ARE ENOUGH OF THEM.":GOSUB1:GOTO100
3200 GOSUB1000
3202 IFRO<>RANDRO<>-1ANDOB(I)<>(R-128)THENPRINT"THAT'S NOT HERE!":GOSUB800:GOTO109
3204 IFN$="CARRO"THENPRINT"IT LOOKS NICE AND JUICY.":GOTO100
3208 IFN$<>"LAMP"THEN3215
3209 IFLATHENPRINT"THE LAMP IS ON.":GOTO100
3210 PRINT"THE LAMP IS OFF.":GOTO100
3215 IFN$<>"BUCKE"THEN3220
3216 IFBUTHENPRINT"THE BUCKET IS FILLED.":GOTO100
3217 PRINT"THE BUCKET IS EMPTY.":GOTO100
3220 IFN$="CROSS"THENPRINT"IT IS ROUGH AND TARNISHED.":GOTO100
3300 IFN$="PAPER"THENPRINT"IT HAS SOME WRITING ON IT.":GOTO100
3310 IFN$="KEY"THENPRINT"IT IS FAIRLY SHINY.":GOTO100
3312 IFN$="TORCH"THENPRINT"IT IS BURNING FAIRLY SLOWLY.":GOTO100
3314 IFN$="PEN"THENPRINT"IT DOES NOT HAVE ANY INK.":GOTO100
3316 IFN$="APPLE"THENM$="IT LOOKS A LITTLE TOO WORMY TO BE EDIBLE.":GOSUB1:GOTO100
3318 PRINT"YOU SEE NOTHING UNUSUAL.":GOTO100
3400 IFV$<>"QUIT"ANDV$<>"Q"ANDV$<>"END"THEN3500
3405 GOSUB25420
3410 INPUT"SURE YOU WANT TO QUIT <Y/N>";QU$
3420 IFQU$="N"THENGOSUB800:GOTO109
3430 IFQU$<>"Y"THEN3410
3440 PRINT"PLAY AGAIN <Y/N>?"
3450 I$=INKEY$:IFI$<>"Y"ANDI$<>"N"THEN3450
3455 IFI$="Y"ORI$="Y"THENRUN
3456 END
3500 IFV$<>"ENTER"THEN3510
3505 V$="GO":GOTO2000
3510 IFV$<>"JUMP"ANDV$<>"LEAP"THEN3640
3520 IF N$="" THEN PRINT"WHEEEEEE!":GOTO100
3530 IF N$<>"OVER" THEN PRINT"THAT WON'T DO ANY GOOD.":GOSUB800:GOTO109
3535 IFR=19ANDNOTDGANDW1$="OOZE"ORW1$="GREY"THENM$="THE OOZE EXPANDS AND PREVENTS YOUR JUMP.":GOSUB1:GOSUB800:GOTO109
3538 IF R=25 AND NOT DB AND W1$="BUNNY" OR W1$="RABBI" THEN M$="THE BUNNY MANAGES TO BITE YOUR LEG.":GOSUB1:GOTO820
3600 IF W1$<>"STREA" AND W1$<>"RIVER" THEN PRINT"THAT WON'T DO ANYTHING!":GOTO100
3610 IF R<>3 AND R<>6 THEN PRINT"THAT WON'T DO ANYTHING!":GOTO100
3620 IFR=3THENR=6:GOTO3630
3621 R=3
3630 PRINT"YOU LEAP OVER THE STREAM.":GOTO90
3640 IF V$<>"READ" THEN 3680
3650 IF N$<>"PAPER" THEN PRINT"YOU CAN'T DO THAT!":GOSUB800:GOTO109
3660 IF OB(6)<>-1 THEN PRINT"YOU DON'T HAVE THAT!":GOSUB800:GOTO109
3670 M$="IT SAYS, 'PUSHING IS BETTER THAN PULLING'.":GOSUB1:GOTO100
3680 IFV$<>"LIGHT"THEN3730
3685 IFN$="COBWE"THEN3697
3690 GOSUB1000:IF FL=0 THEN PRINT"YOU DON'T HAVE THAT!":GOSUB800:GOTO109
3692 IF OB(I)<>-1 THEN PRINT"YOU DON'T HAVE THAT!":GOSUB800:GOTO109
3695 IF N$<>"TORCH" AND N$<>"COBWE" AND N$<>"LAMP" THEN PRINT"YOU CAN'T DO THAT!":GOSUB800:GOTO109
3697 IFN$="COBWE"ANDR=23ANDBC=0THENPRINT"THE COBWEBS LIGHT AND BURN.":SC=SC+5:GOSUB300:BC=-1:MA(23,2)=24:GOTO100
3699 IF N$="COBWE" THEN PRINT"YOU CAN'T DO THAT NOW!":GOSUB800:GOTO109
3700 IF N$="TORCH" AND OB(4)=-1 THEN PRINT"IT'S ALREADY LIT!":GOTO100
3710 IF N$="LAMP" AND OB(1)=-1 AND LA THEN PRINT"IT'S ALREADY LIT!":GOTO100
3720 IFN$="LAMP"ANDNOTLATHEN3725
3721 GOTO3730
3725 M$="THE LAMP LETS OUT A WARM GLOW.":GOSUB1:LA=-1:IFLO$="DARKNESS"THEN90
3726 GOTO100
3730 IFV$<>"LOWER"THEN3780
3740 IF N$<>"ROPE" AND N$<>"WELL" THEN PRINT"YOU CAN'T DO THAT NOW!":GOSUB800:GOTO109
3750 IF R<>4 THEN PRINT"YOU CAN'T DO THAT NOW!":GOSUB800:GOTO109
3760 IF RO THEN PRINT"IT ALREADY IS!":GOSUB800:GOTO109
3770 RO=-1:M$="THE ROPE GOES DOWN INTO THE WELL.":GOSUB1:GOTO100
3780 IF V$<>"RAISE" AND V$<>"LIFT" THEN 3830
3790 IF N$<>"ROPE" AND N$<>"WELL" THEN PRINT"YOU CAN'T DO THAT NOW!":GOSUB800:GOTO109
3800 IF R<>4 THEN PRINT"YOU CAN'T DO THAT NOW!":GOSUB800:GOTO109
3810 IF NOT RO THEN PRINT"IT ALREADY IS!":GOSUB800:GOTO109
3820 RO=0:PRINT"THE ROPE RISES FROM THE WELL.":GOTO100
3830 IF V$<>"FILL" THEN 3880
3840 IFN$<>"BUCKE"OROB(2)<>-1OR(R<>3ANDR<>6)THENPRINT"YOU CAN'T DO THAT NOW!":GOSUB800:GOTO109
3860 IFBUTHENPRINT"IT'S ALREADY FULL!":GOSUB800:GOTO109
3870 PRINT"THE BUCKET FILLS WITH WATER.":BU=-1:GOTO100
3880 IFV$<>"UNLIG"THEN3940
3890 GOSUB1000:IF FL=0 THEN PRINT"YOU DON'T HAVE THAT!":GOSUB800:GOTO109
3892 IF OB(I)<>-1 THEN PRINT"YOU DON'T HAVE THAT!":GOSUB800:GOTO109
3900 IF N$<>"TORCH" AND N$<>"LAMP" THEN PRINT"YOU CAN'T DO THAT!":GOSUB800:GOTO109
3910 IF N$="TORCH" AND OB(4)=-1 THEN PRINT"IT REFUSES TO GO OUT.":GOTO100
3920 IF N$="LAMP" AND OB(1)=-1 AND NOT LA THEN PRINT"IT ALREADY IS!":GOTO100
3930 IF N$="LAMP" AND OB(1)=-1 THEN PRINT"THE LAMP TURNS OFF WITH A CLICK.":LA=0:GOTO100
3940 IF V$<>"OPEN" THEN 4000
3950 IF N$<>"COFFI" THEN PRINT"YOU CAN'T DO THAT NOW!":GOSUB800:GOTO109
3960 IF R<>1 THEN PRINT"YOU CAN'T DO THAT NOW!":GOSUB800:GOTO109
3970 IF OC THEN PRINT"IT'S ALREADY OPEN!":GOSUB800:GOTO109
3980 IF DVAM THEN PRINT"THE COFFIN OPENS.":OC=-1:GOTO100
3990 OC=-1:M$="AS YOU OPEN THE COFFIN, A VAMPIRE REACHES OUT!":GOSUB1:VA=-1:MA(1, 1)=700:GOTO100
4000 IFV$<>"CLOSE"THEN4100
4010 IFN$<>"COFFI"THENPRINT"YOU CAN'T DO THAT NOW!":GOSUB800:GOTO109
4020 IFR<>1THENPRINT"YOU CAN'T DO THAT NOW!":GOSUB800:GOTO109
4030 IFNOTOCTHENPRINT"IT'S NOT OPEN!":GOSUB800:GOTO109
4032 IFVATHENM$="THE COFFIN SLAMS SHUT IN THE VAMPIRE'S FACE.":GOSUB1:VT=0:VA=0:OC=0:MA(1,1)=4:GOTO100
4034 PRINT"THE COFFIN CLOSES.":OC=0:GOTO100
4100 IFV$<>"POUR"ANDV$<>"EMPTY"ANDV$<>"SPLAS"THEN4130
4110 IF N$<>"WATER" AND N$<>"BUCKE" THEN PRINT"YOU CAN'T POUR THAT!":GOSUB800:GOTO109
4112 IF OB(2)<>-1 THEN PRINT"YOU DON'T HAVE THAT!":GOSUB800:GOTO109
4114 IF NOT BU THEN PRINT"YOU CAN'T DO THAT NOW!":GOSUB800:GOTO109
4116 IF W2$="" THEN BU=0:PRINT"YOU EMPTY THE BUCKET.":GOTO100
4118 IFW2$="OOZE"ORW2$="GREY"ANDR=19ANDDG=0THENM$="THE OOZE GROWSAND SPLATTERS YOU WITH A DEADLY FORCE.":GOSUB1:GOTO820
4120 IFW2$="RABBI"ORW2$="BUNNY"ANDR=24ANDDB=0THENM$="THE BUNNY MOVES, AVOIDING THE SPLASH OF WATER.":GOSUB1:BU=0:GOTO100
4122 IF W2$="APPLE" THEN IF OB(8)=-1 OR OB(8)=R THEN M$="THE APPLE IS SPLASHED WITH WATER.":GOSUB1:BU=0:AP=-1:GOTO100
4123 IFW2$="CARRO"THENIFOB(0)=-1OROB(0)=RTHENM$="THE CARROT IS SPLASHED.":GOSUB1:SC=SC+10:GOSUB300:BU=0:CS=-1:GOTO100
4124 IF W2$="SKELE" AND R=9 THEN M$="THE WATER FLIES THROUGH THE SKELTAL BODY.":GOSUB1:BU=0:GOTO100
4125 IFW2$="VAMPI"ANDVATHENM$="THE VAMPIRE SCREAMS AND DISSOLVES.":GOSUB1:MA(1,1)=4:BU=0:VA=0:OB(3)=1:GOTO100
4128 PRINT"I'M NOT SO SURE ABOUT THAT.":GOSUB800:GOTO109
4130 IFV$<>"WEAR"THEN4150
4140 IF N$<>"CROSS" THEN PRINT"YOU CAN'T DO THAT!":GOSUB800:GOTO109
4142 IF OB(3)<>-1 THEN PRINT"YOU DON'T HAVE THAT!":GOSUB800:GOTO109
4144 IF WC THEN M$="YOU ARE ALREADY WEARING THE CROSS!":GOSUB1:GOSUB800:GOTO109
4146 PRINT"IT FITS RATHER NICE.":WC=-1:GOTO100
4150 IF V$="PUT" AND W1$="ON" THEN IF N$="CROSS" THEN V$="WEAR":GOTO4130
4152 IF V$="GET" AND W1$="ON" THEN IF N$="CROSS" THEN V$="WEAR":GOTO4130
4154 IF V$="GET" AND W1$="OFF" THEN V$="UNWEAR":GOTO2000
4156 IF V$="TAKE" AND W1$="OFF" THEN V$="UNWEAR":GOTO2000
4200 IF V$<>"PUT" THEN 4310
4205 IF W1$<>"ON" AND W1$<>"ONTO" THEN 4220
4211 IF N$="WATER" OR N$="BUCKE" THEN V$="POUR":GOTO2000
4212 IF N$<>"TORCH" THEN 4217
4213 IF OB(4)<>-1 THEN PRINT"YOU DON'T HAVE THAT!":GOSUB800:GOTO109
4215 IFW2$="GREY"ORW2$="OOZE"THEN5000
4216 IFW2$="COBWE"ANDR=23ANDNOTBCTHEN5010
4217 PRINT"THAT PROBABLY WON'T DO MUCH.":GOSUB800:GOTO109
4220 IF W1$<>"IN" AND W1$<>"INTO" THEN 4300
4221 IF N$="WATER" AND W2$="BUCKE" THEN V$="FILL":N$="BUCKE":GOTO2000
4222 IF W2$="OOZE" OR W2$="GREY" THEN 4215
4224 IF W2$="COBWE" THEN 4215
4230 IF W2$<>"WELL" THEN 4242
4235 IF R<>4 THEN PRINT"THAT'S NOT HERE!":GOSUB800:GOTO109
4238 GOSUB1000:IF FL=0 THEN PRINT"YOU DON'T HAVE THAT!":GOSUB800:GOTO109
4239 IF RO<>-1 THEN PRINT"YOU DON'T HAVE THAT!":GOSUB800:GOTO109
4240 PRINT"ENUOOOOOAUUUOOOCLUNK!":OB(I)=7:NI=NI-1:GOTO100
4242 IF W2$<>"STREA" AND W2$<>"WATER" AND W2$<>"BUCKE" AND W2$<>"RIVER" THEN 4300
4244 IF W2$="BUCKE" THEN IF ((OB(2)<>-1) AND (OB(2)<>R)) THEN PRINT"YOU CAN'T SEE ANY BUCKET HERE!":GOSUB800:GOTO109
4245 IF W2$="BUCKE" AND NOT BUCK AND N$="WATER" THEN V$="GET":GOTO2000
4246 IFW2$="BUCKE"ANDNOTBUTHENM$="UNFORTUNATELY, THAT'S NOT GOING TO BE VERY HELPFUL.":GOSUB1:GOSUB800:GOTO109
4248 IFW2$="WATER"THENIFR=3ORR=6THEN4254
4249 IFW2$="WATER"THEN4252
4250 GOTO4254
4252 IFOB(2)=-1ORBUTHEN4254
4253 PRINT"THAT'S NOT HERE!":GOSUB800:GOTO100
4254 IFW2$="STREA"ORW2$="RIVER"THENIFR<>3ANDR<>6THEN PRINT"YOU DON'T SEE ANY!":GOSUB800:GOTO109
4255 GOSUB1000:IF FL=0 THEN PRINT"YOU DON'T HAVE THAT!":GOSUB800:GOTO109
4258 IF OB(I)<>-1 THEN PRINT"YOU DON'T HAVE THAT!":GOSUB800:GOTO109
4260 IF N$="CARRO" THEN IF R=3 OR R=6 THEN M$="THE CARROT SOAKS UP THE WATER.":GOSUB1:SC=SC+10:GOSUB300:CS=-1:GOTO100
4262 IF N$="CARRO" AND OB(2)=-1 AND BU THEN M$="THE CARROT SOAKS UP THE WATER.":GOSUB1:SC=SC+10:GOSUB300:CS=-1:GOTO100
4265 IF N$="APPLE" THEN IF R=3 OR R=6 THEN M$="THE APPLE SOAKS UP THE WATER.":GOSUB1:AP=-1:GOTO100
4268 IF N$="APPLE" AND OB(2)=-1 AND BU THEN M$="THE APPLE SOAKS UP THE WATER.":GOSUB1:AP=-1:GOTO100
4300 PRINT"THAT'S NOT GOING TO DO MUCH FOR THIS ADVENTURE.":GOSUB800:GOTO109
4310 IF V$<>"PUSH" THEN 4350
4315 IF N$="COFFI" AND R=1 THEN PRINT"IT IS MUCH TOO HEAVY.":GOTO100
4317 IF N$="MIRRO" AND R=24 AND NOT BR THEN M$="IT IS ATTACHED TO THE WALL.":GOSUB1:GOTO100
4319 IF N$="THRON" AND R=25 THEN PRINT"IT IS BOLTED TO THE WALL.":GOTO100
4320 IF N$<>"LEVER" OR R<>26 THEN PRINT"YOU DON'T NEED TO DO THAT.":GOSUB800:GOTO109
4330 IF PLEV THEN PRINT"NOTHING HAPPENS.":GOTO100
4340 PRINT"YOU HEAR A LOUD 'CLUNK' TO THE SOUTH.":MA(8, 5)=27:PLEV=-1:MA(27, 4)=8:GOTO100
4350 IF V$<>"PULL" THEN 4375
4355 IF N$<>"LEVER" THEN 4315
4360 IF R<>26 THEN PRINT"YOU CAN'T DO THAT NOW!":GOSUB800:GOTO109
4370 PRINT"THE ROOF CAVES IN ON YOU.":GOTO820
4375 IFV$<>"TURN"THEN4400
4380 IFN$<>"CRANK"THEN PRINT"YOU CAN'T DO THAT NOW!":GOSUB800:GOTO109
4390 IFR<>4THENPRINT"YOU CAN'T DO THAT NOW!":GOSUB800:GOTO109
4395 IFROTHENPRINT"THE ROPE RISES FROM THE WELL.":RO=0:GOTO100
4398 M$="THE POLE TURNS, AND THE ROPE GOES DOWN INTO THE WELL.":GOSUB1:RO=-1:GOTO100
4400 IF V$="WAIT" OR V$="Z" THEN PRINT"TIME PASSES...":GOTO100
4410 IF V$<>"EAT" AND V$<>"TASTE" THEN 4450
4415 GOSUB1000:IF FL=0 THEN PRINT"YOU CAN'T DO THAT NOW!":GOSUB800:GOTO100
4418 IF OB(I)<>-1 THEN PRINT"YOU CAN'T DO THAT NOW!":GOSUB800:GOTO100
4420 IF I<>0 AND I<>8 THEN PRINT"YOU CAN'T EAT THAT!":GOSUB800:GOTO100
4430 IFI=8ANDAPTHENM$="YOU TAKE A BITE. YOU GASP, FALL DOWN AND THE APPLE FALLS FROM YOUR LIMP HAND.":GOSUB1:GOTO820
4435 IFI=0ANDCSTHENM$="YOU MUNCH A LITTLE. YOU COLLAPSE WITH THE CARROT CLUTCHED IN YOUR HAND.":GOSUB1:GOTO820
4440 IFI=8THENM$="YOU TAKE A BITE OUT OF THE APPLE AND SPIT IT OUT.":GOSUB1:GOTO100
4442 M$="MMMMMNN... THE CARROT IS DELICOUS. YOU GOBBLE IT UP.":GOSUB1:OB(0)=0:GOTO100
4450 IF V$="SOAK" AND W1$="" THEN V$="PUT":W1$="IN":W2$="WATER":GOTO2000
4452 IF V$="SOAK" THEN V$="PUT":GOTO2000
4460 IF V$<>"BREAK" AND V$<>"SMASH" THEN 4490
4465 IF N$<>"MIRRO" THEN M$="THERE'S EITHER NO POINT OR YOU DON'T HAVE THE OBJECT.":GOSUB1:GOSUB800:GOTO109
4470 IF R<>24 THEN M$="THERE'S EITHER NO POINT OR YOU DON'T HAVE THE OBJECT.":GOSUB1:GOSUB800:GOTO109
4480 IF BR THEN PRINT"IT'S ALREADY BROKEN!":GOSUB800:GOTO109
4485 BR=-1:M$="THE MIRROR BREAKS, REVEALING A PASSAGE TO THE EAST.":GOSUB1:SC=SC+8:GOSUB300:MA(24, 2)=25:GOTO100
4490 IF V$<>"GIVE" THEN 4520
4492 IF W1$="TO" THEN W1$=N$:N$=W2$
4495 IF N$<>"BUNNY" THEN 4510
4498 IF R<>25 THEN PRINT"I DON'T SEE A BUNNY--DO YOU?":GOSUB800:GOTO109
4499 IF DB THEN PRINT"I DON'T SEE A BUNNY--DO YOU?":GOSUB800:GOTO109
4500 N$=W1$:GOSUB1000:IF FL=0 THEN PRINT"YOU DON'T HAVE THAT!":GOSUB800:GOTO109
4502 IF OB(I)<>-1 THEN PRINT"YOU DON'T HAVE THAT!":GOSUB800:GOTO109
4505 IF N$<>"CARRO" THEN PRINT"THE BUNNY REFUSES YOUR OFFER.":GOSUB800:GOTO109
4507 IF NOT CS THEN M$="THE BUNNY GOBBLES UP THE CARROT BUT IS NOT SATISFIED.":GOSUB1:OB(0)=0:GOTO100
4509 IFCSTHENM$="THE BUNNY GOBBLES THE CARROT UP, GASPS, DIES AND THEN DISAPPEARS.":GOSUB1:SC=SC+10:GOSUB300:DB=-1:GOTO100
4510 M$="I'M NOT SURE IT WILL ACCEPT YOUR OFFER.":GOSUB1:GOSUB800:GOTO109
4520 IF V$<>"SIT" THEN 4540
4530 IF N$="ON" THEN N$=W1$
4532 IF N$<>"THRON" THEN PRINT"THAT PROBABLY WON'T WORK.":GOSUB800:GOTO109
4534 IF R<>25 THEN PRINT"THAT PROBABLY WON'T WORK.":GOSUB800:GOTO109
4535 IFSTTHENM$="YOU SIT ON THE THRONE AND NOTHING HAPPENS SO YOU STAND UP AGAIN.":GOSUB1:GOTO100
4538 M$="AS YOU SIT ON THE THRONE, A PASSAGE OPENS TO THE EAST!":GOSUB1:SC=SC+2:ST=-1:GOSUB300:MA(25,2)=100:GOTO100
4540 IF V$="BURN" THEN V$="LIGHT":GOTO2000
4542 IF V$="NORTH" THEN V$="N":GOTO2000
4544 IF V$="SOUTH" THEN V$="S":GOTO2000
4546 IF V$="EAST" THEN V$="E":GOTO2000
4547 IF V$="WEST" THEN V$="W":GOTO2000
4548 IF V$="UP" THEN V$="U":GOTO2000
4549 IF V$="DOWN" THEN V$="D":GOTO2000
4550 IF V$<>"UNWEA" THEN 4565
4555 IF N$<>"CROSS" THEN PRINT"THAT DOESN'T MAKE SENSE!":GOSUB800:GOTO109
4556 IF NOT WC THEN PRINT"YOU'RE NOT WEARING IT!":GOSUB800:GOTO109
4560 PRINT"YOU SLIDE THE CROSS OFF YOUR NECK.":WC=0:GOTO100
4565 IF V$="DESTR" AND N$="MIRRO" THEN V$="BREAK":GOTO2000
4570 IF V$<>"DESTR" AND V$<>"KILL" AND V$<>"ATTAC" THEN 4590
4575 IF N$="SKELE" AND R=9 THEN M$="THE SKELETON REPELS YOUR ATTACK.":GOSUB1:GOTO100
4580 IF N$="VAMPI" AND VA THEN M$="THE VAMPIRE IGNORES YOUR ATTACK.":GOSUB1:GOTO100
4585 IF N$="OOZE" AND NOT DG AND R=19 THEN M$="THE OOZE LETS OUT A SPLATTER WITH KILLING FORCE.":GOSUB1:GOTO820
4588 IF N$="BUNNY" AND NOT DB AND R=25 THEN M$="THE BUNNY BITES YOU IN DEFENSE.":GOSUB1:GOTO820
4589 M$="THAT'S NOT IMPORTANT TO THIS ADVENTURE.":GOSUB1:GOSUB800:GOTO109
4590 IF V$="TALK" THEN PRINT"THAT WON'T DO MUCH.":GOSUB800:GOTO109
4595 IF V$<>"DRINK" AND V$<>"SIP" THEN 4620
4600 IF N$<>"WATER" THEN PRINT"THAT WON'T DO MUCH.":GOSUB800:GOTO109
4605 IF R=3 OR R=6 THEN 4615
4608 IF OB(2)=-1 OR OB(2)=R THEN IF BU THEN 4615
4610 PRINT"THAT WON'T DO MUCH.":GOSUB800:GOTO109
4615 M$="YOU SIP THE WATER, AND FEEL SICK. YOU LUNGS TURN INSIDE OUT AND YOU FALL ON THE FLOOR, DEAD.":GOSUB1:GOTO820
4620 IFV$="SCORE"THENGOSUB25420:GOSUB800:GOTO108
4630 M$="SORRY, BUT I DON'T KNOW THAT VERB.":GOSUB1:GOSUB800:GOTO109
5000 IFR=19ANDDG=0THEN5005
5001 GOTO4216
5005 DG=-1:MA(19,3)=18:OB(4)=22:NI=NI-1
5006 M$="THE OOZE DISSOLVES THE TORCH, LIGHTING IT INSTANTLY. THE OOZE MELTS AWAY INTO NOTHING.":GOSUB1:GOSUB750:GOTO100
5010 M$="THE COBWEBS LIGHT AND BURN AWAY INTO DUST.":GOSUB1:BC=-1:MA(23,2)=24:SC=SC+5:GOSUB300:GOTO100
25000 IF NR=0 THEN RETURN
25030 FOR I=1 TO NR
25040 FOR J=0 TO ND-1
25050 READ MA(I, J):NEXT:NEXT:RETURN
25100 DATA 0,4,0,0,0,0
25110 DATA 0,0,3,0,0,0
25120 DATA 0,200,0,2,0,0
25130 DATA 1,8,5,0,0,201
25140 DATA 0,8,6,4,0,0
25150 DATA 200,9,10,5,0,0
25160 DATA 0,11,0,0,240,0
25170 DATA 5,0,9,4,0,0
25180 DATA 6,0,10,8,0,314
25190 DATA 6,0,0,9,0,0
25200 DATA 7,0,0,0,0,0
25210 DATA 0,0,0,0,0,0
25220 DATA 16,12,17,17,0,0
25230 DATA 0,0,15,0,9,0
25240 DATA 15,19,15,14,0,0
25250 DATA 13,22,20,21,0,0
25260 DATA 13,20,16,16,0,0
25270 DATA 0,0,19,17,0,0
25280 DATA 15,0,0,203,0,0
25290 DATA 13,17,17,16,0,0
25300 DATA 16,23,13,20,0,0
25310 DATA 17,21,20,16,0,0
25320 DATA 21,26,0,0,0,0
25330 DATA 0,0,0,23,0,0
25340 DATA 0,0,0,24,0,0
25350 DATA 23,27,0,0,0,0
25360 DATA 26,0,0,0,0,0
25400 M$="YOU GO THROUGH THE TUNNEL AND END UP OUTSIDE THE GRAVEYARD. WHEN YOU REALIZE WHERE YOU ARE, YOU HURRY ALONG "
25405 M$=M$+"THE PATH BACK INTO TOWN, OVERCOME WITH JOY, SHOUTING THE NEWS.":GOSUB1
25406 M$="PEOPLE RUSH OUT OF THEIR HOUSES TO GREET YOU WITH A HERO'S WELCOME.":GOSUB1
25410 GOSUB25420:GOTO3440
25420 M$="YOU HAVE SCORED"+STR$(SC)+" OUT OF 60 POINTS.":GOSUB1
25422 IFSC<11THENRA$="NOVICE ADVENTURER"
25430 IFSC<21THENRA$="GRAVEYARD EXPLORER"
25440 IFSC<31THENRA$="ADVENTURER"
25450 IFSC<41THENRA$="MONSTER BASHER"
25460 IFSC<51THENRA$="EXPERT ADVENTURER"
25470 IFSC<60THENRA$="EXTERMINATOR"
25480 IFSC>=60THENRA$="BUNNY SLAYER"
25490 M$="YOU HAVE REACHED THE RANK OF "+RA$+".":GOSUB1:RETURN
26000 IFNO=0THENRETURN
26010 FORI=0TONO-1
26020 READ OB$(I), O2$(I), OB(I):NEXT:RETURN
26100 DATA "A CARROT",CARRO,0
26110 DATA "A LAMP",LAMP,2
26120 DATA "A BUCKET",BUCKE,11
26130 DATA "A SILVER CROSS",CROSS,0
26140 DATA "A TORCH",TORCH,14
26150 DATA "A KEY",KEY,18
26160 DATA "A SCRAP OF PAPER",PAPER,8
26170 DATA "A QUILL PEN",PEN,-1
26180 DATA "AN APPLE",APPLE,-1
27000 PRINT@6*32,"the night of the vampire bunnies"
27058 PRINT@9*32,TAB(10)"BY JASON DYER"
27060 PRINT@480,"PRESS ANY KEY TO CONTINUE";
27070 I$=INKEY$:IFI$=""THEN27070
27080 CLS:RETURN
40000 LO$="AT COFFIN":PRINT LO$
40010 M$="YOU ARE AT A COFFIN IN A GRAVEYARD, WITH WALLS TO THE NORTH, EAST, AND WEST.":GOSUB1
40011 MES$="A BLUE MIST HANGS OVER THE GRAVEYARD. "
40015 IFVATHENM=MES$+"THE COFFIN IS OPEN WITH A VAMPIRE IN IT!":GOSUB1:RETURN
40020 IF OC THENM$=MES$+"THE COFFIN IS OPEN.":GOSUB1:GOTO40030
40021 M$=MES$+"THE COFFIN IS CLOSED.":GOSUB1
40030 IF NOT OC THEN RETURN
40040 FOR I=0 TO 8:IF OB(I)=.1 THENM$="THERE IS "+OB$(I)+" IN THE COFFIN.":GOSUB1
40050 NEXT:RETURN
40060 LO$="GRAVEYARD":PRINTLO$
40070 M$="YOU ARE IN A GRAVEYARD SURROUNDED BY WALLS. THERE IS FOG HERE SO DENSE YOU CAN BARELY SEE 2 METERS IN FRONT "
40071 M$=M$+"OF YOU. A HOLE IN THE EAST WALL ALLOWS EXIT FROM THIS GLOOMY PLACE.":GOSUB1:RETURN
40080 LO$="GRAVEYARD":PRINT LO$:IF NOT JRI THEN JRI=-1:SC=SC+3:GOSUB300
40090 M$="YOU ARE IN A GRAVEYARD WITH A STREAM TO THE SOUTH. WALLS SURROUND THE OTHER DIRECTIONS, BUT A HOLE IN THE WEST "
40091 M$=M$+"WALL ALLOWS PASSAGE. IT IS VERY COOL HERE.":GOSUB1:RETURN
40100 LO$="GRAVEYARD":PRINT LO$
40110 M$="YOU ARE IN A DARK GRAVEYARD. A WALL COVERED WITH MOSS IS TO THE WEST, A COFFIN IS IN SIGHT TO THE NORTH, AND AN "
40111 M$=M$+"EMPTY WELL LIES IN THE MIDDLE OF ALL THE FOG.":GOSUB1
40112 IF RO THEN M$="A ROPE LEADS DOWN THE DAMP WELL.":GOSUB1
40120 RETURN
40140 LO$="CARROT FIELD":PRINT LO$
40150 M$="YOU ARE IN A CARROT FIELD IN THE MIDDLE OF A GRAVEYARD. FOG IS ALL AROUND YOU. "
40151 M$=M$+"A WALL IS TO THE NORTH, TOO HIGH TO CLIMB.":GOSUB1:RETURN
40160 LO$="GRAVEYARD":PRINT LO$
40170 M$="YOU ARE IN A FOGGY GRAVEYARD. A STREAM IS TO THE NORTH AND A FIELD CAN BE SEEN TO THE WEST.":GOSUB1
40171 M$="IN ALL OTHER DIRECTIONS ONLY FOG CAN BE SEEN. THE STREAM LOOKS SHALLOW ENOUGH TO CROSS.":GOSUB1:RETURN
40180 LO$="BOTTOM OF WELL":PRINT LO$:IF NOT WELL THEN WELL=-1:SC=SC+5:GOSUB300
40190 M$="YOU ARE AT THE BOTTOM OF AN EMPTY WELL. THE SURROUNDING CRACKS ARE RATHER BIG, "
40191 M$=M$+"AND ONE TO THE SOUTH IS BIG ENOUGH TO GO THROUGH. A ROPE HANGS DOWN ALLOWING YOU TO CLIMB UP.":GOSUB1:RETURN
40200 LO$="GRAVEYARD":PRINT LO$
40205 M$="YOU ARE IN A WALLED GRAVEYARD.  A BLACK WALL IS TO THE SOUTH. ":GOSUB1
40206 IF PLEV THEN M$="A GAPING HOLE IS IN THE MIDDLE OF ALL THE GRAVESTONES.":GOSUB1:RETURN
40207 M$="MANY TOMBSTONES ARE IN THIS AREA--SO MANY, IN FACT, YOU HAVE TO CLIMB OVER SOME OF THEM.":GOSUB1
40210 RETURN
40220 LO$="AT HOLE":PRINT LO$
40230 M$="YOU ARE IN A GLOOMY GRAVEYARD, WITH A WALL TO THE SOUTH, AND PLENTY OF FOG ALL  AROUND YOU. "
40231 M$=M$+"A LARGE HOLE SURROUNDED BY GRAVESTONES LEADS DOWN INTO GLOOM.":GOSUB1
40240 IFWCTHENM$="A SKELETON GUARDING THE HOLE FLEES WHEN HE SEES THE CROSS YOU ARE WEARING.":GOSUB1:RETURN
40245 M$="A SKELETON GUARDS THE HOLE AND DOES NOT ALLOW YOU TO ENTER.":GOSUB1:RETURN
40250 LO$="GRAVEYARD":PRINT LO$
40260 M$="YOU ARE AT THE CORNER OF A WALLED GRAVEYARD. WALLS CONNECT TO THE SOUTH AND EAST, AND MIST HANGS OVER THE "
40261 M$=M$+"SKY LIKE A PROWLER. A CHILL RUNS THROUGH YOUR BACK.":GOSUB1:RETURN
40270 LO$="CREVICE":PRINT LO$
40280 M$="YOU ARE IN A LARGE CREVICE IN A WELL. SMALL AMOUNTS OF WATER TRICKLE DOWN THE SIDES. "
40281 M$=M$+"EXIT CAN BE REACHED TO THE NORTH.":GOSUB1:RETURN
40290 LO$="MAZE":M$="YOU HAVE A NERVOUSNESS ABOUT THIS PASSAGE, BUT YOU TRUDGE ON, AND END UP GETTING "
40291 M$=M$+"KILLED IN A CAVE-IN.":GOSUB1:R=13:FF=-1:RETURN
40300 LO$="MAZE":PRINT"YOU ARE IN A DARK MAZE.":RETURN
40310 LO$="DARK TUNNEL":PRINT LO$
40320 M$="YOU ARE IN A VERY DARK TUNNEL. THE TUNNEL HEADS TO THE EAST, WHILE A HOLE IN THE CEILING ALLOWS EXIT FROM "
40321 M$=M$+"THE DARKNESS.":GOSUB1:RETURN
40330 LO$="DARK TUNNEL":PRINT LO$
40340 M$="YOU ARE IN A DARK TUNNEL. YOU CAN BARELY MAKE OUT EXITS TO ALL DIRECTIONS EXCEPT UP AND DOWN.":GOSUB1:RETURN
40350 LO$="MAZE":PRINT"YOU ARE IN A DAMP MAZE.":RETURN
40360 LO$="MAZE":PRINT"YOU ARE IN A DANK MAZE.":RETURN
40370 LO$="LONG CORRIDOR":PRINT LO$
40375 IF NOT SL THEN SL=-1:SC=SC+5:GOSUB300
40380 M$="YOU ARE IN A LONG TUNNEL STREACHING TO THE EAST AND WEST. YOUR FEET ACHE AS YOU WALK DOWN IT.":GOSUB1:RETURN
40390 LO$="SLIMY ROOM":PRINT LO$
40400 M$="YOU ARE IN A SLIME COVERED MEDIUM-SIZE ROOM. YOUR FEET STICK TO THE GROUND.":GOSUB1
40401 IFNOTDGTHENM$="SOME GREY OOZE GUARDS THE WESTWARD CORRIDOR.":GOSUB1
40405 RETURN
40410 LO$="MAZE":PRINT"YOU ARE IN A DREARY MAZE.":RETURN
40420 LO$="MAZE":PRINT"YOU ARE IN A MAZE.":RETURN
40430 LO$="MAZE":PRINT"YOU ARE IN A DEVLISH MAZE.":RETURN
40440 LO$="TORCH ROOM":PRINT LO$
40445 M$="YOU ARE IN A LARGE MARBLE ROOM WITH TORCH HOLDERS EVERYWHERE. HOWEVER, ":GOSUB1
40446 IFBCTHENM$="MOST OF THEM ARE EMPTY. YOU CAN LEAVE TO THE NORTH, SOUTH, OR EAST.":GOSUB1:RETURN
40447 M$="MOST OF THEM ARE EMPTY. YOU CAN GO SOUTH AND NORTH, BUT THE EAST PASSAGE IS BLOCKED BY COBWEBS.":GOSUB1:RETURN
40450 LO$="DEAD END":PRINT LO$
40460 IFBRTHENM$="YOU ARE IN A DEAD END THAT EXITS WEST, BUT A SHATTERED MIRROR SHOWS A PASSAGE EAST.":GOSUB1:RETURN
40465 M$="YOU ARE IN A DEAD END, WITH A LARGE MIRROR COVERING THE EAST WALL AND AN EXIT TO THE WEST.":GOSUB1:RETURN
40470 LO$="BUNNY ROOM":PRINT LO$
40472 IFNOTDBTHENM$="YOU ARE IN A LARGE SPARSELY FURNISHED CHAMBER EXCEPT FOR A THRONE WITH A BUNNY NEXT TO IT.":GOSUB1
40475 IFNOTDBTHENM$="IT LOOKS LIKE THE VAMPIRE BUNNY! A PASSAGE EXITS WEST FROM THIS UNFRIENDLY PLACE.":GOSUB1:RETURN
40480 IFNOTSTTHENM$="YOU'RE IN A LARGE SPARSELY FURNISHED CHAMBER EXCEPT FOR A THRONE. A PASSAGE EXITS WEST.":GOSUB1:RETURN
40485 M$="YOU ARE IN A LARGE SPARSELY FURNISHED CHAMBER EXCEPT FOR A THRONE. PASSAGES EXIT EAST AND WEST.":GOSUB1:RETURN
40490 LO$="CRAMPED CORRIDOR":PRINT LO$
40500 M$="YOU ARE IN A CRAMPED CORRIDOR GOING NORTH/SOUTH. A WORN LEVER IS SET INTO THE WEST WALL.":GOSUB1:RETURN
40510 LO$="SANDY ROOM":PRINT LO$
40520 IFNOTPLEVTHENM$="YOU ARE IN A SANDY ROOM WITH AN EXIT NORTH. THE CEILING LOOKS LIKE IT COULD CAVE IN.":GOSUB1:RETURN
40530 M$="YOU ARE IN A SANDY ROOM WITH AN EXIT NORTH AND A HOLE IN THE ROOF. THE CEILING LOOKS WEAK.":GOSUB1:RETURN
