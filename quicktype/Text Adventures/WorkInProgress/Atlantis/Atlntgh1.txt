10 CLEAR400:CLS:PRINTCHR$(23):A$="           ATLANTIS":B$="       ":FORI=64TO448STEP64:PRINT@I-64,"                             "::PRINT@I,A$:PRINT@960-I,B$:FORJ=1TO50:NEXTJ,I
20 DEFINTA-Z:DEFSTRM:M0="I DON'T SEE IT HERE.":M1="I'M NOT HOLDING IT.":M2=" WON'T LET ME.":M3="DON'T BE RIDICULOUS.":M4="GIVE ME A BREAK!":M5="DON'T BE FUNNY.":M6="OKAY.":M7="IT'S NOT HUNGRY.":GOTO1730
30 GOTO190
40 GOSUB80:IFNO<9ANDNO<>0AND(VB=0ORVB=1ORVB=27)VB=1
50 IFVB-15ANDVB-16ANDVB-5ANDVB-6ANDLEN(N1$)>0ANDNO=0PRINT"I DON'T KNOW WHAT "CHR$(34)N1$CHR$(34)" IS.":GOTO40
60 IFN1$=""AND(VB>8ORVB=2)ANDVB<>15ANDVB<>30PRINT"PLEASE SUPPLY A DIRECT OBJECT.":GOTO40
70 GOTO370
80 T=T+1:PRINT"   -----";CHR$(94);:IFBTINPUT" WHAT SHOULD I DO";CM$ELSELINEINPUT" WHAT SHOULD I DO? ";CM$
90 IFLEN(CM$)=0THEN80
100 VB$="":NO$="":VB=0:NO=0
110 LC=LEN(CM$):FORI=1TOLC:A$=MID$(CM$,I,1):IFA$<>" "VB$=VB$+A$:NEXTI
120 IFLEN(VB$)+1>=LEN(CM$)NO=0:GOTO140
130 NO$=RIGHT$(CM$,LC-I)
140 N1$=NO$:V1$=VB$:VB$=LEFT$(VB$,3):NO$=LEFT$(NO$,3):FORI=1TONV:IFVB$=VB$(I)THENVB=I:GOTO170ELSENEXTI:VB=0
150 IFN1$=""NO$=VB$
160 VB=0:GOTO180
170 IF VB$=CM$THENO=0:NO$="":RETURN
180 FORI=1TONN:IFNO$=NO$(I)NO=I:RETURNELSENEXTI:NO=0:NO$="":RETURN
190 CLS:II=0:IFLEFT$(P$(CP),1)="*"P$=RIGHT$(P$(CP),LEN(P$(CP))-1):II=1:GOTO240
200 IFCP=29F1=1ELSEIFCP=31F2=1
210 IFCP=51F4=1ELSEIFCP=6F3=1
220 P$="I'M IN "+P$(CP)
230 IFCP-1ANDOB(31,0)<>-1CLS:PRINT"HELP! NO AIR!!! CROAK.":FORI=1TO1000:NEXTI:GOTO1490
240 CLS:PRINTP$".":K=0:FORI=1TOLO:IFOB(I,0)<>CPTHEN280
250 IFK=0PRINT"
VISABLE ITEMS: ";
260 IF3+POS(0)+LEN(OB$(I))>63PRINT
270 PRINTOB$(I)".  ";:K=K+1
280 NEXTI:IFK>0PRINT
290 IFCP=31PRINTSTRING$(63,140):GOTO40
300 K=0:PRINT"
OVIOUS EXITS ARE: ";:FORI=0TO3:IFP(CP,I)=0THEN330
310 IFK>0PRINT", ";
320 PRINTD$(I);:K=K+1
330 NEXTI:PRINT".":IFZZ<2ORCP<>5THEN360ELSEFORI=1TOLO
340 IFOB(I,0)=-1ANDI<>31OB(I,0)=29
350 NEXTI:PRINT"THE WHIRLPOOL SUCKED ALL MY BELONGINGS DOWN!!!":ZZ=1
360 PRINTSTRING$(63,140):GOTO40
370 PRINT"> ";:ONVBGOTO390,460,30,580,620,670,720,740,750,810,820,940,950,1050,1060,1080,1490,1090,1130,1200,1220,1490,1280,1320,1360,460,390,1390,1420,1450
380 PRINT"I NEVER LEARNED HOW TO "CHR$(34)V1$CHR$(34)" SOMETHING.":GOTO40
390 IFNO=0PRINT"I NEED A DIRCECTION.":GOTO40
400 IFNO>8PRINT"HUH?":GOTO40
410 IFNO>4NO=NO-4
420 NO=NO-1:IFP(CP,NO)=0PRINT"THERE IS NO WAY TO GO IN THAT DIRECTION.":GOTO40
430 IFCP=30ANDGF=0PRINT"THE GATE IS LOCKED.":GOTO40
440 IFCP=25ANDNO=0ANDGF=0PRINT"THERE IS A GATE THAT BARS MY WAY. IT IS LOCKED.":GOTO40
450 CP=P(CP,NO):GOTO30
460 IFCP<>OB(NO,0)PRINTM0:GOTO40
470 IFNO=10ORNO=13ORNO=14ORNO=16ORNO=18ORNO=22ORNO=36PRINTM5:GOTO40
480 IFNO=27ORNO=29PRINT"IT IS TOO HEAVY.":GOTO40
490 IFNO=17ANDOB(16,0)=CPPRINT"MANTA"M2:GOTO40
500 IFNO=19ANDOB(18,0)=CPPRINT"SQUID"M2:GOTO40
510 IFNO=35ANDOB(36,0)=CPPRINT"OCTOPUS"M2:GOTO40
520 IFOB(10,0)=CPPRINT"EEL"M2:GOTO40
530 IFNO=44ANDOB(42,0)=CPPRINT"MINOTAUR"M2:GOTO40
540 IFNO=32PRINT"IT IS BEYOND MY POWER TO DO THAT.":GOTO40
550 IFZZ+1>7PRINT"I'M CARRYING TOO MUCH. CLUE: DROP SOMETHING.":GOTO40
560 IFOB(NO,1)>0ANDCP=1TS=TS-1
570 ZZ=ZZ+1:OB(NO,0)=-1:PRINTM6:GOTO40
580 IFZZ=0PRINT"I'M NOT CARRYING ANYTHING.":GOTO40ELSEPRINT"CURRENTLY I'M CARRYING: ";:FORI=1TOLO:IFOB(I,0)<>-1THEN610
590 IF3+POS(0)+LEN(OB$(I))>63PRINT
600 PRINTOB$(I)".  ";
610 NEXTI:PRINT:GOTO40
620 CLS:INPUT"HIT <ENTER>";XX$
630 PRINT"SAVING..."
640 FORI=1TOLO:PRINT#-1,OB(I,0)
650 NEXTI:PRINT#-1,CP;GF;ZZ;F1;F2;F3;F4;T;OB(34,1);OB(37,1);NO$(22);OB$(34);OB$(37)
660 GOTO30
670 CLS:INPUT"HIT <ENTER>";XX$
680 PRINT"RESTORING..."
690 FORI=1TOLO:INPUT#-1,OB(I,0)
700 NEXTI:INPUT#-1,CP,GF,ZZ,F1,F2,F3,F4,T,OB(34,1),OB(37,1),NO$(22),OB$(34),OB$(37)
710 GOTO30
720 SC=0:FORI=1TOLO:IFOB(I,0)=1SC=SC+OB(I,1)
730 NEXTI:PRINT"YOU HAVE"SC"POINTS OUT OF A MAXIMUM OF 170.
PLUS, YOU CAN GET A BONUS UP TO 30 POINTS WHEN YOU QUIT, MAKING
THE ACTUAL MAXIMUM 200.":GOTO40
740 INPUT"DO YOU REALLY WANT TO QUIT NOW";X$:IFLEFT$(X$,1)="Y"THEN1490ELSEPRINT"I DIDN'T THINK SO.":GOTO40
750 IFOB(NO,0)<>-1PRINTM1:GOTO40
760 IFNO<>34PRINT"SORRY, I CAN ONLY THROW THE SPEAR.":GOTO40
770 IFOB(9,0)<>-1OR(OB(36,0)-CPANDCP-OB(42,0))PRINT"THE SPEAR VANISHED IN MIDAIR.":ZZ=ZZ-1:OB(34,0)=0:GOTO40
780 IFCP-4THEN800
790 PRINT"THE SPEAR PLUNGES INTO THE OCTOPUS' HEART AND THEN VANISHES
ALONG WITH THE OCTOPUS!!!!":OB(36,0)=0:OB(34,0)=51:ZZ=ZZ-1:GOTO40
800 PRINT"THE SPEAR HITS THE MINOTAUR, AND HE VANISHES!!!":OB(42,0)=0:PRINT"THE SPEAR THEN TURNS TO PLATINUM!!!":OB$(34)=">>>A PLATINUM SPEAR<<<":OB(34,1)=11:OB(34,0)=CP:ZZ=ZZ-1:GOTO40
810 PRINT"YOU'LL HAVE TO BE MORE SPECIFIC.":GOTO40
820 IFOB(NO,0)<>-1ANDOB(NO,0)<>CPPRINTM0:GOTO40
830 IFNO-43ANDNO-11ANDNO-12ANDNO-17ANDNO-21ANDNO-28ANDNO-32ANDNO-38PRINT"THERE IS NO WRITING THERE."
840 PRINT"IT SAYS:"
850 IFNO=11PRINT"WHAT IS THOUGHT TO BE, ISN'T WHAT YOU SEE! NORI"
860 IFNO=12PRINT"CAPTAIN AHAB WAS HERE."
870 IFNO=17PRINTR1$:PRINTR2$:PRINT"SCRAPE IRON. NORI"
880 IFNO=21PRINT"LEE SAID POW AND DIED."
890 IFNO=28PRINT"USE CRUST TOOTHPASTE FOR SHARPER TEETH. (SNAP!)
SQUIDS HATE LOUD NOISES ( W O P T O N )"
900 IFNO=32PRINT"BRING >>>TREASURES<<< HERE & DROP THEM. SAY SCORE"
910 IFNO=43PRINT"BEWARE THE FIERCE MINOTAUR! MINOS WAS AN EVIL KING!!"
920 IFNO=38PRINT"PUT SHOT IN GUN."
930 GOTO40
940 PRINT"USE COMPASS POINTS, PLEASE.":GOTO40
950 IFNO-40ANDNO-35ANDNO-14PRINT"I DON'T KNOW HOW TO OPEN SUCH A THING.":GOTO40
960 IFNO=40PRINT"TRY UNLOCK GATE.":GOTO40
970 IFNO<>35THEN1020
980 IFOB(26,0)<>-1PRINT"YOU DON'T HAVE THE NECESSARY RESOURCES.":GOTO40
990 IFOB(36,0)=CPPRINT"OCTOPUS"M2:GOTO40
1000 IFOB(38,0)<>0PRINT"OK. IT'S EMPTY.":GOTO40
1010 PRINT"OK. TWO THINGS FALL OUT! (TRY 'LOOK')":OB(38,0)=CP:OB(39,0)=CP:GOTO40
1020 IFOB(33,0)<>-1PRINT"YOU DON'T HAVE THE RIGHT TOOLS!":GOTO40
1030 IFOB(37,0)<>0PRINT"THE CLAM OPENS AND SHUTS. IT'S EMPTY.":GOTO40
1040 PRINT"THE CLAM FLIES OPEN AND SOMETHING FALLS OUT, BUT IT SNAPS SHUT
AGAIN!!! TRY 'LOOK'.":OB(37,0)=CP:GOTO40
1050 PRINT"I'M NOT THE DESTRUCTIVE TYPE MYSELF...":GOTO40
1060 IFOB(18,0)<>CPPRINT"NOTHING HAPPENED.":GOTO40
1070 PRINT"THE SQUID WAS SO STARTLED HE RAN AWAY!!!":OB(18,0)=0:GOTO40
1080 PRINT"OKAY...";N1$:GOTO40
1090 IFNO<>29PRINT"I KNOW NOT HOW TO LOAD SUCH A THING. SAY RESTORE TO START UP
A SAVED GAME.":GOTO40
1100 IFCP<>12PRINTM0:GOTO40
1110 IFOB(20,0)<>-1PRINT"I HAVE NOTHING TO LOAD IT WITH!!":GOTO40
1120 PRINT"OK.":OB(20,0)=-2:ZZ=ZZ-1:GOTO40
1130 IFNO-10ANDNO-14ANDNO-16ANDNO-18ANDNO-22ANDNO-23ANDNO-36PRINTM3:GOTO40
1140 IFNO-22ANDNO-16PRINTM7:GOTO40
1150 IFNO=16THEN1180
1160 IFOB(30,0)<>-1PRINT"I HAVE NO FOOD!!!":GOTO40
1170 IFOB(22,0)=CPPRINT"THE PARANAHS GULP DOWN THE CUP AND...MUCH TO THEIR SUPRISE...
THEIR TEETH FALL OUT!!! THEY ARE NOW RENDERED HELPLESS. TRY
'LOOK'.":OB(22,0)=0:OB(23,0)=CP:NO$(22)="'":OB(30,0)=0:ZZ=ZZ-1:OB(24,0)=CP:GOTO40
1175 PRINT"I DON'T SEE THEM.":GOTO40
1180 IFOB(23,0)<>-1PRINT"HE LIKES THE IDEA...AND EATS ME UP!!! I'M DEAD.":FORI=1TO1500:NEXTI:GOTO1490
1190 PRINT"THE MANTA SWALLOWS UP THE PARANHAS AND SWIMS AWAY.":ZZ=ZZ-1:OB(16,0)=0:OB(23,0)=0:GOTO40
1200 IFOB(25,0)<>-1PRINT"I HAVE NO SCISSORS.":GOTO40
1210 PRINT"THE SCISSORS ARE TOO DULL TO CUT ANYTHING.":GOTO40
1220 IFNO-40ANDNO-35PRINT"HOW?":GOTO40
1230 IFNO=35PRINT"TRY OPEN.":GOTO40
1240 IFCP-25ANDCP-30PRINTM0:GOTO40
1250 IFGF=1PRINT"IT IS ALREADY OPEN!!!":GOTO40
1260 IFOB(26,0)<>-1PRINT"YOU DON'T HAVE THE KEY.":GOTO40
1270 PRINTM6:GF=1:GOTO40
1280 IFNO<>27PRINT"HOW?":GOTO40
1290 IFCP<>9ANDCP<>31PRINTM0:GOTO40
1300 IFCP=9CP=31ELSECP=9
1310 GOTO30
1320 IFNO<>29PRINT"HOW?":GOTO40
1330 IFCP<>12PRINTM0:GOTO40
1340 IFOB(20,0)<>-2PRINT"IT'S NOT LOADED.":GOTO40
1350 PRINTM6"..POW!!!!":OB(20,0)=29:OB(10,0)=0:GOTO40
1360 IFNO<>30PRINTM3:GOTO40
1370 IFOB(30,0)<>-1PRINTM1:GOTO40
1380 PRINT"SMAK! GOOD!":ZZ=ZZ-1:OB(30,0)=0:GOTO40
1390 IFOB(NO,0)<>-1PRINTM1:GOTO40
1400 ZZ=ZZ-1:PRINTM6:OB(NO,0)=CP:IFCP=1ANDOB(NO,1)>0TS=TS+1
1410 GOTO40
1420 IFOB(NO,0)<>-1ANDOB(NO,0)<>CPPRINTM0:GOTO40
1430 IFNO<>37PRINTM6" NOTHING HAPPENED.":GOTO40
1440 PRINT"WAIT A MINUTE...THIS STATUETTE ISN'T IRON...IT'S GOLD!!":OB$(37)=">>>A GOLD STATUETTE<<<":OB(37,1)=16:GOTO40
1450 IFCP<>31PRINT"GEE! FUN!":GOTO40
1460 X=RND(4):IFX=2THEN1480
1470 CP=9:GOTO30
1480 PRINT"WHOOOOOOOA...THUD. I'M DEAD.":FORI=1TO1000:NEXTI:GOTO1490
1490 CLS:GOSUB1680:FORI=1TOLO:IFABS(OB(I,0))=1FS=FS+OB(I,1)
1500 NEXTI:IFT<500FS=FS+5
1510 IFT<400FS=FS+5
1520 IFT<310FS=FS+10
1530 PRINT"YOU SCORED"FS"POINTS OUT OF 200.":IFFS=0PRINT"YOU ARE A PEE WEE CLASS ADVENTURER (THE LOWEST RANK)":GOTO1620
1540 IFFS<30PRINT"ARE YOU VEGETABLE OR MINERAL? RANK 8":GOTO1620
1550 IFFS<50PRINT"NOT TOO GOOD, PAL! RANK 7":GOTO1620
1560 IFFS<90PRINT"YOU HAVE HAD A TASTE OF ADVENTURE. RANK 6":GOTO1620
1570 IFFS<110PRINT"YOU ARE A MASTER ADVENTURER CLASS C (RANK 5)":GOTO1620
1580 IFFS<150PRINT"YOU ARE A MASTER ADVENTURER CLASS B (RANK 4)":GOTO1620
1590 IFFS<170PRINT"YOU ARE A MASTER ADVENTURER CLASS A (RANK 3)":GOTO1620
1600 IFFS<190PRINT"WOWEE! YOU ARE GREAT! YOU'LL SOON BE AN ADVENTUTRER
GRANDMASTER! ONE RANK AWAY... RANK 2":GOTO1620
1610 IFFS<201PRINT"TUM TUM-TUM TUM...TUM TE TUM TE DUM DE TUM...
A REAL ADVENTURER GRANDMASTER...BOY OH BOY! CAN I SHAKE YOUR
HAND??? YOU SHOULD ";:PRINTR1$:PRINTR2$
1620 PRINT:PRINT:PRINT"PLAY AGAIN (Y/N)"
1630 A$=INKEY$:IFA$="Y"ORA$="N"THEN1650
1640 GOTO1630
1650 IFA$="Y"RUN
1660 END
1670 CLS:END
1680 IFF1LETFS=FS+2
1690 IFF2LETFS=FS+2
1700 IFF2LETFS=FS+2
1710 IFF3LETFS=FS+4
1720 RETURN
1730 CP=1:NP=51:LO=44:NN=44:NV=30:DIMP$(NP),P(NP,3),OB(LO,1),OB$(LO),NO$(NN),VB$(NV):FORI=1TO31:FORJ=0TO3:READP(I,J):NEXTJ,I
1740 P$(1)="MY SUBMARINE DOCKED IN THE CITY OF ATLANTIS":P$(2)="A CORRIDOR FILLED WITH WATER":P$(3)="AN ALCOVE":P$(4)="A CORAL REEF":DATA0,0,2,0,8,0,3,1,9,0,4,2,10,0,5,3
1750 P$(5)="A WHIRLPOOL":P$(6)="THE HALL OF SPIRITS (MORE COMMONLY CALED A GRAVEYARD)":P$(7)="*DEAD END":P$(8)="A DINING ROOM":DATA		11,0,0,4,13,0,7,0,0,8,0,0,7,2,9,0
1760 P$(9)="*I'M ON THE STREET":P$(10)="*I'M AT THE END OF THE STREET":P$(11)="A DARK CAVE":P$(12)="THE FORTRESS OF ATLANTIS":DATA15,3,10,8,0,4,11,9,0,5,12,10,0,0,0,11
1770 P$(13)="CAPTAIN AHAB'S QUARTERS":P$(14)="A BARREN CHAMBER":P$(15)="A BARBER SHOP":P$(16)="AN OLYMPIC FIELD":DATA16,6,0,0,17,0,15,0,0,9,0,14,22,13,17,0
1780 P$(17)="A SUPPLY ROOM":P$(18)="THE SQUID'S CHAMBER":P$(19)="THE DRAWING ROOM":P$(20)="THE ROYAL COLLISEUM":DATA23,14,18,16,24,0,19,17,25,0,20,18,0,0,21,19
1790 P$(21)="A TEMPLE":P$(22)="HALLWAY":P$(23)="OLD LIBRARY":P$(24)="THE CLAM ROOM. THE WALLS ARE MADE OF CLAMSHEL":DATA26,0,0,20,27,16,0,0,0,17,24,0,29,18,0,23
1800 P$(25)="A RAIL STATION (SO >THIS< IS HOW THEY GOT AROUND!)":P$(26)=P$(24):P$(24)="THE TOWN HALL":P$(27)="THE KITCHEN.":P$(28)="THE DINING HALL":DATA30,19,0,0,0,21,0,0,0,22,28,0,0,0,0,27
1810 P$(29)="THE ELECTRIC EEL'S ROOM":P$(30)="THE JAIL. THERE IS A GATE TO THE SOUTH":P$(31)="AT THE TOP OF A POST. LOOKING DOWN I SEE ONLY THE
STREET. THE WATER IS TOO MISTY TO SEE FAR AWAY":DATA0,0,30,0,0,25,0,0,0,0,0,0
1820 DATAN,S,E,W,NOR,SOU,EAS,WES,THR,EEL,BOO,PAP,TRA,CLA,MED,MAN,PLA,SQU,COI,SHO,JOU,PAR,PAR,TEE,SCI,KEY,POS,NEW,CAN,CUP,TAN,SIG,PIC,SPE,CHE,OCT,STA,NOT,PEA,GAT,GO,GET,LOO,INV,SAV,RES,SCO,QUI,THR,KIL,REA,ENT,OPE,HIT,YEL,SAY,[,LOA,FEE,CUT,UNL,$F,CLI,FIR,EAT
1830 FORI=1TO40:READNO$(I):NEXTI:FORI=1TONV:READVB$(I):NEXTI:DATATAK,SWI,DRO,SCR,JUM
1840 DATAA SPEAR THROWER,AN ELECTRIC EEL,A SMALL BOOK,A PAPER WITH WRITING ON IT,A,A GIANT CLAM,>>>A JADE MEDALIAN<<<,A BLACK MANTA,>>>A PLATINUM PLACQUE<<<,A GIANT SQUID,>>>MANY COINS<<<,AN IRON SHOTPUT,CAPTAIN AHAB'S JOURNAL
1850 DATASOME FIERCE PARANHAS,SOME HELPLESS PARANAS,>>>SOME VALUABLE PARANAH TEETH<<<,>>>A PAIR OF GOLD SCISSORS<<<,>>>A SILVER KEY<<<,A TALL POST,AN OLD NEWSPAPER,AN OLD CANNON,A PEANUT BUTTER CUP,AN AIR TANK,A SIGN,>>>A PLATINUM PICK<<<,A STEEL SPEAR
1860 DATAA TREASURE CHEST,A FIERCE OCTOPUS,AN IRON STATUETTE,A NOTE,>>>A GLISTENING PEARL<<<,27,0,29,0,23,0,24,0,0,0,26,0,20,10,19,0,19,15,18,0,18,20,16,0,13,0,14,0,0,0,0,15,15,5,8,5,9,0,11,0,12,0,1,0,1,0,1,0,3,7,3,0,4,0,4,0,0,0,0,0,0,12
1870 FORI=9TO39:READOB$(I):NEXTI:FORI=9TO39:READOB(I,0),OB(I,1):NEXTI
1880 D$(0)="NORTH":D$(1)="SOUTH":D$(2)="EAST":D$(3)="WEST"
1890 IFPEEK(16396)=201BT=-1ELSEBT=0
1900 P$(32)="THE PILLARS OF HERCULES":P(26,0)=32:DATA0,26,0,33
1910 P$(33)="PROMETHEUS' CHAMBER IN OLYMPIA":P$(34)="A STRANGE ROOM WITH WALLS THAT SEEM TO COME ALIVE AS I
MOVE ABOUT"	:P$(35)="*I'M ON THE EDGE OF A CLIFF. THE WATER BELOW ME IS TOO MISTY
TO SEE FAR DOWN. THERE MIGHT BE LIFE DOWN THERE..."
1920 P$(36)="A DEEP, DARK, CORRODOR. I CAN SEE NO CELING FROM HERE,
ONLY MURKY WATER. THE WATER SEEMS TO BE MOVING EAST OF ME...":DATA0,34,32,0,33,36,37,35,0,0,34,0,34,0,5,0
1930 P$(37)="THE HALL OF MIRRORS. ALL DIRECTIONS LOOK THE SAME.":P$(38)=P$(37):P$(39)=P$(37):P$(40)="AN AWKWARD CORRODOR":DATA37,38,39,34,37,38,38,38,40,39,39,37,42,39,0,0
1940 FORI=41TO50:P$(I)="A LABYRINTH OF PASSAGES OBVIOUSLY DESIGNED BY
KING MINOS. IT IS ROMOUR THAT THE MINITOUR IS NEAR...":NEXTI:DATA43,41,42,41,44,40,42,41,47,41,43,43,48,42,45,44,49,45,45,44,50,51,0,0,47,43,48,47,48,44,48,47,0,45,50,0,0,46,0,49
1950 P$(51)="*DEAD END":DATA46,0,0,0
1960 FORI=32TONP:FORJ=0TO3:READP(I,J):NEXTJ,I
1970 FORI=41TONN:READNO$(I),OB$(I),OB(I,0),OB(I,1):NEXTI
1980 DATAAPP,>>>A GOLDEN APPLE<<<,33,10,MIN,A FIERCE MINOTAUR,51,0,SCR,AN ANCIENT SCROLL,40,0,FLE,>>>A GOLDEN FLEECE<<<,51,44
1990 R1$="TRY SOME OTHER GREG HASSETT ADVENTURES. THEY ARE:
        JOURNEY TO THE CENTER OF THE EARTH
        THE HOUSE OF SEVEN GABLES
        ENTRY INTO KING TUT'S TOMB
        SORCERER'S CASTLE
        ENCHANTED ISLAND"
2000 R2$="CALL MAD HATTER SOFTWARE TO PLACE AN ORDER
              (617)-682-8131"
2010 GOTO30
