
10 CLEAR 1024
20 CLS(RND(8)):PRINT@169,"SUCCESS MANSION";
40 PLAY"T255;L255;V31":FOR A=1TO31:PLAY"AGCDGEDAGAV-":NEXT
70 PRINT@489,"<PRESS ANY KEY>";
90 A$=INKEY$:IF A$="" THEN 90 ELSE CLS(RND(8))
110 CLSRND(8):PRINT@165,"THE ADVENTURE BEGINS...":GOSUB 3100
120 DIM L$(35),LO$(32),O$(32),C$(25),T(6,34),C(25),O(32),T$(6),TL$(6)
130 FOR C=1 TO 34:READ L$(C):NEXT
140 FOR C=1 TO 31:READ LO$(C),O$(C),O(C):NEXT
150 FOR C=1 TO 34:READ T(1,C),T(2,C),T(3,C),T(4,C),T(5,C),T(6,C):NEXT
160 T$(1)="NORTH":T$(2)="SOUTH":T$(3)="EAST":T$(4)="WEST":T$(5)="UP":T$(6)="DOWN"
170 FOR C=1 TO 22:READ C$(C),C(C):NEXT
180 L=1:M=0
190 CLS:PRINT @480
200 Z$="YOU ARE "+L$(L)+" "
210 Z$=Z$+"YOU SEE "
220 FL=0
230 FOR C=1 TO 31:IF O(C)=L THEN 240 ELSE 270
240 IF FL=0 THEN Z$=Z$+"A "+LO$(C) ELSE Z$=Z$+", "+LO$(C)
250 IF LEN(Z$)>32 THEN GOSUB 3010
260 FL=FL+1
270 NEXT
280 IF FL=0 THEN Z$=Z$+"NOTHING OUT OF THE ORDINARY"
290 Z$=Z$+".  "
300 Z$=Z$+"OBVIOUS EXITS LEAD "
310 FL=0
320 FOR C=1 TO 6:IF T(C,L)>0 THEN 330 ELSE 360
330 IF FL=0 THEN Z$=Z$+T$(C) ELSE Z$=Z$+", "+T$(C)
340 IF LEN(Z$)>32 THEN GOSUB 3010
350 FL=FL+1
360 NEXT:Z$=Z$+".  "
370 IF T(1,L)<0 AND T(1,L)>-3 THEN Z$=Z$+"TO THE NORTH IS A CLOSED DOOR.  "
380 IF T(2,L)<0 AND T(2,L)>-3 THEN Z$=Z$+"TO THE SOUTH IS A CLOSED DOOR.  "
390 IF T(3,L)<0 AND T(3,L)>-3 THEN Z$=Z$+"TO THE EAST IS A CLOSED DOOR.  "
400 IF T(4,L)<0 AND T(4,L)>-3 THEN Z$=Z$+"TO THE WEST IS A CLOSED DOOR.  "
410 IF T(3,L)=-3 THEN Z$=Z$+"TO THE EAST IS A CLOSED METAL PANEL.  "
420 IF T(4,L)=-3 THEN Z$=Z$+"TO THE WEST IS A CLOSED METAL PANEL.  "
430 Z=RND(1000):IF Z>950 THEN Z$=Z$+" YOU CAN HEAR THE PEOPLE STILL LAUGHING AT YOU OUT SIDE.  "
440 IF Z>900 AND Z<950THEN Z$=Z$+" THE TOWNSPEOPLE STILL LAUGH AT YOUR ADVENTURE.  "
450 IF Z<50 THEN Z$=Z$+" YOU REMINISCE OF YOUR BEST FRIENDS LAUGHING AT YOUR TALK OF SUCCESS. "
460 IF LEN(Z$)>32 THEN GOSUB 3010
470 IF Z$<>""THEN PRINT Z$:Z1$=Z$:Z$="":GOSUB3100
480 PRINT@0,"TOTAL MOVES:";STR$(M):PRINT@480,"";:LINE INPUT "=>";A$
490 A4$="":IF A$="" THEN 480
500 FOR C=1 TO LEN(A$):A3$=MID$(A$,C,1):IF ASC(A3$)>96 AND ASC(A3$)<122 THEN A4$=A4$+CHR$(ASC(A3$)-32) ELSE A4$=A4$+A3$
510 NEXT:A$=A4$
515 IF INSTR(A$,"TALK ON")THENTK=1:GOTO480:ELSEIFINSTR(A$,"TALK OFF")THENTK=0:GOTO480
520 M=M+1
530 WN=0:FOR C=19 TO 31:IF O(C)<1000 THEN WN=1 ELSE NEXT
540 IF WN=0 THEN T(2,1)=999
550 IF L=1 OR L=3 OR L=6 OR L=7 OR L=14 OR L=15 OR L=30 OR L=31 THEN DT=1
560 FOR C=1 TO 6:IF T(C,L)<0 THEN DT=2 ELSE NEXT
570 FOR C=1 TO 6:IF T(C,L)<-1 THEN DT=3 ELSE NEXT
580 FOR C=1 TO LEN(A$):IF MID$(A$,C,1)=" " THEN A1$=LEFT$(A$,C-1):B$=MID$(A$,C+1,LEN(A$)-C):GOTO 600 ELSE NEXT
590 A1$=A$
600 FOR C=1 TO 22
610 IF C$(C)=A1$ THEN A=C(C):GOTO640
620 NEXT
630 Z$="I DO NOT UNDERSTAND THE VERB IN YOUR COMMAND. PLEASE TRY ANOTHER COMMAND.":GOSUB3010:GOTO430
640 FL=0:IF INSTR(B$+" "," DESK ")THEN B1$="DESK":FL=1
650 IF INSTR(B$+" "," HUTCH ")THEN B1$="HUTCH":FL=1
660 IF INSTR(B$+" "," TABLE ")THEN B1$="TABLE":FL=1
670 IF INSTR(B$+" "," BOOKSHELF ")THEN B1$="BOOKSHELF":FL=1
680 IF INSTR(B$+" "," STATUE ")THEN B1$="STATUE":FL=1
690 IF INSTR(B$+" "," MAT ")THEN B1$="MAT":FL=1
700 IF INSTR(B$+" "," KEY ")THEN B1$="KEY":FL=1
710 IF INSTR(B$+" "," SCROLL ")THEN B1$="SCROLL":FL=1
720 IF INSTR(B$+" "," DOOR ")THEN B1$="DOOR":FL=1
730 IF INSTR(B$+" "," PANEL ")THENB1$="PANEL":FL=1
740 IF FL=1 THEN B$=B1$
750 FOR C=1 TO 31:IF INSTR(B$+" "," "+O$(C)+" ") THEN B$=O$(C)
760 NEXT
770 FOR C=1 TO LEN(B$):IF MID$(B$,C,1)=" " THEN B1$=LEFT$(B$,C-1) ELSE NEXT
780 IF B1$="A" OR B1$="THE" OR B1$="AT"OR B1$="TO"THEN B$=MID$(B$,C+1,LEN(B$)-C):B1$="":GOTO770
790 ON A GOTO 810,930,1360,1440,1550,1640,1790,1970,2210,2370,2420,2470,2520,2560
800 ' INVENTORY
810 CLS:PRINT"YOU ARE CARRYING THE FOLLOWING:":PRINT
820 Z$=""
830 FOR C=1 TO 31
840 IF O(C)=1000 THEN Z$=Z$+"-"+LO$(C)+"- "
850 IF LEN(Z$)>32THEN GOSUB3010
860 NEXT
870 IF Z$="" THEN PRINT:PRINT TAB(12);"NOTHING":Z1$="NOTHING":GOSUB3100
880 IF LEN(Z$)>32THENGOSUB3010
890 IF Z$<>""THEN PRINT Z$:Z1$=Z$:GOSUB3100
900 PRINT@489,"<PRESS ANY KEY>";:Z1$="PRESS ANY KEY TO CONTINUE":GOSUB3100
910 A$=INKEY$:IF A$=""THEN 910
920 CLS:GOTO 200
930 IF A$=A1$ THEN 200
940 IF B$="KEY"OR B$="SCROLL"THENZ$="THIS MANSION HAS MORE THAN ONE "+B$+" IN IT. YOU WILL HAVE TO SPECIFY WHICH ONE YOU WANT.":GOTO430
950 IF B$="KEY1"AND O(3)=1000THENZ$="IT IS A LARGE OLD-FASHIONED SKELETON KEY THAT LOOKS LIKE IT MIGHT UNLOCK A DOOR.":GOTO430
960 IF B$="KEY2"AND O(4)=1000THENZ$="IT LOOKS LIKE AN ORDINARY METAL KEY.":GOTO430
970 IF B$="KEY3"AND O(5)=1000 THENZ$="IT IS A METAL KEY THAT READS -KEEP THE FAITH-.":GOTO430
980 IF B$="KEY4"AND O(6)=1000THENZ$="IT HAS THE LETTERS M.B. ON IT.":GOTO430
990 IF B$="PAPER"AND O(1)=1000THENZ$="THE NUMBERS 9-6-3-1-4-7 ARE WRITTEN ON IT.":GOTO430
1000 IF B$="CARD"AND O(2)=1000THENZ$="THE PLASTIC CARD READS 2-4-6-8 .":GOTO430
1010 IF B$="MAT"AND L=1THENZ$="IT READS -WELCOME TO SUCCESS MANSION-. ONE CORNER IS WORN, AS IF IT HAS BEEN MOVED QUITE OFTEN IN THE PAST.":GOSUB3010:GOTO430
1020 IF B$="SCROLL1"AND O(20)=1000THENZ$="IT READS -THE SCROLL OF THE BRAIN.-.":GOTO430
1030 IF B$="SCROLL2"AND O(21)=1000THENZ$="IT READS -THE SCROLL OF FAITH-.":GOSUB3010:GOTO430
1040 IF B$="SCROLL3"AND O(22)=1000THENZ$="IT READS -THE SCROLL OF AUTOSUGGESTION-.":GOSUB3010:GOTO430
1050 IF B$="SCROLL4"AND O(23)=1000THENZ$="IT READS -THE SCROLL OF THE MASTER MIND-.":GOTO430
1060 IF B$="SCROLL5"AND O(24)=1000THENZ$="IT READS -THE SCROLL OF IMAGINATION-.":GOSUB3010:GOTO430
1070 IF B$="SCROLL6"AND O(25)=1000THENZ$="IT READS -THE SCROLL OF ORGANIZED PLANNING-.":GOSUB3010:GOTO430
1080 IF B$="SCROLL7"AND O(26)=1000THENZ$="IT READS -THE SCROLL OF DECISION-.":GOSUB3010:GOTO430
1090 IF B$="SCROLL8"AND O(27)=1000THENZ$="IT READS -THE SCROLL OF PERSISTENCE-.":GOSUB3010:GOTO430
1100 IF B$="SCROLL9"AND O(28)=1000THENZ$="IT READS -THE SCROLL OF SPECIALIZED KNOWLEDGE-.":GOTO430
1110 IF B$="SCROLL10"AND O(29)=1000THENZ$="IT READS -THE SCROLL OF SEX TRANSMUTATION.  BEHIND EVERY SUCCESSFULL MAN IS A SUCCESSFULL WOMAN- .":GOSUB3010:GOTO430
1120 IF B$="SCROLL11"AND O(30)=1000THENZ$="IT READS -THE SCROLL OF THE UNCONSCIOUS MIND-.":GOSUB3010:GOTO430
1130 IF B$="SCROLL12"AND O(31)=1000THENZ$="IT READS -THE SCROLL OF DESIRE-. ":GOTO430
1140 IF B$="SCROLL13"AND O(19)=1000THENZ$="IT READS -THE SCROLL OF THE SIXTH SENSE-.":GOSUB3010:GOTO430
1150 IF B$="DOOR"AND DT=2THENZ$="THE DOOR IS CLOSED.":GOSUB3010:GOTO430
1160 IF B$="DOOR"AND DT=3THENZ$="THE DOOR IS LOCKED.":GOSUB3010:GOTO430
1170 IF B$="DOOR"AND DT=0THENZ$="YOU LOOK AROUND, BUT DON'T SEE A DOOR.":GOSUB3010:GOTO430
1180 IF B$="DOOR"AND DT=1THENZ$="THE DOOR IS AJAR.":GOSUB3010:GOTO430
1190 IF B$="DESK"AND L=7THENZ$="IT IS A SOLID OAK DESK. THE DESK HAS A SMALL DRAWER.":GOSUB3010:GOTO430
1200 IF B$="DESK"AND L=29THENZ$="IT IS A LARGE WALNUT DESK. THERE IS A LARGE DRAWER ON THE LEFT SIDE OF THE DESK.":GOSUB3010:GOTO430
1210 IF B$="DESK"AND L=14THENZ$="IT IS A LARGE METAL DESK. THERE IS A SMALL METAL DRAWER ON THE RIGHT SIDE OF THE DESK.":GOSUB3010:GOTO430
1220 IF B$="DRAWER"AND(L=7OR L=29OR L=14)THEN Z$="THE DRAWER IS UNLOCKED.":GOSUB3010:GOTO430
1240 IF B$="BOOKSHELF"AND L=2 AND O(16)=-4THENZ$="IT IS A LARGE BOOKSHELF EXTENDING THE LEGNTH OF THE WALL. THERE IS A PAPERBACK BOOK ON ONE OF THE SHELVES.":O(16)=L:GOTO430
1250 IF B$="BOOK"AND O(16)=1000THENZ$="THE TITLE OF THE BOOK IS think and grow rich WRITTEN BY NAPOLEON HILL.":GOSUB3010:GOTO430
1260 IF B$="HUTCH"AND L=17THENZ$="IT IS MADE OF MAHOGANY, AND IT IS NOT LOCKED.":GOSUB3010:GOTO430
1270 IF L=7OR L=8OR L=29OR L=30AND B$="PANEL"THENZ$="EXAMINING THE PANEL, YOU NOTICE A SMALL KEY PAD FOR ENTERING NUMBERS. YOU REALIZE YOU WILL HAVE TO HAVE THE CORRECT COMBINATION TO 'OPEN' OR 'CLOSE' THE PANEL.":GOSUB3010:GOTO430
1280 IF B$="TABLE" AND L=18 AND O(26)<0 THEN O(26)=L:O(17)=L:Z$="YOU NOTICE A SMALL PAMPHLET AND THE 'SCROLL7'.":GOSUB3010:GOTO 430
1290 IF B$="TABLE" AND L=18 AND O(26)>0 THEN Z$="IT IS A LARGE TABLE WITH A TIGER CLAW PEDESTAL.":GOSUB3010:GOTO430
1300 IF B$="BOOKLET" AND O(15)=1000 THEN Z$="IT READS the junkfood withdrawal manual BY MONTE KLINE.":GOSUB3010:GOTO430
1310 IF B$="PAMPHLET" AND O(17)=1000 THEN Z$="IT CONTAINS A LISTOF BOOKS: holy bible, 7 keys to a longer life BY DR. HANS J. KUGLER, the magic of believing, BY CLAUDE BRISTOL, the greatest salesman in the world, BY OG MANDINO.":GOSUB3010:GOTO430
1320 IF B$="JACKET" AND O(14)=1000 THEN Z$="IT IS YOUR AVERAGE DRESS JACKET.":GOSUB3010:GOTO 430
1330 IF B$="STATUE" AND L=10 THEN Z$="THIS IS A BEAUTIFUL MARBLE STATUE OF CUPID.":GOSUB3010:GOTO430
1340 Z$="YOU DO NOT HAVE THE "+B$+". ":GOSUB3010
1350 GOTO430
1360 FOR C=1 TO 6:IF B$=T$(C) THEN DR=C:GOTO1380
1370 NEXT:GOTO 1430
1380 IF T(DR,L)>0 THEN L=T(DR,L):GOTO1400
1390 IFT(DR,L)=<0THEN Z$="YOU CAN NOT GO THAT WAY.":GOSUB3010:GOTO430
1400 IF L=1000AND WN>0THEN2720
1410 IF L=999THEN2770
1420 GOTO200
1430 Z$="PLEASE TRY A DIRECTION.":GOSUB3010:GOTO430
1440 IF B$="STATUE"AND O(19)=-3THENZ$="AS YOU TAKE THE STATUE, YOU UNCOVER A SECRET COMPARTMENT HOLDING 'SCROLL13'":O(13)=1000:O(19)=10:GOTO430
1450 IF B$="SCROLL"OR B$="KEY"THENZ$="THIS MANSION HAS MORE THAN ONE "+B$+" IN IT, THEREFORE, YOU WILL HAVE TO BE MORE SPECIFIC.":GOSUB3010:GOTO430
1460 IF B$="JACKET"AND O(22)=-1THENZ$="AS YOU PICK UP THE JACKET, SOMETHING FALLS FROM ONE OF THE POCKETS. YOU TAKE A CLOSER LOOK, AND REALIZE IT IS THE VALUABLE 'SCROLL3'.":GOSUB 3010:O(22)=21:O(14)=1000:GOTO430
1470 FOR C=1TO31
1480 IF B$=O$(C)AND O(C)=L AND O$(C)<>"*"THEN O(C)=1000:Z$=LO$(C)+" HAS BEEN TAKEN.":GOSUB 3010:GOTO430
1490 NEXT
1500 FOR C=1TO31
1510 IF B$=O$(C)AND O(C)=1000THENZ$="YOU ALREADY HAVE THE "+LO$(C)+". ":GOTO430
1520 NEXT
1530 Z$="YOU ARE UNABLE TO TAKE THE "+B$+". ":GOTO430
1540 '
1550 IF B$="MAT"AND L=1AND O(3)=-1THENZ$="AS YOU MOVE THE WELCOME MAT, YOU NOTICE THE 'KEY1' UNDER THE MAT.":O(3)=L:GOTO430
1560 IFB$="MAT"AND L=1AND O(3)>1THENZ$="YOU MOVE THE MAT, BUT SEE ONLY A PILE OF DIRT.":GOSUB3010:GOTO430
1570 IF B$="MAT"AND L=1AND O(3)=1THENZ$="THE 'KEY1' IS STILL HERE ON THE PORCH.":GOSUB3010:GOTO430
1580 IF B$="STATUE"AND L=10AND O(19)>0 THEN Z$="YOU MOVE THE STATUE OF CUPID, BUT NOTHING HAPPENS.":GOTO430
1590 IF B$="STATUE"AND L=10AND O(19)<0THENZ$="YOU MOVE THE STATUE OF CUPID, REVEALING A COMPARTMENT CONTAINING THE 'SCROLL13'.":O(19)=10:GOSUB3010:GOTO430
1600 IF B$="DOOR"THENZ$="AROUND HERE WE 'OPEN' AND 'CLOSE' DOORS.":GOSUB3010:GOTO430
1610 IF B$="PANEL"THENZ$="THE PANEL WILL 'OPEN' OR 'CLOSE WITH THE CORRECT COMBINATION.":GOSUB3010:GOTO430
1620 Z$="YOU ARE UNABLE TO MOVE THE "+B$+". ":GOSUB3010:GOTO430
1630 '
1640 IF A$=A1$THENZ$="YOU MUST SPECIFY WHAT YOU WANT TO UNLOCK.":GOSUB3010:GOTO430
1650 IF B$="DOOR"AND(L=31OR L=30)AND T(3,30)=-2AND O(6)=1000THENZ$="YOU HAVE UNLOCKED THE DOOR WITH THE 'KEY4'.":T(4,31)=-1:T(3,30)=-1:GOSUB3010:GOTO430
1660 IF B$="DOOR"AND(L=31OR L=30)AND T(3,30)>-2THENZ$="THE DOOR IS ALREADY UNLOCKED.":GOSUB3010:GOTO430
1670 IF B$="DOOR"AND(L=31OR L=30)AND T(3,30)=-2 AND O(6)<1000THENZ$="YOU DO NOT HAVE THE PROPER KEY TO UNLOCK THIS DOOR.":GOSUB3010:GOTO430
1680 IF B$="DOOR"AND(L=14OR L=15)AND T(3,14)=-2AND O(5)=1000 THENZ$="YOU HAVE UNLOCKED THE DOOR WITH THE 'KEY3'.":T(3,14)=-1:T(4,15)=-1:GOSUB3010:GOTO430
1690 IF B$="DOOR"AND(L=14OR L=15)AND T(3,14)>-2THENZ$="THE DOOR IS ALREADY UNLOCKED.":GOSUB3010:GOTO430
1700 IF B$="DOOR"AND(L=14OR L=15)AND T(3,14)=-2AND O(5)<1000THENZ$="YOU DO NOT HAVE THE PROPER KEY TO UNLOCK THIS DOOR.":GOSUB3010:GOTO430
1710 IF B$="DOOR"AND(L=6OR L=7)AND T(3,6)=-2AND O(4)=1000THENZ$="YOU HAVE UNLOCKED THE DOOR WITH THE 'KEY2'.":T(3,6)=-1:T(4,7)=-1:GOSUB3010:GOTO430
1720 IF B$="DOOR"AND(L=6OR L=7)AND T(3,6)>-2THENZ$="THE DOOR IS ALREADY UNLOCKED.":GOSUB3010:GOTO430
1730 IF B$="DOOR"AND(L=6OR L=7)AND O(4)<1000THENZ$="YOU DO NOT HAVE THE PROPER KEY TO UNLOCK THIS DOOR.":GOSUB3010:GOTO430
1740 IF B$="DOOR"AND(L=1OR L=3)AND T(1,1)=-2AND O(3)=1000THENZ$="YOU HAVE UNLOCKED THE DOOR WITH THE 'KEY1'.":T(1,1)=-1:T(2,3)=-1:GOSUB3010:GOTO430
1750 IF B$="DOOR"AND(L=1OR L=3)AND T(1,1)>-2THENZ$="THE DOOR IS ALREADY UNLOCKED.":GOSUB3010:GOTO430
1760 IF B$="DOOR"AND(L=1OR L=3)AND T(1,1)=-2AND O(3)<1000THENZ$="YOU DO NOT HAVE THE PROPER KEY TO UNLOCK THIS DOOR.":GOSUB3010:GOTO430
1770 Z$="YOU ARE UNABLE TO UNLOCK THE "+B$+". ":GOSUB3010:GOTO430
1780 '
1790 IF B$="DOOR"AND(L=31OR L=30)AND T(3,30)=-1AND O(6)=1000THENZ$="YOU HAVE LOCKED THE DOOR WITH THE 'KEY4'.":T(3,30)=-2:T(4,31)=-2:GOTO430
1800 IF B$="DOOR"AND(L=31OR L=30)AND T(3,30)=-1THENZ$="THE DOOR IS ALREADY LOCKED.":GOTO430
1810 IF B$="DOOR"AND(L=31OR L=30)AND T(3,30)>-1THENZ$="THE DOOR MUST BE CLOSED BEFORE YOU CAN LOCK IT.":GOTO430
1820 IF B$="DOOR"AND(L=30OR L=31)AND O(6)<1000THENZ$="YOU DO NOT HAVE THE PROPER KEY TO LOCK THIS DOOR.":GOTO430
1830 IF B$="DOOR"AND(L=14OR L=15)AND T(3,14)=-1AND O(5)=1000THENZ$="YOU HAVE LOCKED THE DOOR WITH THE 'KEY3'.":T(3,14)=-2:T(4,15)=-2:GOTO430
1840 IF B$="DOOR"AND(L=14OR L=15)AND T(3,14)=-2THENZ$="THE DOOR IS ALREADY LOCKED.":GOSUB3010:GOTO430
1850 IF B$="DOOR"AND(L=14OR L=15)AND T(3,14)>-1THENZ$="THE DOOR MUST BE CLOSED BEFORE YOU CAN LOCK IT.":GOSUB3010:GOTO430
1860 IF B$="DOOR"AND(L=14OR L=15)AND O(5)<1000THENZ$="YOU DO NOT HAVE THE PROPER KEY TO LOCK THIS DOOR.":GOSUB3010:GOTO430
1870 IF B$="DOOR"AND(L=6OR L=7)AND T(3,6)=-1AND O(4)=1000THENZ$="YOU HAVE LOCKED THE DOOR WITH THE 'KEY2'.":T(3,6)=-2:T(4,6)=-2:GOSUB3010:GOTO430
1880 IF B$="DOOR"AND(L=6OR L=7)AND T(3,6)=-2THENZ$="THE DOOR IS ALREADY LOCKED.":GOSUB3010:GOTO430
1890 IF B$="DOOR"AND(L=6OR L=7)AND T(3,6)>-1THENZ$="YOU MUST CLOSE THE DOOR BEFORE YOU CAN LOCK IT.":GOSUB3010:GOTO430
1900 IF B$="DOOR"AND(L=6OR L=7)AND O(4)<1000THENZ$="YOU DO NOT HAVE THE PROPER KEY TO LOCK THIS DOOR.":GOSUB3010:GOTO430
1910 IF B$="DOOR"AND(L=1OR L=3)AND T(1,1)=-1AND O(3)=1000THENZ$="YOU HAVE LOCKED THE DOOR WITH THE 'KEY1'.":T(1,1)=-2:T(2,3)=-2:GOSUB3010:GOTO430
1920 IF B$="DOOR"AND(L=1OR L=3)AND T(1,1)=-2AND O(3)=1000THENZ$="THE DOOR IS ALREADY LOCKED.":GOTO430
1930 IF B$="DOOR"AND(L=1OR L=3)AND T(1,1)>-1THENZ$="YOU MUST CLOSE THE DOOR BEFORE YOU CAN LOCK IT.":GOTO430
1940 IF B$="DOOR"AND(L=1OR L=3)AND O(3)<1000THENZ$="YOU DO NOT HAVE THE PROPER KEY TO LOCK THIS DOOR.":GOTO430
1950 Z$="YOU ARE UNABLE TO LOCK THE "+B$+".":GOSUB3010:GOTO430
1960 '
1970 IF A$=A1$THENZ$="YOU MUST SPECIFY WHAT YOU WANT TO OPEN.":GOSUB3010:GOTO430
1980 IF B$="DOOR"AND(L=31OR L=30)AND T(3,30)=-1THENZ$="YOU HAVE OPENED THE DOOR.":T(3,30)=31:T(4,31)=30:GOSUB3010:GOTO430
1990 IF B$="DOOR"AND(L=31OR L=30)AND T(3,30)>-1THENZ$="THE DOOR IS ALREADY OPEN.":GOSUB3010:GOTO430
2000 IF B$="DOOR"AND(L=31OR L=30)AND T(3,30)=-2THENZ$="YOU MUST UNLOCK THE DOOR BEFORE YOU CAN OPEN IT.":GOSUB3010:GOTO430
2010 IF B$="DOOR"AND(L=14OR L=15)AND T(3,14)=-1THENZ$="YOU HAVE OPENED THE DOOR.":T(3,14)=15:T(4,15)=14:GOSUB3010:GOTO430
2020 IF B$="DOOR"AND(L=14OR L=15)AND T(3,14)=-2THENZ$="YOU MUST UNLOCK THE DOOR BEFORE YOU CAN OPEN IT.":GOSUB3010:GOTO430
2030 IF B$="DOOR"AND(L=14OR L=15)AND T(3,14)>-1THENZ$="THE DOOR IS ALREADY OPEN.":GOSUB3010:GOTO430
2040 IF B$="DOOR"AND(L=6OR L=7)AND T(3,6)=-1THENZ$="YOU HAVE OPENED THE DOOR.":T(3,6)=7:T(4,7)=6:GOSUB3010:GOTO430
2050 IF B$="DOOR"AND(L=6OR L=7)AND T(3,6)>-1THENZ$="THE DOOR IS ALREADY OPEN.":GOSUB3010:GOTO430
2060 IF B$="DOOR"AND(L=6OR L=7)AND T(3,6)=-2THENZ$="YOU MUST UNLOCK THE DOOR BEFORE YOU CAN OPEN IT.":GOSUB3010:GOTO430
2070 IF B$="DOOR"AND(L=1OR L=3)AND T(1,1)=-1THENZ$="YOU HAVE OPENED THE DOOR.":T(1,1)=3:T(2,3)=1:GOSUB3010:GOTO430
2080 IF B$="DOOR"AND(L=1OR L=3)AND T(1,1)>-1THENZ$="THE DOOR IS ALREADY OPEN.":GOSUB3010:GOTO430
2090 IF B$="DOOR"AND(L=1OR L=3)AND T(1,1)=-2THENZ$="YOU MUST UNLOCK THE DOOR BEFORE YOU CAN OPEN IT.":GOSUB3010:GOTO430
2100 IF L=17AND B$="HUTCH"AND O(28)<0THENZ$="AS YOU OPEN THE HUTCH, THE 'SCROLL9' DROPS TO THE FLOOR.  THE DOOR ON THE HUTCH SWINGS SHUT WHEN YOU LET IT GO.":O(28)=17:GOTO430
2110 IF L=17AND B$="HUTCH"AND O(28)>0THENZ$="YOU OPEN THE HUTCH,AND SEE NOTHING INSIDE.  THE DOOR ON THE HUTCH SWINGS SHUT WHEN YOU LET IT GO.":GOTO430
2120 IF B$="PANEL"AND(L=7OR L=8OR L=29OR L=30)THEN GOSUB 2600:GOTO430
2130 IF B$="DRAWER"AND L=7AND O(2)<0THEN O(2)=L:O(29)=L:Z$="WHEN YOU OPEN UP THE DRAWER, YOU SEE A PLASTIC ID CARD, AND THE VALUABLE 'SCROLL10'.":GOTO430
2140 IF B$="DRAWER"AND L=7AND O(2)>0THENZ$="THE DRAWER IS NOW OPEN.":GOSUB3010:GOTO430
2150 IF B$="DRAWER"AND L=14AND O(21)<0THENZ$="OPENING THE DRAWER REVEALS THE VALUABLE 'SCROLL2'.":O(21)=L:GOSUB3010:GOTO430
2160 IF B$="DRAWER"AND L=14 ANDO(21)>0THENZ$="THE DRAWER IS OPEN.":GOSUB3010:GOTO430
2170 IF B$="DRAWER"AND L=29AND O(1)<0THENZ$="OPENING THE DRAWER REVEALS A WRINKLED PIECE OF PAPER, THE 'SCROLL8', AND THE 'SCROLL11'.":O(1)=L:O(27)=L:O(30)=L:GOSUB3010:GOTO430
2180 IF B$="DRAWER"AND L=29AND O(1)>0THENZ$="THE DRAWER IS NOW OPEN.":GOSUB3010:GOTO430
2190 Z$="YOU ARE UNABLE TO OPEN THE "+B$+".":GOSUB3010:GOTO430
2200 '
2210 IF A$=A1$THENZ$="YOU MUST SPECIFY WHAT YOU WANT TO CLOSE.":GOSUB3010:GOTO430
2220 IF B$="DOOR"AND(L=31OR L=30)AND T(3,30)>0THENZ$="YOU HAVE CLOSED THE DOOR.":T(3,30)=-1:T(4,31)=-1:GOSUB3010:GOTO430
2230 IF B$="DOOR"AND(L=31OR L=30)AND T(3,30)<0THENZ$="THE DOOR IS ALREADY CLOSED.":GOSUB3010:GOTO430
2240 IF B$="DOOR"AND(L=14OR L=15)AND T(3,14)>-1THENZ$="YOU HAVE CLOSED THE DOOR.":T(3,14)=-1:T(4,15)=-1:GOSUB3010:GOTO430
2250 IF B$="DOOR"AND(L=14OR L=15)AND T(3,14)<0THENZ$="THE DOOR IS ALREADY CLOSED.":GOSUB3010:GOTO430
2260 IF B$="DOOR"AND(L=6OR L=7)AND T(3,6)=-1THENZ$="YOU HAVE CLOSED THE DOOR.":T(3,6)=-1:T(4,7)=-1:GOSUB3010:GOTO430
2270 IF B$="DOOR"AND(L=6OR L=7)ANDT(3,6)>-1THENZ$="THE DOOR IS ALREADY CLOSED.":GOSUB3010:GOTO430
2280 IF B$="DOOR"AND(L=1OR L=3)AND T(1,1)>-1THENZ$="YOU HAVE CLOSED THE DOOR.":T(1,1)=-1:T(2,3)=-1:GOSUB3010:GOTO430
2290 IF B$="DOOR"AND(L=1OR L=2)AND T(1,1)<0THENZ$="THE DOOR IS ALREADY CLOSED.":GOSUB3010:GOTO430
2300 IF L=17AND B$="HUTCH"THENZ$="THE HUTCH IS ALREADY CLOSED.":GOSUB3010:GOTO430
2310 IF B$="PANEL"AND(L=7OR L=8OR L=29OR L=30)THEN GOSUB2600:GOTO430
2320 IF B$="DRAWER"AND L=7THENZ$="THE DRAWER IS CLOSED.":GOSUB3010:GOTO430
2330 IF B$="DRAWER"AND L=14THENZ$="THE DRAWER IS CLOSED.":GOSUB3010:GOTO430
2340 IF B$="DRAWER"AND L=29THENZ$="THE DRAWER IS NOW CLOSED.":GOSUB3010:GOTO430
2350 Z$="YOU ARE UNABLE TO CLOSE THE "+B$+".":GOSUB3010:GOTO430
2360 '
2370 IF B$="SCROLL"OR B$="KEY"THENZ$="THIS MANSION HAS MORE THAN ONE "+B$+" IN IT. YOU WILL HAVE TO SPECIFY WHICH ONE.":GOSUB3010:GOTO430
2380 FOR C=1 TO 31
2390 IF B$=O$(C)AND O(C)=1000AND O$(C)<>"*"THEN O(C)=L:Z$=LO$(C)+" HAS BEEN DROPPED.":GOSUB3010:GOTO430
2400 NEXT
2410 Z$="YOU DO NOT HAVE A "+B$+".":GOSUB3010:GOTO430
2520 ' QUIT
2530 Z$="ARE YOU SURE YOU WANT TO QUIT (YES OR NO)?":GOSUB3010:PRINTZ$;:Z1$=Z$:GOSUB3100:Z$="":INPUTQ$
2540 IF Q$<>"YES"AND Q$<>"NO"THENZ$="IF YOU ARE GOING TO FIND YOUR SUCCESS, YOU WILL NEED TO LEARN TO ASK FOR EXACTLY WHAT YOU WANT.":GOSUB3010:PRINTZ$:Z1$=Z$:GOSUB3100:GOTO2530
2550 IF Q$="YES"THEN END ELSE430
2560 ' HELP
2570 IF L=1THENZ$="A VOICE IN YOUR HEAD SEEMS TO SAY, TO THE SOUTH IS HEART BREAK AND TO THE NORTH DESTINY. WHEN ALL SEEMS LOST, LOOK TO BE WELCOMED.":GOSUB3010:GOTO430
2580 Z$="YOU WILL LEARN MORE BY USING YOUR OWN JUDGEMENT AND NOT THE JUDGEMENT OF OTHERS.":GOSUB3010:GOTO430
2590 '
2600 Z$="IN ORDER TO 'OPEN' OR 'CLOSE' THE PANEL, YOU WILL NEED TO ENTER THE COMBINATION AS A SERIES OF NUMBERS.":GOSUB3010:Z1$=Z$:GOSUB 3100:PRINTZ$:Z1$="THE COMBINATION PLEASE":PRINTZ1$;:GOSUB3100:INPUTA2$
2610 IF(L=7OR L=8)AND T(3,7)<0AND A2$="963147"THENZ$="THE PANEL SLIDES OPEN.":T(3,7)=8:T(4,8)=7:GOSUB3010:GOTO2700
2620 IF(L=7OR L=8)AND T(3,7)>0AND A2$="963147"THENZ$="THE PANEL SLIDES SHUT.":T(3,7)=-3:T(4,8)=-3:GOSUB3010:GOTO2700
2630 IF(L=29OR L=30)AND T(3,29)<0AND A2$="2468"THENZ$="THE PANEL SLIDES OPEN.":T(3,29)=30:T(4,30)=29:GOSUB3010:GOTO2700
2640 IF(L=29OR L=30)AND T(3,29)>0THENZ$="THE PANEL SLIDES SHUT.":T(3,29)=-3:T(4,30)=-3:GOSUB3010:GOTO2700
2650 Z$="THAT IS NOT THE CORRECT COMBINATION.":GOSUB3010
2660 IF(L=7OR L=8)AND O(1)=1000THENZ$="THE CORRECT COMBINATION IS AT ARMS LEGNTH.":GOSUB3010
2670 IF(L=7OR L=8)AND O(1)<1000THENZ$="PERHAPS YOU SHOULD SEARCH THE MANSION FOR THE CORRECT COMBINATION.":GOSUB3010
2680 IF(L=29OR L=30)AND O(2)=1000THENZ$="THE CORRECT COMBINATION IS CLOSER TO YOUR HEART THAN YOU THINK.":GOSUB3010
2690 IF(L=29OR L=30)AND O(2)<1000THENZ$="YOU MIGHT DO BETTER TO SEARCH THE MANSION FOR THE COMBINATION.":GOSUB3010
2700 RETURN
2710 '
2720 CLS:Z$="YOU SHOULD TRY AGAIN SOME OTHER TIME. YOU DID NOT FIND THE 13 SCROLLS. THE TOWNSPEOPLE ARE LAUGHING AT YOU.":GOSUB3010:PRINTZ$:Z1$=Z$:GOSUB3100
2730 Z$="IF YOU DESIRE SUCCESS BAD ENOUGH, YOU WILL DISREGARD THE  LAUGHTER OF THE TOWNSPEOPLE AND TRY AGAIN.":GOSUB3010:PRINTZ$:Z1$=Z$:GOSUB3100
2740 PRINT:Z$="PLAY AGAIN (YES/NO)?":GOSUB3010:INPUTZ$
2750 IF Z$<>"YES"AND Z$<>"NO"THENZ$="PLEASE TYPE 'YES' OR 'NO',NOT BOTH.":GOSUB3010:GOTO2740
2760 IF Z$="YES"THEN RUN ELSE END
2770 CLS:Z$="YOU HAVE FOUND ALL 13 SCROLLS. YOU ARE WELL ON YOUR WAY TO YOUR SUCCESS.":GOSUB3010:PRINTZ$:Z1$=Z$:GOSUB3100
2780 Z$="THE TOWNSPEOPLE BEG TO SEE THE PRECIOUS SCROLLS THAT ONLY YOU HAD THE COURAGE TO GET. DO YOU WISH TO SHOW THE SCROLLS TO THESE TOWNSPEOPLE?":GOSUB 3010:PRINTZ$:INPUTV$:IF V$="YES"THEN PRINT"WHY?"
2783 IF V$<>"YES"THEN Z$="I DON'T BLAME YOU. YOU HAD THE COURAGE TO GET THEM.":GOSUB3010:PRINTZ$:PRINT:END
2785 END
2790 Z$=INKEY$: IF Z$=""THEN2790ELSE END
2800 ' DATA STATEMENTS
2810 DATA ON THE FRONT PORCH OF SUCCESS MANSION.,IN THE MANSION'S LARGE LIBRARY.,IN THE FOYER AT THE MANSION'S ENTRANCE.,IN THE DOWNSTAIRS LAVATORY.,IN A LARGE GAME ROOM.
2820 DATA AT THE SOUTH END OF A LONG HALLWAY EXTENDING NORTH AND SOUTH.,IN THE OFFICE WHERE BIG MONEY DEALS WERE ONCE CLOSED.,IN A LARGE VAULT WHERE VALUABLES ARE KEPT.,ON THE LANDING HALF OF THE WAY UP THE STAIRCASE.
2830 DATA AT THE FOOT OF A BEAUTIFUL MARBLE STAIRCASE LEADING UPWARD.
2840 DATA IN THE MIDDLE OF A LONG HALLWAY EXTENDING NORTH AND SOUTH.,AT THE SOUTHERN END OF A LARGE LIVING ROOM.,ON A MARBLE STAIRCASE LEADING UP FROM A SMALL LANDING.,IN THE MANSION'S LARGE STUDY.,AT THE NORTH END OF A LONG HALLWAY LEADING SOUTH.
2850 DATA AT THE NORTH END OF THE LIVING ROOM.,AT THE WESTERN END OF A LARGE DINING ROOM.
2860 DATA IN THE MIDDLE OF A LARGE DINING ROOM.,AT THE EAST END OF A LARGE DINING ROOM.,IN A LARGE KITCHEN.,IN A SMALL LAUNDRY ROOM.,IN THE KITCHEN PANTRY.,ON THE PRIVATE BALCONY.,IN THE MASTER BATHROOM.
2870 DATA IN THE GUEST BEDROOM.,IN THE GUEST BATHROOM.,IN A LARGE BEDROOM.
2880 DATA IN THE BATHROOM WHICH ADJOINS TWO BEDROOMS.,IN THE PRIVATE STUDY.,IN THE MASTER BEDROOM.,AT THE EAST END OF A LONG BALCONY.,AT THE TOP OF A STAIRCASE.,AT THE WEST END OF A LONG BALCONY.,IN A SMALL BEDROOM.
2890 DATA WRINKLED PIECE OF PAPER,PAPER,-2,PLASTIC ID CARD,CARD,-2,KEY1,KEY1,-1,KEY2,KEY2,30,KEY3,KEY3,7,KEY4,KEY4,20,LARGE OAK DESK,*,7,LARGE WALNUT DESK,*,29,METAL DESK,*,14
2900 DATA CHINA HUTCH,*,17,LARGE OAK TABLE,*,18,HANDCARVED BOOKSHELF,*,2,STATUE OF CUPID,STATUE,10,PLUSH DESIGNER JACKET,JACKET,21,SMALL BOOKLET,BOOKLET,22,PAPERBACK BOOK,BOOK,-4
2910 DATA SMALL PAMPHLET,PAMPHLET,-1,DESIGNER WELCOME MAT,*,1,SCROLL13,SCROLL13,-3,SCROLL1,SCROLL1,9,SCROLL2,SCROLL2,-1,SCROLL3,SCROLL3,-1,SCROLL4,SCROLL4,8,SCROLL5,SCROLL5,30,SCROLL6,SCROLL6,8,SCROLL7,SCROLL7,-1
2920 DATA SCROLL8,SCROLL8,-1,SCROLL9,SCROLL9,-1,SCROLL10,SCROLL10,-1,SCROLL11,SCROLL11,-1,SCROLL12,SCROLL12,2
2930 DATA -2,1000,,,,,,,3,,,,6,-2,4,2,,,,,,3,,,,,6,,,
2940 DATA 11,3,-2,5,,,,,-3,-1,,,,,,-3,,,,,,,13,10,,,11,,9,
2950 DATA 15,6,12,10,,,16,,,11,,,,,,,32,9,,,-2,,,,17,11,16,-2,,
2960 DATA 18,12,,15,,,,15,18,,,,,16,19,17,,,,,20,18,,,22,,21,19,,
2970 DATA ,,,20,,,,20,,,,,,29,,,,,,30,,,,,,31,26,,,
2980 DATA ,,,25,,,,33,28,,,,,34,,27,,,23,,-3,,,,24,,-2,-3,,
2990 DATA 25,,32,-2,,,,,33,31,,13,27,,34,32,,,28,,,33,,
3000 DATA INVENTORY,1,LOOK,2,EXAMINE,2,READ,2,GO,3,RUN,3,WALK,3,GET,4,TAKE,4,MOVE,5,PUSH,5,PULL,5,UNLOCK,6,LOCK,7,OPEN,8,CLOSE,9,DROP,10,PUT,10,SAVE,11,LOAD,12,QUIT,13,HELP,14
3010 CX$=CHR$(32):LL=32
3020 CL=INT(LEN(Z$)/LL):CR$=RIGHT$(Z$,CL)
3030 IF LEN(Z$)<=LL THEN PRINTZ$:Z$="":GOTO3090
3040 FOR CX=LL TO 1 STEP-1
3050 IF MID$(Z$,CX,1)=CX$ THEN CC=CX:GOTO3070
3060 NEXT:GOTO3070
3070 PRINT LEFT$(Z$,CC-1):Z1$=LEFT$(Z$,CC-1):GOSUB3100:Z$=MID$(Z$,CC+1,LEN(Z$)-CC)
3080 IF LEN(Z$)>LL THENGOTO3040
3090 RETURN
3100 IF TK=0THEN RETURN 
