100 REM BIORHYTHM
110 REM (C) 1981, TOM RUGG AND PHIL. FELDMAN
120 CLEAR 200:CLS
130 L=0:T=11:P=3.14159265
140 PRINT"** BIORHYTHM **":PRINT
150 PRINT"ENTER BIRTH DATE"
160 GOSUB 500:GOSUB 600:JB=JD
190 PRINT"ENTER START DATE FOR CHART"
200 GOSUB 500:GOSUB 600:JC=JD
230 IF JC>=JB THEN 270
240 PRINT"CHART DATE CAN'T BE EARLIER"
250 PRINT"THAN BIRTH DATE."
260 SOUND 8,8:PRINT:GOTO 150
270 FOR K=1 TO 1000:NEXT
280 GOSUB 700
300 N=JC-JB
310 V=23:GOSUB 800
320 V=28:GOSUB 800 
330 V=33:GOSUB 800
340 GOSUB 1000
350 PRINT C$;TAB(8);L$
360 JC=JC+1:L=L+1:IF L<12THEN 300
370 PRINT"'E' = END, SPACE = GO ON";
380 R$=INKEY$:IF R$="" THEN 380
390 IF R$="E" THEN 120
400 L=0:GOTO 280
500 PRINT
505 INPUT"MONTH(1 TO 12)";M
510 M=INT(M):IF M<1 OR M>12 THEN 505
520 INPUT"DAY (1 TO 31)";D
530 D=INT(D):IF D<1 OR D>31 THEN 520
540 INPUT"YEAR";Y
550 Y=INT(Y):IF Y<0 THEN 540
560 IF Y>99 THEN 580
570 Y=Y+1900:PRINT Y;"ASSUMED."
580 RETURN
600 W=0:IF M<3 THEN W=-1
610 JD=INT(1461*(Y+4800+H)/4)
620 B=INT(367*(M-2-W*12)/12)
630 IF B<0 THEN B=B+1
640 JD=JD+B
650 B=INT(INT(3*(Y+4900+W)/100)/4)
660 JD=JD+D-32075-B
670 RETURN
700 CLS
710 PRINT TAB(10);"BIORHYTHM"
720 PRINT"--DATE--";TAB(12);
730 PRINT"DOWN";TAB(19);"0";TAB(24);"UP"
740 PRINT TAB(8);
750 FOR K=1 TO T+T+1:PRINT CHR$(175);
760 NEXT:PRINT:RETURN
800 W=INT(N/V):R=N-(W*V)
850 IF V<>23 THEN 900
860 L$=CHR$(32):FOR K=1 TO 4
870 L$=L$+L$:NEXT
880 L$=LEFT$(L$,T)+CHR$(175)+LEFT$(L$,T)
890 IF V=23 THEN C$="P"
900 IF V=28 THEN C$="E"
910 IF V=33 THEN C$="I"
920 W=R/V:W=W*2*P
930 W=T*SIN(W):W=W+T+1.5
940 W=INT(W):A$=MID$(L$,W,1)
950 IF A$="P" OR A$="E" OR A$="*" THEN C$="*"
955 IF W=1 THEN 980
957 IF W=T+T+1 THEN 990
960 L$=LEFT$(L$,W-1)+C$+RIGHT$(L$,T+T+1-W)
970 RETURN
980 L$=C$+RIGHT$(L$,T+T):RETURN
990 L$=LEFT$(L$,T+T)+C$:RETURN
1000 W=JC+68569:R=INT(4*W/146097)
1010 W=W-INT((146097*R+3)/4)
1020 Y=INT(4000*(W+1)/1461001)
1030 W=W-INT(1461*Y/4)+31
1040 M=INT(80*W/2447)
1050 D=W-INT(2447*M/80)
1060 W=INT(M/11):M=M+2-12*W
1070 Y=100*(R-49)+Y+W
1080 A$=STR$(M):W=LEN(A$)-1
1090 C$=MID$(A$,2,W)+"/"
1100 A$=STR$(D):W=LEN(A$)-1
1110 C$=C$+MID$(A$,2,W)+"/"
1120 A$=STR$(Y):W=LEN(A$)-1
1130 C$=C$+MID$(A$,W,2)
1140 RETURN
