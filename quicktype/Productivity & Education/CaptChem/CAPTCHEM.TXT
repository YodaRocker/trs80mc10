
10 CLS:CLEAR200:DIME$(50),S$(50),JW(50)
20 FORA=1TO7:READ C$(A),IC$(A):NEXTA
30 DATA QUIT CHEATING,HA HA,PUT THE CHART AWAY,BAD GUESS,SHOW OFF,SORRY,GOOD GUESS,EVIL LIKED THAT
40 DATA CAPTAIN IS PROUD,TRY HARDER,YEA TEAM,BOO HISS,GOOD SHOW,STUDY MUCH?
50 C(1)=4:C(2)=5:C(3)=3
60 FORA=1TO25:BL$=BL$+CHR$(128):NEXTA
70 FORA=1TO8:R$=R$+CHR$(191):NEXTA:L$=CHR$(175)+CHR$(170)+CHR$(165)+CHR$(175)
80 PRINTTAB(4)"HELP CAPTAIN CHEMISTRY"
90 PRINTTAB(3)"BATTLE THE FORCES OF THE"
100 PRINTTAB(8)"EVIL ELEMENTS."
110 PRINT:PRINT"  YOU ARE TO GIVE THE CORRECT"
120 PRINT"SYMBOL FOR EACH ELEMENT NAMED."
130 PRINT"(USE ALL CAPITOL LETTERS.)
140 PRINT"CORRECT ANSWERS STRENGTHEN THE"
150 PRINT"CAPTAIN'S DESTRUCTO RAY.
160 PRINT"INCORRECT ANSWERS HELP THE"
170 PRINT"EVIL SIDE OF THE ELEMENTS."
180 PRINT:PRINT"DON'T LET CAPTAIN CHEMISTRY BE"
190 PRINT"DESTROYED BY EVIL ELEMENTS."
200 PRINT:INPUT"PRESS <ENTER> TO CONTINUE.";EN$
210 CLS3:FORA=1TO50:READ E$(A),S$(A):NEXTA
220 DATA HYDROGEN,H,HELIUM,HE,LITHIUM,LI,BERYLLIUM,BE
230 DATA BORON,B,CARBON,C,NITROGEN,N,OXYGEN,O
240 DATA FLUORINE,F,NEON,NE,SODIUM,NA,MAGNESIUM,MG
250 DATA ALUMINUM,AL,SILICON,SI,PHOSPHORUS,P,SULPHUR,S
260 DATA CHLORINE,CL,ARGON,AR,POTASSIUM,K,CALCIUM,CA
270 DATA CHROMIUM,CR,MANGANESE,MN,IRON,FE,COBALT,CO
280 DATA NICKEL,NI,COPPER,CU,ZINC,ZN,GALLIUM,GA
290 DATA GERMANIUM,GE,ARSENIC,AS,SELENIUM,SE,BROMINE,BR
300 DATA KRYPTON,KR,RUBIDIUM,RB,STRONTIUM,SR,SILVER,AG
310 DATA TIN,SN,ANTIMONY,SB,IODINE,I,XENON,XE
320 DATA CESIUM,CS,BARIUM,BA,PLATINUM,PT,GOLD,AU
330 DATA MERCURY,HG,LEAD,PB,RADON,RN,FRANCIUM,FR
340 DATA RADIUM,RA,URANIUM,U
350 CLS0
360 PRINT@32,CHR$(145)+CHR$(145);:PRINT@64,CHR$(149)+CHR$(159)+CHR$(163);
370 PRINT@96,CHR$(149)+CHR$(151);:PRINT @128,CHR$(148)+CHR$(156);
380 PRINT@29,CHR$(178)+CHR$(178)+CHR$(178);:PRINT@61,CHR$(191)+CHR$(189)+CHR$(186);
390 PRINT@92,CHR$(163)+CHR$(191)+CHR$(190)+CHR$(186);
400 PRINT@125,CHR$(191)+CHR$(189)+CHR$(186);
410 PRINT@157,CHR$(188)+CHR$(188)+CHR$(184);
420 PRINT@160,"CAPTAIN    ";:PRINT@192,"CHEMISTRY'S";:PRINT@224,"CASTLE     ";
430 PRINT@183,"EVIL     ";:PRINT@215,"ELEMENT'S";:PRINT@247,"EMPIRE   ";
440 PRINT@290,"WHAT IS THE SYMBOL FOR---";
450 R=RND(50):IF JW(R)=1 THEN 450
460 PRINT@354,BL$;:PRINT@374,LEFT$(BL$,10);
470 PRINT@418,BL$;:PRINT@480,BL$;
480 PRINT@354,E$(R);:PRINT@374,"";:INPUTAN$
490 IF AN$=S$(R) THEN JW(R)=1:GOTO500
492 GOTO 530
500 NC=NC+1:FORA=1TO3:SOUND150,1:FORB=6 TO 6+NC:SET(B,5,C(A)):NEXTB:FORB=6 TO 6+NC:RESET(B,5):NEXTB:NEXTA
510 PRINT@418,C$(RND(7));
520 IF NC=50 THEN 600 
525 GOTO560
530 NI=NI+2:SOUND30,3:FORA=55TO55-NI STEP-1:SET(A,5,2):NEXTA:FORA=55TO 55-NI STEP-1:RESET(A,5):NEXTA
540 IF NI=50 THEN 770 
550 PRINT@418,IC$(RND(7))+". IT'S "+S$(R)+" ";
560 PRINT@480,"PRESS <ENTER> TO CONTINUE";
570 IN$=INKEY$
580 A$=INKEY$:IFA$=""THEN580
590 IFASC(A$)=13 THEN 450
595 GOTO580
600 CLS0:PRINT@110,CHR$(147)+CHR$(147)+CHR$(147)+CHR$(147);
610 PRINT@141,CHR$(148)+CHR$(159)+CHR$(168)+CHR$(168)+CHR$(159)+CHR$(152);
620 PRINT@174,CHR$(159)+CHR$(159)+CHR$(159)+CHR$(159);:PRINT@207,CHR$(149)+CHR$(154);
630 PRINT@236,R$;:PRINT@268,CHR$(191)+CHR$(128)+LEFT$(R$,4)+CHR$(128)+CHR$(191);
640 PRINT@300,CHR$(191)+CHR$(128)+LEFT$(R$,4)+CHR$(128)+CHR$(191);
650 PRINT@332,CHR$(158)+CHR$(128)+LEFT$(R$,4)+CHR$(128)+CHR$(157);
660 PRINT@366,CHR$(175)+CHR$(175)+CHR$(175)+CHR$(175);
670 PRINT@398,L$;:PRINT@430,L$;:PRINT@462,L$;
680 PRINT@493,CHR$(179)+CHR$(183)+CHR$(128)+CHR$(128)+CHR$(187)+CHR$(179);
690 PRINT@36,"YOU HAVE EARNED YOUR CAPE":FORT=1TO300:NEXTT
700 FORA=14TO20:SET(37-A,A,4):NEXTA
710 SET(17,21,4):SET(17,22,4)
720 FORA=16TO27:SET(A,23,4):NEXTA
730 FORA=36TO47:SET(A,23,4):NEXTA
740 SET(46,22,4):SET(46,21,4)
750 FORA=20TO14STEP-1:SET(A+26,A,4):NEXTA
760 GOTO780
770 FORA=8 TO 0 STEP-1:SOUND20,1:CLS(A):NEXTA:PRINT@196,"YOU LET THE CAPTAIN DOWN"
780 PRINT"TRY AGAIN (Y/N)?"
790 A$=INKEY$:IFA$="Y"THEN RUN
795 IF A$="N" THEN END
800 GOTO 790
