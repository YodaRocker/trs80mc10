100 REM*O CANADA*TRS-80 LEVEL II AND COLOR BASIC 4K/R.RAMELLA 
110 REM*FUN HOUSE/ OCTOBER 83/ SCHOOL STUFF 
120 CLS
130 DATA 1,QUEBEC,QUEBEC,WHITE GARDEN LILY,1867
140 DATA 2,ONTARIO,TORONTO,WHITE TRILLIUM,1867
150 DATA 3,BRITISH COLUMBIA,VICTORIA,FLOWERING DOGWOOD,1871
160 DATA 4,ALBERTA,EDMONTON,WILD ROSE,1905
170 DATA 5,SASKATCHEWAN,REGINA,PRAIRIE LILY,1905
180 DATA 6,MANITOBA,WINNIPEG,PASQUEFLOWER,1870
190 DATA 7,NEWFOUNDLAND,ST. JOHN'S,PITCHER PLANT,1949
200 DATA 8,NEW BRUNSWICK,FREDERICTON,VIOLET,1867
210 DATA 9,NOVA SCOTIA,HALIFAX,TRAILING ARBUTUS,1867
220 DATA 10,PRINCE EDWARD ISLAND,CHARLOTTETOWN,LADY'S-SLIPPER,1873
230 DATA 0,NORTHWEST TERRITORIES,YELLOWKNIFE,MOUNTAIN AVENS,0
240 DATA 0,YUKON TERRITORY,WHITEHORSE,FIREWEED,0
250 CLEAR 200:A=RND(-(PEEK(9)*256+PEEK(10)))
260 DIM A$(60)
270 FOR A=1 TO 60
280 READA$(A)
290 NEXT
300 CLS
310 A=RND(5)
320 B=RND(12)*5-4
330 ON A GOSUB 350,410,470,530,610
340 GOTO 300
350 PRINT "WHAT IS THE CAPITAL"
360 PRINT "OF "A$(B+1)"?"
370 INPUT B$
380 IF B$=A$(B+2) THEN PRINT "RIGHT":GOTO390
381 PRINT "IT'S "A$(B+2)
390 GOSUB 670
400 RETURN
410 PRINT "OF WHAT PROVINCE OR TERRITORY"
420 PRINT "IS "A$(B+2)" CAPITAL?"
430 INPUT B$
440 IF B$=A$(B+1) THEN PRINT "RIGHT":GOTO450
441 PRINT"IT'S "A$(B+1)
450 GOSUB 670
460 RETURN
470 PRINT "WHAT IS THE FLORAL EMBLEM"
480 PRINT "OF "A$(B+1)"?"
490 INPUT B$ 
500 IF B$=A$(B+3)THEN PRINT "CORRECT":GOTO510
501 PRINT "IT'S "A$(B+3)
510 GOSUB 670
520 RETURN 
530 IF B>50 THEN RETURN
540 PRINT "IN WHAT YEAR DID"
550 PRINT A$(B+1)
560 PRINT "BECOME A PROVINCE?"
570 INPUT B$
580 IF B$=A$(B+4) THEN PRINT "RIGHT":GOTO590
581 PRINT "IT WAS "A$(B+4)
590 GOSUB 670
600 RETURN
610 IF B>50 THEN RETURN
620 PRINT "AMONG THE PROVINCES,"
630 PRINT "WHAT IS THE RANK IN AREA"
640 PRINT "OF "A$(B+1) "?"
650 INPUT B$
660 IF B$<>A$(B) THEN PRINT "IT IS NO. ";A$(B);" IN SIZE":GOTO670
661 PRINT "RIGHT"
670 FOR T=1 TO 1000
680 NEXT T
690 RETURN
700 END
