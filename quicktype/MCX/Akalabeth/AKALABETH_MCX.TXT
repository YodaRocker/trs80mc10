0 CLS:CLEAR350:GOTO18
1 DRAW"NU3":RETURN
2 DRAW"BU3U4BD7":RETURN
3 DRAW"BU7R5BL5BD7":RETURN
4 DRAW"BR5BU3U4BD7BL5":RETURN
5 DRAW"BR5NU3BL5":RETURN
6 DRAW"R5L5":RETURN
7 DRAW"BU3R5L5BD3":RETURN
8 DRAW"BU3BR3NU4BL3BD3":RETURN
9 DRAW"BU3BR3ND3BL3BD3":RETURN
11 FORZZ=1TOLEN(M$):CC=ASC(MID$(M$,ZZ,1)):IFCC>64THENIFC<91THENFORT=1TO9:IFMID$(A$,(CC-65)*9+T,1)="1"THENONTGOSUB1,2,3,4,5,6,7,8,9:NEXTELSENEXT
12 DRAW"BR8":NEXT:RETURN
18 A$="111110100110011100111001000100111100111001100111000100111011100110110100000000011100111000110010110110001000111110010111110000111111000111100100011110100111100101011011100001000011110111000010100101110111001010010111010111100101101100"
19 GOSUB60000:LE=0:GO=0:PRINT" WELCOME TO AKALABETH, WORLD OF DOOM!"
20 DIMDN(10,10),TE(20,20),XX(10),YY(10),PE(10,3),LD(10,5),CD(10,3),FT(10,5),LA(10,3)
30 FORX=0TO20:TE(X,0)=1:TE(0,X)=1:TE(X,20)=1:TE(20,X)=1:NEXT
35 PRINT"  (PLEASE WAIT)";
40 FORX=1TO19:FORY=1TO19:TE(X,Y)=INT(RND(0)^5*4.5)
41 IFTE(X,Y)=3 AND RND(0)>.5THENTE(X,Y)=0
42 NEXT:PRINT".";:NEXT
50 TE(INT(RND(0)*19+1),INT(RND(0)*19+1))=5:TX=INT(RND(0)*19+1):TY=INT(RND(0)*19+1):TE(TX,TY)=3
51 XX(0)=127:YY(0)=79
52 FORX=2TO20STEP2:XX(X/2)=INT(ATN(1/X)/ATN(1)*128+.5):YY(X/2)=INT(XX(X/2)*4/7)
53 PE(X/2,0)=127-XX(X/2):PE(X/2,1)=127+XX(X/2):PE(X/2,2)=79-YY(X/2):PE(X/2,3)=79+YY(X/2):NEXT
54 PE(0,0)=0:PE(0,1)=255:PE(0,2)=0:PE(0,3)=159
55 FORX=1TO10:CD(X,0)=127-XX(X)/3:CD(X,1)=127+XX(X)/3:CD(X,2)=79-YY(X)*.7:CD(X,3)=79+YY(X):NEXT:PRINT".";
56 FORX=0TO9:LD(X,0)=(PE(X,0)*2+PE(X+1,0))/3:LD(X,1)=(PE(X,0)+2*PE(X+1,0))/3:W=LD(X,0)-PE(X,0)
57 LD(X,2)=PE(X,2)+W*4/7:LD(X,3)=PE(X,2)+2*W*4/7:LD(X,4)=(PE(X,3)*2+PE(X+1,3))/3:LD(X,5)=(PE(X,3)+2*PE(X+1,3))/3
58 LD(X,2)=LD(X,4)-(LD(X,4)-LD(X,2))*.8:LD(X,3)=LD(X,5)-(LD(X,5)-LD(X,3))*.8:IFLD(X,3)=LD(X,4)THENLD(X,3)=LD(X,3)-1
59 NEXT
60 FORX=0TO9:FT(X,0)=127-XX(X)/3:FT(X,1)=127+XX(X)/3:FT(X,2)=127-XX(X+1)/3:FT(X,3)=127+XX(X+1)/3
61 FT(X,4)=79+(YY(X)*2+YY(X+1))/3:FT(X,5)=79+(YY(X)+2*YY(X+1))/3:NEXT
62 FORX=0TO9:LA(X,0)=(FT(X,0)*2+FT(X,1))/3:LA(X,1)=(FT(X,0)+2*FT(X,1))/3:LA(X,3)=FT(X,4):LA(X,2)=159-LA(X,3):NEXT
70 GOSUB100:GOTO1000
90 FORX=0TO9:FORY=0TO5:PRINTLD(X,Y);" ";:NEXT:PRINT:NEXT:INPUTQ$
100 GOSUB60900:FORY=-1TO1:FORX=-1TO1
105 LINE(126,75)-(130,75),1:LINE(128,73)-(128,77),1
110 ZZ=TE(TX+X,TY+Y):X1=53+(X+1)*50:Y1=(Y+1)*50:IFZZ=2THENLINE(X1+20,Y1+20)-(X1+30,Y1+20),1:LINE-(X1+30,Y1+30),1:LINE-(X1+20,Y1+30),1:LINE-(X1+20,Y1+20),1
130 IFZZ=3THENLINE(X1+10,Y1+10)-(X1+20,Y1+10),1:LINE-(X1+20,Y1+40),1:LINE-(X1+10,Y1+40),1:LINE-(X1+10,Y1+30),1:LINE-(X1+40,Y1+30),1
135 IFZZ=3THENLINE-(X1+40,Y1+40),1:LINE-(X1+30,Y1+40),1:LINE-(X1+30,Y1+10),1:LINE-(X1+40,Y1+10),1:LINE-(X1+40,Y1+20),1:LINE-(X1+10,Y1+20),1:LINE-(X1+10,Y1+10),1
140 IFZZ=4THENLINE(X1+20,Y1+20)-(X1+30,Y1+30),1:LINE-(X1+20,Y1+30),1:LINE-(X1+30,Y1+20),1
150 IFZZ=5THENLINE(X1,Y1)-(X1+50,Y1),1:LINE-(X1+50,Y1+50),1:LINE-(X1,Y1+50),1:LINE-(X1,Y1),1:LINE(X1+10,Y1+10)-(X1+10,Y1+40),1
155 IFZZ=5THENLINE-(X1+40,Y1+40),1:LINE-(X1+40,Y1+10),1:LINE-(X1+10,Y1+10),1:LINE-(X1+40,Y1+40),1:LINE(X1+10,Y1+40)-(X1+40,Y1+10),1
160 IFZZ=1THENLINE(X1+10,Y1+50)-(X1+10,Y1+40),1:LINE-(X1+20,Y1+30),1:LINE-(X1+40,Y1+30),1:LINE-(X1+40,Y1+50),1:LINE(X1,Y1+10)-(X1+10,Y1+10),1:LINE(X1+50,Y1+10)-(X1+40,Y1+10),1:LINE(X1,Y1+40)-(X1+10,Y1+40),1:LINE(X1+40,Y1+40)-(X1+50,Y1+40),1
170 IFZZ=1THENLINE(X1+10,Y1)-(X1+10,Y1+20),1:LINE-(X1+20,Y1+20),1:LINE-(X1+20,Y1+30),1:LINE-(X1+30,Y1+30),1:LINE-(X1+30,Y1+10),1:LINE-(X1+40,Y1+10),1:LINE-(X1+40,Y1),1
190 NEXT:NEXT:RETURN
200 GOSUB60900:DI=0
202 CE=DN(PX+DX*DI,PY+DY*DI):LE=DN(PX+DX*DI+DY,PY+DY*DI-DX):RI=DN(PX+DX*DI-DY,PY+DY*DI+DX)
204 L1=PE(DI,0):R1=PE(DI,1):T1=PE(DI,2):B1=PE(DI,3):L2=PE(DI+1,0):R2=PE(DI+1,1):T2=PE(DI+1,2):B2=PE(DI+1,3)
205 CE=INT(CE):LE=INT(LE):RI=INT(RI)
206 MC=INT(CE/10):CE=CE-MC*10:LE=INT((LE/10-INT(LE/10))*10):RI=INT((RI/10-INT(RI/10))*10)
208 IFDI=0THEN216
210 IFCE=1ORCE=3ORCE=4THENLINE(L1,T1)-(R1,T1),1:LINE-(R1,B1),1:LINE-(L1,B1),1:LINE-(L1,T1),1
212 IFCE=1ORCE=3THENEN=1:GOTO260
214 IFCE=4THENLINE(CD(DI,0),CD(DI,3))-(CD(DI,0),CD(DI,2)),1:LINE-(CD(DI,1),CD(DI,2)),1:LINE-(CD(DI,1),CD(DI,3)),1:EN=1:GOTO260
216 IFLE=1ORLE=3ORLE=4THENLINE(L1,T1)-(L2,T2),1:LINE(L1,B1)-(L2,B2),1
218 IFRI=1ORRI=3ORRI=4THENLINE(R1,T1)-(R2,T2),1:LINE(R1,B1)-(R2,B2),1
220 IFLE=4ANDDI>0THENLINE(LD(DI,0),LD(DI,4))-(LD(DI,0),LD(DI,2)),1:LINE-(LD(DI,1),LD(DI,3)),1:LINE-(LD(DI,1),LD(DI,5)),1
222 IFLE=4ANDDI=0THENLINE(0,LD(DI,2)-3)-(LD(DI,1),LD(DI,3)),1:LINE-(LD(DI,1),LD(DI,5)),1
224 IFRI=4ANDDI>0THENLINE(255-LD(DI,0),LD(DI,4))-(255-LD(DI,0),LD(DI,2)),1:LINE-(255-LD(DI,1),LD(DI,3)),1:LINE-(255-LD(DI,1),LD(DI,5)),1
226 IFRI=4ANDDI=0THENLINE(255,LD(DI,2)-3)-(255-LD(DI,1),LD(DI,3)),1:LINE-(255-LD(DI,1),LD(DI,5)),1
228 IFLE=3ORLE=1ORLE=4THEN234
230 IFDI<>0THENLINE(L1,T1)-(L1,B1),1
232 LINE(L1,T2)-(L2,T2),1:LINE-(L2,B2),1:LINE-(L1,B2),1
234 IFRI=3ORRI=1ORRI=4THEN240
236 IFDI<>0THENLINE(R1,T1)-(R1,B1),1
238 LINE(R1,T2)-(R2,T2),1:LINE-(R2,B2),1:LINE-(R1,B2),1
240 IFCE=7ORCE=9THENLINE(FT(DI,0),FT(DI,4))-(FT(DI,2),FT(DI,5)),1:LINE-(FT(DI,3),FT(DI,5)),1:LINE-(FT(DI,1),FT(DI,4)),1:LINE-(FT(DI,0),FT(DI,4)),1
242 IFCE=8THENLINE(FT(DI,0),158-FT(DI,4))-(FT(DI,2),158-FT(DI,5)),1:LINE-(FT(DI,3),158-FT(DI,5)),1:LINE-(FT(DI,1),158-FT(DI,4)),1:LINE-(FT(DI,0),158-FT(DI,4)),1
244 IFCE=7ORCE=8THENBA=LA(DI,3):TP=LA(DI,2):LX=LA(DI,0):RX=LA(DI,1):LINE(LX,BA)-(LX,TP),1:LINE(RX,TP)-(RX,BA),1
246 IFCE=7ORCE=8THENY1=(BA*4+TP)/5:Y2=(BA*3+TP*2)/5:Y3=(BA*2+TP*3)/5:Y4=(BA+TP*4)/5:LINE(LX,Y1)-(RX,Y1),1:LINE(LX,Y2)-(RX,Y2),1:LINE(LX,Y3)-(RX,Y3),1:LINE(LX,Y4)-(RX,Y4),1
248 IFDI>0ANDCE=5THENLINE(127-10/DI,PE(DI,3))-(127-10/DI,PE(DI,3)-10/DI),1:LINE-(127+10/DI,PE(DI,3)-10/DI),1:LINE-(127+10/DI,PE(DI,3)),1:LINE-(127-10/DI,PE(DI,3)),1
249 IFCE=5ANDDI>0THENPRESET(0,0):PRINT"CHEST!"
250 IFDI>0ANDCE=5THENLINE(127-10/DI,PE(DI,3)-10/DI)-(127-5/DI,PE(DI,3)-15/DI),1:LINE-(127+15/DI,PE(DI,3)-15/DI),1:LINE-(127+15/DI,PE(DI,3)-5/DI),1:LINE-(127+10/DI,PE(DI,3)),1
252 IFDI>0ANDCE=5THENLINE(127+10/DI,PE(DI,3)-10/DI)-(127+15/DI,PE(DI,3)-15/DI),1
260 IFMC<1THEN490
265 B=79+YY(DI):C=127
266 IFMC=8THENPRESET(0,0):PRINT"CHEST!";:GOSUB60868:PRINT:GOTO269
267 PRESET(0,8):PRINTM$(MC)
269 IFDI=0THEN490
270 ON MC GOTO300,310,320,330,340,350,360,370,380,390
280 GOTO490
300 LINE(C-23/DI,B)-(C-15/DI,B),1:LINE-(C-15/DI,B-15/DI),1:LINE-(C-8/DI,B-30/DI),1:LINE-(C+8/DI,B-30/DI),1:LINE-(C+15/DI,B-15/DI),1:LINE-(C+15/DI,B),1:LINE-(C+23/DI,B),1
301 LINE(C,B-26/DI)-(C,B-65/DI),1:LINE(C-2/DI+.5,B-38/DI)-(C+2/DI+.5,B-38/DI),1:LINE(C-3/DI+.5,B-45/DI)-(C+3/DI+.5,B-45/DI),1:LINE(C-5/DI+.5,B-53/DI)-(C+5/DI+.5,B-53/DI),1
302 LINE(C-23/DI,B-56/DI)-(C-30/DI,B-53/DI),1:LINE-(C-23/DI,B-45/DI),1:LINE-(C-23/DI,B-53/DI),1:LINE-(C-8/DI,B-38/DI),1
303 LINE(C-15/DI,B-45/DI)-(C-8/DI,B-60/DI),1:LINE-(C+8/DI,B-60/DI),1:LINE-(C+15/DI,B-45/DI),1:LINE(C+15/DI,B-42/DI)-(C+15/DI,B-57/DI),1:LINE(C+12/DI,B-45/DI)-(C+20/DI,B-45/DI),1
304 LINE(C,B-75/DI)-(C-5/DI+.5,B-80/DI),1:LINE-(C-8/DI,B-75/DI),1:LINE-(C-5/DI+.5,B-65/DI),1:LINE-(C+5/DI+.5,B-65/DI),1:LINE-(C+5/DI+.5,B-68/DI),1:LINE-(C-5/DI+.5,B-68/DI),1:LINE-(C-5/DI+.5,B-65/DI),1
305 LINE-(C+5/DI+.5,B-65/DI),1:LINE-(C+8/DI,B-75/DI),1:LINE-(C+5/DI+.5,B-80/DI),1:LINE-(C-5/DI+.5,B-80/DI),1:PSET(C-5/DI+.5,B-72/DI),1:PSET(C+5/DI+.5,B-72/DI),1
309 GOTO490
310 LINE(C,B-56/DI)-(C,B-8/DI),1:LINE-(C+10/DI,B),1:LINE-(C+30/DI,B),1:LINE-(C+30/DI,B-45/DI),1:LINE-(C+10/DI,B-64/DI),1:LINE-(C,B-56/DI),1
311 LINE-(C-10/DI,B-64/DI),1:LINE-(C-30/DI,B-45/DI),1:LINE-(C-30/DI,B),1:LINE-(C-10/DI,B),1:LINE-(C,B-8/DI),1
312 LINE(C-10/DI,B-64/DI)-(C-10/DI,B-75/DI),1:LINE-(C,B-83/DI),1:LINE-(C+10/DI,B-75/DI),1:LINE-(C,B-79/DI),1:LINE-(C-10/DI,B-75/DI),1:LINE-(C,B-60/DI),1:LINE-(C+10/DI,B-75/DI),1:LINE-(C+10/DI,B-64/DI),1
319 GOTO490
320 LINE(C+5/DI,B-30/DI)-(C,B-25/DI),1:LINE-(C-5/DI,B-30/DI),1:LINE-(C-15/DI,B-5/DI),1:LINE-(C-10/DI,B),1:LINE-(C+10/DI,B),1:LINE-(C+15/DI,B-5/DI),1
321 LINE-(C+20/DI,B-5/DI),1:LINE-(C+10/DI,B),1:LINE-(C+15/DI,B-5/DI),1:LINE-(C+5/DI,B-30/DI),1:LINE-(C+10/DI,B-40/DI),1:LINE-(C+3/DI+.5,B-35/DI),1:LINE-(C-3/DI+.5,B-35/DI),1:LINE-(C-10/DI,B-40/DI),1:LINE-(C-5/DI,B-30/DI),1
322 LINE(C-5/DI,B-33/DI)-(C-3/DI+.5,B-30/DI),1:LINE(C+5/DI,B-33/DI)-(C+3/DI+.5,B-30/DI),1:LINE(C-5/DI,B-20/DI)-(C-5/DI,B-15/DI),1
323 LINE(C+5/DI,B-20/DI)-(C+5/DI,B-15/DI),1:LINE(C-7+DI,B-20/DI)-(C-7/DI,B-15/DI),1:LINE(C+7/DI,B-20/DI)-(C+7/DI,B-15/DI),1
329 GOTO490
330 LINE(C,B)-(C-15/DI,B),1:LINE-(C-8/DI,B-8/DI),1:LINE-(C-8/DI,B-15/DI),1:LINE-(C-15/DI,B-23/DI),1:LINE-(C-15/DI,B-15/DI),1:LINE-(C-23/DI,B-23/DI),1
331 LINE-(C-23/DI,B-45/DI),1:LINE-(C-15/DI,B-53/DI),1:LINE-(C-8/DI,B-53/DI),1:LINE-(C-15/DI,B-68/DI),1:LINE-(C-8/DI,B-75/DI),1:LINE-(C,B-75/DI),1
332 LINE(C,B)-(C+15/DI,B),1:LINE-(C+8/DI,B-8/DI),1:LINE-(C+8/DI,B-15/DI),1:LINE-(C+15/DI,B-23/DI),1:LINE-(C+15/DI,B-15/DI),1:LINE-(C+23/DI,B-23/DI),1
333 LINE-(C+23/DI,B-45/DI),1:LINE-(C+15/DI,B-53/DI),1:LINE-(C+8/DI,B-53/DI),1:LINE-(C+15/DI,B-68/DI),1:LINE-(C+8/DI,B-75/DI),1:LINE-(C,B-75/DI),1
334 LINE(C-15/DI,B-68/DI)-(C+15/DI,B-68/DI),1:LINE(C-8/DI,B-53/DI)-(C+8/DI,B-53/DI),1:LINE(C-23/DI,B-15/DI)-(C+8/DI,B-45/DI),1
335 LINE(C-8/DI,B-68/DI)-(C,B-60/DI),1:LINE-(C+8/DI,B-68/DI),1:LINE-(C+8/DI,B-60/DI),1:LINE-(C-8/DI,B-60/DI),1:LINE-(C-8/DI,B-68/DI),1
336 LINE(C,B-38/DI)-(C-8/DI,B-38/DI),1:LINE-(C+8/DI,B-53/DI),1:LINE-(C+8/DI,B-45/DI),1:LINE-(C+15/DI,B-45/DI),1:LINE-(C,B-30/DI),1:LINE-(C,B-38/DI),1
339 GOTO490
340 LINE(C-10/DI,B-15/DI)-(C-10/DI,B-30/DI),1:LINE-(C-15/DI,B-20/DI),1:LINE-(C-15/DI,B-15/DI),1:LINE-(C-15/DI,B),1:LINE-(C+15/DI,B),1:LINE-(C+15/DI,B-15/DI),1:LINE  -(C-15/DI,B-15/DI),1
341 LINE(C-15/DI,B-10/DI)-(C+15/DI,B-10/DI),1:LINE(C-15/DI,B-5/DI)-(C+15/DI,B-5/DI),1
342 LINE(C,B-15/DI)-(C-5/DI,B-20/DI),1:LINE-(C-5/DI,B-35/DI),1:LINE-(C+5/DI,B-35/DI),1:LINE-(C+5/DI,B-20/DI),1:LINE-(C+10/DI,B-15/DI),1
343 LINE(C-5/DI,B-20/DI)-(C+5/DI,B-20/DI),1:LINE(C-5/DI,B-25/DI)-(C+5/DI,B-25/DI),1:LINE(C-5/DI,B-30/DI)-(C+5/DI,B-30/DI),1
344 LINE(C-10/DI,B-35/DI)-(C-10/DI,B-40/DI),1:LINE-(C-5/DI,B-45/DI),1:LINE-(C+5/DI,B-45/DI),1:LINE-(C+10/DI,B-40/DI),1:LINE-(C+10/DI,B-35/DI),1
345 LINE(C-10/DI,B-40/DI)-(C,B-45/DI),1:LINE-(C+10/DI,B-40/DI),1
346 LINE(C-5/DI,B-40/DI)-(C+5/DI,B-40/DI),1:LINE-(C+15/DI,B-30/DI),1:LINE-(C,B-40/DI),1:LINE-(C-15/DI,B-30/DI),1:LINE-(C-5/DI+.5,B-40/DI),1
349 GOTO490
350 LINE(C-20/DI,79-YY(DI))-(C-20/DI,B-88/DI),1:LINE-(C-10/DI,B-83/DI),1:LINE-(C+10/DI,B-83/DI),1:LINE-(C+20/DI,B-88/DI),1:LINE-(C+20/DI,79-YY(DI)),1:LINE-(C-20/DI,79-YY(DI)),1
351 LINE(C-20/DI,B-88/DI)-(C-30/DI,B-83/DI),1:LINE-(C-30/DI,B-78/DI),1:LINE(C+20/DI,B-88/DI)-(C+30/DI,B-83/DI),1:LINE-(C+40/DI,B-83/DI),1
352 LINE(C-15/DI,B-86/DI)-(C-20/DI,B-83/DI),1:LINE-(C-20/DI,B-78/DI),1:LINE-(C-30/DI,B-73/DI),1:LINE-(C-30/DI,B-68/DI),1:LINE-(C-20/DI,B-63/DI),1
353 LINE(C-10/DI,B-83/DI)-(C-10/DI,B-58/DI),1:LINE-(C,B-50/DI),1:LINE(C+10/DI,B-83/DI)-(C+10/DI,B-78/DI),1:LINE-(C+20/DI,B-73/DI),1:LINE-(C+20/DI,B-40/DI),1
354 LINE(C+15/DI,B-85/DI)-(C+20/DI,B-78/DI),1:LINE-(C+30/DI,B-76/DI),1:LINE-(C+30/DI,B-60/DI),1
355 LINE(C,B-83/DI)-(C,B-73/DI),1:LINE-(C+10/DI,B-68/DI),1:LINE-(C+10/DI,B-63/DI),1:LINE-(C,B-58/DI),1
359 GOTO490
360 LINE(C+5/DI+.5,B-10/DI)-(C-5/DI+.5,B-10/DI),1:LINE-(C,B-15/DI),1:LINE-(C+10/DI,B-20/DI),1:LINE-(C+5/DI+.5,B-15/DI),1:LINE-(C+5/DI+.5,B-10/DI),1
361 LINE-(C+7/DI+.5,B-6/DI),1:LINE-(C+5/DI+.5,B-3/DI),1:LINE-(C-5/DI+.5,B-3/DI),1:LINE-(C-7/DI+.5,B-6/DI),1:LINE-(C-5/DI+.5,B-10/DI),1
362 LINE(C+2/DI+.5,B-3/DI)-(C+5/DI+.5,B),1:LINE-(C+8/DI,B),1:LINE(C-2/DI+.5,B-3/DI)-(C-5/DI+.5,B),1:LINE-(C-8/DI,B),1:PSET(C+3/DI+.5,B-8/DI):PSET(C-3/DI+.5,B-8/DI):LINE(C+3/DI+.5,B-5/DI)-(C-3/DI+.5,B-5/DI),1
363 GOTO490
370 LINE(127-10/DI,PE(DI,3))-(127-10/DI,PE(DI,3)-10/DI):LINE-(127+10/DI,PE(DI,3)-10/DI):LINE-(127+10/DI,PE(DI,3)):LINE-(127-10/DI,PE(DI,3))
371 LINE(127-10/DI,PE(DI,3)-10/DI)-(127-5/DI,PE(DI,3)-15/DI):LINE-(127+15/DI,PE(DI,3)-15/DI):LINE-(127+15/DI,PE(DI,3)-5/DI):LINE-(127+10/DI,PE(DI,3))
372 LINE(127+10/DI,PE(DI,3)-10/DI)-(127+15/DI,PE(DI,3)-15/DI)
373 GOTO490
380 LINE(C-14/DI,B-46/DI)-(C-12/DI,B-37/DI),1:LINE-(C-20/DI,B-32/DI),1:LINE-(C-30/DI,B-32/DI),1:LINE-(C-22/DI,B-24/DI),1:LINE-(C-40/DI,B-17/DI),1:LINE-(C-40/DI,B-7/DI),1:LINE-(C-38/DI,B-5/DI),1:LINE-(C-40/DI,B-3/DI),1:LINE-(C-40/DI,B),1
381 LINE-(C-36/DI,B),1:LINE-(C-34/DI,B-2/DI),1:LINE-(C-32/DI,B),1:LINE-(C-28/DI,B),1:LINE-(C-28/DI,B-3/DI),1:LINE-(C-30/DI,B-5/DI),1:LINE-(C-28/DI,B-7/DI),1:LINE-(C-28/DI,B-15/DI),1:LINE-(C,B-27/DI),1
382 LINE(C+14/DI,B-46/DI)-(C+12/DI,B-37/DI),1:LINE-(C+20/DI,B-32/DI),1:LINE-(C+30/DI,B-32/DI),1:LINE-(C+22/DI,B-24/DI),1:LINE-(C+40/DI,B-17/DI),1:LINE-(C+40/DI,B-7/DI),1:LINE-(C+38/DI,B-5/DI),1:LINE-(C+40/DI,B-3/DI),1:LINE-(C+40/DI,B),1
383 LINE-(C+36/DI,B),1:LINE-(C+34/DI,B-2/DI),1:LINE-(C+32/DI,B),1:LINE-(C+28/DI,B),1:LINE-(C+28/DI,B-3/DI),1:LINE-(C+30/DI,B-5/DI),1:LINE-(C+28/DI,B-7/DI),1:LINE-(C+28/DI,B-15/DI),1:LINE-(C,B-27/DI),1
384 LINE(C+6/DI,B-48/DI)-(C+38/DI,B-41/DI),1:LINE-(C+40/DI,B-42/DI),1:LINE-(C+18/DI,B-56/DI),1:LINE-(C+12/DI,B-56/DI),1:LINE-(C+10/DI,B-57/DI),1:LINE-(C+8/DI,B-56/DI),1:LINE-(C-8/DI,B-56/DI),1:LINE-(C-10/DI,B-58/DI),1:LINE-(C+14/DI,B-58/DI),1
385 LINE-(C+16/DI,B-59/DI),1
386 LINE-(C+8/DI,B-63/DI),1:LINE-(C+6/DI,B-63/DI),1:LINE-(C+2/DI+.5,B-70/DI),1:LINE-(C+2/DI+.5,B-63/DI),1:LINE-(C-2/DI+.5,B-63/DI),1:LINE-(C-2/DI+.5,B-70/DI),1:LINE-(C-6/DI,B-63/DI),1:LINE-(C-8/DI,B-63/DI),1:LINE-(C-16/DI,B-59/DI),1:LINE-(C-14/DI,B-58/DI),1
387 LINE-(C+10/DI,B-57/DI),1:LINE-(C-12/DI,B-56/DI),1:LINE-(C-18/DI,B-56/DI),1:LINE-(C-36/DI,B-47/DI),1:LINE-(C-36/DI,B-39/DI),1:LINE-(C-28/DI,B-41/DI),1:LINE-(C-28/DI,B-46/DI),1:LINE-(C-20/DI,B-50/DI),1:LINE-(C-18/DI,B-50/DI),1:LINE-(C-14/DI,B-46/DI),1
388 GOTO3087
390 LINE(C+6/DI,B-60/DI)-(C+30/DI,B-90/DI),1:LINE-(C+60/DI,B-30/DI),1:LINE-(C+60/DI,B-10/DI),1:LINE-(C+30/DI,B-40/DI),1:LINE-(C+15/DI,B-40/DI),1
391 LINE(C-6/DI,B-60/DI)-(C-30/DI,B-90/DI),1:LINE-(C-60/DI,B-30/DI),1:LINE-(C-60/DI,B-10/DI),1:LINE-(C-30/DI,B-40/DI),1:LINE-(C-15/DI,B-40/DI),1
392 LINE(C,B-25/DI)-(C+6/DI,B-25/DI),1:LINE-(C+10/DI,B-20/DI),1:LINE-(C+12/DI,B-10/DI),1:LINE-(C+10/DI,B-6/DI),1:LINE-(C+10/DI,B),1:LINE-(C+14/DI,B),1:LINE-(C+15/DI,B-5/DI),1:LINE-(C+16/DI,B),1:LINE-(C+20/DI,B),1
393 LINE-(C+20/DI,B-6/DI),1:LINE-(C+18/DI,B-10/DI),1:LINE-(C+18/DI,B-20/DI),1:LINE-(C+15/DI,B-30/DI),1:LINE-(C+15/DI,B-45/DI),1:LINE-(C+40/DI,B-60/DI),1:LINE-(C+40/DI,B-70/DI),1
394 LINE-(C+10/DI,B-55/DI),1:LINE-(C+6/DI,B-60/DI),1:LINE-(C+10/DI,B-74/DI),1:LINE-(C+6/DI,B-80/DI),1:LINE-(C+4/DI+.5,B-80/DI),1:LINE-(C+3/DI+.5,B-82/DI),1:LINE-(C+2/DI+.5,B-80/DI),1:LINE-(C,B-80/DI),1
395 LINE(C,B-25/DI)-(C-6/DI,B-25/DI),1:LINE-(C-10/DI,B-20/DI),1:LINE-(C-12/DI,B-10/DI),1:LINE-(C-10/DI,B-6/DI),1:LINE-(C-10/DI,B),1:LINE-(C-14/DI,B),1:LINE-(C-15/DI,B-5/DI),1:LINE-(C-16/DI,B),1:LINE-(C-20/DI,B),1
396 LINE-(C-20/DI,B-6/DI),1:LINE-(C-18/DI,B-10/DI),1:LINE-(C-18/DI,B-20/DI),1:LINE-(C-15/DI,B-30/DI),1:LINE-(C-15/DI,B-45/DI),1:LINE-(C-40/DI,B-60/DI),1:LINE-(C-40/DI,B-70/DI),1
397 LINE-(C-10/DI,B-55/DI),1:LINE-(C-6/DI,B-60/DI),1:LINE-(C-10/DI,B-74/DI),1:LINE-(C-6/DI,B-80/DI),1:LINE-(C-4/DI+.5,B-80/DI),1:LINE-(C-3/DI+.5,B-82/DI),1:LINE-(C-2/DI+.5,B-80/DI),1:LINE-(C,B-80/DI),1
398 LINE(C-6/DI,B-25/DI)-(C,B-6/DI):LINE-(C+10/DI,B):LINE-(C+4/DI+.5,B-8/DI):LINE-(C+6/DI,B-25/DI):LINE(C-40/DI,B-64/DI)-(C-40/DI,B-90/DI):LINE-(C-52/DI,B-80/DI):LINE-(C-52/DI,B-40/DI)
399 LINE(C+40/DI,B-86/DI)-(C+38/DI,B-92/DI),1:LINE-(C+42/DI,B-92/DI),1:LINE-(C+40/DI,B-86/DI),1:LINE-(C+40/DI,B-50/DI),1
400 LINE(C+4/DI+.5,B-70/DI)-(C+6/DI,B-74/DI),1:LINE(C-4/DI+.5,B-70/DI)-(C-6/DI,B-74/DI),1:LINE(C,B-64/DI)-(C,B-60/DI),1:GOTO490
490 IFEN=1THENEN=0:RETURN
491 DI=DI+1:GOTO202
500 FORX=1TO9:FORY=1TO9:DN(X,Y)=0:NEXT:NEXT
510 FORX=0TO10:DN(X,0)=1:DN(X,10)=1:DN(0,X)=1:DN(10,X)=1:NEXT
520 FORX=2TO8STEP2:FORY=1TO9:DN(X,Y)=1:DN(Y,X)=1:NEXT:NEXT
530 FORX=2TO8STEP2:FORY=1TO9STEP2
540 IFRND(0)>.95THENDN(X,Y)=2
541 IFRND(0)>.95THENDN(Y,X)=2
542 IFRND(0)>.6THENDN(Y,X)=3
543 IFRND(0)>.6THENDN(X,Y)=3
544 IFRND(0)>.6THENDN(X,Y)=4
545 IFRND(0)>.6THENDN(Y,X)=4
546 IFRND(0)>.97THENDN(Y,X)=9
547 IFRND(0)>.97THENDN(X,Y)=9
548 IFRND(0)>.94THENDN(X,Y)=5
549 IFRND(0)>.94THENDN(Y,X)=5
568 NEXT:NEXT
569 DN(2,1)=0:IFIN/2=INT(IN/2)THENDN(7,3)=7:DN(3,7)=8
570 IFIN/2<>INT(IN/2)THENDN(7,3)=8:DN(3,7)=7
580 IFIN=1THENDN(1,1)=8:DN(7,3)=0
585 GOSUB2000
590 RETURN
900 DRAW"BM160,174":M$="FOOD "+MID$(STR$(INT(PW(0)*10+0.5)/10),2):GOSUB11:DRAW"BM160,182":M$="HP"+STR$(C(0)):GOSUB11
901 DRAW"BM160,190":M$="GOLD"+STR$(C(5)):GOSUB11:DRAW"BM8,160":RETURN
1000 GOSUB60910:DRAW"BM0,168":M$="COMMAND":GOSUB11
1001 X$=INKEY$:IFX$=""THEN1001
1003 X=ASC(X$)
1030 IFX=73THENONSGN(IN)+1GOTO1100,1150
1040 IFX=76THENONSGN(IN)+1GOTO1200,1250
1050 IFX=74THENONSGN(IN)+1GOTO1300,1350
1060 IFX=75THENONSGN(IN)+1GOTO1400,1450
1070 IFX=13THENONSGN(IN)+1GOTO1500,1550
1080 IFX=65THENONSGN(IN)+1GOTO1600,1650
1081 IFX=32THENPRINT"PASS":GOTO1090
1085 IFX=83ORX=115THEN1700
1086 IFX=80ORX=112THENIFPA=1THENPA=0:PRINT"PAUSE OFF":GOTO1000
1087 IFX=80ORX=112THENIFPA=0THENPA=1:PRINT"PAUSE ON":GOTO1000
1089 PRINT"HUH?":GOTO1000
1090 PW(0)=PW(0)-1+SGN(IN)*.9:IFPW(0)<0THENC(0)=0:PRINT"YOU HAVE STARVED!":GOTO1093
1091 GOSUB900
1092 PW(0)=INT(PW(0)*10)/10
1093 IFC(0)<=0THEN6000
1095 IFIN>0THENGOSUB60910:GOSUB4000:IFC(0)<=0THEN1093
1096 IFIN=0THENGOSUB100
1097 IFIN>0THENGOSUB200
1098 GOSUB900:GOTO1000
1100 PRINT"NORTH":IFTE(TX,TY-1)=1THENPRINT"YOU CAN'T PASS THE MOUNTAINS":GOTO1090
1110 TY=TY-1:GOTO1090
1150 IFDN(PX+DX,PY+DY)<>1 AND DN(PX+DX,PY+DY)<10THENPX=PX+DX:PY=PY+DY
1155 PRINT"FORWARD"
1160 IFDN(PX,PY)=2THENPRINT"AAARRRGGGHHH!!! A TRAP!":C(0)=C(0)-INT(RND(0)*IN+3):MR=1:IN=IN+1:PRINT"FALLING TO LEVEL ";IN:GOSUB500:GOSUB60910:GOTO1090
1165 Z=0
1170 IFDN(PX,PY)=5THENDN(PX,PY)=0:PRINT"GOLD!!!!!":Z=INT(RND(0)*5*IN+IN):PRINTZ;"-PIECES OF EIGHT":C(5)=C(5)+Z
1175 IFZ>0THENZ=INT(RND(0)*6):PRINT"AND A ";W$(Z):PW(Z)=PW(Z)+1:GOTO1090
1190 GOTO1090
1200 PRINT"EAST":IFTE(TX+1,TY)=1THENPRINT"YOU CAN'T PASS THE MOUNTAINS":GOTO1090
1210 TX=TX+1:GOTO1090
1250 PRINT"TURN RIGHT"
1255 IFDX<>0THENDY=DX:DX=0:GOTO1090
1260 DX=-DY:DY=0:GOTO1090
1300 PRINT"WEST":IFTE(TX-1,TY)=1THENPRINT"YOU CAN'T PASS THE MOUNTAINS":GOTO1090
1310 TX=TX-1:GOTO1090
1350 PRINT"TURN LEFT"
1355 IFDX<>0THENDY=-DX:DX=0:GOTO1090
1360 DX=DY:DY=0:GOTO1090
1400 PRINT"SOUTH":IFTE(TX,TY+1)=1THENPRINT"YOU CAN'T PASS THE MOUNTAINS":GOTO1090
1410 TY=TY+1:GOTO1090
1450 PRINT"TURN AROUND":DX=-DX:DY=-DY:GOTO1090
1500 IFTE(TX,TY)=3THENCLOSE #1:GOSUB60080:GOSUB60200:GOSUB60900:GOTO1090
1510 IFTE(TX,TY)=4 AND IN=0THENPRINT"GO DUNGEON":PRINT"PLEASE WAIT ":IN=1:GOSUB500:DX=1:DY=0:PX=1:PY=1:GOTO1090
1515 IFTE(TX,TY)=5THEN7000
1520 PRINT"HUH?":GOTO1000
1550 IFDN(PX,PY)<>7 AND DN(PX,PY)<>9THEN1580
1555 PRINT"GO DOWN TO LEVEL ";IN+1
1560 IN=IN+1:GOSUB500:MR=1:GOTO1090
1580 IFDN(PX,PY)<>8THENPRINT"HUH?":GOTO1090
1581 IFIN=1THENPRINT"LEAVE DUNGEON":IN=0:GOTO1586
1584 PRINT"GO UP TO LEVEL ";IN-1
1585 IN=IN-1:GOSUB500:MR=1
1586 IFIN=0THENPRINT"THOU HAST GAINED":PRINTLK;" HIT POINTS":C(0)=C(0)+LK:LK=0
1587 GOTO1090
1600 GOTO1090
1650 MN=0:DA=0:PRINT"ATTACK":PRINT"WHICH WEAPON ";:INPUT Q$
1651 IFQ$="R"THENDA=10:PRINT"RAPIER":IFPW(1)<1THENPRINT"NOT OWNED":GOTO1650
1652 IFQ$="A"THENDA=5:PRINT"AXE":IFPW(2)<1THENPRINT"NOT OWNED":GOTO1650
1653 IFQ$="S"THENDA=1:PRINT"SHIELD":IFPW(3)<1THENPRINT"NOT OWNED":GOTO1650
1654 IFQ$="B"THENDA=4:PRINT"BOW":IFPW(4)<1THENPRINT"NOT OWNED":GOTO1650
1655 IFQ$="M"THENPRINT"MAGIC AMULET":GOTO1680
1656 IFQ$="B" AND PT$="M"THENPRINT"MAGES CAN'T USE BOWS!":GOTO1650
1657 IFQ$="R" AND PT$="M"THENPRINT"MAGES CAN'T USE RAPIERS!":GOTO1650
1659 IFDA=0THENPRINT"HANDS"
1660 IFDA=5 OR DA=4THEN1670
1661 MN=DN(PX+DX,PY+DY)/10:MN=INT(MN)
1662 IFMN<1 OR C(2)-RND(0)*25<MN+INTHENPRINT"YOU MISSED":GOTO1668
1663 PRINT"HIT!!! ":DA=(RND(0)*DA+C(1)/5):MZ(MN,1)=MZ(MN,1)-DA
1664 PRINTM$(MN);"'S HIT POINTS=";MZ(MN,1)
1665 IFMZ(MN,1)<1THENPRINT"THOU HAST KILLED A ";M$(MN):PRINT"THOU SHALT RECEIVE":DA=INT(MN+IN):PRINTDA;" PIECES OF EIGHT"
1666 IFMZ(MN,1)<1THENC(5)=INT(C(5)+DA):DN(ML(MN,0),ML(MN,1))=DN(ML(MN,0),ML(MN,1))-10*MN:MZ(MN,0)=0
1667 LK=LK+INT(MN*IN/2):IFMN=TATHENTA=-TA
1668 IFPA=1THENPRINT"-CR- TO CONT. ";:INPUT Q$
1669 GOSUB60910:GOTO1090
1670 IFDA=5THENPRINT"TO THROW OR SWING:";:INPUT$ Q$:IFQ$<>"T"THENPRINT"SWING":GOTO1661
1671 IFDA=5THENPRINT"THROW":PW(2)=PW(2)-1
1672 FOR Y=1 TO 5:IFPX+DX*Y<1 OR PX+DX*Y>9 OR PY+DY*Y>9 OR PY+DY*Y<0THEN1662
1673 MN=DN(PX+DX*Y,PY+DY*Y):MN=INT(MN/10):IFMN>0THEN1662
1674 NEXT:GOTO1662
1680 IFPW(5)<1THENPRINT"NONE OWNED":GOTO1650
1681 IFPT$="F"THENQ=INT(RND(0)*4+1):GOTO1685
1682 PRINT"1-LADDER-UP","2-LADDER-DN":PRINT"3-KILL","4-BAD??":PRINT"CHOICE ";:INPUT Q$:Q=VAL(Q$):PRINT Q:IFQ<1 OR Q>4THEN1682
1683 IFRND(0)>.75THENPRINT"LAST CHARGE ON THIS AMULET!":PW(5)=PW(5)-1
1685 ON Q GOTO1686,1690,1691,1692
1686 PRINT"LADDER UP":DN(PX,PY)=8:GOTO1090
1690 PRINT"LADDER DOWN":DN(PX,PY)=7:GOTO1090
1691 PRINT"MAGIC ATTACK":DA=10+IN:GOTO1672
1692 ON INT(RND(0)*3+1) GOTO1693,1695,1697
1693 PRINT"YOU HAVE BEEN TURNED":PRINT"INTO A TOAD!"
1694 FOR Z2=1 TO 4:C(Z2)=3:NEXT Z2:GOTO1090
1695 PRINT"YOU HAVE BEEN TURNED":PRINT"INTO A LIZARD MAN":FOR Y=0 TO 4:C(Y)=INT(C(Y)*2.5):NEXT:GOTO1090
1697 PRINT"BACKFIRE":C(0)=C(0)/2:GOTO1090
1700 CLOSE #1:GOSUB60080:PRINT"PRESS ENTER TO CONTINUE";:INPUT Q$:GOSUB60900:GOTO1090
2000 NM=0:FOR X=1 TO 10
2005 MZ(X,0)=0:MZ(X,1)=X+3+IN
2010 IFX-2>IN OR RND(0)>.4THEN2090
2020 ML(X,0)=INT(RND(0)*9+1):ML(X,1)=INT(RND(0)*9+1)
2030 IFDN(ML(X,0),ML(X,1))<>0THEN2020
2040 IFML(X,0)=PX AND ML(X,1)=PYTHEN2020
2050 DN(ML(X,0),ML(X,1))=X*10
2051 MZ(X,0)=1
2052 NM=NM+1
2055 MZ(X,1)=X*2+IN*2*LP
2090 NEXT:RETURN
3087 LINE(C-28/DI,B-41/DI)-(C+30/DI,B-55/DI),1:LINE(C+28/DI,B-58/DI)-(C+22/DI,B-56/DI),1:LINE-(C+22/DI,B-53/DI),1:LINE-(C+28/DI,B-52/DI),1:LINE-(C+34/DI,B-54/DI),1:LINE-(C+20/DI,B-50/DI),1:LINE-(C+26/DI,B-47/DI),1
3088 LINE(C+10/DI,B-58/DI)-(C+10/DI,B-61/DI),1:LINE-(C+4/DI,B-58/DI),1:LINE(C-10/DI,B-58/DI)-(C-10/DI,B-61/DI),1:LINE-(C-4/DI,B-58/DI),1:LINE(C+40/DI,B-9/DI)-(C+50/DI,B-12/DI),1:LINE-(C+40/DI,B-7/DI),1
3089 LINE(C-8/DI,B-25/DI)-(C+6/DI,B-7/DI),1:LINE-(C+28/DI,B-7/DI),1:LINE-(C+28/DI,B-9/DI),1:LINE-(C+20/DI,B-9/DI),1:LINE-(C+6/DI,B-25/DI),1:GOTO490
4000 FOR MM=1 TO 10:IFMZ(MM,0)=0THEN4999
4010 RA=SQR((PX-ML(MM,0))^2+(PY-ML(MM,1))^2)
4011 IFMZ(MM,1)<IN*LPTHEN4030
4020 IFRA<1.3THEN4500
4025 IFMM=8 AND RA<3THEN4999
4030 X1=SGN(PX-ML(MM,0)):Y1=SGN(PY-ML(MM,1))
4031 IFMZ(MM,1)<IN*LPTHENX1=-X1:Y1=-Y1
4035 IFY1=0THEN4045
4040 D=DN(ML(MM,0),ML(MM,1)+Y1):IFD=1ORD>9ORD=2THEN4045
4042 X1=0:GOTO4050
4045 Y1=0:IFX1=0THEN4050
4046 D=DN(ML(MM,0)+X1,ML(MM,1)):IFD=1ORD>9ORD=2THENX1=0:GOTO4081
4050 DN(ML(MM,0),ML(MM,1))=DN(ML(MM,0),ML(MM,1))-10*MM
4055 IFML(MM,0)+X1=PX AND ML(MM,1)+Y1=PYTHEN4999
4060 ML(MM,0)=ML(MM,0)+X1:ML(MM,1)=ML(MM,1)+Y1
4080 DN(ML(MM,0),ML(MM,1))=DN(ML(MM,0),ML(MM,1))+10*MM
4081 IFX1<>0 OR Y1<>0THEN4999
4082 IFMZ(MM,1)<IN*LP AND RA<1.3THEN4500
4083 IFMZ(MM,1)<IN*LPTHENMZ(MM,1)=MZ(MM,1)+MM+IN
4499 GOTO4999
4500 IFMM=2 OR MM=7THEN4600
4509 PRESET(0,160):PRINT"YOU ARE BEING ATTACKED":PRINT"BY A ";M$(MM)
4510 IFRND(0)*20-SGN(PW(3))-C(3)+MM+IN<0THENPRINT"MISSED":GOTO4525
4520 PRINT"HIT":C(0)=C(0)-INT(RND(0)*MM+IN)
4525 IFPA=1THENPRINT"-CR- TO CONT. ";:INPUT Q$
4530 GOTO4999
4600 IFRND(0)<.5THEN4509
4610 IFMM=7THENPW(0)=INT(PW(0)/2):PRINT"A GREMLIN STOLE SOME FOOD":GOTO4525
4620 ZZ=INT(RND(0)*6):IFPW(ZZ)<1THEN4620
4630 PRINT"A THIEF STOLE A ";W$(ZZ):PW(ZZ)=PW(ZZ)-1:GOTO4525
4999 NEXT:RETURN
6000 PRINT"":PRINT"":PRINT"        WE MOURN THE PASSING OF"
6005 IFLEN(PN$)>22THENPN$=""
6010 IFPN$=""THENPN$="THE PEASANT"
6020 PN$=PN$+" AND HIS COMPUTER"
6030 PRINTPN$
6035 PRINT"MIRACLE OF RESURRECTION":PRINT"<HIT R KEY>";
6050 IFINKEY$="R"THENRUNELSE6050
7000 SCREEN0,0:CLS:GO=0
7010 IFPN$<>""THEN7500
7920 IFLP=10THEN7950
7930 PRINT:PRINT"   NOW MAYBE THOU ART FOOLHEARTY":PRINT"ENOUGH TO TRY DIFFICULTY LEVEL ";LP+1
7940 GOTO7070
7950 PRINT:PRINT"...CALL CALIFORNIA PACIFIC COMPUTER":PRINT"AT (415)-569-9126 TO REPORT THIS":PRINT"AMAZING FEAT!"
7990 GOTO7070
60000 SCREEN0,0:CLS:GO=0:INPUT"LEVEL OF PLAY(1-10)......";Q$:LP=INT(VAL(Q$))
60006 IFLP<1ORLP>10THEN60005
60020 DATA "HIT POINTS.....","STRENGTH.......","DEXTERITY......","STAMINA........","WISDOM.........","GOLD..........."
60025 DIMPW(5),C$(5):FORX=0TO5:READ C$(X):NEXT
60040 DIMC(5),M$(10),ML(10,1),MZ(10,1)
60042 DATA"SKELETON","THIEF","GIANT RAT","ORC","VIPER","CARRION CRAWLER","GREMLIN","MIMIC","DAEMON","BALROG"
60043 FORX=1TO10:READM$(X):NEXT
60050 FORX=0TO5:C(X)=INT(SQR(RND(0))*21+4):NEXT X
60060 CLS:FORX=0TO5:PRINT C$(X);C(X):NEXT:PRINT:PRINT"SHALT THOU PLAY WITH THESE QUALITIES?":LINE INPUT Q$:IFQ$<>"Y"THEN60050
60061 PRINT:PRINT"AND SHALT THOU BE A FIGHTER OR A MAGE?":INPUT PT$
60062 IFPT$="M"ORPT$="F"THEN60070
60063 GOTO60061
60070 DIMW$(5):DATA"FOOD","RAPIER","AXE","SHIELD","BOW AND ARROWS","MAGIC AMULET":FORX=0TO5:READW$(X):NEXT
60075 GOSUB60080:GOSUB60200:RETURN
60080 SCREEN0,0:CLS:GO=0:PRINT:PRINT:PRINT"     STAT'S              WEAPONS":PRINT:FOR X=0 TO 5:PRINT C$(X);C(X);TAB(24);"0-";W$(X):NEXT
60081 PRINT"Q-QUIT"
60085 FORZ=0TO5:PRINT PW(Z):NEXT
60090 PRINT"PRICE";:PRINT"DAMAGE";:PRINT"ITEM"
60100 FORX=0TO5:PRINT W$(X);:NEXT X
60110 PRINT"1 FOR 10";:PRINT"N/A":PRINT"8";:PRINT"1-10":PRINT"5";:PRINT"1-5"
60120 PRINT"6";:PRINT"1":PRINT"3";:PRINT"1-4":PRINT"15";:PRINT"?????";
60130 RETURN
60200 PRINT"WELCOME TO THE ADVENTURE SHOP"
60210 PRINT"WHICH ITEM SHALT THOU BUY ";:INPUT Q$:IFQ$="Q"THENPRINT:PRINT"BYE":SCREEN 0,0:CLS:GO=0:RETURN
60215 Z=-1
60220 IFQ$="F"THENPRINT"FOOD":Z=0:P=1
60221 IFQ$="R"THENPRINT"RAPIER":Z=1:P=8
60222 IFQ$="A"THENPRINT"AXE":Z=2:P=5
60223 IFQ$="S"THENPRINT"SHIELD":Z=3:P=6
60224 IFQ$="B"THENPRINT"BOW":Z=4:P=3
60225 IFQ$="M"THENPRINT"AMULET":Z=5:P=15
60226 IFZ=-1THENPRINT Q$:PRINT"WE DON'T HAVE THAT.":GOTO60210
60227 IF(Q$="R"ORQ$="B")ANDPT$="M"THENPRINT"I'M SORRY MAGES":PRINT"CAN'T USE THAT!":GOTO60210
60230 IFC(5)-P<0THENPRINT"THOU CANNOT AFFORD THAT.":GOTO60210
60235 IFZ=0THENPW(Z)=PW(Z)+9
60236 PW(Z)=PW(Z)+1:C(5)=C(5)-P
60237 PRINTC(5);"  "
60240 PRINTPW(Z);
60250 PRINT:GOTO60210
60900 SCREEN1:PMODE2:IFGO=0THENGO=1:PCLS:RETURNELSEPCLS:RETURN
60910 LINE(0,160)-(167,191),1,B
60915 RETURN
