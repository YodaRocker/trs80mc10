0 CLS:CLEAR500:DIMT,A,G,A$,B$,A$(58),I,J,M$,P(5),C$(5),C(5),M$(10),ML(10,1),MZ(10,1):I=64:J=47:GOTO15
1 DRAW"BD6":FORG=1TOLEN(M$):A=ASC(MID$(M$,G)):IFA>ITHENA=(A-I)*9:FORT=ATOA+8:DRAWA$(ASC(MID$(A$,T))):NEXT:DRAW"BR8":NEXT:DRAW"BL255BD2":RETURN
2 IFA>JTHENA=(A-J)*9:FORT=ATOA+8:DRAWA$(ASC(MID$(B$,T))):NEXT:DRAW"BR8":NEXT:DRAW"BL255BD2":RETURN
3 DRAW"BR8":NEXT:DRAW"BL255BD2":RETURN
6 Q$=INKEY$:ON-(Q$="")GOTO6:RETURN
7 PRINTM$;:A=((PEEK(384)AND1)*256+PEEK(385))+16383:FORT=A-LEN(M$)+1TOA:POKET,PEEK(T)-I:NEXT:RETURN
8 LINE(167,191)-(0,160),0,BF:RETURN
9 IFGO=0THENPMODE2,1:SCREEN1,1:GO=1:PCLS:RETURNELSEPCLS:RETURN
   REM         123456789123456789123456789123456789123456789123456789123456789123456789123456789123456789123456789123456789123456789123456789123456789123456789123456789123456789123456789123456789123456789123456789123456789123456789123456789123456789
   REM         A        B        C        D        E        F        G        H        I        J        K        L        M        N        O        P        Q        R        S        T        U        V        W        X        Y        Z
15 A$="        103456700120056700123006000100456700123406700123000700023456700120050700000000009100456000120050780000000089123450080123450000123456000123400700023450700123400000023056700000000789120456000020400709120456009020050789020456700103406700"
16 B$="        123456000000000089103406700003456700020450700023056700123056700003450000123456700023450700003400009000000700"
17 A$(48)="":A$(49)="NU3":A$(50)="BU3U3BD6":A$(51)="BU6NR5BD6":A$(52)="BR5BU3U3BD6BL5":A$(53)="BR5NU3BL5":A$(54)="NR5":A$(55)="BU3NR5BD3":A$(56)="BU6BR3D3BD3BL3":A$(57)="BR3NU3BL3"
19 GOSUB60000:LE=0:GO=0:PRINT@198,"WELCOME TO AKALABETH,":PRINT@233,"WORLD OF DOOM!"
20 DIMDN(10,10),TE(20,20),XX(10),YY(10),PE(10,3),LD(10,5),CD(10,3),FT(10,5),LA(10,3)
30 FORX=0TO20:TE(X,0)=1:TE(0,X)=1:TE(X,20)=1:TE(20,X)=1:NEXT:PRINT@448,"PLEASE WAIT";
40 FORX=1TO19:FORY=1TO19:TE(X,Y)=INT(RND(0)^5*4.5):IFRND(0)>.5THENIFTE(X,Y)=3THENTE(X,Y)=0
42 NEXT:PRINT".";:NEXT
50 TE(INT(RND(0)*19+1),INT(RND(0)*19+1))=5:TX=INT(RND(0)*19+1):TY=INT(RND(0)*19+1):TE(TX,TY)=3
51 XX(0)=127:YY(0)=79
52 FORX=2TO20STEP2:XX(X/2)=INT(ATN(1/X)/ATN(1)*128+.5):YY(X/2)=INT(XX(X/2)*4/7):PE(X/2,0)=127-XX(X/2):PE(X/2,1)=127+XX(X/2):PE(X/2,2)=79-YY(X/2):PE(X/2,3)=79+YY(X/2):NEXT
54 PE(0,0)=0:PE(0,1)=255:PE(0,2)=0:PE(0,3)=159
55 FORX=1TO10:CD(X,0)=127-XX(X)/3:CD(X,1)=127+XX(X)/3:CD(X,2)=79-YY(X)*.7:CD(X,3)=79+YY(X):NEXT:PRINT".";
56 FORX=0TO9:LD(X,0)=(PE(X,0)*2+PE(X+1,0))/3:LD(X,1)=(PE(X,0)+2*PE(X+1,0))/3:W=LD(X,0)-PE(X,0):LD(X,2)=PE(X,2)+W*4/7:LD(X,3)=PE(X,2)+2*W*4/7:LD(X,4)=(PE(X,3)*2+PE(X+1,3))/3:LD(X,5)=(PE(X,3)+2*PE(X+1,3))/3
58 LD(X,2)=LD(X,4)-(LD(X,4)-LD(X,2))*.8:LD(X,3)=LD(X,5)-(LD(X,5)-LD(X,3))*.8:IFLD(X,3)=LD(X,4)THENLD(X,3)=LD(X,3)-1
59 NEXT
60 FORX=0TO9:FT(X,0)=127-XX(X)/3:FT(X,1)=127+XX(X)/3:FT(X,2)=127-XX(X+1)/3:FT(X,3)=127+XX(X+1)/3:FT(X,4)=79+(YY(X)*2+YY(X+1))/3:FT(X,5)=79+(YY(X)+2*YY(X+1))/3:NEXT
62 FORX=0TO9:LA(X,0)=(FT(X,0)*2+FT(X,1))/3:LA(X,1)=(FT(X,0)+2*FT(X,1))/3:LA(X,3)=FT(X,4):LA(X,2)=159-LA(X,3):NEXT
70 GOSUB100:GOTO1000
100 GOSUB9:FORY=-1TO1:FORX=-1TO1:LINE(126,75)-(130,75),1:LINE(128,73)-(128,77),1
110 G=TE(TX+X,TY+Y):X1=53+(X+1)*50:Y1=(Y+1)*50:IFG=2THENLINE(X1+20,Y1+20)-(X1+30,Y1+20),1:LINE-(X1+30,Y1+30),1:LINE-(X1+20,Y1+30),1:LINE-(X1+20,Y1+20),1
130 IFG=3THENLINE(X1+10,Y1+10)-(X1+20,Y1+10),1:LINE-(X1+20,Y1+40),1:LINE-(X1+10,Y1+40),1:LINE-(X1+10,Y1+30),1:LINE-(X1+40,Y1+30),1
135 IFG=3THENLINE-(X1+40,Y1+40),1:LINE-(X1+30,Y1+40),1:LINE-(X1+30,Y1+10),1:LINE-(X1+40,Y1+10),1:LINE-(X1+40,Y1+20),1:LINE-(X1+10,Y1+20),1:LINE-(X1+10,Y1+10),1
140 IFG=4THENLINE(X1+20,Y1+20)-(X1+30,Y1+30),1:LINE-(X1+20,Y1+30),1:LINE-(X1+30,Y1+20),1
150 IFG=5THENLINE(X1,Y1)-(X1+50,Y1),1:LINE-(X1+50,Y1+50),1:LINE-(X1,Y1+50),1:LINE-(X1,Y1),1:LINE(X1+10,Y1+10)-(X1+10,Y1+40),1
155 IFG=5THENLINE-(X1+40,Y1+40),1:LINE-(X1+40,Y1+10),1:LINE-(X1+10,Y1+10),1:LINE-(X1+40,Y1+40),1:LINE(X1+10,Y1+40)-(X1+40,Y1+10),1
160 IFG=1THENLINE(X1+10,Y1+50)-(X1+10,Y1+40),1:LINE-(X1+20,Y1+30),1:LINE-(X1+40,Y1+30),1:LINE-(X1+40,Y1+50),1:LINE(X1,Y1+10)-(X1+10,Y1+10),1:LINE(X1+50,Y1+10)-(X1+40,Y1+10),1:LINE(X1,Y1+40)-(X1+10,Y1+40),1:LINE(X1+40,Y1+40)-(X1+50,Y1+40),1
170 IFG=1THENLINE(X1+10,Y1)-(X1+10,Y1+20),1:LINE-(X1+20,Y1+20),1:LINE-(X1+20,Y1+30),1:LINE-(X1+30,Y1+30),1:LINE-(X1+30,Y1+10),1:LINE-(X1+40,Y1+10),1:LINE-(X1+40,Y1),1
190 NEXT:NEXT:RETURN
200 GOSUB9:DI=0
202 CE=DN(PX+DX*DI,PY+DY*DI):LE=DN(PX+DX*DI+DY,PY+DY*DI-DX):RI=DN(PX+DX*DI-DY,PY+DY*DI+DX)
204 L1=PE(DI,0):R1=PE(DI,1):T1=PE(DI,2):B1=PE(DI,3):L2=PE(DI+1,0):R2=PE(DI+1,1):T2=PE(DI+1,2):B2=PE(DI+1,3)
205 CE=INT(CE):LE=INT(LE):RI=INT(RI)
206 MC=INT(CE/10):CE=CE-MC*10:LE=INT((LE/10-INT(LE/10))*10):RI=INT((RI/10-INT(RI/10))*10)
208 IFDI=0THEN216
210 IFCE=1ORCE=3ORCE=4THENLINE(L1,T1)-(R1,T1),1:LINE-(R1,B1),1:LINE-(L1,B1),1:LINE-(L1,T1),1
212 IFCE=1ORCE=3THENEN=1:GOTO260
214 IFCE=4THENLINE(CD(DI,0),CD(DI,3))-(CD(DI,0),CD(DI,2)),1:LINE-(CD(DI,1),CD(DI,2)),1:LINE-(CD(DI,1),CD(DI,3)),1:EN=1:GOTO260
216 IFLE=1ORLE=3ORLE=4THENLINE(L1,T1)-(L2,T2),1:LINE(L1,B1)-(L2,B2),1
218 IFRI=1ORRI=3ORRI=4THENLINE(R1,T1)-(R2,T2),1:LINE(R1,B1)-(R2,B2),1
220 IFLE=4ANDDI>0THENLINE(LD(DI,0),LD(DI,4))-(LD(DI,0),LD(DI,2)),1:LINE-(LD(DI,1),LD(DI,3)),1:LINE-(LD(DI,1),LD(DI,5)),1
222 IFLE=4ANDDI=0THENLINE(0,LD(DI,2)-3)-(LD(DI,1),LD(DI,3)),1:LINE-(LD(DI,1),LD(DI,5)),1
224 IFRI=4ANDDI>0THENLINE(255-LD(DI,0),LD(DI,4))-(255-LD(DI,0),LD(DI,2)),1:LINE-(255-LD(DI,1),LD(DI,3)),1:LINE-(255-LD(DI,1),LD(DI,5)),1
226 IFRI=4ANDDI=0THENLINE(255,LD(DI,2)-3)-(255-LD(DI,1),LD(DI,3)),1:LINE-(255-LD(DI,1),LD(DI,5)),1
228 IFLE=3ORLE=1ORLE=4THEN234
230 IFDITHENLINE(L1,T1)-(L1,B1),1
232 LINE(L1,T2)-(L2,T2),1:LINE-(L2,B2),1:LINE-(L1,B2),1
234 IFRI=3ORRI=1ORRI=4THEN240
236 IFDITHENLINE(R1,T1)-(R1,B1),1
238 LINE(R1,T2)-(R2,T2),1:LINE-(R2,B2),1:LINE-(R1,B2),1
240 IFCE=7ORCE=9THENLINE(FT(DI,0),FT(DI,4))-(FT(DI,2),FT(DI,5)),1:LINE-(FT(DI,3),FT(DI,5)),1:LINE-(FT(DI,1),FT(DI,4)),1:LINE-(FT(DI,0),FT(DI,4)),1
242 IFCE=8THENLINE(FT(DI,0),158-FT(DI,4))-(FT(DI,2),158-FT(DI,5)),1:LINE-(FT(DI,3),158-FT(DI,5)),1:LINE-(FT(DI,1),158-FT(DI,4)),1:LINE-(FT(DI,0),158-FT(DI,4)),1
244 IFCE=7ORCE=8THENBA=LA(DI,3):TP=LA(DI,2):LX=LA(DI,0):RX=LA(DI,1):LINE(LX,BA)-(LX,TP),1:LINE(RX,TP)-(RX,BA),1
246 IFCE=7ORCE=8THENY1=(BA*4+TP)/5:Y2=(BA*3+TP*2)/5:Y3=(BA*2+TP*3)/5:Y4=(BA+TP*4)/5:LINE(LX,Y1)-(RX,Y1),1:LINE(LX,Y2)-(RX,Y2),1:LINE(LX,Y3)-(RX,Y3),1:LINE(LX,Y4)-(RX,Y4),1
248 IFDI>0ANDCE=5THENLINE(127-10/DI,PE(DI,3))-(127-10/DI,PE(DI,3)-10/DI),1:LINE-(127+10/DI,PE(DI,3)-10/DI),1:LINE-(127+10/DI,PE(DI,3)),1:LINE-(127-10/DI,PE(DI,3)),1
249 IFCE=5ANDDI>0THENLINE(32,7)-(0,0),0,BF:M$="CHEST":GOSUB1
250 IFDI>0ANDCE=5THENLINE(127-10/DI,PE(DI,3)-10/DI)-(127-5/DI,PE(DI,3)-15/DI),1:LINE-(127+15/DI,PE(DI,3)-15/DI),1:LINE-(127+15/DI,PE(DI,3)-5/DI),1:LINE-(127+10/DI,PE(DI,3)),1
252 IFDI>0ANDCE=5THENLINE(127+10/DI,PE(DI,3)-10/DI)-(127+15/DI,PE(DI,3)-15/DI),1
260 IFMC<1THEN490
265 B=79+YY(DI):C=127
266 IFMC=8THENLINE(32,7)-(0,0),0,BF:M$="CHEST":GOSUB1:GOTO269
267 M$=M$(MC):A=LEN(M$)*8:LINE(A,15)-(0,8),0,BF:GOSUB1
269 IFDI=0THEN490
270 ONMCGOTO300,310,320,330,340,350,360,370,380,390:GOTO490
300 LINE(C-23/DI,B)-(C-15/DI,B),1:LINE-(C-15/DI,B-15/DI),1:LINE-(C-8/DI,B-30/DI),1:LINE-(C+8/DI,B-30/DI),1:LINE-(C+15/DI,B-15/DI),1:LINE-(C+15/DI,B),1:LINE-(C+23/DI,B),1
301 LINE(C,B-26/DI)-(C,B-65/DI),1:LINE(C-2/DI+.5,B-38/DI)-(C+2/DI+.5,B-38/DI),1:LINE(C-3/DI+.5,B-45/DI)-(C+3/DI+.5,B-45/DI),1:LINE(C-5/DI+.5,B-53/DI)-(C+5/DI+.5,B-53/DI),1
302 LINE(C-23/DI,B-56/DI)-(C-30/DI,B-53/DI),1:LINE-(C-23/DI,B-45/DI),1:LINE-(C-23/DI,B-53/DI),1:LINE-(C-8/DI,B-38/DI),1
303 LINE(C-15/DI,B-45/DI)-(C-8/DI,B-60/DI),1:LINE-(C+8/DI,B-60/DI),1:LINE-(C+15/DI,B-45/DI),1:LINE(C+15/DI,B-42/DI)-(C+15/DI,B-57/DI),1:LINE(C+12/DI,B-45/DI)-(C+20/DI,B-45/DI),1
304 LINE(C,B-75/DI)-(C-5/DI+.5,B-80/DI),1:LINE-(C-8/DI,B-75/DI),1:LINE-(C-5/DI+.5,B-65/DI),1:LINE-(C+5/DI+.5,B-65/DI),1:LINE-(C+5/DI+.5,B-68/DI),1:LINE-(C-5/DI+.5,B-68/DI),1:LINE-(C-5/DI+.5,B-65/DI),1
305 LINE-(C+5/DI+.5,B-65/DI),1:LINE-(C+8/DI,B-75/DI),1:LINE-(C+5/DI+.5,B-80/DI),1:LINE-(C-5/DI+.5,B-80/DI),1:PSET(C-5/DI+.5,B-72/DI):PSET(C+5/DI+.5,B-72/DI):GOTO490
310 LINE(C,B-56/DI)-(C,B-8/DI),1:LINE-(C+10/DI,B),1:LINE-(C+30/DI,B),1:LINE-(C+30/DI,B-45/DI),1:LINE-(C+10/DI,B-64/DI),1:LINE-(C,B-56/DI),1
311 LINE-(C-10/DI,B-64/DI),1:LINE-(C-30/DI,B-45/DI),1:LINE-(C-30/DI,B),1:LINE-(C-10/DI,B),1:LINE-(C,B-8/DI),1:LINE(C-10/DI,B-64/DI)-(C-10/DI,B-75/DI),1:LINE-(C,B-83/DI),1:LINE-(C+10/DI,B-75/DI),1
312 LINE-(C,B-79/DI),1:LINE-(C-10/DI,B-75/DI),1:LINE-(C,B-60/DI),1:LINE-(C+10/DI,B-75/DI),1:LINE-(C+10/DI,B-64/DI),1:GOTO490
320 LINE(C+5/DI,B-30/DI)-(C,B-25/DI),1:LINE-(C-5/DI,B-30/DI),1:LINE-(C-15/DI,B-5/DI),1:LINE-(C-10/DI,B),1:LINE-(C+10/DI,B),1:LINE-(C+15/DI,B-5/DI),1
321 LINE-(C+20/DI,B-5/DI),1:LINE-(C+10/DI,B),1:LINE-(C+15/DI,B-5/DI),1:LINE-(C+5/DI,B-30/DI),1:LINE-(C+10/DI,B-40/DI),1:LINE-(C+3/DI+.5,B-35/DI),1:LINE-(C-3/DI+.5,B-35/DI),1:LINE-(C-10/DI,B-40/DI),1:LINE-(C-5/DI,B-30/DI),1
322 LINE(C-5/DI,B-33/DI)-(C-3/DI+.5,B-30/DI),1:LINE(C+5/DI,B-33/DI)-(C+3/DI+.5,B-30/DI),1:LINE(C-5/DI,B-20/DI)-(C-5/DI,B-15/DI),1
323 LINE(C+5/DI,B-20/DI)-(C+5/DI,B-15/DI),1:LINE(C-7+DI,B-20/DI)-(C-7/DI,B-15/DI),1:LINE(C+7/DI,B-20/DI)-(C+7/DI,B-15/DI),1:GOTO490
330 LINE(C,B)-(C-15/DI,B),1:LINE-(C-8/DI,B-8/DI),1:LINE-(C-8/DI,B-15/DI),1:LINE-(C-15/DI,B-23/DI),1:LINE-(C-15/DI,B-15/DI),1:LINE-(C-23/DI,B-23/DI),1
331 LINE-(C-23/DI,B-45/DI),1:LINE-(C-15/DI,B-53/DI),1:LINE-(C-8/DI,B-53/DI),1:LINE-(C-15/DI,B-68/DI),1:LINE-(C-8/DI,B-75/DI),1:LINE-(C,B-75/DI),1
332 LINE(C,B)-(C+15/DI,B),1:LINE-(C+8/DI,B-8/DI),1:LINE-(C+8/DI,B-15/DI),1:LINE-(C+15/DI,B-23/DI),1:LINE-(C+15/DI,B-15/DI),1:LINE-(C+23/DI,B-23/DI),1
333 LINE-(C+23/DI,B-45/DI),1:LINE-(C+15/DI,B-53/DI),1:LINE-(C+8/DI,B-53/DI),1:LINE-(C+15/DI,B-68/DI),1:LINE-(C+8/DI,B-75/DI),1:LINE-(C,B-75/DI),1
334 LINE(C-15/DI,B-68/DI)-(C+15/DI,B-68/DI),1:LINE(C-8/DI,B-53/DI)-(C+8/DI,B-53/DI),1:LINE(C-23/DI,B-15/DI)-(C+8/DI,B-45/DI),1
335 LINE(C-8/DI,B-68/DI)-(C,B-60/DI),1:LINE-(C+8/DI,B-68/DI),1:LINE-(C+8/DI,B-60/DI),1:LINE-(C-8/DI,B-60/DI),1:LINE-(C-8/DI,B-68/DI),1
336 LINE(C,B-38/DI)-(C-8/DI,B-38/DI),1:LINE-(C+8/DI,B-53/DI),1:LINE-(C+8/DI,B-45/DI),1:LINE-(C+15/DI,B-45/DI),1:LINE-(C,B-30/DI),1:LINE-(C,B-38/DI),1:GOTO490
340 LINE(C-10/DI,B-15/DI)-(C-10/DI,B-30/DI),1:LINE-(C-15/DI,B-20/DI),1:LINE-(C-15/DI,B-15/DI),1:LINE-(C-15/DI,B),1:LINE-(C+15/DI,B),1:LINE-(C+15/DI,B-15/DI),1:LINE  -(C-15/DI,B-15/DI),1
341 LINE(C-15/DI,B-10/DI)-(C+15/DI,B-10/DI),1:LINE(C-15/DI,B-5/DI)-(C+15/DI,B-5/DI),1
342 LINE(C,B-15/DI)-(C-5/DI,B-20/DI),1:LINE-(C-5/DI,B-35/DI),1:LINE-(C+5/DI,B-35/DI),1:LINE-(C+5/DI,B-20/DI),1:LINE-(C+10/DI,B-15/DI),1
343 LINE(C-5/DI,B-20/DI)-(C+5/DI,B-20/DI),1:LINE(C-5/DI,B-25/DI)-(C+5/DI,B-25/DI),1:LINE(C-5/DI,B-30/DI)-(C+5/DI,B-30/DI),1
344 LINE(C-10/DI,B-35/DI)-(C-10/DI,B-40/DI),1:LINE-(C-5/DI,B-45/DI),1:LINE-(C+5/DI,B-45/DI),1:LINE-(C+10/DI,B-40/DI),1:LINE-(C+10/DI,B-35/DI),1
345 LINE(C-10/DI,B-40/DI)-(C,B-45/DI),1:LINE-(C+10/DI,B-40/DI),1
346 LINE(C-5/DI,B-40/DI)-(C+5/DI,B-40/DI),1:LINE-(C+15/DI,B-30/DI),1:LINE-(C,B-40/DI),1:LINE-(C-15/DI,B-30/DI),1:LINE-(C-5/DI+.5,B-40/DI),1:GOTO490
350 LINE(C-20/DI,79-YY(DI))-(C-20/DI,B-88/DI),1:LINE-(C-10/DI,B-83/DI),1:LINE-(C+10/DI,B-83/DI),1:LINE-(C+20/DI,B-88/DI),1:LINE-(C+20/DI,79-YY(DI)),1:LINE-(C-20/DI,79-YY(DI)),1
351 LINE(C-20/DI,B-88/DI)-(C-30/DI,B-83/DI),1:LINE-(C-30/DI,B-78/DI),1:LINE(C+20/DI,B-88/DI)-(C+30/DI,B-83/DI),1:LINE-(C+40/DI,B-83/DI),1
352 LINE(C-15/DI,B-86/DI)-(C-20/DI,B-83/DI),1:LINE-(C-20/DI,B-78/DI),1:LINE-(C-30/DI,B-73/DI),1:LINE-(C-30/DI,B-68/DI),1:LINE-(C-20/DI,B-63/DI),1
353 LINE(C-10/DI,B-83/DI)-(C-10/DI,B-58/DI),1:LINE-(C,B-50/DI),1:LINE(C+10/DI,B-83/DI)-(C+10/DI,B-78/DI),1:LINE-(C+20/DI,B-73/DI),1:LINE-(C+20/DI,B-40/DI),1
354 LINE(C+15/DI,B-85/DI)-(C+20/DI,B-78/DI),1:LINE-(C+30/DI,B-76/DI),1:LINE-(C+30/DI,B-60/DI),1
355 LINE(C,B-83/DI)-(C,B-73/DI),1:LINE-(C+10/DI,B-68/DI),1:LINE-(C+10/DI,B-63/DI),1:LINE-(C,B-58/DI),1:GOTO490
360 LINE(C+5/DI+.5,B-10/DI)-(C-5/DI+.5,B-10/DI),1:LINE-(C,B-15/DI),1:LINE-(C+10/DI,B-20/DI),1:LINE-(C+5/DI+.5,B-15/DI),1:LINE-(C+5/DI+.5,B-10/DI),1
361 LINE-(C+7/DI+.5,B-6/DI),1:LINE-(C+5/DI+.5,B-3/DI),1:LINE-(C-5/DI+.5,B-3/DI),1:LINE-(C-7/DI+.5,B-6/DI),1:LINE-(C-5/DI+.5,B-10/DI),1:LINE(C+2/DI+.5,B-3/DI)-(C+5/DI+.5,B),1:LINE-(C+8/DI,B),1
362 LINE(C-2/DI+.5,B-3/DI)-(C-5/DI+.5,B),1:LINE-(C-8/DI,B),1:PSET(C+3/DI+.5,B-8/DI):PSET(C-3/DI+.5,B-8/DI):LINE(C+3/DI+.5,B-5/DI)-(C-3/DI+.5,B-5/DI),1:GOTO490
370 LINE(127-10/DI,PE(DI,3))-(127-10/DI,PE(DI,3)-10/DI),1:LINE-(127+10/DI,PE(DI,3)-10/DI),1:LINE-(127+10/DI,PE(DI,3)),1:LINE-(127-10/DI,PE(DI,3)),1
371 LINE(127-10/DI,PE(DI,3)-10/DI)-(127-5/DI,PE(DI,3)-15/DI),1:LINE-(127+15/DI,PE(DI,3)-15/DI),1:LINE-(127+15/DI,PE(DI,3)-5/DI),1:LINE-(127+10/DI,PE(DI,3)),1
372 LINE(127+10/DI,PE(DI,3)-10/DI)-(127+15/DI,PE(DI,3)-15/DI),1:GOTO490
380 LINE(C-14/DI,B-46/DI)-(C-12/DI,B-37/DI),1:LINE-(C-20/DI,B-32/DI),1:LINE-(C-30/DI,B-32/DI),1:LINE-(C-22/DI,B-24/DI),1:LINE-(C-40/DI,B-17/DI),1:LINE-(C-40/DI,B-7/DI),1:LINE-(C-38/DI,B-5/DI),1:LINE-(C-40/DI,B-3/DI),1:LINE-(C-40/DI,B),1
381 LINE-(C-36/DI,B),1:LINE-(C-34/DI,B-2/DI),1:LINE-(C-32/DI,B),1:LINE-(C-28/DI,B),1:LINE-(C-28/DI,B-3/DI),1:LINE-(C-30/DI,B-5/DI),1:LINE-(C-28/DI,B-7/DI),1:LINE-(C-28/DI,B-15/DI),1:LINE-(C,B-27/DI),1
382 LINE(C+14/DI,B-46/DI)-(C+12/DI,B-37/DI),1:LINE-(C+20/DI,B-32/DI),1:LINE-(C+30/DI,B-32/DI),1:LINE-(C+22/DI,B-24/DI),1:LINE-(C+40/DI,B-17/DI),1:LINE-(C+40/DI,B-7/DI),1:LINE-(C+38/DI,B-5/DI),1:LINE-(C+40/DI,B-3/DI),1:LINE-(C+40/DI,B),1
383 LINE-(C+36/DI,B),1:LINE-(C+34/DI,B-2/DI),1:LINE-(C+32/DI,B),1:LINE-(C+28/DI,B),1:LINE-(C+28/DI,B-3/DI),1:LINE-(C+30/DI,B-5/DI),1:LINE-(C+28/DI,B-7/DI),1:LINE-(C+28/DI,B-15/DI),1:LINE-(C,B-27/DI),1
384 LINE(C+6/DI,B-48/DI)-(C+38/DI,B-41/DI),1:LINE-(C+40/DI,B-42/DI),1:LINE-(C+18/DI,B-56/DI),1:LINE-(C+12/DI,B-56/DI),1:LINE-(C+10/DI,B-57/DI),1:LINE-(C+8/DI,B-56/DI),1:LINE-(C-8/DI,B-56/DI),1:LINE-(C-10/DI,B-58/DI),1:LINE-(C+14/DI,B-58/DI),1
385 LINE-(C+16/DI,B-59/DI),1:LINE-(C+8/DI,B-63/DI),1:LINE-(C+6/DI,B-63/DI),1:LINE-(C+2/DI+.5,B-70/DI),1:LINE-(C+2/DI+.5,B-63/DI),1:LINE-(C-2/DI+.5,B-63/DI),1:LINE-(C-2/DI+.5,B-70/DI),1:LINE-(C-6/DI,B-63/DI),1:LINE-(C-8/DI,B-63/DI),1
386 LINE-(C-16/DI,B-59/DI),1:LINE-(C-14/DI,B-58/DI),1:LINE-(C+10/DI,B-57/DI),1:LINE-(C-12/DI,B-56/DI),1:LINE-(C-18/DI,B-56/DI),1:LINE-(C-36/DI,B-47/DI),1:LINE-(C-36/DI,B-39/DI),1:LINE-(C-28/DI,B-41/DI),1:LINE-(C-28/DI,B-46/DI),1
387 LINE-(C-20/DI,B-50/DI),1:LINE-(C-18/DI,B-50/DI),1:LINE-(C-14/DI,B-46/DI),1:GOTO3087
390 LINE(C+6/DI,B-60/DI)-(C+30/DI,B-90/DI),1:LINE-(C+60/DI,B-30/DI),1:LINE-(C+60/DI,B-10/DI),1:LINE-(C+30/DI,B-40/DI),1:LINE-(C+15/DI,B-40/DI),1
391 LINE(C-6/DI,B-60/DI)-(C-30/DI,B-90/DI),1:LINE-(C-60/DI,B-30/DI),1:LINE-(C-60/DI,B-10/DI),1:LINE-(C-30/DI,B-40/DI),1:LINE-(C-15/DI,B-40/DI),1
392 LINE(C,B-25/DI)-(C+6/DI,B-25/DI),1:LINE-(C+10/DI,B-20/DI),1:LINE-(C+12/DI,B-10/DI),1:LINE-(C+10/DI,B-6/DI),1:LINE-(C+10/DI,B),1:LINE-(C+14/DI,B),1:LINE-(C+15/DI,B-5/DI),1:LINE-(C+16/DI,B),1:LINE-(C+20/DI,B),1
393 LINE-(C+20/DI,B-6/DI),1:LINE-(C+18/DI,B-10/DI),1:LINE-(C+18/DI,B-20/DI),1:LINE-(C+15/DI,B-30/DI),1:LINE-(C+15/DI,B-45/DI),1:LINE-(C+40/DI,B-60/DI),1:LINE-(C+40/DI,B-70/DI),1
394 LINE-(C+10/DI,B-55/DI),1:LINE-(C+6/DI,B-60/DI),1:LINE-(C+10/DI,B-74/DI),1:LINE-(C+6/DI,B-80/DI),1:LINE-(C+4/DI+.5,B-80/DI),1:LINE-(C+3/DI+.5,B-82/DI),1:LINE-(C+2/DI+.5,B-80/DI),1:LINE-(C,B-80/DI),1
395 LINE(C,B-25/DI)-(C-6/DI,B-25/DI),1:LINE-(C-10/DI,B-20/DI),1:LINE-(C-12/DI,B-10/DI),1:LINE-(C-10/DI,B-6/DI),1:LINE-(C-10/DI,B),1:LINE-(C-14/DI,B),1:LINE-(C-15/DI,B-5/DI),1:LINE-(C-16/DI,B),1:LINE-(C-20/DI,B),1
396 LINE-(C-20/DI,B-6/DI),1:LINE-(C-18/DI,B-10/DI),1:LINE-(C-18/DI,B-20/DI),1:LINE-(C-15/DI,B-30/DI),1:LINE-(C-15/DI,B-45/DI),1:LINE-(C-40/DI,B-60/DI),1:LINE-(C-40/DI,B-70/DI),1
397 LINE-(C-10/DI,B-55/DI),1:LINE-(C-6/DI,B-60/DI),1:LINE-(C-10/DI,B-74/DI),1:LINE-(C-6/DI,B-80/DI),1:LINE-(C-4/DI+.5,B-80/DI),1:LINE-(C-3/DI+.5,B-82/DI),1:LINE-(C-2/DI+.5,B-80/DI),1:LINE-(C,B-80/DI),1
398 LINE(C-6/DI,B-25/DI)-(C,B-6/DI),1:LINE-(C+10/DI,B),1:LINE-(C+4/DI+.5,B-8/DI),1:LINE-(C+6/DI,B-25/DI),1:LINE(C-40/DI,B-64/DI)-(C-40/DI,B-90/DI),1:LINE-(C-52/DI,B-80/DI),1:LINE-(C-52/DI,B-40/DI),1
399 LINE(C+40/DI,B-86/DI)-(C+38/DI,B-92/DI),1:LINE-(C+42/DI,B-92/DI),1:LINE-(C+40/DI,B-86/DI),1:LINE-(C+40/DI,B-50/DI),1
400 LINE(C+4/DI+.5,B-70/DI)-(C+6/DI,B-74/DI),1:LINE(C-4/DI+.5,B-70/DI)-(C-6/DI,B-74/DI),1:LINE(C,B-64/DI)-(C,B-60/DI),1
490 IFEN=1THENEN=0:RETURN
491 DI=DI+1:GOTO202
500 G=RND(-ABS(LN)-TX*10-TY*1000+IN*31.4):FORX=1TO9:FORY=1TO9:DN(X,Y)=0:NEXT:NEXT:FORX=0TO10:DN(X,0)=1:DN(X,10)=1:DN(0,X)=1:DN(10,X)=1:NEXT
520 FORX=2TO8STEP2:FORY=1TO9:DN(X,Y)=1:DN(Y,X)=1:NEXT:NEXT:FORX=2TO8STEP2:FORY=1TO9STEP2
540 IFRND(0)>.95THENDN(X,Y)=2
541 IFRND(0)>.95THENDN(Y,X)=2
542 IFRND(0)>.6THENDN(Y,X)=3
543 IFRND(0)>.6THENDN(X,Y)=3
544 IFRND(0)>.6THENDN(X,Y)=4
545 IFRND(0)>.6THENDN(Y,X)=4
546 IFRND(0)>.97THENDN(Y,X)=9
547 IFRND(0)>.97THENDN(X,Y)=9
548 IFRND(0)>.94THENDN(X,Y)=5
549 IFRND(0)>.94THENDN(Y,X)=5
568 NEXT:NEXT
569 DN(2,1)=0:IFIN/2=INT(IN/2)THENDN(7,3)=7:DN(3,7)=8
570 IFIN/2<>INT(IN/2)THENDN(7,3)=8:DN(3,7)=7
580 IFIN=1THENDN(1,1)=8:DN(7,3)=0
585 GOSUB2000:RETURN
900 PRINT@480,;:M$="PRESS ENTER TO CONTINUE":GOSUB7
901 Q$=INKEY$:ON-(Q$<>CHR$(13))GOTO901:RETURN
910 M$="BLOCKED BY MOUNTAINS":GOSUB1:RETURN
920 LINE(255,167)-(168,160),0,BF:M$=";HIT ENTER":GOSUB1:GOSUB901:LINE(255,167)-(168,160),0,BF:RETURN
1000 GOSUB8:M$="COMMAND":GOSUB1
1001 X$=INKEY$:IFX$=""THEN1001ELSEX=ASC(X$)
1030 IFX=73THENONSGN(IN)+1GOTO1100,1150
1040 IFX=76THENONSGN(IN)+1GOTO1200,1250
1050 IFX=74THENONSGN(IN)+1GOTO1300,1350
1060 IFX=75THENONSGN(IN)+1GOTO1400,1450
1070 IFX=13THENONSGN(IN)+1GOTO1500,1550
1080 IFX=65THENONSGN(IN)+1GOTO1090,1650
1081 IFX=32THENM$="PASS":GOSUB1:GOTO1090
1085 IFX=83THEN1700
1086 IFX=80THENIFPA=1THENPA=0:M$="UNPAUSED":GOSUB1:GOTO1000ELSEPA=1:M$="PAUSED":GOSUB1:GOTO1000
1089 M$="HUH":GOSUB1:GOTO1000
1090 P(0)=P(0)-1+SGN(IN)*.9:IFP(0)<0THENC(0)=0:M$="YOU HAVE STARVED":GOSUB1:GOTO1093
1091 P(0)=INT(P(0)*10)/10
1093 IFC(0)<=0THEN6000
1095 IFIN>0THENGOSUB8:GOSUB4000:IFC(0)<=0THEN1093
1096 IFIN=0THENGOSUB100
1097 IFIN>0THENGOSUB200
1098 LINE(255,191)-(168,168),0,BF:M$="FO"+STR$(INT(P(0)*10+0.5)/10):GOSUB1:DRAW"BM168,176":M$="HP"+STR$(C(0)):GOSUB1:DRAW"BM168,184":M$="GP"+STR$(C(5)):GOSUB1:GOTO1000
1100 M$="NORTH":GOSUB1:IFTE(TX,TY-1)=1THENGOSUB910:GOTO1090
1110 TY=TY-1:GOTO1090
1150 IFDN(PX+DX,PY+DY)<>1THENIFDN(PX+DX,PY+DY)<10THENPX=PX+DX:PY=PY+DY
1155 M$="FORWARD":GOSUB1:IFDN(PX,PY)=2THENM$="A TRAP":GOSUB1:C(0)=C(0)-INT(RND(0)*IN+3):MR=1:IN=IN+1:M$="FALL TO LEVEL"+STR$(IN):GOSUB1:GOSUB500:GOSUB8:GOTO1090
1165 Z=0:IFDN(PX,PY)=5THENDN(PX,PY)=0:Z=INT(RND(0)*5*IN+IN):M$=MID$(STR$(Z),2)+" GP":GOSUB1:C(5)=C(5)+Z
1175 IFZ>0THENZ=INT(RND(0)*6):M$="AND A "+W$(Z):GOSUB1:P(Z)=P(Z)+1:GOTO1668
1190 GOTO1090
1200 M$="EAST":GOSUB1:IFTE(TX+1,TY)=1THENGOSUB910:GOTO1090
1210 TX=TX+1:GOTO1090
1250 M$="RIGHT":GOSUB1
1255 IFDXTHENDY=DX:DX=0:GOTO1090
1260 DX=-DY:DY=0:GOTO1090
1300 M$="WEST":GOSUB1:IFTE(TX-1,TY)=1THENGOSUB910:GOTO1090
1310 TX=TX-1:GOTO1090
1350 M$="LEFT":GOSUB1
1355 IFDXTHENDY=-DX:DX=0:GOTO1090
1360 DX=DY:DY=0:GOTO1090
1400 M$="SOUTH":GOSUB1:IFTE(TX,TY+1)=1THENGOSUB910:GOTO1090
1410 TY=TY+1:GOTO1090
1450 M$="TURN AROUND":GOSUB1:DX=-DX:DY=-DY:GOTO1090
1500 IFTE(TX,TY)=3THENGOSUB60080:GOSUB60200:GOSUB9:GOTO1090
1510 IFTE(TX,TY)=4ANDIN=0THENM$="GO DUNGEON":GOSUB1:M$="PLEASE WAIT":GOSUB1:IN=1:GOSUB500:DX=1:DY=0:PX=1:PY=1:GOTO1090
1515 IFTE(TX,TY)=5THEN7000
1520 M$="HUH":GOTO1000
1550 IFDN(PX,PY)<>7ANDDN(PX,PY)<>9THEN1580
1555 IN=IN+1:M$="GO DOWN TO LEVEL"+STR$(IN):GOSUB1:GOSUB500:MR=1:GOTO1090
1580 IFDN(PX,PY)<>8THENM$="HUH":GOSUB1:GOTO1090
1581 IFIN=1THENM$="LEAVE DUNGEON":GOSUB1:IN=0:GOTO1586
1584 IN=IN-1:M$="GO UP TO LEVEL"+STR$(IN):GOSUB1:GOSUB500:MR=1
1586 IFIN=0THENM$="GAINED"+STR$(LK)+" HP":GOSUB1:C(0)=C(0)+LK:LK=0
1587 GOTO1090
1599 M$="NOT OWNED":GOSUB1:GOTO1650
1650 MN=0:DA=0:M$="ATTACK WEAPON":GOSUB1:GOSUB6:GOSUB8
1651 IFQ$="R"THENDA=10:M$="RAPIER":GOSUB1:IFP(1)<1THEN1599
1652 IFQ$="A"THENDA=5:M$="AXE":GOSUB1:IFP(2)<1THEN1599
1653 IFQ$="S"THENDA=1:M$="SHIELD":GOSUB1:IFP(3)<1THEN1599
1654 IFQ$="B"THENDA=4:M$="BOW":GOSUB1:IFP(4)<1THEN1599
1655 IFQ$="M"THENM$="MAGIC AMULET":GOSUB1:GOTO1680
1656 IFQ$="B"ORQ$="R"THENIFPT$="M"THENM$="A MAGE CANNOT":GOSUB1:GOTO1650
1659 IFDA=0THENM$="HANDS":GOSUB1
1660 IFDA=5ORDA=4THEN1670
1661 MN=DN(PX+DX,PY+DY)/10:MN=INT(MN)
1662 IFMN<1ORC(2)-RND(0)*25<IN+MNTHENM$="MISS":GOSUB1:GOTO1668
1663 M$="HIT":GOSUB1:DA=(RND(0)*DA+C(1)/5):MZ(MN,1)=MZ(MN,1)-DA:Z=INT(MZ(MN,1)):IFZ<1THEN1665
1664 M$=M$(MN)+" HP"+STR$(Z):GOSUB1
1665 IFMZ(MN,1)<1THENM$=LEFT$(M$(MN)+" KILLED",20):GOSUB1:DA=INT(MN+IN):M$="GP WON"+STR$(DA):GOSUB1:C(5)=INT(C(5)+DA):DN(ML(MN,0),ML(MN,1))=DN(ML(MN,0),ML(MN,1))-10*MN:MZ(MN,0)=0
1667 LK=LK+INT(MN*IN/2):IFMN=TATHENTA=-TA
1668 IFPA=1THENGOSUB920
1669 GOSUB8:GOTO1090
1670 IFDA=5THENM$="THROW OR SWING":GOSUB1:GOSUB6:GOSUB8:IFQ$<>"T"THENM$="SWING":GOSUB1:GOTO1661
1671 IFDA=5THENM$="THROW":GOSUB1:P(2)=P(2)-1
1672 FORY=1TO5:IFPX+DX*Y<1ORPX+DX*Y>9ORPY+DY*Y>9ORPY+DY*Y<0THEN1662
1673 MN=DN(PX+DX*Y,PY+DY*Y):MN=INT(MN/10):IFMN>0THEN1662
1674 NEXT:GOTO1662
1680 IFP(5)<1THEN1599
1681 IFPT$="F"THENQ=INT(RND(0)*4+1):GOTO1685
1682 M$="1 UP 2 DOWN LADDER":GOSUB1:M$="3 KILL 4 BAD ;CHOICE":GOSUB1:GOSUB6:GOSUB8:Q=VAL(Q$):IFQ<1ORQ>4THEN1682
1683 IFRND(0)>.75THENM$="LAST CHARGE":GOSUB1:P(5)=P(5)-1:GOSUB8
1685 ONQGOTO1686,1690,1691,1692
1686 M$="UP":GOSUB1:DN(PX,PY)=8:GOTO1090
1690 M$="DOWN":GOSUB1:DN(PX,PY)=7:GOTO1090
1691 M$="MAGIC ATTACK":GOSUB1:DA=10+IN:GOTO1672
1692 ONRND(3)GOTO1695,1697:M$="YOU HAVE BEEN":GOSUB1:M$="TURNED INTO A TOAD":GOSUB1:FORT=1TO4:C(T)=3:NEXT:GOTO1668
1695 M$="YOU HAVE BECOME":GOSUB1:M$="A LIZARD MAN":GOSUB1:FORY=0TO4:C(Y)=INT(C(Y)*2.5):NEXT:GOTO1668
1697 M$="BACKFIRE":GOSUB1:C(0)=C(0)/2:GOTO1668
1700 GOSUB60080:GOSUB900:GOSUB9:GOTO1090
2000 NM=0:FORX=1TO10:MZ(X,0)=0:MZ(X,1)=X+3+IN:IFX-2>INORRND(0)>.4THEN2090
2020 ML(X,0)=INT(RND(0)*9+1):ML(X,1)=INT(RND(0)*9+1):IFDN(ML(X,0),ML(X,1))THEN2020
2040 IFML(X,0)=PXANDML(X,1)=PYTHEN2020
2050 DN(ML(X,0),ML(X,1))=X*10:MZ(X,0)=1:NM=NM+1:MZ(X,1)=X*2+IN*2*LP
2090 NEXT:RETURN
3087 LINE(C-28/DI,B-41/DI)-(C+30/DI,B-55/DI),1:LINE(C+28/DI,B-58/DI)-(C+22/DI,B-56/DI),1:LINE-(C+22/DI,B-53/DI),1:LINE-(C+28/DI,B-52/DI),1:LINE-(C+34/DI,B-54/DI),1:LINE-(C+20/DI,B-50/DI),1:LINE-(C+26/DI,B-47/DI),1
3088 LINE(C+10/DI,B-58/DI)-(C+10/DI,B-61/DI),1:LINE-(C+4/DI,B-58/DI),1:LINE(C-10/DI,B-58/DI)-(C-10/DI,B-61/DI),1:LINE-(C-4/DI,B-58/DI),1:LINE(C+40/DI,B-9/DI)-(C+50/DI,B-12/DI),1:LINE-(C+40/DI,B-7/DI),1
3089 LINE(C-8/DI,B-25/DI)-(C+6/DI,B-7/DI),1:LINE-(C+28/DI,B-7/DI),1:LINE-(C+28/DI,B-9/DI),1:LINE-(C+20/DI,B-9/DI),1:LINE-(C+6/DI,B-25/DI),1:GOTO490
4000 FORMM=1TO10:IFMZ(MM,0)=0THEN4999
4010 RA=SQR((PX-ML(MM,0))^2+(PY-ML(MM,1))^2)
4011 IFMZ(MM,1)<IN*LPTHEN4030
4020 IFRA<1.3THEN4500
4025 IFMM=8ANDRA<3THEN4999
4030 X1=SGN(PX-ML(MM,0)):Y1=SGN(PY-ML(MM,1))
4031 IFMZ(MM,1)<IN*LPTHENX1=-X1:Y1=-Y1
4035 IFY1=0THEN4045
4040 D=DN(ML(MM,0),ML(MM,1)+Y1):IFD=1ORD>9ORD=2THEN4045
4042 X1=0:GOTO4050
4045 Y1=0:IFX1=0THEN4050
4046 D=DN(ML(MM,0)+X1,ML(MM,1)):IFD=1ORD>9ORD=2THENX1=0:GOTO4081
4050 DN(ML(MM,0),ML(MM,1))=DN(ML(MM,0),ML(MM,1))-10*MM
4055 IFML(MM,0)+X1=PX AND ML(MM,1)+Y1=PYTHEN4999
4060 ML(MM,0)=ML(MM,0)+X1:ML(MM,1)=ML(MM,1)+Y1
4080 DN(ML(MM,0),ML(MM,1))=DN(ML(MM,0),ML(MM,1))+10*MM
4081 IFX1<>0ORY1<>0THEN4999
4082 IFMZ(MM,1)<IN*LPTHENIFRA<1.3THEN4500
4083 IFMZ(MM,1)<IN*LPTHENMZ(MM,1)=MZ(MM,1)+MM+IN
4499 GOTO4999
4500 IFMM=2ORMM=7THEN4600
4509 GOSUB8:M$=LEFT$(M$(MM),9)+" ATTACKING":GOSUB1
4510 IFRND(0)*20-SGN(P(3))-C(3)+MM+IN<0THENM$="MISS":GOSUB1:GOTO4525
4520 M$="HIT":GOSUB1:C(0)=C(0)-INT(RND(0)*MM+IN)
4525 IFPA=1THENGOSUB920:GOSUB8
4530 GOTO4999
4600 IFRND(0)<.5THEN4509
4610 IFMM=7THENP(0)=INT(P(0)/2):M$="GREMLIN STOLE FOOD":GOSUB1:GOTO4525
4615 IFP(0)+P(1)+P(2)+P(3)+P(4)+P(5)<1THENTHEN4999
4620 Z=INT(RND(0)*6):IFP(Z)<1THEN4620
4630 M$=LEFT$("THIEF STOLE "+W$(Z),20):GOSUB1:P(Z)=P(Z)-1:GOTO4525
4999 NEXT:RETURN
6000 FORG=1TO2500:NEXT:SCREEN0,0:CLS:PRINTTAB(5)"WE MOURN THE PASSING OF"
6005 IFLEN(PN$)>22THENPN$=""
6010 IFPN$=""THENPN$="THE PEASANT"
6020 PN$=PN$+" AND HIS COMPUTER"
6030 PRINTTAB((32-LEN(PN$))/2)PN$:PRINT:M$="FOR RESURRECTION, PRESS R":GOSUB7
6050 IFINKEY$="R"THENRUNELSE6050
7000 SCREEN0,0:CLS:GO=0:IFPN$<>""THEN7500
7020 PRINT"WELCOME PEASANT TO THE HALLS OF":PRINT"MIGHTY LORD BRITISH!"
7030 PRINT:INPUT"WHAT IS THY NAME";PN$
7050 PRINT:PRINT"THY FIRST TASK IS TO GO TO":PRINT"THE DUNGEONS AND RETURN AFTER":TA=INT(C(4)/3):PRINT"KILLING A(N) "M$(TA)
7060 PRINT:PRINT"ALSO I, BRITISH, HAVE INCREASED":PRINT"EACH OF THY ATTRIBUTES BY ONE!"
7070 GOSUB900:FORX=0TO5:C(X)=C(X)+1:NEXT:CLS:GOTO1090
7500 IFTA>0THENPRINTPN$"!?":PRINT"WHY HAST THOU RETURNED?":PRINT"THY QUEST IS TO KILL A(N)":PRINTM$(TA):GOSUB900:CLS:GOTO1090
7510 PRINT"THY QUEST IS ACOMPLISHED!":IFABS(TA)=10THEN7900
7520 PRINT"BUT IT IS STILL NOT ENOUGH TO":PRINT"BECOME A KNIGHT. THOU MUST NOW":TA=ABS(TA)+1:PRINT"KILL A(N) "M$(TA)
7530 GOTO7060
7900 SCREEN0,0:CLS:GO=0:PRINT:PN$="LORD "+PN$:PRINTPN$","
7910 PRINT"YOU HAVE EARNED A KNIGHTHOOD!"
7920 PRINT:IFLP=10THENPRINT"CONGRATULATIONS!":GOTO7070
7930 PRINT"BE THOU FOOLHEARTY ENOUGH TO":PRINT"TRY DIFFICULTY LEVEL"STR$(LP+1)"?":GOTO7070
60000 SCREEN0,0:CLS:GO=0:PRINT@96,;:INPUT"TYPE THY LUCKY NUMBER";Q$:LN=VAL(Q$):PRINT:INPUT"LEVEL OF PLAY (1-10)";Q$:LP=INT(VAL(Q$))
60006 IFLP<1ORLP>10THEN60000
60010 G=RND(-ABS(LN)):FORX=0TO5:READC$(X):NEXT:FORX=1TO10:READM$(X):NEXT
60020 DATA"HP ","STR","DEX","STA","WIS","GP "
60042 DATA"SKELETON","THIEF","GIANT RAT","ORC","VIPER","CARRION CRAWLER","GREMLIN","MIMIC","DAEMON","BALROG","FOOD","RAPIER","AXE","SHIELD","BOW AND ARROWS","MAGIC AMULET"
60050 FORX=0TO5:C(X)=INT(SQR(RND(0))*21+4):NEXT:CLS:FORX=0TO5:PRINTC$(X)C(X):NEXT:PRINT:INPUT"PLAY THOU WITH THESE QUALITIES";Q$:IFQ$<>"Y"THEN60050
60060 PRINT:INPUT"BE THOU A FIGHTER OR A MAGE";PT$:IFPT$="M"ORPT$="F"THEN60070ELSE60060
60070 DIMW$(5):FORX=0TO5:READW$(X):NEXT:GOSUB60080:GOSUB60200:RETURN
60080 SCREEN0,0:CLS:GO=0
60081 PRINT@2,"STAT'S":PRINT@20,"WEAPONS":FORX=0TO5:PRINT@(X+1)*32,C$(X)C(X)TAB(11)RIGHT$("     "+STR$(INT(P(X))),5)TAB(17)W$(X);:NEXT
60082 PRINT@480,;:M$="Q=QUIT":GOSUB7:PRINT"                         ";
60090 PRINT@256,"PRICE"TAB(9)"DAMAGE"TAB(17)"ITEM"
60110 PRINT"1 FOR 10"TAB(9)"N/A"TAB(17)W$(0):PRINT"8"TAB(9)"1-10"TAB(17)W$(1):PRINT"5"TAB(9)"1-5"TAB(17)W$(2):PRINT"6"TAB(9)"1"TAB(17)W$(3):PRINT"3"TAB(9)"1-4"TAB(17)W$(4):PRINT"15"TAB(9)"?"TAB(17)W$(5):RETURN
60200 PRINT@224,"":PRINT@224,;:M$="WHAT DOST THOU WANT? ":GOSUB7:GOSUB6:IFQ$="Q"THENCLS:GO=0:RETURN
60215 Z=-1
60220 IFQ$="F"THENPRINT"FOOD":Z=0:P=1
60221 IFQ$="R"THENPRINT"RAPIER":Z=1:P=8
60222 IFQ$="A"THENPRINT"AXE":Z=2:P=5
60223 IFQ$="S"THENPRINT"SHIELD":Z=3:P=6
60224 IFQ$="B"THENPRINT"BOW":Z=4:P=3
60225 IFQ$="M"THENPRINT"AMULET":Z=5:P=15
60226 PRINT@487,;:IFZ=-1THENPRINT"WE DON'T HAVE THAT.     ";:GOTO60200
60227 IF(Q$="R"ORQ$="B")ANDPT$="M"THENPRINT"MAGES CAN'T USE THAT!   ";:GOTO60200
60230 IFC(5)-P<0THENPRINT"THOU CANNOT AFFORD THAT.";:GOTO60200
60235 IFZ=0THENP(Z)=P(Z)+9
60236 P(Z)=P(Z)+1:C(5)=C(5)-P:GOSUB60081:GOTO60200
