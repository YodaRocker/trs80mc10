0 PCLEAR2:CLS:CLEAR400:DIMT,A,G,A$,B$,A$(58),I,J,M$,P(5),C$(5),C(5),M$(10),ML(10,1),MZ(10,1):I=64:J=47:GOTO9
1 DRAW"BD6":FORG=1TOLEN(M$):A=ASC(MID$(M$,G)):IFA>I THENA=(A-I)*7:FORT=A TOA+6:DRAWA$(ASC(MID$(A$,T))):NEXT:DRAW"BR8":NEXT:DRAW"BL255BD2":RETURN
2 IFA>J THENA=(A-J)*7:FORT=A TOA+6:DRAWA$(ASC(MID$(B$,T))):NEXT:DRAW"BR8":NEXT:DRAW"BL255BD2":RETURN
3 DRAW"BR8":NEXT:DRAW"BL255BD2":RETURN
4 Q$=INKEY$:ON-(Q$="")GOTO4:RETURN
5 PRINTM$;:A=((PEEK(136)AND1)*256+PEEK(137))+1023:FORT=A-LEN(M$)+1TOA:POKET,PEEK(T)-I:NEXT:RETURN
6 LINE(167,191)-(0,160),0,BF:RETURN
7 IFGO THENLINE(0,0)-(255,159),0,BF:RETURN
8 PMODE2,1:SCREEN1,1:GO=1:CM=0:PCLS:DRAW"BM168,168":M$="FO"+STR$(INT(P(0)*10+0.5)/10):GOSUB1:DRAW"BM168,176":M$="HP"+STR$(C(0)):GOSUB1:DRAW"BM168,184":M$="GP"+STR$(C(5)):GOSUB1:RETURN
9 A$="      10345671256700123600014567001234670123070002345671250700900000014560001250780000008912345801234500123456012347000234570123400023567000000789120456024007091245609205078902045671340670"
10 B$="      123456000000891346700345670024507002356700123567034500001234567234570034000090000700"
11 A$(48)="":A$(49)="NU3":A$(50)="BU3U3BD6":A$(51)="BU6NR5BD6":A$(52)="BR5BU3U3BD6BL5":A$(53)="BR5NU3BL5":A$(54)="NR5":A$(55)="BU3NR5BD3":A$(56)="BU6BR3D3BD3BL3":A$(57)="BR3NU3BL3"
12 GOSUB297:LE=0:GO=0:PRINT@198,"WELCOME TO AKALABETH,":PRINT@233,"WORLD OF DOOM!"
13 DIMD(10,10),T(20,20),XX(10),YY(10),PE(10,3),LD(10,5),CD(10,3),FT(10,5),LA(10,3)
14 FORX=0TO20:T(X,0)=1:T(0,X)=1:T(X,20)=1:T(20,X)=1:NEXT:PRINT@448,"PLEASE WAIT";
15 FORX=1TO19:FORY=1TO19:T(X,Y)=INT(RND(0)^5*4.5):IFRND(0)>.5THENIFT(X,Y)=3THENT(X,Y)=0
16 NEXT:PRINT".";:NEXT
17 T(INT(RND(0)*19+1),INT(RND(0)*19+1))=5:TX=INT(RND(0)*19+1):TY=INT(RND(0)*19+1):T(TX,TY)=3
18 XX(0)=127:YY(0)=79
19 FORX=2TO20STEP2:XX(X/2)=INT(ATN(1/X)/ATN(1)*128+.5):YY(X/2)=INT(XX(X/2)*4/7):PE(X/2,0)=127-XX(X/2):PE(X/2,1)=127+XX(X/2):PE(X/2,2)=79-YY(X/2):PE(X/2,3)=79+YY(X/2):NEXT
20 PE(0,0)=0:PE(0,1)=255:PE(0,2)=0:PE(0,3)=159
21 FORX=1TO10:CD(X,0)=127-XX(X)/3:CD(X,1)=127+XX(X)/3:CD(X,2)=79-YY(X)*.7:CD(X,3)=79+YY(X):NEXT:PRINT".";
22 FORX=0TO9:LD(X,0)=(PE(X,0)*2+PE(X+1,0))/3:LD(X,1)=(PE(X,0)+2*PE(X+1,0))/3:W=LD(X,0)-PE(X,0):LD(X,2)=PE(X,2)+W*4/7:LD(X,3)=PE(X,2)+2*W*4/7:LD(X,4)=(PE(X,3)*2+PE(X+1,3))/3:LD(X,5)=(PE(X,3)+2*PE(X+1,3))/3
23 LD(X,2)=LD(X,4)-(LD(X,4)-LD(X,2))*.8:LD(X,3)=LD(X,5)-(LD(X,5)-LD(X,3))*.8:IFLD(X,3)=LD(X,4)THENLD(X,3)=LD(X,3)-1
24 NEXT
25 FORX=0TO9:FT(X,0)=127-XX(X)/3:FT(X,1)=127+XX(X)/3:FT(X,2)=127-XX(X+1)/3:FT(X,3)=127+XX(X+1)/3:FT(X,4)=79+(YY(X)*2+YY(X+1))/3:FT(X,5)=79+(YY(X)+2*YY(X+1))/3:NEXT
26 FORX=0TO9:LA(X,0)=(FT(X,0)*2+FT(X,1))/3:LA(X,1)=(FT(X,0)+2*FT(X,1))/3:LA(X,3)=FT(X,4):LA(X,2)=159-LA(X,3):NEXT
27 GOSUB28:GOTO154
28 GOSUB7:FORY=-1TO1:FORX=-1TO1:LINE(126,75)-(130,75),1:LINE(128,73)-(128,77),1
29 G=T(TX+X,TY+Y):X1=53+(X+1)*50:Y1=(Y+1)*50:IFG=2THENLINE(X1+20,Y1+20)-(X1+30,Y1+20),1:LINE-(X1+30,Y1+30),1:LINE-(X1+20,Y1+30),1:LINE-(X1+20,Y1+20),1
30 IFG=3THENLINE(X1+10,Y1+10)-(X1+20,Y1+10),1:LINE-(X1+20,Y1+40),1:LINE-(X1+10,Y1+40),1:LINE-(X1+10,Y1+30),1:LINE-(X1+40,Y1+30),1
31 IFG=3THENLINE-(X1+40,Y1+40),1:LINE-(X1+30,Y1+40),1:LINE-(X1+30,Y1+10),1:LINE-(X1+40,Y1+10),1:LINE-(X1+40,Y1+20),1:LINE-(X1+10,Y1+20),1:LINE-(X1+10,Y1+10),1
32 IFG=4THENLINE(X1+20,Y1+20)-(X1+30,Y1+30),1:LINE-(X1+20,Y1+30),1:LINE-(X1+30,Y1+20),1
33 IFG=5THENLINE(X1,Y1)-(X1+50,Y1),1:LINE-(X1+50,Y1+50),1:LINE-(X1,Y1+50),1:LINE-(X1,Y1),1:LINE(X1+10,Y1+10)-(X1+10,Y1+40),1
34 IFG=5THENLINE-(X1+40,Y1+40),1:LINE-(X1+40,Y1+10),1:LINE-(X1+10,Y1+10),1:LINE-(X1+40,Y1+40),1:LINE(X1+10,Y1+40)-(X1+40,Y1+10),1
35 IFG=1THENLINE(X1+10,Y1+50)-(X1+10,Y1+40),1:LINE-(X1+20,Y1+30),1:LINE-(X1+40,Y1+30),1:LINE-(X1+40,Y1+50),1:LINE(X1,Y1+10)-(X1+10,Y1+10),1:LINE(X1+50,Y1+10)-(X1+40,Y1+10),1:LINE(X1,Y1+40)-(X1+10,Y1+40),1:LINE(X1+40,Y1+40)-(X1+50,Y1+40),1
36 IFG=1THENLINE(X1+10,Y1)-(X1+10,Y1+20),1:LINE-(X1+20,Y1+20),1:LINE-(X1+20,Y1+30),1:LINE-(X1+30,Y1+30),1:LINE-(X1+30,Y1+10),1:LINE-(X1+40,Y1+10),1:LINE-(X1+40,Y1),1
37 NEXT:NEXT:RETURN
38 GOSUB7:DI=0
39 CE=D(PX+DX*DI,PY+DY*DI):LE=D(PX+DX*DI+DY,PY+DY*DI-DX):RI=D(PX+DX*DI-DY,PY+DY*DI+DX)
40 L1=PE(DI,0):R1=PE(DI,1):T1=PE(DI,2):B1=PE(DI,3):L2=PE(DI+1,0):R2=PE(DI+1,1):T2=PE(DI+1,2):B2=PE(DI+1,3)
41 CE=INT(CE):LE=INT(LE):RI=INT(RI)
42 MC=INT(CE/10):CE=CE-MC*10:LE=INT((LE/10-INT(LE/10))*10):RI=INT((RI/10-INT(RI/10))*10)
43 IFDI=0THEN47
44 IFCE=1ORCE=3ORCE=4THENLINE(L1,T1)-(R1,T1),1:LINE-(R1,B1),1:LINE-(L1,B1),1:LINE-(L1,T1),1
45 IFCE=1ORCE=3THENEN=1:GOTO67
46 IFCE=4THENLINE(CD(DI,0),CD(DI,3))-(CD(DI,0),CD(DI,2)),1:LINE-(CD(DI,1),CD(DI,2)),1:LINE-(CD(DI,1),CD(DI,3)),1:EN=1:GOTO67
47 IFLE=1ORLE=3ORLE=4THENLINE(L1,T1)-(L2,T2),1:LINE(L1,B1)-(L2,B2),1
48 IFRI=1ORRI=3ORRI=4THENLINE(R1,T1)-(R2,T2),1:LINE(R1,B1)-(R2,B2),1
49 IFLE=4ANDDI>0THENLINE(LD(DI,0),LD(DI,4))-(LD(DI,0),LD(DI,2)),1:LINE-(LD(DI,1),LD(DI,3)),1:LINE-(LD(DI,1),LD(DI,5)),1
50 IFLE=4ANDDI=0THENLINE(0,LD(DI,2)-3)-(LD(DI,1),LD(DI,3)),1:LINE-(LD(DI,1),LD(DI,5)),1
51 IFRI=4ANDDI>0THENLINE(255-LD(DI,0),LD(DI,4))-(255-LD(DI,0),LD(DI,2)),1:LINE-(255-LD(DI,1),LD(DI,3)),1:LINE-(255-LD(DI,1),LD(DI,5)),1
52 IFRI=4ANDDI=0THENLINE(255,LD(DI,2)-3)-(255-LD(DI,1),LD(DI,3)),1:LINE-(255-LD(DI,1),LD(DI,5)),1
53 IFLE=3ORLE=1ORLE=4THEN56
54 IFDI THENLINE(L1,T1)-(L1,B1),1
55 LINE(L1,T2)-(L2,T2),1:LINE-(L2,B2),1:LINE-(L1,B2),1
56 IFRI=3ORRI=1ORRI=4THEN59
57 IFDI THENLINE(R1,T1)-(R1,B1),1
58 LINE(R1,T2)-(R2,T2),1:LINE-(R2,B2),1:LINE-(R1,B2),1
59 IFCE=7ORCE=9THENLINE(FT(DI,0),FT(DI,4))-(FT(DI,2),FT(DI,5)),1:LINE-(FT(DI,3),FT(DI,5)),1:LINE-(FT(DI,1),FT(DI,4)),1:LINE-(FT(DI,0),FT(DI,4)),1
60 IFCE=8THENLINE(FT(DI,0),158-FT(DI,4))-(FT(DI,2),158-FT(DI,5)),1:LINE-(FT(DI,3),158-FT(DI,5)),1:LINE-(FT(DI,1),158-FT(DI,4)),1:LINE-(FT(DI,0),158-FT(DI,4)),1
61 IFCE=7ORCE=8THENBA=LA(DI,3):TP=LA(DI,2):LX=LA(DI,0):RX=LA(DI,1):LINE(LX,BA)-(LX,TP),1:LINE(RX,TP)-(RX,BA),1
62 IFCE=7ORCE=8THENY1=(BA*4+TP)/5:Y2=(BA*3+TP*2)/5:Y3=(BA*2+TP*3)/5:Y4=(BA+TP*4)/5:LINE(LX,Y1)-(RX,Y1),1:LINE(LX,Y2)-(RX,Y2),1:LINE(LX,Y3)-(RX,Y3),1:LINE(LX,Y4)-(RX,Y4),1
63 IFDI>0ANDCE=5THENLINE(127-10/DI,PE(DI,3))-(127-10/DI,PE(DI,3)-10/DI),1:LINE-(127+10/DI,PE(DI,3)-10/DI),1:LINE-(127+10/DI,PE(DI,3)),1:LINE-(127-10/DI,PE(DI,3)),1
64 IFCE=5ANDDI>0THENLINE(32,7)-(0,0),0,BF:M$="CHEST":GOSUB1
65 IFDI>0ANDCE=5THENLINE(127-10/DI,PE(DI,3)-10/DI)-(127-5/DI,PE(DI,3)-15/DI),1:LINE-(127+15/DI,PE(DI,3)-15/DI),1:LINE-(127+15/DI,PE(DI,3)-5/DI),1:LINE-(127+10/DI,PE(DI,3)),1
66 IFDI>0ANDCE=5THENLINE(127+10/DI,PE(DI,3)-10/DI)-(127+15/DI,PE(DI,3)-15/DI),1
67 IFMC<1THEN131
68 B=79+YY(DI):C=127
69 IFMC=8THENLINE(32,7)-(0,0),0,BF:M$="CHEST":GOSUB1:GOTO71
70 M$=M$(MC):A=LEN(M$)*8+8:LINE(A,15)-(0,8),0,BF:GOSUB1
71 IFDI=0THEN131
72 ONMC GOTO73,79,82,86,93,100,106,109,112,120:GOTO131
73 LINE(C-23/DI,B)-(C-15/DI,B),1:LINE-(C-15/DI,B-15/DI),1:LINE-(C-8/DI,B-30/DI),1:LINE-(C+8/DI,B-30/DI),1:LINE-(C+15/DI,B-15/DI),1:LINE-(C+15/DI,B),1:LINE-(C+23/DI,B),1
74 LINE(C,B-26/DI)-(C,B-65/DI),1:LINE(C-2/DI+.5,B-38/DI)-(C+2/DI+.5,B-38/DI),1:LINE(C-3/DI+.5,B-45/DI)-(C+3/DI+.5,B-45/DI),1:LINE(C-5/DI+.5,B-53/DI)-(C+5/DI+.5,B-53/DI),1
75 LINE(C-23/DI,B-56/DI)-(C-30/DI,B-53/DI),1:LINE-(C-23/DI,B-45/DI),1:LINE-(C-23/DI,B-53/DI),1:LINE-(C-8/DI,B-38/DI),1
76 LINE(C-15/DI,B-45/DI)-(C-8/DI,B-60/DI),1:LINE-(C+8/DI,B-60/DI),1:LINE-(C+15/DI,B-45/DI),1:LINE(C+15/DI,B-42/DI)-(C+15/DI,B-57/DI),1:LINE(C+12/DI,B-45/DI)-(C+20/DI,B-45/DI),1
77 LINE(C,B-75/DI)-(C-5/DI+.5,B-80/DI),1:LINE-(C-8/DI,B-75/DI),1:LINE-(C-5/DI+.5,B-65/DI),1:LINE-(C+5/DI+.5,B-65/DI),1:LINE-(C+5/DI+.5,B-68/DI),1:LINE-(C-5/DI+.5,B-68/DI),1:LINE-(C-5/DI+.5,B-65/DI),1
78 LINE-(C+5/DI+.5,B-65/DI),1:LINE-(C+8/DI,B-75/DI),1:LINE-(C+5/DI+.5,B-80/DI),1:LINE-(C-5/DI+.5,B-80/DI),1:PSET(C-5/DI+.5,B-72/DI):PSET(C+5/DI+.5,B-72/DI):GOTO131
79 LINE(C,B-56/DI)-(C,B-8/DI),1:LINE-(C+10/DI,B),1:LINE-(C+30/DI,B),1:LINE-(C+30/DI,B-45/DI),1:LINE-(C+10/DI,B-64/DI),1:LINE-(C,B-56/DI),1
80 LINE-(C-10/DI,B-64/DI),1:LINE-(C-30/DI,B-45/DI),1:LINE-(C-30/DI,B),1:LINE-(C-10/DI,B),1:LINE-(C,B-8/DI),1:LINE(C-10/DI,B-64/DI)-(C-10/DI,B-75/DI),1:LINE-(C,B-83/DI),1:LINE-(C+10/DI,B-75/DI),1
81 LINE-(C,B-79/DI),1:LINE-(C-10/DI,B-75/DI),1:LINE-(C,B-60/DI),1:LINE-(C+10/DI,B-75/DI),1:LINE-(C+10/DI,B-64/DI),1:GOTO131
82 LINE(C+5/DI,B-30/DI)-(C,B-25/DI),1:LINE-(C-5/DI,B-30/DI),1:LINE-(C-15/DI,B-5/DI),1:LINE-(C-10/DI,B),1:LINE-(C+10/DI,B),1:LINE-(C+15/DI,B-5/DI),1
83 LINE-(C+20/DI,B-5/DI),1:LINE-(C+10/DI,B),1:LINE-(C+15/DI,B-5/DI),1:LINE-(C+5/DI,B-30/DI),1:LINE-(C+10/DI,B-40/DI),1:LINE-(C+3/DI+.5,B-35/DI),1:LINE-(C-3/DI+.5,B-35/DI),1:LINE-(C-10/DI,B-40/DI),1:LINE-(C-5/DI,B-30/DI),1
84 LINE(C-5/DI,B-33/DI)-(C-3/DI+.5,B-30/DI),1:LINE(C+5/DI,B-33/DI)-(C+3/DI+.5,B-30/DI),1:LINE(C-5/DI,B-20/DI)-(C-5/DI,B-15/DI),1
85 LINE(C+5/DI,B-20/DI)-(C+5/DI,B-15/DI),1:LINE(C-7+DI,B-20/DI)-(C-7/DI,B-15/DI),1:LINE(C+7/DI,B-20/DI)-(C+7/DI,B-15/DI),1:GOTO131
86 LINE(C,B)-(C-15/DI,B),1:LINE-(C-8/DI,B-8/DI),1:LINE-(C-8/DI,B-15/DI),1:LINE-(C-15/DI,B-23/DI),1:LINE-(C-15/DI,B-15/DI),1:LINE-(C-23/DI,B-23/DI),1
87 LINE-(C-23/DI,B-45/DI),1:LINE-(C-15/DI,B-53/DI),1:LINE-(C-8/DI,B-53/DI),1:LINE-(C-15/DI,B-68/DI),1:LINE-(C-8/DI,B-75/DI),1:LINE-(C,B-75/DI),1
88 LINE(C,B)-(C+15/DI,B),1:LINE-(C+8/DI,B-8/DI),1:LINE-(C+8/DI,B-15/DI),1:LINE-(C+15/DI,B-23/DI),1:LINE-(C+15/DI,B-15/DI),1:LINE-(C+23/DI,B-23/DI),1
89 LINE-(C+23/DI,B-45/DI),1:LINE-(C+15/DI,B-53/DI),1:LINE-(C+8/DI,B-53/DI),1:LINE-(C+15/DI,B-68/DI),1:LINE-(C+8/DI,B-75/DI),1:LINE-(C,B-75/DI),1
90 LINE(C-15/DI,B-68/DI)-(C+15/DI,B-68/DI),1:LINE(C-8/DI,B-53/DI)-(C+8/DI,B-53/DI),1:LINE(C-23/DI,B-15/DI)-(C+8/DI,B-45/DI),1
91 LINE(C-8/DI,B-68/DI)-(C,B-60/DI),1:LINE-(C+8/DI,B-68/DI),1:LINE-(C+8/DI,B-60/DI),1:LINE-(C-8/DI,B-60/DI),1:LINE-(C-8/DI,B-68/DI),1
92 LINE(C,B-38/DI)-(C-8/DI,B-38/DI),1:LINE-(C+8/DI,B-53/DI),1:LINE-(C+8/DI,B-45/DI),1:LINE-(C+15/DI,B-45/DI),1:LINE-(C,B-30/DI),1:LINE-(C,B-38/DI),1:GOTO131
93 LINE(C-10/DI,B-15/DI)-(C-10/DI,B-30/DI),1:LINE-(C-15/DI,B-20/DI),1:LINE-(C-15/DI,B-15/DI),1:LINE-(C-15/DI,B),1:LINE-(C+15/DI,B),1:LINE-(C+15/DI,B-15/DI),1:LINE  -(C-15/DI,B-15/DI),1
94 LINE(C-15/DI,B-10/DI)-(C+15/DI,B-10/DI),1:LINE(C-15/DI,B-5/DI)-(C+15/DI,B-5/DI),1
95 LINE(C,B-15/DI)-(C-5/DI,B-20/DI),1:LINE-(C-5/DI,B-35/DI),1:LINE-(C+5/DI,B-35/DI),1:LINE-(C+5/DI,B-20/DI),1:LINE-(C+10/DI,B-15/DI),1
96 LINE(C-5/DI,B-20/DI)-(C+5/DI,B-20/DI),1:LINE(C-5/DI,B-25/DI)-(C+5/DI,B-25/DI),1:LINE(C-5/DI,B-30/DI)-(C+5/DI,B-30/DI),1
97 LINE(C-10/DI,B-35/DI)-(C-10/DI,B-40/DI),1:LINE-(C-5/DI,B-45/DI),1:LINE-(C+5/DI,B-45/DI),1:LINE-(C+10/DI,B-40/DI),1:LINE-(C+10/DI,B-35/DI),1
98 LINE(C-10/DI,B-40/DI)-(C,B-45/DI),1:LINE-(C+10/DI,B-40/DI),1
99 LINE(C-5/DI,B-40/DI)-(C+5/DI,B-40/DI),1:LINE-(C+15/DI,B-30/DI),1:LINE-(C,B-40/DI),1:LINE-(C-15/DI,B-30/DI),1:LINE-(C-5/DI+.5,B-40/DI),1:GOTO131
100 LINE(C-20/DI,79-YY(DI))-(C-20/DI,B-88/DI),1:LINE-(C-10/DI,B-83/DI),1:LINE-(C+10/DI,B-83/DI),1:LINE-(C+20/DI,B-88/DI),1:LINE-(C+20/DI,79-YY(DI)),1:LINE-(C-20/DI,79-YY(DI)),1
101 LINE(C-20/DI,B-88/DI)-(C-30/DI,B-83/DI),1:LINE-(C-30/DI,B-78/DI),1:LINE(C+20/DI,B-88/DI)-(C+30/DI,B-83/DI),1:LINE-(C+40/DI,B-83/DI),1
102 LINE(C-15/DI,B-86/DI)-(C-20/DI,B-83/DI),1:LINE-(C-20/DI,B-78/DI),1:LINE-(C-30/DI,B-73/DI),1:LINE-(C-30/DI,B-68/DI),1:LINE-(C-20/DI,B-63/DI),1
103 LINE(C-10/DI,B-83/DI)-(C-10/DI,B-58/DI),1:LINE-(C,B-50/DI),1:LINE(C+10/DI,B-83/DI)-(C+10/DI,B-78/DI),1:LINE-(C+20/DI,B-73/DI),1:LINE-(C+20/DI,B-40/DI),1
104 LINE(C+15/DI,B-85/DI)-(C+20/DI,B-78/DI),1:LINE-(C+30/DI,B-76/DI),1:LINE-(C+30/DI,B-60/DI),1
105 LINE(C,B-83/DI)-(C,B-73/DI),1:LINE-(C+10/DI,B-68/DI),1:LINE-(C+10/DI,B-63/DI),1:LINE-(C,B-58/DI),1:GOTO131
106 LINE(C+5/DI+.5,B-10/DI)-(C-5/DI+.5,B-10/DI),1:LINE-(C,B-15/DI),1:LINE-(C+10/DI,B-20/DI),1:LINE-(C+5/DI+.5,B-15/DI),1:LINE-(C+5/DI+.5,B-10/DI),1
107 LINE-(C+7/DI+.5,B-6/DI),1:LINE-(C+5/DI+.5,B-3/DI),1:LINE-(C-5/DI+.5,B-3/DI),1:LINE-(C-7/DI+.5,B-6/DI),1:LINE-(C-5/DI+.5,B-10/DI),1:LINE(C+2/DI+.5,B-3/DI)-(C+5/DI+.5,B),1:LINE-(C+8/DI,B),1
108 LINE(C-2/DI+.5,B-3/DI)-(C-5/DI+.5,B),1:LINE-(C-8/DI,B),1:PSET(C+3/DI+.5,B-8/DI):PSET(C-3/DI+.5,B-8/DI):LINE(C+3/DI+.5,B-5/DI)-(C-3/DI+.5,B-5/DI),1:GOTO131
109 LINE(127-10/DI,PE(DI,3))-(127-10/DI,PE(DI,3)-10/DI),1:LINE-(127+10/DI,PE(DI,3)-10/DI),1:LINE-(127+10/DI,PE(DI,3)),1:LINE-(127-10/DI,PE(DI,3)),1
110 LINE(127-10/DI,PE(DI,3)-10/DI)-(127-5/DI,PE(DI,3)-15/DI),1:LINE-(127+15/DI,PE(DI,3)-15/DI),1:LINE-(127+15/DI,PE(DI,3)-5/DI),1:LINE-(127+10/DI,PE(DI,3)),1
111 LINE(127+10/DI,PE(DI,3)-10/DI)-(127+15/DI,PE(DI,3)-15/DI),1:GOTO131
112 LINE(C-14/DI,B-46/DI)-(C-12/DI,B-37/DI),1:LINE-(C-20/DI,B-32/DI),1:LINE-(C-30/DI,B-32/DI),1:LINE-(C-22/DI,B-24/DI),1:LINE-(C-40/DI,B-17/DI),1:LINE-(C-40/DI,B-7/DI),1:LINE-(C-38/DI,B-5/DI),1:LINE-(C-40/DI,B-3/DI),1:LINE-(C-40/DI,B),1
113 LINE-(C-36/DI,B),1:LINE-(C-34/DI,B-2/DI),1:LINE-(C-32/DI,B),1:LINE-(C-28/DI,B),1:LINE-(C-28/DI,B-3/DI),1:LINE-(C-30/DI,B-5/DI),1:LINE-(C-28/DI,B-7/DI),1:LINE-(C-28/DI,B-15/DI),1:LINE-(C,B-27/DI),1
114 LINE(C+14/DI,B-46/DI)-(C+12/DI,B-37/DI),1:LINE-(C+20/DI,B-32/DI),1:LINE-(C+30/DI,B-32/DI),1:LINE-(C+22/DI,B-24/DI),1:LINE-(C+40/DI,B-17/DI),1:LINE-(C+40/DI,B-7/DI),1:LINE-(C+38/DI,B-5/DI),1:LINE-(C+40/DI,B-3/DI),1:LINE-(C+40/DI,B),1
115 LINE-(C+36/DI,B),1:LINE-(C+34/DI,B-2/DI),1:LINE-(C+32/DI,B),1:LINE-(C+28/DI,B),1:LINE-(C+28/DI,B-3/DI),1:LINE-(C+30/DI,B-5/DI),1:LINE-(C+28/DI,B-7/DI),1:LINE-(C+28/DI,B-15/DI),1:LINE-(C,B-27/DI),1
116 LINE(C+6/DI,B-48/DI)-(C+38/DI,B-41/DI),1:LINE-(C+40/DI,B-42/DI),1:LINE-(C+18/DI,B-56/DI),1:LINE-(C+12/DI,B-56/DI),1:LINE-(C+10/DI,B-57/DI),1:LINE-(C+8/DI,B-56/DI),1:LINE-(C-8/DI,B-56/DI),1:LINE-(C-10/DI,B-58/DI),1:LINE-(C+14/DI,B-58/DI),1
117 LINE-(C+16/DI,B-59/DI),1:LINE-(C+8/DI,B-63/DI),1:LINE-(C+6/DI,B-63/DI),1:LINE-(C+2/DI+.5,B-70/DI),1:LINE-(C+2/DI+.5,B-63/DI),1:LINE-(C-2/DI+.5,B-63/DI),1:LINE-(C-2/DI+.5,B-70/DI),1:LINE-(C-6/DI,B-63/DI),1:LINE-(C-8/DI,B-63/DI),1
118 LINE-(C-16/DI,B-59/DI),1:LINE-(C-14/DI,B-58/DI),1:LINE-(C+10/DI,B-57/DI),1:LINE-(C-12/DI,B-56/DI),1:LINE-(C-18/DI,B-56/DI),1:LINE-(C-36/DI,B-47/DI),1:LINE-(C-36/DI,B-39/DI),1:LINE-(C-28/DI,B-41/DI),1:LINE-(C-28/DI,B-46/DI),1
119 LINE-(C-20/DI,B-50/DI),1:LINE-(C-18/DI,B-50/DI),1:LINE-(C-14/DI,B-46/DI),1:GOTO242
120 LINE(C+6/DI,B-60/DI)-(C+30/DI,B-90/DI),1:LINE-(C+60/DI,B-30/DI),1:LINE-(C+60/DI,B-10/DI),1:LINE-(C+30/DI,B-40/DI),1:LINE-(C+15/DI,B-40/DI),1
121 LINE(C-6/DI,B-60/DI)-(C-30/DI,B-90/DI),1:LINE-(C-60/DI,B-30/DI),1:LINE-(C-60/DI,B-10/DI),1:LINE-(C-30/DI,B-40/DI),1:LINE-(C-15/DI,B-40/DI),1
122 LINE(C,B-25/DI)-(C+6/DI,B-25/DI),1:LINE-(C+10/DI,B-20/DI),1:LINE-(C+12/DI,B-10/DI),1:LINE-(C+10/DI,B-6/DI),1:LINE-(C+10/DI,B),1:LINE-(C+14/DI,B),1:LINE-(C+15/DI,B-5/DI),1:LINE-(C+16/DI,B),1:LINE-(C+20/DI,B),1
123 LINE-(C+20/DI,B-6/DI),1:LINE-(C+18/DI,B-10/DI),1:LINE-(C+18/DI,B-20/DI),1:LINE-(C+15/DI,B-30/DI),1:LINE-(C+15/DI,B-45/DI),1:LINE-(C+40/DI,B-60/DI),1:LINE-(C+40/DI,B-70/DI),1
124 LINE-(C+10/DI,B-55/DI),1:LINE-(C+6/DI,B-60/DI),1:LINE-(C+10/DI,B-74/DI),1:LINE-(C+6/DI,B-80/DI),1:LINE-(C+4/DI+.5,B-80/DI),1:LINE-(C+3/DI+.5,B-82/DI),1:LINE-(C+2/DI+.5,B-80/DI),1:LINE-(C,B-80/DI),1
125 LINE(C,B-25/DI)-(C-6/DI,B-25/DI),1:LINE-(C-10/DI,B-20/DI),1:LINE-(C-12/DI,B-10/DI),1:LINE-(C-10/DI,B-6/DI),1:LINE-(C-10/DI,B),1:LINE-(C-14/DI,B),1:LINE-(C-15/DI,B-5/DI),1:LINE-(C-16/DI,B),1:LINE-(C-20/DI,B),1
126 LINE-(C-20/DI,B-6/DI),1:LINE-(C-18/DI,B-10/DI),1:LINE-(C-18/DI,B-20/DI),1:LINE-(C-15/DI,B-30/DI),1:LINE-(C-15/DI,B-45/DI),1:LINE-(C-40/DI,B-60/DI),1:LINE-(C-40/DI,B-70/DI),1
127 LINE-(C-10/DI,B-55/DI),1:LINE-(C-6/DI,B-60/DI),1:LINE-(C-10/DI,B-74/DI),1:LINE-(C-6/DI,B-80/DI),1:LINE-(C-4/DI+.5,B-80/DI),1:LINE-(C-3/DI+.5,B-82/DI),1:LINE-(C-2/DI+.5,B-80/DI),1:LINE-(C,B-80/DI),1
128 LINE(C-6/DI,B-25/DI)-(C,B-6/DI),1:LINE-(C+10/DI,B),1:LINE-(C+4/DI+.5,B-8/DI),1:LINE-(C+6/DI,B-25/DI),1:LINE(C-40/DI,B-64/DI)-(C-40/DI,B-90/DI),1:LINE-(C-52/DI,B-80/DI),1:LINE-(C-52/DI,B-40/DI),1
129 LINE(C+40/DI,B-86/DI)-(C+38/DI,B-92/DI),1:LINE-(C+42/DI,B-92/DI),1:LINE-(C+40/DI,B-86/DI),1:LINE-(C+40/DI,B-50/DI),1
130 LINE(C+4/DI+.5,B-70/DI)-(C+6/DI,B-74/DI),1:LINE(C-4/DI+.5,B-70/DI)-(C-6/DI,B-74/DI),1:LINE(C,B-64/DI)-(C,B-60/DI),1
131 IFEN=1THENEN=0:RETURN
132 DI=DI+1:GOTO39
133 G=RND(-ABS(LN)-TX*10-TY*1000+IN*31.4):FORX=1TO9:FORY=1TO9:D(X,Y)=0:NEXT:NEXT:FORX=0TO10:D(X,0)=1:D(X,10)=1:D(0,X)=1:D(10,X)=1:NEXT
134 FORX=2TO8STEP2:FORY=1TO9:D(X,Y)=1:D(Y,X)=1:NEXT:NEXT:FORX=2TO8STEP2:FORY=1TO9STEP2
135 IFRND(0)>.95THEND(X,Y)=2
136 IFRND(0)>.95THEND(Y,X)=2
137 IFRND(0)>.6THEND(Y,X)=3
138 IFRND(0)>.6THEND(X,Y)=3
139 IFRND(0)>.6THEND(X,Y)=4
140 IFRND(0)>.6THEND(Y,X)=4
141 IFRND(0)>.97THEND(Y,X)=9
142 IFRND(0)>.97THEND(X,Y)=9
143 IFRND(0)>.94THEND(X,Y)=5
144 IFRND(0)>.94THEND(Y,X)=5
145 NEXT:NEXT
146 D(2,1)=0:IFIN/2=INT(IN/2)THEND(7,3)=7:D(3,7)=8
147 IFIN/2<>INT(IN/2)THEND(7,3)=8:D(3,7)=7
148 IFIN=1THEND(1,1)=8:D(7,3)=0
149 GOSUB237:RETURN
150 PRINT@480,;:M$="PRESS ENTER TO CONTINUE":GOSUB5
151 Q$=INKEY$:ON-(Q$<>CHR$(13))GOTO151:RETURN
152 M$="BLOCKED BY MOUNTAINS":GOSUB1:RETURN
153 LINE(255,167)-(168,160),0,BF:M$=";HIT ENTER":GOSUB1:GOSUB151:LINE(255,167)-(168,160),0,BF:RETURN
154 IFCM THENLINE(167,191)-(0,168),0,BF:ELSEGOSUB6:M$="COMMAND":CM=1:GOSUB1
155 X$=INKEY$:IFX$=""THEN155ELSEX=ASC(X$)
156 IFX=73THENONSGN(IN)+1GOTO173,175
157 IFX=76THENONSGN(IN)+1GOTO180,182
158 IFX=74THENONSGN(IN)+1GOTO184,186
159 IFX=75THENONSGN(IN)+1GOTO188,190
160 IFX=13THENONSGN(IN)+1GOTO191,195
161 IFX=65THENONSGN(IN)+1GOTO166,203
162 IFX=32THENM$="PASS":GOSUB1:GOTO166
163 IFX=83THEN236
164 IFX=80THENIFPA=1THENPA=0:M$="UNPAUSED":GOSUB1:GOTO154ELSEPA=1:M$="PAUSED":GOSUB1:GOTO154
165 GOTO194
166 P(0)=P(0)-1+SGN(IN)*.9:IFP(0)<0THENGOSUB6:C(0)=0:M$="YOU HAVE STARVED":GOSUB1:GOTO168
167 P(0)=INT(P(0)*10)/10
168 IFC(0)<=0THEN277
169 IFIN>0THENGOSUB245:IFC(0)<=0THEN277
170 IFIN=0THENGOSUB28
171 IFIN>0THENGOSUB38
172 LINE(255,191)-(184,168),0,BF:M$=STR$(INT(P(0)*10+0.5)/10):GOSUB1:DRAW"BM184,176":M$=STR$(C(0)):GOSUB1:DRAW"BM184,184":M$=STR$(C(5)):GOSUB1:GOTO154
173 M$="NORTH":GOSUB1:IFT(TX,TY-1)=1THENGOSUB152:GOTO166
174 TY=TY-1:GOTO166
175 IFD(PX+DX,PY+DY)<>1THENIFD(PX+DX,PY+DY)<10THENPX=PX+DX:PY=PY+DY
176 M$="FORWARD":GOSUB1:IFD(PX,PY)=2THENM$="A TRAP":GOSUB1:C(0)=C(0)-INT(RND(0)*IN+3):MR=1:IN=IN+1:M$="FALL TO LEVEL"+STR$(IN):GOSUB1:GOSUB133:CM=0:GOSUB6:GOTO166
177 Z=0:IFD(PX,PY)=5THEND(PX,PY)=0:Z=INT(RND(0)*5*IN+IN):M$=MID$(STR$(Z),2)+" GP":GOSUB1:C(5)=C(5)+Z
178 IFZ>0THENZ=INT(RND(0)*6):M$="AND A "+W$(Z):GOSUB1:P(Z)=P(Z)+1:GOTO218
179 GOTO166
180 M$="EAST":GOSUB1:IFT(TX+1,TY)=1THENGOSUB152:GOTO166
181 TX=TX+1:GOTO166
182 M$="RIGHT":GOSUB1:IFDX THENDY=DX:DX=0:GOTO166
183 DX=-DY:DY=0:GOTO166
184 M$="WEST":GOSUB1:IFT(TX-1,TY)=1THENGOSUB152:GOTO166
185 TX=TX-1:GOTO166
186 M$="LEFT":GOSUB1:IFDX THENDY=-DX:DX=0:GOTO166
187 DX=DY:DY=0:GOTO166
188 M$="SOUTH":GOSUB1:IFT(TX,TY+1)=1THENGOSUB152:GOTO166
189 TY=TY+1:GOTO166
190 M$="TURN AROUND":GOSUB1:DX=-DX:DY=-DY:GOTO166
191 IFT(TX,TY)=3THENGOSUB304:GOSUB309:GOSUB7:GOTO166
192 IFT(TX,TY)=4ANDIN=0THENM$="GO DUNGEON":GOSUB1:M$="PLEASE WAIT":GOSUB1:IN=1:GOSUB133:DX=1:DY=0:PX=1:PY=1:GOTO166
193 IFT(TX,TY)=5THEN282
194 M$="HUH":GOSUB1:GOTO154
195 IFD(PX,PY)<>7ANDD(PX,PY)<>9THEN197
196 IN=IN+1:M$="GO DOWN TO LEVEL"+STR$(IN):GOSUB1:GOSUB133:MR=1:GOTO166
197 IFD(PX,PY)<>8THENM$="HUH":GOSUB1:GOTO166
198 IFIN=1THENM$="LEAVE DUNGEON":GOSUB1:IN=0:GOTO200
199 IN=IN-1:M$="GO UP TO LEVEL"+STR$(IN):GOSUB1:GOSUB133:MR=1
200 IFIN=0THENM$="GAINED"+STR$(LK)+" HP":GOSUB1:C(0)=C(0)+LK:LK=0
201 GOTO166
202 M$="NOT OWNED":GOSUB1:GOTO203
203 MN=0:DA=0:M$="ATTACK WEAPON":GOSUB1:GOSUB4:CM=0:GOSUB6
204 IFQ$="R"THENDA=10:M$="RAPIER":GOSUB1:IFP(1)<1THEN202
205 IFQ$="A"THENDA=5:M$="AXE":GOSUB1:IFP(2)<1THEN202
206 IFQ$="S"THENDA=1:M$="SHIELD":GOSUB1:IFP(3)<1THEN202
207 IFQ$="B"THENDA=4:M$="BOW":GOSUB1:IFP(4)<1THEN202
208 IFQ$="M"THENM$="MAGIC AMULET":GOSUB1:GOTO225
209 IFQ$="B"ORQ$="R"THENIFPT$="M"THENM$="A MAGE CANNOT":GOSUB1:GOTO203
210 IFDA=0THENM$="HANDS":GOSUB1
211 IFDA=5ORDA=4THEN220
212 MN=D(PX+DX,PY+DY)/10:MN=INT(MN)
213 IFMN<1ORC(2)-RND(0)*25<IN+MN THENM$="MISS":GOSUB1:GOTO218
214 M$="HIT":GOSUB1:DA=(RND(0)*DA+C(1)/5):MZ(MN,1)=MZ(MN,1)-DA:Z=INT(MZ(MN,1)):IFZ<1THEN216
215 M$=M$(MN)+" HP"+STR$(Z):GOSUB1
216 IFMZ(MN,1)<1THENM$=LEFT$(M$(MN)+" KILLED",20):GOSUB1:DA=INT(MN+IN):M$="GP WON"+STR$(DA):GOSUB1:C(5)=INT(C(5)+DA):D(ML(MN,0),ML(MN,1))=D(ML(MN,0),ML(MN,1))-10*MN:MZ(MN,0)=0
217 LK=LK+INT(MN*IN/2):IFMN=TA THENTA=-TA
218 IFPA=1THENGOSUB153
219 GOTO166
220 IFDA=5THENM$="THROW OR SWING":GOSUB1:GOSUB4:CM=0:GOSUB6:IFQ$<>"T"THENM$="SWING":GOSUB1:GOTO212
221 IFDA=5THENM$="THROW":GOSUB1:P(2)=P(2)-1
222 FORY=1TO5:IFPX+DX*Y<1ORPX+DX*Y>9ORPY+DY*Y>9ORPY+DY*Y<0THEN213
223 MN=D(PX+DX*Y,PY+DY*Y):MN=INT(MN/10):IFMN>0THEN213
224 NEXT:GOTO213
225 IFP(5)<1THEN202
226 IFPT$="F"THENQ=INT(RND(0)*4+1):GOTO229
227 M$="1 UP 2 DOWN LADDER":GOSUB1:M$="3 KILL 4 BAD ;CHOICE":GOSUB1:GOSUB4:CM=0:GOSUB6:Q=VAL(Q$):IFQ<1ORQ>4THEN227
228 IFRND(0)>.75THENM$="LAST CHARGE":GOSUB1:P(5)=P(5)-1:GOSUB6
229 ONQ GOTO230,231,232,233
230 M$="UP":GOSUB1:D(PX,PY)=8:GOTO166
231 M$="DOWN":GOSUB1:D(PX,PY)=7:GOTO166
232 M$="MAGIC ATTACK":GOSUB1:DA=10+IN:GOTO222
233 ONRND(3)GOTO234,235:M$="YOU HAVE BEEN":GOSUB1:M$="TURNED INTO A TOAD":GOSUB1:FORT=1TO4:C(T)=3:NEXT:GOTO218
234 M$="YOU HAVE BECOME":GOSUB1:M$="A LIZARD MAN":GOSUB1:FORY=0TO4:C(Y)=INT(C(Y)*2.5):NEXT:GOTO218
235 M$="BACKFIRE":GOSUB1:C(0)=C(0)/2:GOTO218
236 GOSUB304:GOSUB150:GOSUB7:GOTO166
237 NM=0:FORX=1TO10:MZ(X,0)=0:MZ(X,1)=X+3+IN:IFX-2>IN ORRND(0)>.4THEN241
238 ML(X,0)=INT(RND(0)*9+1):ML(X,1)=INT(RND(0)*9+1):IFD(ML(X,0),ML(X,1))THEN238
239 IFML(X,0)=PX ANDML(X,1)=PY THEN238
240 D(ML(X,0),ML(X,1))=X*10:MZ(X,0)=1:NM=NM+1:MZ(X,1)=X*2+IN*2*LP
241 NEXT:RETURN
242 LINE(C-28/DI,B-41/DI)-(C+30/DI,B-55/DI),1:LINE(C+28/DI,B-58/DI)-(C+22/DI,B-56/DI),1:LINE-(C+22/DI,B-53/DI),1:LINE-(C+28/DI,B-52/DI),1:LINE-(C+34/DI,B-54/DI),1:LINE-(C+20/DI,B-50/DI),1:LINE-(C+26/DI,B-47/DI),1
243 LINE(C+10/DI,B-58/DI)-(C+10/DI,B-61/DI),1:LINE-(C+4/DI,B-58/DI),1:LINE(C-10/DI,B-58/DI)-(C-10/DI,B-61/DI),1:LINE-(C-4/DI,B-58/DI),1:LINE(C+40/DI,B-9/DI)-(C+50/DI,B-12/DI),1:LINE-(C+40/DI,B-7/DI),1
244 LINE(C-8/DI,B-25/DI)-(C+6/DI,B-7/DI),1:LINE-(C+28/DI,B-7/DI),1:LINE-(C+28/DI,B-9/DI),1:LINE-(C+20/DI,B-9/DI),1:LINE-(C+6/DI,B-25/DI),1:GOTO131
245 FORMM=1TO10:IFMZ(MM,0)=0THEN276
246 RA=SQR((PX-ML(MM,0))^2+(PY-ML(MM,1))^2)
247 IFMZ(MM,1)<IN*LP THEN250
248 IFRA<1.3THEN265
249 IFMM=8ANDRA<3THEN276
250 X1=SGN(PX-ML(MM,0)):Y1=SGN(PY-ML(MM,1))
251 IFMZ(MM,1)<IN*LP THENX1=-X1:Y1=-Y1
252 IFY1=0THEN255
253 D=D(ML(MM,0),ML(MM,1)+Y1):IFD=1ORD>9ORD=2THEN255
254 X1=0:GOTO257
255 Y1=0:IFX1=0THEN257
256 D=D(ML(MM,0)+X1,ML(MM,1)):IFD=1ORD>9ORD=2THENX1=0:GOTO261
257 D(ML(MM,0),ML(MM,1))=D(ML(MM,0),ML(MM,1))-10*MM
258 IFML(MM,0)+X1=PX ANDML(MM,1)+Y1=PY THEN276
259 ML(MM,0)=ML(MM,0)+X1:ML(MM,1)=ML(MM,1)+Y1
260 D(ML(MM,0),ML(MM,1))=D(ML(MM,0),ML(MM,1))+10*MM
261 IFX1<>0ORY1<>0THEN276
262 IFMZ(MM,1)<IN*LP THENIFRA<1.3THEN265
263 IFMZ(MM,1)<IN*LP THENMZ(MM,1)=MZ(MM,1)+MM+IN
264 GOTO276
265 IFMM=2ORMM=7THEN271
266 CM=0:GOSUB6:M$=LEFT$(M$(MM),9)+" ATTACKING":GOSUB1
267 IFRND(0)*20-SGN(P(3))-C(3)+MM+IN<0THENM$="MISS":GOSUB1:GOTO269
268 M$="HIT":GOSUB1:C(0)=C(0)-INT(RND(0)*MM+IN)
269 IFPA=1THENGOSUB153:CM=0:GOSUB6
270 GOTO276
271 IFRND(0)<.5THEN266
272 IFMM=7THENP(0)=INT(P(0)/2):M$="GREMLIN STOLE FOOD":GOSUB1:GOTO269
273 IFP(0)+P(1)+P(2)+P(3)+P(4)+P(5)<1THEN276
274 Z=INT(RND(0)*6):IFP(Z)<1THEN274
275 M$=LEFT$("THIEF STOLE "+W$(Z),20):GOSUB1:P(Z)=P(Z)-1:GOTO269
276 NEXT:RETURN
277 FORG=1TO2500:NEXT:SCREEN0,0:CLS:PRINTTAB(5)"WE MOURN THE PASSING OF":IFLEN(PN$)>12THENPN$=""
278 IFPN$=""THENPN$="THE PEASANT"
279 PN$=PN$+" AND HIS COMPUTER"
280 PRINTTAB((32-LEN(PN$))/2)PN$:PRINT:M$="FOR RESURRECTION, PRESS R":GOSUB5
281 IFINKEY$="R"THENRUNELSE281
282 SCREEN0,0:CLS:GO=0:IFPN$<>""THEN289
283 PRINT"WELCOME PEASANT TO THE HALLS OF":PRINT"MIGHTY LORD BRITISH!"
284 PRINT:INPUT"WHAT IS THY NAME";PN$
285 PRINT:PRINT"THY FIRST TASK IS TO GO TO":PRINT"THE DUNGEONS AND RETURN AFTER":TA=INT(C(4)/3):IFTA>10THENTA=10
286 PRINT"KILLING A(N) "M$(TA)
287 PRINT:PRINT"ALSO I, BRITISH, HAVE INCREASED":PRINT"EACH OF THY ATTRIBUTES BY ONE!"
288 GOSUB150:FORX=0TO5:C(X)=C(X)+1:NEXT:CLS:GOTO166
289 IFTA>0THENPRINTPN$"!?":PRINT"WHY HAST THOU RETURNED?":PRINT"THY QUEST IS TO KILL A(N)":PRINTM$(TA):GOSUB150:CLS:GOTO166
290 PRINT"THY QUEST IS ACOMPLISHED!":IFABS(TA)=10THEN293
291 PRINT"BUT IT IS STILL NOT ENOUGH TO":PRINT"BECOME A KNIGHT. THOU MUST NOW":TA=ABS(TA)+1:PRINT"KILL A(N) "M$(TA)
292 GOTO287
293 PRINT:PN$="LORD "+PN$:PRINTPN$","
294 PRINT"YOU HAVE EARNED A KNIGHTHOOD!"
295 PRINT:IFLP=10THENPRINT"CONGRATULATIONS!":GOTO288
296 PRINT"BE THOU FOOLHEARTY ENOUGH TO":PRINT"TRY DIFFICULTY LEVEL"STR$(LP+1)"?":GOTO288
297 SCREEN0,0:CLS:GO=0:INPUT"TYPE THY LUCKY NUMBER";Q$:LN=VAL(Q$):PRINT:INPUT"LEVEL OF PLAY (1-10)";Q$:LP=INT(VAL(Q$)):IFLP<1ORLP>10THEN297
298 G=RND(-ABS(LN)):FORX=0TO5:READC$(X):NEXT:FORX=1TO10:READM$(X):NEXT
299 DATA"HP ","STR","DEX","STA","WIS","GP "
300 DATA"SKELETON","THIEF","GIANT RAT","ORC","VIPER","CARRION CRAWLER","GREMLIN","MIMIC","DAEMON","BALROG","FOOD","RAPIER","AXE","SHIELD","BOW AND ARROWS","MAGIC AMULET"
301 FORX=0TO5:C(X)=INT(SQR(RND(0))*21+4):NEXT:PRINT@128,;:FORX=0TO5:PRINTC$(X)C(X):NEXT:PRINT@352,"PLAY THOU WITH THESE QUALITIES?":GOSUB4:IFQ$<>"Y"THEN301
302 PRINT@352,"BE THOU A FIGHTER OR A MAGE?":GOSUB4:IFQ$<>"M"ANDQ$<>"F"THEN302
303 PT$=Q$:DIMW$(5):FORX=0TO5:READW$(X):NEXT:GOSUB304:GOSUB309:RETURN
304 SCREEN0,0:CLS:GO=0
305 PRINT@2,"STAT'S":PRINT@20,"WEAPONS":FORX=0TO5:PRINT@(X+1)*32,C$(X)C(X)TAB(11)RIGHT$("     "+STR$(INT(P(X))),5)TAB(17)W$(X);:NEXT306 PRINT@480,;:M$="Q=QUIT":GOSUB5:PRINT"                         ";
307 PRINT@256,"PRICE"TAB(9)"DAMAGE"TAB(17)"ITEM"
308 PRINT"1 FOR 10"TAB(9)"N/A"TAB(17)W$(0):PRINT"8"TAB(9)"1-10"TAB(17)W$(1):PRINT"5"TAB(9)"1-5"TAB(17)W$(2):PRINT"6"TAB(9)"1"TAB(17)W$(3):PRINT"3"TAB(9)"1-4"TAB(17)W$(4):PRINT"15"TAB(9)"?"TAB(17)W$(5):RETURN
309 PRINT@224,"":PRINT@224,;:M$="WHAT DOST THOU WANT? ":GOSUB5:GOSUB4:IFQ$="Q"THENCLS:GO=0:RETURN
310 Z=-1
311 IFQ$="F"THENPRINT"FOOD":Z=0:P=1
312 IFQ$="R"THENPRINT"RAPIER":Z=1:P=8
313 IFQ$="A"THENPRINT"AXE":Z=2:P=5
314 IFQ$="S"THENPRINT"SHIELD":Z=3:P=6
315 IFQ$="B"THENPRINT"BOW":Z=4:P=3
316 IFQ$="M"THENPRINT"AMULET":Z=5:P=15
317 PRINT@487,;:IFZ=-1THENPRINT"WE DON'T HAVE THAT.     ";:GOTO309
318 IF(Q$="R"ORQ$="B")ANDPT$="M"THENPRINT"MAGES CAN'T USE THAT!   ";:GOTO309
319 IFC(5)-P<0THENPRINT"THOU CANNOT AFFORD THAT.";:GOTO309
320 IFZ=0THENP(Z)=P(Z)+9
321 P(Z)=P(Z)+1:C(5)=C(5)-P:GOSUB305:GOTO309
