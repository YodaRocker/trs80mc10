0 MC=15360
1 REM *** FROM DRAGON VERSION ***
2 REM *** ABM COMMAND FOR TRS-80 MC-10 WITH MCX ***
10 DIMSH$(2)
20 SH$(1)=CHR$(128)+CHR$(128)+CHR$(143)+CHR$(128):SH$(2)=CHR$(128)+CHR$(141+96)+CHR$(140+96)+CHR$(142+96)+CHR$(128)
30 EX=0:SC=0:A2=0:SH=352:HS=SH:LV=3:E=1:F=5:S=-1:SK=2:IFCL$<>""THEN60
40 CL$=CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128):DIMEX(2)
50 EX$(1)=CHR$(136+16)+CHR$(141+112):EX$(2)=CHR$(132+32)+CHR$(137+96)+CHR$(140+64)
60 A=RND(3):PO=RND(11)+10:OP=PO:IFPO=BPTHEN60
70 IFA=1THENAN=31:AN$=CHR$(134+64)
80 IFA=2THENAN=32:AN$=CHR$(138+64)
90 IFA=3THENAN=33:AN$=CHR$(137+64)
100 IFA2>0THEN190
110 A=RND(3):PB=RND(11)+10:BP=PB:IFPB=OPTHEN110
120 IFA=1THENAB=31:AB$=CHR$(134+16)
130 IFA=2THENAB=32:AB$=CHR$(138+16)
140 IFA=3THENAB=33:AB$=CHR$(137+16)
150 A2=2
160 IFH=3THEN190
170 IFY=0THENY=3:GOSUB810
180 GOSUB470
190 A$=INKEY$:IFA$="S"THENQ=E:GOTO210
191 IFA$="A"THENQ=S:GOTO210
200 IFA$="W"THEN850
210 SH=SH+Q:PLAY"O3T255V8CG"
220 IFSH<352ORSH>380THENSH=HS
230 PRINT@SH,SH$(1);:PRINT@SH+32,SH$(2);:IFA$=" "THEN280
240 R=R+E:IFR<FTHENHS=SH:GOTO190
250 R=0:PO=PO+AN:PB=PB+AB:IFPB>352ORPO>352THEN540
260 PRINT@PO,AN$;:PRINT@PB,AB$;
270 HS=SH:GOTO190
280 PLAY"V1":FORA=SH+2-32TOSH+2-320STEP-32
290 IFA=POORA=PBTHEN320
291 PRINT@A,CHR$(131+80);:IFA<SH+2-32THENPRINT@A+32,CHR$(128);
300 PLAY"O5L255V+V+V+C":NEXTA
310 PRINT@SH+2-320,CHR$(128);:SOUND200,2:GOTO250
320 IFA=PBANDA<>POTHEN410
330 PRINT@PO-1,EX$(2);:PRINT@PO+32,EX$(1);:PRINT@PO+65,CHR$(134+112);
340 EX=EX+10:SC=SC+10:IFEX>2000THENEX=0:HT=4:LV=LV+1:GOSUB790
350 PRINT@1," SCORE:";SC;
360 PLAY"O4V31T255CFCFCFCACACADGDGDGDBDBDB"
370 PRINT@PO-1,CL$;:PRINT@PO+32,CL$;:PRINT@PO+64,CL$;
380 FORA=POTO32STEP-AN
390 PRINT@A,CHR$(128);:NEXTA
400 GOTO60
410 PRINT@PB-1,EX$(1);:PRINT@PB+32,EX$(2);:PRINT@PB+65,CHR$(134+112);
420 EX=EX+25:SC=SC+25:IFEX>1000THENEX=0:LV=LV+1:HT=4:GOSUB790
430 PRINT@1," SCORE :";SC;:PLAY"O2T255V31CO3CO4CO5CO2EO3EO4EO5EO2GO3GO4GO5GO1CO2CO3CO4CO1CO2CO3CO4C"
440 PRINT@PB-1,CL$;:PRINT@PB+32,CL$;:PRINT@PB+65,CL$;
450 FORA=PBTO32STEP-AB:PRINT@A,CHR$(128);:NEXT
460 GOTO110
470 H=3:CLS0:PRINT@0,STRING$(32,252);
480 PRINT@1," SCORE :";SC;
490 PRINT@20," LIVES ";LV;:IFHT=4THENHT=0:RETURN
500 FORA=1440TO1535
510 READB:POKEMC+A,B:NEXT
520 PRINT@SH,SH$(1);:PRINT@SH+32,SH$(2);
530 PRINT@238,"READY";:PLAY"O3T4L5V30CT8CT6CO3T4GT8GT6GT4ET8ET6ET3GT255DFGDFGDFGDFGDFGDFG":PRINT@238,CL$;:RETURN
540 LV=LV-1
550 PRINT@20," LIVES :";LV;
560 PRINT@SH+2,CHR$(135);:PLAY"O2L2T5V20C":PRINT@SH+2,CHR$(131);
561 PLAY"C":PRINT@SH+2,CHR$(129);:PLAY"T10C":PRINT@SH+2,CHR$(128);:PLAY"T5C"
570 PRINT@SH+33,CHR$(133+96);:PLAY"D#":PRINT@SH+33,CHR$(129+96);
571 PLAY"T10D":PRINT@SH+33,CHR$(128);:PLAY"T5D"
580 PRINT@SH+34,CHR$(132+96);:PLAY"T10C":PRINT@SH+34,CHR$(128);
581 PLAY"T5C":PRINT@SH+35,CHR$(134+96);:PLAY"O1T10B"
582 PRINT@SH+35,CHR$(132+96);:PLAY"O2T3C":PRINT@SH+35,CHR$(128);:PLAY"O1C"
590 CLS5:PRINTSTRING$(32,252);:PRINT@8," TORPEDO ATTACK ";
600 IFLV<1THENPRINT@132," YOUR GAME IS OVER ";:PRINT@292," YOUR SCORE WAS:";SC;:GOTO730
610 PRINT@65," LIVES LEFT:";LV;:PRINT@129," SCORE:";SC;:PRINT@201,"news";:PRINT@206,"headlines";
620 POKEMC+1224,32:POKEMC+1229,32:POKEMC+1239,32
630 PRINT@256," ";PL$;" WIPED OUT IN":PRINT" DEVASTATING TORPEDO RAID"
640 PRINT@224,STRING$(32,"#");:PRINT@320,STRING$(32,"#");
650 PRINT@384," PLEASE TYPE IN THE PLACE YOU    WISH TO DEFEND";:INPUTPL$
660 PRINT@486," HIT A KEY TO START ";
670 A$=INKEY$:IFA$=""THEN670
680 A2=0:H=0:SH=352:HS=SH:RESTORE:GOTO60
690 DATA128,128,223,128,223,128,128,128,211,223,128,128,128,128,128,223,223,211,223,128,128
691 DATA128,128,128,128,128,223,218,128,128,128,223
700 DATA223,211,223,128,223,223,128,213,223,223,223,128,128,211,128,223,223,223,223,211,211
701 DATA211,223,211,211,211,223,218,128,128,223,223
710 DATA223,223,223,223,223,223,220,221,223,223,223,211,211,223,223,223,223,223,223,163,163
711 DATA163,163,163,163,163,223,219,211,211,223,223
720 GOTO720
730 PRINT@487," PRESS THE 'G' KEY ";:SOUND90,1:A$=INKEY$:IFA$<>"G"THENPRINT@487," press the 'g' key ";:SOUND130,1:GOTO730
740 Y=0:IFSC<HI+1THENH=0:RESTORE:GOTO30
750 CLS3:PRINTSTRING$(96,"#");:PRINT@32,"#well#done##highest#score#today#";:PRINT@416,STRING$(64,"#");
760 PRINT@128,"PREVIOUS HIGH SCORE :";HI;:PRINT@192,"   YOUR SCORE :";SC;:HI=SC:PRINT@352," WHAT'S YOUR NAME CHAMP";
770 PLAY"O4L10T20CO1CO4CO1CO4DO1DO4DO1DO4EO1EO4EO1EO4FO1FO4FO1FO4GO1GO4GO1GO4EO1EO4EO1EO4CO1CO4CO1CO4CO1C":INPUTMN$
780 PLAY"O1L5T20V20CCCO2CO1BAGFEDCEGO2CEGCEGO3CEGCEGO4CEGCEGCEGCEGO5L1C":H=0:GOTO30
790 PRINT@20,"extra life";:PLAY"O5T255CDCFCACDCFCACDCFCAO3T5L20BAGFEFGFEDCO2BAGFGAGFEDC"
800 PRINT@20," LIVES :";LV;:RETURN
810 CLS0:PRINT"-------TORPEDO----ATTACK--------";:PRINT@96," HIGHEST SCORE TODAY :";HI;
811 PRINT@128," BY : ";MN$;" ";:PRINT@224,"PRESS ANY KEY";
820 PRINT@430,SH$(1);:PRINT@462,SH$(2);:FORA=95TO352STEP31:PRINT@A,CHR$(198);:NEXTA
821 PRINT@336,CHR$(211);:A$=INKEY$:IFA$=""THEN820
830 CLS2:PRINT@8," TORPEDO ATTACK ";:PRINT@170," levels  ";:PRINT@226," 1. THAT'S BAD MAN! ";
831 PRINT@258," 2. GULP ! ";:PRINT@290," 3. KAMIKAZE ";
840 PRINT@390," WHAT LEVEL (1-3)";:INPUTA:F=(11-(A*3))*4:SOUND58,1:SOUND187,1
841 CLS3:PRINT@128," PLEASE TYPE IN THE PLACE YOU    WISH TO DEFEND";:INPUTPL$:SOUND240,1:SOUND1,2:RESTORE:RETURN
850 A$=INKEY$:IFA$=""THEN850
851 GOTO210 
