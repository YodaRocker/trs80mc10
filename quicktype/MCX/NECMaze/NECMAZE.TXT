110 DE=3:RO=4:CO=4
120 MX=CO*2+2:MY=RO*2+2:PX=MX-1:PY=MY-1:PD=0
130 DIM DX(3),DY(3),MZ(MX,MY),SX(DE+1,3)
140 DX(0)=0:DX(1)=1:DX(2)=0:DX(3)=-1
150 DY(0)=-1:DY(1)=0:DY(2)=1:DY(3)=0
160 CLS:I=RND(-(PEEK(9)*256+PEEK(10)))
170 PRINT "*SCREEN COORD";DE
180 SX(0,1)=0:SX(0,2)=191
190 FOR I=1 TO DE+1
200 Z=(I-0.5)+0.125:X=INT(96*0.5/Z)
205 PRINT I;(96*0.5/Z)
210 T=95-X*3:IF T<0 THEN T=0
220 SX(I,0)=T
230 SX(I,1)=95-X
240 SX(I,2)=96+X
250 T=96+X*3:IF 191<T THEN T=191
260 SX(I,3)=T
280 NEXT
300 REM
310 PRINT "*GENERATE MAZE";MX;MY
320 FOR X=0 TO MX:MZ(X,0)=1:MZ(X,MY)=1:NEXT
330 FOR Y=0 TO MY:MZ(0,Y)=1:MZ(MX,Y)=1:NEXT
340 FOR C=1 TO CO:X=C*2:N=3-(C=1):PRINT C;
350 FOR R=1 TO RO:Y=R*2:MZ(X,Y)=1
360 D=INT(RND(0)*N):IF MZ(X+DX(D),Y+DY(D)) THEN 360
370 MZ(X+DX(D),Y+DY(D))=1
380 NEXT R,C:PRINT
390 REM
400 REM *DRAW
410 PMODE 2,1:SCREEN 1,1:PCLS
415 PD=PD-4*INT(PD/4)
420 FOR I=DE TO 1 STEP -1
430 X=PX+DX(PD)*I:Y=PY+DY(PD)*I
440 X0=SX(I,0):X1=SX(I,1):X2=SX(I,2):X3=SX(I,3)
450 X4=SX(I+1,1):X5=SX(I+1,2)
460 REM *LEFT
470 CX=X+DY(PD):CY=Y-DX(PD):GOSUB 920:IF CP=0 GOTO 520
490 LINE(X1,X1)-(X4,X4),PSET:LINE-(X4,X5) ,PSET:LINE-(X1,X2) ,PSET
500 LINE(X0,X1)-(X1,X2),PSET,B
510 REM *RIGHT
520 CX=X-DY(PD):CY=Y+DX(PD):GOSUB 920:IF CP=0 GOTO 570
540 LINE(X2,X1)-(X5,X4) ,PSET:LINE-(X5,X5) ,PSET:LINE-(X2,X2) ,PSET
550 LINE(X2,X1)-(X3,X2), PSET,B
560 REM *CENTER
570 CX=X:CY=Y:GOSUB 920:IF CP=0 GOTO 590
580 LINE(X1,X1)-(X2,X2), PSET,B
590 NEXT
600 X1=SX(0,1):X2=SX(0,2):X4=SX(1,1):X5=SX(1,2)
610 REM *LEFT 0
620 CX=PX+DY(PD):CY=PY-DX(PD):GOSUB 920:IF CP=0 GOTO 660
640 LINE(X1,X1)-(X4,X4) ,PSET:LINE-(X4,X5) ,PSET:LINE-(X1,X2) ,PSET
650 REM *RIGHT 0
660 CX=PX-DY(PD):CY=PY+DX(PD):GOSUB 920:IF CP=0 THEN 690
680 LINE(X2,X1)-(X5,X4) ,PSET:LINE-(X5,X5) ,PSET:LINE-(X2,X2) ,PSET
690 REM LOCATE 5,0:PRINT "X=";PX;" Y=";PY;" ";MID$("NESW",PD+1,1)
710 IF PX=1 AND PY=1 THEN 960

800 REM
810 REM *MOVE
820 K$=INKEY$:IF K$="" GOTO 820
830 IF K$="D" OR K$="S" THEN PD=PD+1:GOTO 410
840 IF K$="A" THEN PD=PD-1:GOTO 410
850 IF K$<>"W" THEN 820
860 IF MZ(PX+DX(PD),PY+DY(PD)) GOTO 820
870 PX=PX+DX(PD):PY=PY+DY(PD)
890 GOTO 410

900 REM
910 REM *CHECK POS
920 IF CX<0 OR MX<CX OR CY<0 OR MY<CY THEN CP=0:RETURN
930 CP=MZ(CX,CY):RETURN
940 REM
950 REM *GOAL
960 LINE(SX(1,1),SX(2,1))-(SX(1,2),SX(2,2)),PSET,BF
970 FOR I=1 TO 5:PLAY "O3CEGO5C":NEXT
980 PMODE0,1:CLS:PRINT:PRINT "CONGRATULATIONS!"
990 END
