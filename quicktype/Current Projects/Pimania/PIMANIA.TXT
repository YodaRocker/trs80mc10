10 PCLEAR4:PMODE4,1:CLEAR2520,&H7D00:SCREEN0,1:DIMQ(20),O(20),A(0,1),B(0,1),C(0,1),D(0,1),E(0,1),F(0,1),H(0,1),I(0,1),J(0,1),K(0,1),L(0,1),M(0,1),N(0,1),R(0,1),P(0,1),T(1,2),V(2,5),W(2,5),S(10),P$(2),G(0,1)
20 CQ=PEEK(1024):GOSUB3210:IFCQ=127THEN3170
30 POKE&HFFD7,0:CLS:INPUT"BEFORE WE START THE QUEST PLEASETELL ME YOUR NAME.:";S$:IFLEN(S$)=0ORLEN(S$)>13THEN30
40 FORX=1TO19 STEP2: LETQ(X)=RND(4):NEXT
50 FORX=1TO20:O(X)=Q(X):NEXT
60 FORI=1TO10:S(I)=0:NEXT
70 IFGA<>0THEN100
80 SCREEN0,1:POKE&HFFD2,0:POKE&HFFD0,0:POKE&HFFCE,0:POKE&HFFCD,1:POKE&HFFCB,0:POKE&HFFC8,0:POKE&HFFC6,0
90 FORI=1TO999:FORJ=1TO5:IFINKEY$=""THEN NEXTJ,I
100 GA=6
110 SCREEN0,1
130 GOSUB3560
140 CLS:PRINT@128,CI$:CI$=""
150 FORI=207TO15STEP-32:PRINT@I,"#";:SOUND208-I,7:IF I<>15 THENPRINT@I," ";:NEXT
160 FORI=1TO999:NEXT
170 PRINT@352,;:LINEINPUT"   A KEY TURNS THE LOCK: ";A$
180 IFA$<>"PI"THENPRINT@352:GOTO170
190 GOSUB3450
200 CLS:ONGA GOSUB2080,2090,2100,2110,2120,2130,2140,2180,2190,2200,2210,2220,2230,2270,2280,2290,2300,2310,2320,2330,2340,2350
205 GOSUB9000
210 F=0
220 FORX=1TO20
230 IFO(X)<>GA THEN310
240 IFF THEN300
250 IFGA<=4THENPRINT,," WITHIN EASY REACH YOU CAN SEE"
260 IFGA=5ORGA=6THENPRINT,," THIS IS WHERE YOU LEFT...."
270 IFGA=7THENPRINT" HERE YOU HAVE LAID TO REST"
280 IFGA>7THENPRINT,," EARLIER YOU DUMPED....."
290 F=1
300 PRINT" "O$(X),
310 NEXT
312 IF( GA=13 OR GA=18 OR GA=14 OR GA=7 )THEN FORI=1TO5000:NEXTI
315 GOSUB9000
320 T=1
330 GOTO920
340 PRINT:PRINT" YOU HAVE:",
350 F=1
360 FORX=1TO20
370 IFO(X)>=0THEN400
380 PRINT" "O$(X),
390 F=0
400 NEXT
410 IFF THENPRINT" NOTHING",," WITH YOU"
420 PRINT@416,"IT'S UP TO YOU "S$"!":LINEINPUT":";K$:IFK$=""THEN200
430 Y=0:Z$=K$:CLS:PRINT@191," COMMAND <"K$">"
440 IFK$="YES"THENPRINT"YES WE HAVE NO BANANAS!":PLAY"O3T2L4GL8DL4EL8DCL4O-BO+L2D":FORX=1TO999:NEXT:GOTO200
450 IFK$="NO"THENPRINT"THERE'S NO BUSINESS","LIKE SHOW-BUSINESS!":PLAY"O2T3L4AL2O3DO2L4DP199DL8P210DL2AL4DP199DT2":FORX=1TO999:NEXT:GOTO200
460 IFLEN(K$)<4THEN570
470 FORL=1TO29STEP4
480 X=INSTR(1,K$,MID$(Q$,L,4)):IFX>0THEN510
490 NEXT
500 GOTO570
510 I$=MID$(K$,X,4):IFI$="WHER"ORI$="WHAT"ORI$="WHO "THENPRINT"THAT'S THE WHOLE POINT OF THIS","QUEST YOU SILLY LITTLE SEEKER!":FORX=0TO49:PLAY"O4L49BO5CO2":NEXT:GOTO200
520 IFI$="LOOK"THENPRINT"USE A TELESCOPE WHEN YOU CAN!":FORX=0TO25:PLAY"T3L50O4CBO3GO5A":NEXT:GOTO200
530 IFI$="STOP"THEN1680
540 IFI$="HELP"THENPRINT"SORRY MY FRIEND,BUT YOU HAVE","TO HELP YOURSELF AROUND HERE!":FORX=0TO1:PLAY"T3O2L4GL8G#L4AL8G#AG#L1A":NEXT:GOTO200
550 IFI$="QUIT"THENPRINT,," WHAT A VULGAR AMERICANISM!"," THE CORRECT WORD IS stop":PLAY"O2L2AP50ABL2.G#L4AL2B":GOTO200
560 IFI$="EAT "THENPRINT" UNFORTUNATELY THAT'S GONE OFF!"," YOU'LL HAVE TO REMAIN HUNGRY!!":PLAY"T3O3L2CO2L4GEL8CL1B-L2AL4FL4DL8O1AO2L4EL2.D":GOTO200
570 P$(2)="00"
580 FORW=1TO2
590 GOSUB1750
600 VD=99
610 IFP$(2)<>"00"THENVD=VAL(P$(2))-20
620 AU=VAL(P$(1))
630 IFAU<=12THENS(7)=0
640 IFY>=LEN(K$)THEN670
650 IFP$(W)="00"THEN590
660 NEXT
670 IFP$(1)<>"00"THEN830
680 FORI=1TO20
690 IFAU=14ANDO(I)=-1THENPRINT"WHAT DO YOU WANT TO USE?":GOTO820
700 IFAU=15ANDO(I)=GA THENPRINT"WHAT DO YOU WANT TO TAKE?":GOTO820
710 IFAU=16ANDS(7)=0ANDO(I)=-1THENPRINT" I CAN'T SEE THE PI MAN":GOTO820
720 IFAU=16ANDS(7)=1ANDO(I)=-1THENPRINT"WHAT ARE YOU GIVING HIM?":GOTO820
730 IFAU=17ANDVD<>22ANDO(I)=-1THENPRINT" NOW WHAT ARE YOU","TRYING TO GET RID OF?":GOTO820
740 NEXT
750 IFAU=14THENPRINT" USE WHAT? YOU'VE GOT NOTHING!":GOTO820
760 IFAU=15THENPRINT" TAKE! TAKE WHAT!"," THERE'S NOTHING HERE!":GOTO820
770 IFAU=17ANDVD<>22THENPRINT"YOU HAVE NOTHING WITH YOU!":GOTO820
780 IFAU=16ANDS(7)=1THENPRINT"BUT YOU'VE NOTHING TO GIVE!":GOTO820
790 IFAU=16ANDS(7)=0THENPRINT" GIVE WHAT?",," YOU'VE NOTHING WITH YOU"," AND I CAN'T SEE THE PI MAN!":GOTO820
800 '
810 PRINT" PLEASE WILL YOU REPHRASE THAT!"
820 PLAY"O3L4T2DECO-CL2G":GOTO200
830 Z=1
840 T$=MID$(M$(GA),Z,2)
850 IFT$="00"THEN910
860 IFT$<>P$(1)THEN890
870 GA=VAL(MID$(M$(GA),Z+2,2))
880 GOTO200
890 Z=Z+4
900 GOTO840
910 T=0:ZZ=0
920 MC=0
930 MC=MC+1
940 IFT<=0THEN970
950 E$=C$(MC)
960 GOTO1220
970 IFMC<=50THEN1180
980 IFZZ THEN320
990 IFAU<=12THENPRINT" NO RIGHT OF WAY AT"AU:GOTO1160
1000 IF VD>20 OR VD<1 THEN680
1010 IFAU=14ANDO(VD)=-1THENPRINT" SORRY!",," YOU CAN'T USE "O$(VD)," HERE!":GOTO1160
1020 FORX=0TO20STEP2
1030 IFGA=7ANDO(VD)=7ANDAU=15ANDVD=X THENPRINT" THIS IS THE FINAL RESTING"," PLACE OF "O$(VD)," AND IT CANNOT BE MOVED!":FORI=0 TO 5000:NEXT:GOTO200
1040 NEXT
1050 IFAU=17ANDO(VD)=-1THEN1120
1060 IFAU=15ANDO(VD)=-1THENPRINT" YOU ALREADY HAVE"," "O$(VD):GOTO1160
1070 IFAU=15ANDO(VD)<>-1THENPRINT" I CAN'T SEE "O$(VD)," ANYWHERE NEAR HERE!":GOTO1160
1080 IFAU=14ORAU=17ANDO(VD)<>-1THENPRINT" YOU HAVEN'T GOT "O$(VD)," WITH YOU!":GOTO1160
1090 IFAU=16ANDS(7)=0THENPRINT"I CAN'T SEE THE PI MAN!":GOTO1160
1100 IFAU=16ANDS(7)=1ANDO(VD)=-1THENPRINT"I DON'T WANT "O$(VD):GOTO1160
1110 IFO(VD)=0ANDAU=16ANDS(7)=1THENPRINT"ONLY GIVE ME WHAT YOU HAVE!":GOTO1160
1120 PRINT"YOU CAN'T DO THAT ROUND HERE!"
1130 IFGA=13THENS(7)=0
1140 FORI=4TO31STEP3:PLAY"V"+STR$(I)+"O4T3L4FC#":NEXT:PLAY"V15"
1150 GOTO200
1160 IFGA=13THENS(7)=0
1170 FORI=1TO5:PLAY"O4T3L4FC#":NEXT:GOTO200
1180 IFMID$(A$(MC),1,2)<>P$(1)THEN930
1190 K$=MID$(A$(MC),3,2)
1200 IFK$<>"00"ANDK$<>P$(2)THEN930
1210 E$=MID$(A$(MC),5)
1220 E=1
1230 IFMID$(E$,E,1)="."THEN1350
1240 GS=ASC(MID$(E$,E,1))-65
1250 N=VAL(MID$(E$,E+1,2))
1260 ONGS+1GOSUB1300,1310,1320,1330,1340
1270 IFMM<=0THEN930
1280 E=E+3
1290 GOTO1230
1300 MM=-(N=GA):RETURN
1310 MM=-(O(N)=GA):RETURN
1320 MM=-(O(N)<0):RETURN
1330 MM=-(RND(100)<=N):RETURN
1340 MM=S(N):RETURN
1350 ZZ=1
1360 E=E+1
1370 IFMID$(E$,E,1)="."THEN930
1380 GS=ASC(MID$(E$,E,1))-65
1390 IFMID$(E$,E+1,1)<>"."THENN=VAL(MID$(E$,E+1,2))
1400 EE=0
1410 ONGS+1GOSUB1450,1490,1520,1560,1600,1610,1620,1650,1660,1670
1420 ONEE+1GOTO1430,200,340
1430 E=E+3
1440 GOTO1370
1450 IFS(1)<5THEN1480
1460 PRINT" YOU'LL HAVE TO DROP SOMETHING   IF YOU WANT TO TAKE THAT TOO."
1470 PLAY"T3O3L4EL8GL4.EP50L4EL8GL4.EL8FGFL4.DL8EGEL4.CP4L8EL2CP4L8EL2C":RETURN
1480 O(N)=-1:S(1)=S(1)+1:RETURN
1490 IFO(N)=-1THEN1510
1500 EE=1:RETURN
1510 O(N)=GA:S(1)=S(1)-1:RETURN
1520 PRINT:ONN GOTO2400,2410,2420,2450,2470,2480,2490,2500,2550,2560,2570,2580,2610,2620,2680,2740
1530 IFN=34THEN2890ELSEIFN=36THEN2970
1540 IFN=40THEN2990ELSEIFN=41THEN3020
1550 GOTO3050
1560 X=O(N)
1570 IFGA<>12THENO(N)=O(N+1)ELSEO(N)=O(N-1)
1580 IFGA<>12THENO(N+1)=X ELSEO(N-1)=X
1590 HH=N+1:RETURN
1600 S(N)=1:RETURN
1610 S(N)=0:RETURN
1620 IFO(N)<0THENS(1)=S(1)-1
1630 O(N)=RND(4)
1640 RETURN
1650 GA=N:RETURN
1660 EE=2:RETURN
1670 EE=1:RETURN
1680 CLS0:PRINT@160,"PLEASE ENTER A CODE NUMBER.",">0< TO NEW THE PROGRAM.",">1< TO CONTINUE THE QUEST.",">2< TO START THIS QUEST AGAIN.  >3< TO SAVE THE GAME ON ANOTHER     TAPE AND CONTINUE LATER.":SCREEN0,1
1690 N$=INKEY$:IFN$=""THEN1690ELSEIF(VAL(N$)=0ANDN$<>"0")ORVAL(N$)>3THEN1690
1700 POKE1217+32*VAL(N$),48+VAL(N$):FORX=1TO2000:NEXT
1710 IFN$="0"THENNEW
1720 IFN$="2"THEN30
1730 IFN$="3"THEN3060
1740 GOTO200
1750 W$="    "
1760 P$(W)="00"
1770 GOSUB1990
1780 IFGP THENRETURN
1790 FORQ=1TO4
1800 MID$(W$,Q,1)=MID$(K$,Y,1)
1810 GOSUB1940
1820 IFGP THEN1860
1830 NEXT
1840 GOSUB1940
1850 IFGP<=0THEN1840
1860 IFW$="    "THENRETURN
1870 FORQ=4TO2STEP-1:IFMID$(W$,Q,1)=" "THENW$=LEFT$(W$,Q-1):NEXT
1880 FORQ=1TOV
1890 IFW$=MID$(V$(Q),3)THEN1920
1900 NEXT
1910 RETURN
1920 P$(W)=LEFT$(V$(Q),2)
1930 RETURN
1940 Y=Y+1
1950 GP=-(Y>LEN(K$))
1960 IFGP THENRETURN
1970 GP=-(MID$(K$,Y,1)=" ")
1980 RETURN
1990 Y=Y+1
2000 GP=-(Y>LEN(K$))
2010 IFGP THENRETURN
2020 IFMID$(K$,Y,1)=" "THEN1990
2030 RETURN
2040 CLS:RETURN
2080 GOSUB2040:PRINT:PRINT" IMPASSABLE FOOTHILLS BAR YOUR   WAY.RETURN TO WHERE YOU CAME.":RETURN
2090 GOSUB2040:PRINT"     WHAT A RUN DOWN GHETTO!","     YOU'VE HIT ROCK BOTTOM!":RETURN
2100 GOSUB2040:PRINT" LOOKS LIKE THE END OF THE LINE   WAS IT REALLY WORTH COMING?":RETURN
2110 GOSUB2040:PRINT"   THIS LOOKS LIKE A DEAD END.","   GRAB WHAT YOU CAN AND RUN!":RETURN
2120 GOSUB2040:PRINT" 7 IS A TURNING POINT,"," IF 2 IS NOT FOR YOU!":RETURN
2130 GOSUB2040:PRINT" YOU ARE CAST INTO AN ARENA OF   DESPAIR. A CAGE SURROUNDS YOU.  IT IS ONLY UNLOCKED 3 FROM 12.":RETURN
2140 GOSUB2040:PRINT"  THIS IS THE 'CAVERN OF IVORY'","   UNDERFOOT IS HEAVING OOZE!","  OVERHEAD IS DRIPPING SLIME!!","     THERE IS AN EXIT AT 1.","   THE ULTIMATE GATE OF  'PI'              IS  ABOVE."
2150 PRINT"  IT REMAINS SHUT UNTIL ENOUGH  CLUES HAVE BEEN SACRIFICED HERE."
2160 PRINT@452,"PRESS A KEY WHEN READY."
2170 IFINKEY$=""THEN2170ELSECLS:RETURN
2180 GOSUB2040:PRINT" YOU DRIFT ALONG A CHOKED CANAL  SLUICES LIE AHEAD AND BEHIND.":RETURN
2190 GOSUB2040:PRINT" I SAY! I SAY! I SAY! AN ECHO"," CHAMBER! I GO ON & ON & ON!!":RETURN
2200 GOSUB2040:PRINT" YOU CRAWL INSIDE AN OILY DRUM"," IS THERE BRIGHT LIGHT AHEAD?":RETURN
2210 GOSUB2040:PRINTTAB(12)"WELCOME!","    THIS IS THE OBSERVATORY        THERE IS AN ENTRANCE AT 7            AND AN EXIT AT 4":RETURN
2220 GOSUB2040:PRINT" AT THIS NECK OF THE WOODS"," TRAILS POINT OUT 5 AND 3.":RETURN
2230 GOSUB2040:PRINT" YOU'VE FALLEN INTO A STINKING   WASTE  DISPOSAL UNIT. YOU CAN   CLAMBER OUT BY HITTING BOTTOM   OR BY HEADING TOWARDS 9."
2240 PRINT@448,"   PRESS ANY KEY TO CONTINUE"
2250 IF INKEY$=""THEN2250
2255 PRINT@448,"                              ":PRINT@128,"";
2260 RETURN
2270 GOSUB2040:PRINT" BAD TO WORSE!",," WHAT A SCAB-INFESTED ODOROUS    PIT. ESCAPE AT 12, 4, 9 OR 5.":GOTO2240
2280 GOSUB2040:PRINT" YOU CAN CRAWL ALONG THE RANCID  SEWAGE PIPE THAT VEERS OFF AT   8, OR CARRY ON REGARDLESS.":RETURN
2290 GOSUB2040:PRINTTAB(12)"WARNING!",,,"  THIS IS A private CUL-DE-SAC.            GET OUT!!":FORX=0TO5:SCREEN0,1:SOUND200,4:SCREEN0,0:PLAY"T2P5":NEXT:RETURN
2300 GOSUB2040:PRINT" YOU HAVE WANDERED INTO A"," CRUMMY JOINT, BETWEEN THE"," SEWAGE WORKS AND A DEAD END.":RETURN
2310 GOSUB2040:PRINT" THIS IS A PARTING OF THE WAYS."," FOLLOW 3, 7, 8 OR 11.":GOTO2240
2320 GOSUB2040:PRINT" THIS BROKEN PATHWAY TWISTS AT   THE 5 AND 10.":RETURN
2330 GOSUB2040:PRINT" WHAT A DOWN AT HEEL ALLEY!"," ARE YOU PASSING THROUGH?":RETURN
2340 GOSUB2040:PRINT" 1 CAN PROCEED, OR AT THE BASE   OF YOUR FACE!":RETURN
2350 GOSUB2040:GOSUB2680:PLAY"CECFCECD":CLS:PRINT@33,"AT LAST, BY ACCIDENT OR DESIGN   YOU HAVE RETURNED THROUGH THE","     'ULTIMATE GATE OF PI'"," REMEMBER WHERE YOU HAVE BEEN &"," WHAT YOU HAVE SEEN AND HEARD..",
2360 PRINT" REMEMBER TOO THOSE GIFTS THAT"," UNLOCKED THE GATE. DISTIL THIS"," KNOWLEDGE & MEET ME AT NOON ON"," THE CORRECT DATE  AND LOCATION"," REVEALED.I'LL BE WAITING THERE"," FOR YOU EVERY YEAR BEARING THE","     'GOLDEN SUNDIAL OF PI'":PRINT
2370 PRINT"  PRESS / KEY TO RESTART QUEST"
2380 IF INKEY$<>"/"THEN2380
2390 GOTO30
2400 GOSUB3490:GOSUB2460:PRINT" YUM YUM YUM!",," IN MY TUM!",," HAVE ";O$(HH):PRINT" LITTLE CHUM!":FORX=0TO7250:NEXT:CLS:RETURN
2410 GOSUB3490:GOSUB2440:PRINT" PORKY PIES!",," YOU'VE FED ME LIES!"," GIVE BACK YOUR GRUB"," AND DAMN YOUR EYES!":FORX=0TO7200:NEXT:RETURN
2420 IFHH=14THEN GOSUB 2870
2430 IFHH=8THEN3030ELSEGOSUB3490:GOSUB2460:PRINT" THANKS A LOT "S$:PRINT" SEEN ONE OF THESE BEFORE?"," "O$(HH):FORX=0TO7250:NEXT:CLS:RETURN
2440 PLAY"O3T3L4GL8DP99DL4D#L4DL4P8F#L8G":RETURN
2450 GOSUB3490:GOSUB2440:PRINT" A RUBBER DUCK!":PRINT" WHAT FILTHY MUCK!":PRINT" GIVE ME THOSE.":PRINT" HA HA! TOUGH LUCK!":FORI=0TO7200:NEXT:CLS:RETURN
2460 FORI=50TO100STEP4:SOUNDI,1:NEXT:FORI=100TO50STEP-4:SOUNDI,1:NEXT:RETURN
2470 GOSUB2510:GOSUB3490:PRINT" HELLO STRANGER I'M THE       "," PI MAN AND I'M STARVING         GIVE ME FOOD OR ELSE!":PLAY"O4DCECECCO3":RETURN
2480 GOSUB2510:GOSUB3490:PRINT" YAWN! IT'S THE PI MAN, &     "," I AM BORED. I BET YOU'VE      "," GOT SOMETHING TO GIVE ME.":RETURN2490 GOSUB2510:GOSUB3490:PRINT" PSSST! EXCUSE ME! THIS IS    "," THE PI MAN. I'M S-SCARED.       C-CAN YOU C-COMFORT M-ME?":RETURN
2500 GOSUB3490:PRINT" OK "S$:PRINT" HOLD IT RIGHT THERE"," YOU'VE GOT "O$(HH-1):PRINT" AND I'M TAKING IT BACK!";:PLAY"O3T2L4CD#GO4CO3L2G#L8GFD#D":RETURN
2510 PLAY"O2T4":PCLS1:SCREEN1,0:PUT(128,96)-(135,103),K,PSET:PUT(136,100)-(139,103),P,PSET:PUT(130,104)-(139,111),P,PSET:PUT(130,112)-(135,119),I,PSET:PUT(136,112)-(143,119),N,PSET:FORK=0TO1:PLAY"L8G":PUT(130,112)-(135,119),E,PSET:PLAY"L4E"
2520 LINE(142,112)-(143,119),PSET,B:PUT(136,112)-(141,119),J,PSET:PLAY"L8E":PUT(130,112)-(135,119),I,PSET:PLAY"F":PUT(136,112)-(143,119),N,PSET:PLAY"G":PUT(130,112)-(135,119),E,PSET:PUT(128,96)-(135,103),A,PSET:PLAY"O+L4E"
2530 LINE(142,112)-(143,119),PSET,B:PUT(136,112)-(141,119),J,PSET:PUT(128,96)-(135,103),K,PSET:PLAY"E":PUT(130,112)-(135,119),I,PSET:PLAY"L2C":PUT(136,112)-(143,119),N,PSET:NEXTK:RETURN
2540 RETURN
2550 PRINT"SUDDENLY A GREEN DOOR YAWNS WIDEAT 10.":PLAY"O3T2L4EC#L8F#P199F#P199F#L32G#L8.F#L4EC#":RETURN
2560 PMODE4,1:SCREEN1,1:PCLS:FORI=9TO96STEP2:CIRCLE(I,191-I),I-9:NEXT:CLS:PRINT" I WILL BLESS THE LORD AT ALL   TIMES. HIS PRAISE SHALL ALWAYS         BE IN MY MOUTH.         ":FORI=1TO4000:NEXT:RETURN
2570 FORI=5TO8:CLSI:PLAY"V12T3O3L8C#AL8.G#L6.P199G#V31":NEXT:CLS:RETURN
2580 CLS:PMODE1,1:PCLS3:COLOR2,3:SCREEN1,0:LINE(0,128)-(255,191),PSET,BF:DRAW"S16C3BM0,127R4D1R4D1R5U1R17U1R26D2R8L6D4L8D1L10D1L4U1L6D1L4D1L5R1D1L3D1R1L8U2R1U1R1U1R1U1R4U2L3U1":PAINT(28,128),3,3:PAINT(250,128),3,3
2590 DRAW"S4":FORI=124TO4STEP-2:DRAW"BM142,"+STR$(I)+"C2NU1NL1NR2ND2":FORX=0TO99:NEXT:DRAW"C3NU1NL1NR2D2":NEXT
2600 PLAY"O4T2L8CDL16CO-B-L3AL16GFL8EDL4EP220L2E":RETURN
2610 PRINT" ONLY NOW ARE YOU ABLE TO OPEN     THE 'ULTIMATE GATE OF PI'.":RETURN
2620 PMODE3,1:PCLS:SCREEN1,0:PLAY"L99O2"
2630 FORI=0TO110STEP5:PUT(I,180)-(I+19,189),T,PSET:PLAYSTR$(RND(12)):PCLS:NEXT
2640 FORI=0TO19:PUT(115,180)-(134,189),T,PSET:CIRCLE(135+I,181-I),I*1.55,3:PLAYSTR$(INT(I/2)+1):PCLS:NEXT
2650 SOUND230,1:PUT(114,181)-(133,190),T,PSET:PCLS:PUT(113,182)-(132,191),T,PSET:FORI=0TO400:NEXT
2660 PCLS:FORI=115TO235STEP5:PUT(I,180)-(I+19,189),T,PSET:PLAYSTR$(RND(12)):PCLS:NEXT
2670 PLAY"L2O2":PMODE4,1:RETURN
2680 PMODE4,1:PCLS:SCREEN1,0:FORX=0TO110:LINE(25+X,191)-(255,0),PSET:NEXT
2690 PLAY"T3O2":FORX=0TO1:PUT(120,144)-(127,151),K,PSET:PUT(128,148)-(131,151),P,PSET:PUT(124,152)-(131,159),P,PSET:PUT(122,160)-(127,167),E,PSET:PUT(128,160)-(133,167),J,PSET:PLAY"L20G":PUT(128,160)-(135,167),N,PSET:PLAY"F#":PUT(122,160)-(127,167),I,PSE
2700 PLAY"G":LINE(131,163)-(136,168),PSET,BF:PUT(128,160)-(133,167),J,PSET:PLAY"A":PUT(120,144)-(127,151),A,PSET:PUT(120,160)-(127,167),G,PSET:PUT(128,160)-(135,167),H,PSET:PLAY"L8G":NEXTX:LINE(120,144)-(136,168),PSET,BF:PUT(112,144)-(119,151),K,PSET
2710 PUT(120,148)-(123,151),P,PSET:PUT(116,152)-(123,159),P,PSET:PUT(114,160)-(119,167),E,PSET:PUT(120,160)-(127,167),N,PSET:PLAY"L20G":PUT(114,160)-(119,167),I,PSET:PLAY"L10G":LINE(112,144)-(127,167),PSET,BF:PUT(104,144)-(111,151),K,PSET
2720 PUT(112,148)-(115,151),P,PSET:PUT(108,152)-(115,159),P,PSET:PUT(106,160)-(111,167),E,PSET:PUT(112,160)-(117,167),J,PSET:PLAY"L20G":PUT(106,160)-(111,167),I,PSET:PLAY"L10A":LINE(104,144)-(117,167),PSET,BF:PUT(96,144)-(103,151),K,PSET
2730 PUT(104,148)-(107,151),P,PSET:PUT(100,152)-(107,159),P,PSET:PUT(98,160)-(103,167),E,PSET:PUT(104,160)-(109,167),J,PSET:PLAY"L10A#":PUT(98,160)-(103,167),I,PSET:PLAY"L8B":PUT(96,144)-(103,151),A,PSET:LINE(104,148)-(107,151),PSET,BF
2740 PUT(88,152)-(95,159),G,PSET:PUT(96,152)-(103,159),B,PSET:PUT(104,152)-(111,159),H,PSET:PUT(94,160)-(99,167),I,PSET:PUT(100,160)-(105,167),J,PSET:LINE(106,160)-(113,167),PSET,BF:PLAY"GT5"
2750 FORL=20TO40STEP2:PLAY"O"+STR$(INT(L/5-3))+"L"+STR$(L):PUT(90,152)-(95,159),E,PSET:PUT(104,152)-(111,159),C,PSET:PLAY"C":PUT(88,152)-(95,159),G,PSET:PUT(104,152)-(111,159),H,PSET:PUT(94,160)-(99,167),F,PSET:PUT(100,160)-(105,167),E,PSET:PLAY"B"
2760 PUT(96,144)-(103,151),L,PSET:PUT(88,152)-(95,159),D,PSET:PUT(106,152)-(111,159),J,PSET:PLAY"D#":PUT(96,136)-(103,143),M,PSET:LINE(96,144)-(103,151),PSET,BF:PRESET(99,146):PRESET(99,149):PUT(88,152)-(95,159),G,PSET:PUT(104,152)-(111,159),H,PSET
2770 PLAY"E":PUT(80,152)-(87,159),G,PSET:LINE(88,152)-(95,159),PSET,BF:PUT(88,152)-(95,153),P,PSET:LINE(104,152)-(111,159),PSET,BF:PUT(104,152)-(111,153),P,PSET:PUT(112,152)-(119,159),H,PSET:PLAY"G":PUT(96,144)-(103,151),B,PSET
2780 PUT(94,152)-(99,159),F,PSET:PUT(100,152)-(105,159),E,PSET:LINE(96,160)-(103,167),PSET,BF:PLAY"C":LINE(96,136)-(103,143),PSET,BF:PUT(96,144)-(103,151),A,PSET:PUT(96,152)-(103,159),B,PSET:PLAY"G#":PUT(90,160)-(95,167),I,PSET
2790 PUT(104,160)-(111,167),N,PSET:PLAY"A":LINE(80,152)-(87,159),PSET,BF:LINE(112,152)-(119,159),PSET,BF:PUT(88,152)-(95,159),G,PSET:PUT(104,152)-(111,159),H,PSET:LINE(88,160)-(111,167),PSET,BF:PUT(94,160)-(99,167),I,PSET:PUT(100,160)-(105,167),J,PSET
2800 PLAY"C"
2810 PUT(88,152)-(95,159),D,PSET:PUT(104,152)-(111,159),C,PSET:PLAY"B":PUT(94,160)-(99,167),F,PSET:PUT(100,160)-(105,167),E,PSET:PLAY"A#":PUT(88,152)-(95,159),D,PSET:PLAY"A":PUT(88,152)-(95,159),G,PSET
2820 PLAY"G":PUT(88,152)-(95,159),D,PSET:PLAY"A":PUT(88,152)-(95,159),G,PSET:PLAY"D#":PUT(88,152)-(95,159),D,PSET:PUT(104,152)-(111,159),H,PSET:PLAY"E":NEXTL
2830 PLAY"G"
2840 RETURN
2850 PLAY"T2O2L2":RETURN
2860 FORX=99TO59STEP-3:SOUNDX,1:NEXT:RETURN
2870 PLAY"O3T1L4CP200CGP200GAP200AL2GL4FP200FEP200EDP200DL2C":RETURN
2880 FORI=0TO5:FORX=50TO80STEP2:SOUNDX,1:NEXTX,I:RETURN
2890 IFRND(0)>.5THEN2950
2900 CLS0:PRINT"  IT'S 'PIMANIA' T-SHIRT TIME":PRINT@64," AN EXTRA LARGE T-SHIRT COULD"," BE YOURS! JUST SEND 5 POUNDS"," TO:-",," 'PIMANIA' T-SHIRTS UNLIMITED"," AUTOMATA U.K. LIMITED"," 65A. OSBORNE ROAD"," PORTSMOUTH",," HAMPSHIRE PO5 3LR."
2910 PRINT@483,"PRESS ANY KEY TO CONTINUE";
2920 IFINKEY$=""THEN2920
2930 RETURN
2940 GOTO200
2950 RESTORE:PCLS:SCREEN1,0:LINE(28,55)-(32,90),PSET,BF:FORX=1TO21:CIRCLE(30,55),X:NEXT:FORX=225TO55STEP-2:PUT(X,65)-(X+34,82),V,PSET:NEXT:FORB=0TO17:PUT(X,65)-(X+33,82),W,PSET:READA$:PLAYA$:PUT(X,65)-(X+34,82),V,PSET:NEXT:FORX=0TO99:NEXT:RETURN
2960 DATAT3O3L5G,O4C,O3G,E,L3C,L10A,G,F#,L5G,L2.D,L5G,B,A,G,F,E,D,L2.CO2L2T2
2970 PMODE3,1:PCLS:SCREEN1,1:FORI=1TO8:FORX=1TO12:CIRCLE(128,191),I*13+X,I:NEXTX,I
2980 PLAY"T3O2L2CO+CO-L4BL8GAL4BO+CO-P199L2CAL1G":RETURN
2990 CLS:PRINT" TSK! TSK! TSK! YOU REALLY MUST"," NOT BE SO RUDE AND AGGRESSIVE!"," I'M GOING TO HAVE TO TEACH YOU"," A LESSON YOU WILL NOT FORGET!!"
3000 PLAY"O1DP99DP199L4DP199L2DP99FL4EP99L2EL4DL2P99DL4CL2DO3L4"
3010 FORI=0TO4000:NEXT:GOTO30
3020 PRINT" THAT DIRECTION IS USELESS HERE"," TAKE A GOOD LOOK AT YOUR FACE!":GOSUB2880:RETURN
3030 FORI=0TO8:CLSI:PRINT@97," A SAXOPHONE! WOW! I'VE ALWAYS";:PRINT@129," WANTED TO GET INTO SHOWBIZ!  ";:PRINT@161," HERE, HAVE SOME OXYGEN.      ";:FORX=1TO500:NEXTX,I
3040 PLAY"O1T4L4EE-DD-CC#DD#L1E":FORX=0TO1999:NEXT:CLS:RETURN
3050 CLS3:PLAY"O3T3L8GAGO+L4CP99CP99L2CP99L4CP99CP99L2CP99L4CP99CP99CP99CL8O-BB-BB-L2BL8BB-BB-BL3O+DO-L4BB-L2BL4GL8P99GP99GL4ABO+L2C":RETURN
3060 POKE&HFFD6,0:PRINT"GET YOUR BLANK CASSETTE READY,  THEN PRESS ANY KEY WHEN READY.":MOTORON
3070 IFINKEY$=""THEN3070ELSEMOTOROFF
3080 CLS7:PRINT"START THE TAPE THEN PRESS A KEY."
3090 IF INKEY$=""THEN 3090
3100 MOTORON:CLS7:PRINT"I AM NOW RECORDING THE EVENTS OFYOUR GAME.   DO NOT DISTURB ME!":FORX=0TO5500:NEXT
3110 OPEN"O",#-1,"PI-FILE"
3120 PRINT#-1,S$:FORI=1TO20:PRINT#-1,Q(I):NEXT:FORI=1TO20:PRINT#-1,O(I):NEXT:PRINT#-1,GA:FORI=1TO10:PRINT#-1,S(I):NEXT:CLOSE#-1
3130 CLS5:PRINT" SAVED! NOW LABEL YOUR CASSETTE  'PI-FILE' AND KEEP IT SAFE. TO  CONTINUE YOUR QUEST FIRST LOAD  THE ORIGINAL CASSETTE. WHEN IT  HAS LOADED, PRESS 'CLEAR'  AND  TYPE  ?:RUN  NOW PRESS 'ENTER'":PRINT,,"PRESS ANY KEY TO RETURN TO MENU."
3140 IF INKEY$=""THEN3140
3150 POKE&HFFD7,0
3160 GOTO1680
3170 POKE&HFFD6,0:CLS7:PRINT"INSERT PI-FILE CASSETTE AND","REWIND IT. THEN PRESS PLAY."
3180 OPEN"I",#-1,"PI-FILE"
3190 INPUT#-1,S$:FORI=1TO20:INPUT#-1,Q(I):NEXT:FORI=1TO20:INPUT#-1,O(I):NEXT:INPUT#-1,GA:FORI=1TO10:INPUT#-1,S(I):NEXT:CLOSE#-1
3200 POKE&HFFD7,0:GOTO200
3210 DIMO$(20),M$(22),V$(73),A$(50),C$(25)
3220 POKE&HFFD6,0:CLOADM"A$":OPEN"I",#-1,"*PIMANIA"
3230 FORI=1TO20:INPUT#-1,O$(I):NEXT:FORI=1TO22:INPUT#-1,M$(I):NEXT:FORI=1TO73:INPUT#-1,V$(I):NEXT:FORI=1TO50:INPUT#-1,A$(I):NEXT:FORI=1TO25:INPUT#-1,C$(I):NEXT:INPUT#-1,CX$:CLOSE#-1
3240 CLOADM"gPIMANIA":PSET(63,7,1)
3249 PMODE4,1:LINE(0,0)-(4,0),PRESET
3250 GET(0,0)-(7,7),A,G:GET(8,0)-(15,7),B,G:GET(16,0)-(23,7),C,G:GET(24,0)-(31,7),D,G:GET(32,0)-(37,7),E,G:GET(40,0)-(45,7),F,G:GET(48,0)-(55,7),G,G:GET(56,0)-(63,7),H,G:GET(64,0)-(69,7),I,G:GET(72,0)-(77,7),J,G:GET(80,0)-(87,7),K,G
3260 GET(88,0)-(95,7),L,G:GET(96,0)-(103,7),M,G:GET(104,0)-(111,7),N,G:GET(112,0)-(119,7),R,G
3270 PCLS:PMODE3,1:DRAW"C4BM1,1U1F2R1F1R5U3L1R2F2R2F1L9D3F1R4D1R1U1R4E1H1E1BL3L0BM0,9E1R1E1R1E2"
3280 PAINT(8,5)
3290 GET(0,0)-(19,9),T,G
3300 PMODE4,1:PCLS
3310 DRAW"BM3,0R1D1G1D1G3F2E4F2D3F1D6L1D1R2U7R3D6L1D1R2U7R3D1R5D5L1D1R2U6R3D5L1D1R2U6R1U5E4G4L2H1L15H4L2D1F1D1L1H1":PAINT(6,2):PAINT(2,6):GET(0,0)-(34,17),V,G
3320 LINE(0,5)-(5,8),PRESET,BF:DRAW"BU3D1G2L1E2U1H1G2L1E1":LINE(29,2)-(33,6),PRESET,BF:DRAW"L5":GET(0,0)-(33,17),W,G
3330 Q$="WHERWHATWHO LOOKSTOPHELPQUITEAT ":V=73
3340 CLOADM"SUN",&H6000
3350 CLOADM"INTRO"
3360 CLOADM"A$2"
3370 FORI=1TO999:FORJ=1TO10:IF INKEY$=""THENNEXTJ,I
3440 RETURN
3450 GOSUB2680:PLAY"O4C":FORX=0TO999:NEXT:GOSUB3490:PRINT@105,"HELLO";:PRINT@169,"I'M A PI MAN!";:PLAY"O1T3L4D#C":I=1:FORX=0TO4999:NEXT:CLS:FORX=10.6TO4.6STEP-.3:PRINT@240+SIN(X)*9+INT(COS(X)*7)*32,MID$("& I'LL BE UOY GNIEES",I,1);:I=I+1:NEXT
3455 FORI=1TO100:PRINT@(RND(9)-1)*4+(RND(15)-1)*32,"haha";:SOUND(RND(100)+I),1:NEXTI
3460 PRINT@71,TAB(24);:PRINT@103," CONGRATULATIONS ";:PRINT@135," SEEKER,YOUR KEY ";:PRINT@167," HAS TURNED THIS ";:PRINT@199," LOCK!  YOU HAVE ";:PRINT@231," PASSED  THROUGH ";:PRINT@263," THE  GATE OF PI ";:PRINT@295," TIME IS ON YOUR ";
3470 PRINT@327,"      SIDE!      ";:PRINT@359,TAB(24);:PRINT@391,"    GOOD LUCK    ";:PRINT@423,TAB(24);
3480 PLAY"O2T2L4GP222GO+L2CL4O-BO+CL2DL4CDEP222EFEO-L2AO+L4DP222DL2.CO-BO+L1CP1":RETURN
3490 CLS:SET(3,5,3):SET(4,5,3):SET(3,6,3):SET(4,6,3):SET(7,5,3):SET(8,5,3):SET(7,6,3):SET(8,6,3):SET(3,8,6):SET(4,8,6):SET(4,9,6):SET(5,9,6):SET(5,10,6):SET(6,9,6):SET(6,10,6):SET(7,9,6):SET(7,8,6):SET(8,8,6)
3500 PRINT@161,CHR$(139);:PRINT@164,CHR$(135)
3510 RETURN
3520 CLS5:PRINT"INSERT THE TAPE I RECORDED YOUR GAME STATUS ON,REWIND AND PRESS PLAY."
3530 POKE&HFFD6,0:OPEN"I",#-1,"sPIMANIA"
3540 INPUT#-1,S$:FORI=1TO20:INPUT#-1,Q(I):NEXT:FORO=1TO20:INPUT#-1,O(I):NEXT:INPUT#-1,GA:FORI=1TO10:INPUT#-1,S(I):NEXT:CLOSE#-1
3550 POKE&HFFD7,0:GOTO200
3560 CI$="ELSEELSEIFRUNRUNRUNREADLETONDIMRUNREADREM-RUNRUNRUNRUNLETRUNIREADRUNRUNIENDFORRUNENDONONONENDFORRUNENDLETRUNELSEFORFORFORELSEIFENDLET/RUN-LETFORELSEIIFIFIFORENDONDIMRUNFORFORIFFORFORELSEFORFORELSEELSEELSEELSEELSEELSEFORONFORRUN#RUNELSEELSEELSEE
3570 RETURN
9000 Z=PEEK(65314):POKE65314,(Z AND 7)+8+16
9010 POKE65476,0:POKE65474,0:POKE65472,0
9020 POKE&HFFD2,0:POKE&HFFD1,0:POKE&HFFCF,1:POKE&HFFCD,1:POKE&HFFCB,1:POKE&HFFC9,1:POKE&HFFC7,0
9030 RETURN
10000 GOSUB9000
10010 GOTO10010
