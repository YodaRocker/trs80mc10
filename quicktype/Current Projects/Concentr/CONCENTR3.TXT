0 CLEAR500:DIMC1,CC,M$,C$(52),XX,YY,I,I$,Z:GOTO8020
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)+16384:CC=ASC(M$):POKEC1,CC-(CCAND64):RETURN
8 PRINT@YY*64+XX*4,;:GOSUB4500:PRINT@YY*64+XX*4+32,;:GOSUB4600:RETURN

10 CLS:PRINT TAB(10)"CONCENTRATION"
20 PRINT TAB(8)"CREATIVE COMPUTING":PRINTTAB(6)"MORRISTOWN NEW JERSEY":PRINT
21 PRINT"USE awsz AND enter TO SELECT TWO";
22 PRINT"CARDS. IF THEY DON'T MATCH VALUE";
23 PRINT"THEY ARE TURNED BACK DOWN."
24 PRINT"THERE IS ONLY ONE PLAYER TRYING"
25 PRINT"TO GET ALL FIFTY-TWO CARDS IN A"
26 PRINT"MAXIMUM OF"MX"MOVES."
30 PRINT
40 PRINTTAB(13)"CURRENT"
50 IFS>HSTHENHS=S
60 PRINTTAB(4)"HIGH SCORE:"HS"OUT OF"MX:S=0
210 RESTORE:FOR X=1 TO 52
220 READ E$
230 C$(X)=E$
240 NEXT X
250 REM--SHUFFLE AND DEAL 
260 FORI=1TO51:K$=C$(I)
270 L=RND(52):IFL=ITHEN270
280 C$(I)=C$(L):C$(L)=K$:NEXT
320 PRINT@448+17,"hit a key";:FORC1=1TO25:M$=INKEY$:NEXT
330 ON-(INKEY$="")GOTO330:CLS:Z=1:GOSUB5000:I=1:XX=0:YY=0:GOSUB570
340 REM--START TO PLAY
350 FOR N=1 TO MX:Z=0
360 PRINT@401,"FIRST CARD?":GOSUB6000:U=I:X1=XX:Y1=YY
380 G=1
390 IF C$(U)=" " THEN 840
395 GOSUB8:PRINT@401+13,C$(U);
400 PRINT@433,"SECOND CARD?":GOSUB6000:W=I:X2=XX:Y2=YY
420 G=2 
430 IF C$(W)=" " THEN 840
440 IF U<>W THEN 470
450 PRINT@448,"you can't pick the same card!";:SOUND1,1:FORC1=1TO2500:NEXT:GOSUB570
460 GOTO 400
470 GOSUB8:PRINT@433+13,C$(W);
480 IF MID$(C$(U),1,1)=MID$(C$(W),1,1) THEN 580
510 Z=1:GOTO620
570 PRINT@448,"":PRINT@480,"                              ";:PRINT@480,"SCORE"S," PICKS"N;:RETURN
580 PRINT@448,"THAT'S A MATCH!";:SOUND87,1:SOUND155,1:SOUND142,1:SOUND113,1:Z=0
590 C$(U)=" "
600 C$(W)=" "
610 S=S+1:PRINT@480,"SCORE"S;
620 PRINT@448+17,"hit a key";:FORC1=1TO50:M$=INKEY$:NEXT
626 ON-(INKEY$="")GOTO626
630 PRINT@401,"":PRINT@433,"":XX=X1:YY=Y1:I=U:GOSUB8:XX=X2:YY=Y2:I=W:GOSUB8:GOSUB570:NEXT N
640 REM--THE RESULTS
650 SOUND1,10:Z=0:GOSUB5000:S1=S/((MX+1)/4)
660 PRINT@448,"YOU SCORED";S;"OUT OF";STR$(MX)". THAT'S"
670 ON S1+1.5 GOTO 680,700,720,740:PRINT "CHEATING!    ";:GOTO770
680 PRINT "POOR.        ";:GOTO 770
700 PRINT "FAIR.        ";:GOTO 770
720 PRINT "GOOD.        ";:GOTO 770
740 PRINT "EXCELLENT!   ";:GOTO 770
770 PRINT "PLAY AGAIN? (Y/N)?";
790 I$=INKEY$:ON-(I$="")GOTO790
800 IF I$="Y" THEN CLS:GOTO 10
810 IF I$="N" THEN CLS:PRINT "COME BACK AGAIN!":END
830 GOTO790
840 PRINT@448,"you already matched that card!";:SOUND1,1:FORC1=1TO2500:NEXT:GOSUB570
850 IF G=1 THEN 360
860 GOTO 400
870 DATA "AS","2S","3S","4S","5S","6S","7S","8S","9S","TS","JS","QS"
875 DATA "KS"
880 DATA "AH","2H","3H","4H","5H","6H","7H","8H","9H","TH","JH","QH"
885 DATA "KH" 
890 DATA "AD","2D","3D","4D","5D","6D","7D","8D","9D","TD","JD","QD"
895 DATA "KD"
900 DATA "AC","2C","3C","4C","5C","6C","7C","8C","9C","TC","JC","QC"
905 DATA "KC"
910 END

1000 PRINT" —Ÿ’";:RETURN
1010 PRINT" ”ž";:GOSUB7:RETURN

2000 PRINT" ··²";:RETURN
2010 PRINT" ´¾";:GOSUB7:RETURN

3000 PRINT" ÑÛ";:GOSUB7:RETURN
3010 PRINT" ÜÞØ";:RETURN

4000 PRINT" ¥¯";:GOSUB7:RETURN
4010 PRINT" ¬®¨";:RETURN

4500 IFZTHENPRINT" þüý";:RETURN
4510 M$=LEFT$(C$(I),1):IFM$=" "THENPRINT"    ";:RETURN
4520 I$=RIGHT$(C$(I),1):IFI$="D"THENGOSUB1000:RETURN
4530 IFI$="H"THENGOSUB2000:RETURN
4540 IFI$="S"THENGOSUB3000:RETURN
4550 IFI$="C"THENGOSUB4000:RETURN
4560 STOP:RETURN

4600 IFZTHENPRINT" ûó÷";:RETURN
4610 M$=LEFT$(C$(I),1):IFM$=" "THENPRINT"    ";:RETURN
4620 I$=RIGHT$(C$(I),1):IFI$="D"THENGOSUB1010:RETURN
4630 IFI$="H"THENGOSUB2010:RETURN
4640 IFI$="S"THENGOSUB3010:RETURN
4650 IFI$="C"THENGOSUB4010:RETURN
4660 STOP:RETURN

5000 YY=0:XX=0:FORI=1TO52:PRINT@YY*64+XX*4,;:GOSUB4500:PRINT@YY*64+XX*4+32,;:GOSUB4600:XX=XX+1:IFXX=8THENXX=0:YY=YY+1
5010 NEXT:RETURN

6000 PRINT@YY*64+XX*4,CHR$(207);:FORC1=1TO25:M$=INKEY$:NEXT
6010 FORC1=1TO25:NEXT:PRINT@YY*64+XX*4," ";:FORC1=1TO25:NEXT:PRINT@YY*64+XX*4,CHR$(207);
6015 CC=PEEK(2)ANDPEEK(17023):IFCC=.THEN6010
6020 PRINT@YY*64+XX*4," ";:IFCC=65THENXX=XX-1:IFXX<.THENXX=.
6030 IFCC=83THENXX=XX+1:IFXX>7THENXX=7
6040 IFCC=87THENYY=YY-1:IFYY<.THENYY=.
6050 IFCC=90THENYY=YY+1:IFYY>6THENYY=6
6055 I=XX+YY*8+1:IFCC=13THENRETURN
6060 IFI>52THENXX=3:GOTO6055
6070 PRINT@YY*64+XX*4,CHR$(207);:GOTO6010

7000 REM MC-10 EDITS JIM GERRIE
8000 REM MX=MAX NUMBER OF CARD
8010 REM MATCHES TO ACHIEVE
8020 MX=26
8030 C1=RND(-(PEEK(9)*256+PEEK(10))):REM RANDOMIZE
8040 GOTO10
