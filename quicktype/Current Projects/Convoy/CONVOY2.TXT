0 CLS0:DIMN(21):X=32768:GOSUB3000:MC=16384:REM INSTALL COUNTER
1 CC=1:FORC1=1TO63STEP3:N(CC)=VAL(MID$("087103113129142149161171181185193200203209215219221223229231233",C1,3)):CC=CC+1
2 NEXT:GOTO10
4 POKE8,0:FORCC=1TOLEN(M$)STEP2:SOUNDN(ASC(MID$(M$,CC,1))-64),ASC(MID$(M$,CC+1,1))-48:NEXT:POKE8,8:RETURN
9 FORC1=1TO250:NEXT:RETURN
10 REM CONVOY ADAPTED FOR DRAGON 32 BY D.A. PONTING
15 GOSUB1000
20 N=3:U=.058:V=.04:P=22/(7*180)
25 SH$=CHR$(135)+CHR$(131):SN$=CHR$(128)+CHR$(128):W$(1)="SPLASH":W$(2)="BOOM! "
30 DIMM(13),S(9):OD=PEEK(173)+256*PEEK(174)
40 E=25:B=20:H=0:K=0:R=0
60 POKE174,INT(OD/256):POKE173,OD-256*PEEK(174)
70 REM DRAW INITIAL SCREEN DISPLAY
75 CLS0:A$="":C1=USR(0)
80 FORI=1TO32:A$=A$+CHR$(143):NEXTI:PRINT@32,A$;
100 PRINT@114,LEFT$(A$,12);
110 PRINT@148,LEFT$(A$,10);
120 PRINT@182,LEFT$(A$,8);
130 PRINT@216,LEFT$(A$,6);
140 PRINT@250,LEFT$(A$,4);
145 PRINT@264,LEFT$(A$,2);
150 PRINT@288,LEFT$(A$,2);
160 PRINT@320,LEFT$(A$,4);
170 PRINT@352,LEFT$(A$,5);
180 PRINT@384,LEFT$(A$,7);
190 PRINT@416,LEFT$(A$,9);
200 PRINT@448,LEFT$(A$,10);
210 PRINT@480,LEFT$(A$,10);
215 PRINT@482,"+";
220 FORT=31TO511STEP32:POKE(T+MC),32:NEXT
250 FORT=491TO511:POKE(MC+T),32:NEXT
260 FORI=1TON:READX:S(I)=X+MC
270 PRINT@X,SH$;:NEXTI:SB=TI
280 PRINT@0,"hits"H" elevation"E" bearing"B;
300 K=K+1:IFK>NTHENK=1
330 C1=PEEK(2)ANDPEEK(17023):E=E-(E>10)*(C1=90)+(E<85)*(C1=87)
340 B=B-(B>-5)*(C1=65)+(E<90)*(C1=83)
360 IFC1<>32ORR THEN420
365 M$="G2":GOSUB4
375 TS=200*SIN(E*P)
380 T=TI
390 R=12*SIN(2*E*P)
400 X=INT(2.5*R*SIN(B*P)+.5)
410 Y=14-INT(R*COS(B*P)+.5)
420 IFR>.THEN600
430 D=-(RND(.)<U)-32*(RND(.)<V)
440 M=S(K)+D:IFPEEK(M+1)=32THEN920
450 IFPEEK(M+1)<>128THEN280
460 PRINT@S(K)-MC,SN$;:S(K)=M
500 PRINT@S(K)-MC,SH$;:GOTO280
600 REM SHELL LANDS
610 R=.:W=1:Q=X+Y*32
620 IFPEEK(Q+MC)=135THENW=2
630 FORI=1TO5:PRINT@Q,I;:FORT=1TO25:NEXTT:PRINT@Q," ";:FORT=1TO25:NEXTT:NEXTI
640 PRINT@Q,CHR$(128)CHR$(128)CHR$(128);:PRINT@264,LEFT$(A$,2);
650 FORI=-3TO3:T(I+3)=PEEK(Q+MC+I):NEXT
660 PRINT@Q-2,W$(W);
670 IFW=1THEN720
690 FORI=1TON:IFS(I)<>Q+MCTHENNEXT
700 S(I)=S(N):N=N-1:H=H+1:T(3)=43
720 FORI=1TO500:NEXT
730 FORI=-3TO3:POKEQ+I+MC,T(I+3)
740 NEXT:IFNTHEN280
800 REM *** ALL DESTROYED ***
810 T=INT(USR(1)/50):CLS
815 PRINT"        *** GAME OVER ***"
820 PRINT:PRINT"    YOU DESTROYED"H"SHIPS"
830 PRINT:PRINT"    YOUR TIME WAS"T"SECS"
840 IFKTHEN880
850 PRINT:PRINT"   BECAUSE OF YOUR MISERABLE"
855 PRINT" FAILURE YOU WILL HAVE TO TYPE                run"
860 PRINT"   IF YOU WANT TO PLAY AGAIN"
870 END
880 PRINT:PRINT"  PRESS enter FOR A HARDER GAME"
890 IFINKEY$<>CHR$(13)THEN890
900 IFH=9THENH=8:U=U*2:V=V*2
910 N=H+1:GOTO40
920 POKES(K),128:POKES(K)+1,128
930 PRINT@37,"*** SHIP ESCAPES!! ***"
940 FORDL=1TO100:NEXTDL
945 M$="E2F2F2":GOSUB4:GOSUB9:M$="E2F2E2D2C2B1B1G1E2C2F2D2G1F1E1D1C1":GOSUB4
950 K=0:GOTO810
960 DATA HPAAAIDLDLDLPOECBGKGACEKCGPCCBDLGOAD
970 DATA INCFCABAABNMALMDDKEDJFAINNALNOPNAINPPNDLLNOLMHCGAEEPFPNNPNNMPNNNMKMGJAANHOPCMHDIPPECBGIGAIJHAIAODJ
990 DATA67,104,129,170,195,73,68,108,133
1000 REM *** INSTRUCTIONS ***
1010 CLS:PRINT@9,"*** CONVOY ***"
1020 PRINT"  A CONVOY IS SAILING S-E AND"
1030 PRINT"  YOU HAVE TO DESTROY THEM ALL"
1040 PRINT"  USING THE GUN IN THE BOTTOM"
1050 PRINT"     CORNER OF THE SCREEN."
1060 PRINT:PRINT"elevation IS MEASURED IN DEGREES";
1070 PRINT" (10-85) FROM THE HORIZONTAL."
1080 PRINT"   PRESS W/Z ARROW KEYS TO         ALTER THE elevation"
1100 PRINT"   MAX.RANGE=elevation OF 45'."
1120 PRINT"bearing =-IS MEASURED IN DEGREES";
1130 PRINT" FROM -5 THRU 0=NORTH TO 90=EAST";
1140 PRINT"   PRESS A/S ARROWS TO ALTER."
1170 PRINT"      TO fire PRESS space"
1180 PRINT"PRESS ANY KEY TO START THE GAME";
1190 M$=INKEY$:IFM$=""THEN1190
1200 RETURN
2999 END
3000 Z=16896:GOSUB 3001:Z=X:GOSUB 3001:EXEC X:RETURN
3001 READ U$:FOR Y=1 TO LEN(U$) STEP 2:POKE Z,(ASC(MID$(U$,Y,1))-65)*16+ASC(MID$(U$,Y+1,1))-65:Z=Z+1:NEXT:RETURN
