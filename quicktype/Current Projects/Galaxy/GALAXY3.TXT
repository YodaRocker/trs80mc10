5 CLEAR300:GOTO20010
10 IFDSKTHENQ9=USR0(0):RETURNELSEPOKE16526,S0:POKE16527,S1:Q9=USR(0):RETURN
20 IFDSKTHENQ9=USR1(0):RETURNELSEPOKE16526,S2:POKE16527,S3:Q9=USR(0):RETURN
30 IFDSKTHENQ9=USR2(0):RETURNELSEPOKE16526,S4:POKE16527,S5:Q9=USR(0):RETURN
510 GOSUB1510
511 IN$=INKEY$
512 IN$=INKEY$:IFIN$=""THEN512ELSEKY=ASC(IN$):RETURN
1110 IFSOTHENPOKEEP,70:POKEDR,1:POKEML,1:POKESP,60:GOSUB 20:RETURNELSERETURN
1210 IFSOTHENPOKEEP,33:POKEDR,1:POKEML,5:POKESP,35:GOSUB20:RETURNELSERETURN
1310 IFSOTHEN1320ELSEFORZ=1TO200:NEXT:RETURN
1320 POKEDR,5:POKEML,1:IFDSKTHENFORZ=8TO14STEP2:POKEFR,Z:Q9=USR2(0):NEXT:POKEDR,10:FORZ=1TO11STEP2:POKEML,Z:Q9=USR2(0):NEXT:RETURN
1330 POKE16526,S4:POKE16527,S5:FORZ=8TO14STEP2:POKEFR,Z:Q9=USR(0):NEXT:POKEDR,10:FORZ=1TO11STEP2:POKEML,Z:Q9=USR(0):NEXT:RETURN
1410 POKEDR,7:POKEML,1:FORZ=1TO20:POKESP,RND(240)+15:GOSUB10:NEXT:RETURN
1510 POKEDR,21/(P+1):POKEML,P+1:POKESP,200:GOSUB10:POKESP,100:GOSUB10:POKESP,50:GOSUB10:RETURN
1610 FORZ=1TO2:POKEEP,100:POKEDR,1:POKEML,1:POKESP,140:GOSUB20:POKEEP,140:POKESP,100:GOSUB20:NEXT:RETURN
1660 POKEEP,100:POKEDR,6:POKEML,1:POKESP,20:GOSUB20:GOSUB1810:RETURN
1710 POKEEP,75:POKEDR,1:POKEML,1:POKESP,150:FORZ=1TO5:GOSUB20:NEXT:RETURN
1760 POKEDR,5:POKEML,1:POKEFR,10:GOSUB30:POKEFR,14:GOSUB30:FORZ=1TO20:NEXT:RETURN
1810 POKEDR,10:POKEML,1:IFDSKTHENFORZ=10TO14STEP2:POKEFR,Z:Q9=USR2(0):NEXT:POKEDR,40:FORZ=1TO10:POKEML,Z:Q9=USR2(0):NEXT:RETURN
1820 POKE16526,S4:POKE16527,S5:FORZ=10TO14STEP2:POKEFR,Z:Q9=USR(0):NEXT:POKEDR,40:FORZ=1TO10:POKEML,Z:Q9=USR(0):NEXT:RETURN
1910 PRINT@979,CHR$(31);:PRINT"HIT ANY KEY TO CONTINUE.";:GOSUB510:RETURN
1960 POKEDR,6:POKEML,1:POKESP,W+60:GOSUB10:RETURN
2010 GOSUB510:IFKY=91V=-10:RETURNELSEIFKY=13V=0:RETURNELSEV=-1:FORI=1TOWN:IFIN$=MID$(SN$,I,1)V=I:NEXT:RETURNELSENEXT:RETURN
2510 V=0:GOSUB1510
2520 GOSUB511:IFKY=91V=-10:RETURNELSEIFKY=13RETURNELSEIFKY<48ORKY>57V=-1:RETURNELSEV=V*10+VAL(IN$):GOTO2520:RETURN
3010 W=1
3040 IFWC(W)=PTHEN3100ELSEW=W+1:IFW<=WNTHEN3040ELSEPRINT@QQ,P$NA$(P)" CONTROLS NO WORLDS.":GOSUB1910:RETURN
3100 PRINT@QQ,CHR$(31);P$NA$(P)" FROM WORLD :";:GOSUB2010:IFV=-10THENGOSUB9010:IFFC(0)=-7THENRETURNELSE3010
3130 IFV=0THENRETURNELSEIFV<0ORV>WNTHEN3171ELSEIFWC(V)=PTHEN3180ELSEPRINT@QQ,CHR$(31)"YOU DON'T CONTROL THAT WORLD.";:GOSUB1610:GOTO3100
3171 PRINT@QQ,CHR$(31);"THAT WORLD DOESN'T EXIST.":GOSUB1610:GOTO3100
3180 WR=V:PRINT" TO WORLD : ";:GOSUB2010
3200 IFV=-10GOSUB9010I:IFFC(0)=-7THENRETURNELSE3010GOTO3010
3210 IFV<1ORV>WNTHEN3171
3220 WD=V:PRINT"NUMBER OF SHIPS :";
3230 GOSUB2510
3240 PRINT:IFV=-10THENGOSUB9010:IFFC(0)=-7THENRETURNELSE3010
3250 IFV<1THENGOSUB1610:GOTO3100
3260 IFV>WS(WR)PRINT@QQ,CHR$(31);"YOU DON'T HAVE THAT MANY SHIPS THERE.";:GOSUB1610:GOTO3100
3270 FM=V
3300 GOSUB4010
3310 IFF<=50THEN3100
3999 RETURN
4010 F=1
4020 IFFC(F)=0THENWS(WR)=WS(WR)-FM:FC(F)=P:FD(F)=WD:FS(F)=FM:FT(F)=T+(SQR((WX(WR)-WX(WD))[2+(WY(WR)-WY(WD))[2)+.99)/3:GOTO4999
4030 F=F+1:IFF<=50THEN4020
4040 PRINT@QQ,CHR$(31);"ION STORM PREVENTS FURTHER RELEASE OF SHIPS THIS YEAR.";:P(0)=PN
4050 PRINT:GOSUB1410
4999 RETURN
5010 W=FD(F):DE!=WS(W):AK!=FS(F):G1!=SQR(RND(0)*.8+RND(0)*.2):G2!=SQR(1-G1![2):AK$=NA$(FC(F)):DE$=NA$(WC(W))
5020 CLS:FORI=0TO8:SET(38,I):SET(39,I):SET(89,I):SET(88,I):NEXT:FORI=38TO89:SET(I,9):NEXT:PRINT@21,"SUBSPACE COMMUNICATION";:PRINT@156,"WORLD "MID$(SN$,W,1)".";:GOSUB1410
5030 IFWC(W)=FC(F)THENPRINT@88,"REINFORCEMENT TO";:PRINT@320,P$;AK$" HAS SENT"AK!"SHIPS TO REINFORCE HIS WORLD.":WS(W)=AK!+DE!:GOTO5300
5040 PRINT@85,"ATTEMPTED INVASION OF";:PRINT@259,"ATTACKER : "AK$;:PRINT@299,"DEFENDER : "DE$;:GOSUB1710:PRINT@323,"INITIAL FORCE :"AK!;:PRINT@363,"INITIAL FORCE :"DE!;:PRINT@387,"GUNNERY FACTOR :";INT(G2!*100);
5045 PRINT@427,"GUNNERY FACTOR :";INT(G1!*100);
5050 PRINT@451,"SHIPS LEFT :"AK!;:PRINT@491,"SHIPS LEFT :"DE!;
5070 IFDE!=0THENPRINT@720,CHR$(31);"ATTACKER WINS WITHOUT A FIGHT.":WS(W)=AK!:WC(W)=FC(F):GOTO5300
5080 PRINT@960,CHR$(30);:PRINT@1003,"DEFENDER FIRES.";:J=0
5090 J=J+1:PRINT@672,CHR$(131);:GOSUB1210:PRINT@672," ";
5100 IFRND(0)>G1!*SQR(AK!/(AK!+DE!))THEN5130
5110 PRINT@604,CHR$(160)CHR$(188)STRING$(5,191)CHR$(188)CHR$(144);:PRINT@668,CHR$(139)STRING$(7,191)CHR$(135);:PRINT@734,STRING$(5,131);:GOSUB1310:AK!=AK!-1:PRINT@463,AK!" ";
5115 PRINT@604,STRING$(9,32);:PRINT@668,STRING$(9,32);:PRINT@734,STRING$(5,32);
5120 IFAK!=0THENPRINT@717,CHR$(31);"DEFENDERS FIGHT OFF THE AGGRESSORS.";:WS(W)=DE!:GOTO5300
5130 IFJ<DE!THEN5090
5200 PRINT@960,CHR$(30)"ATTACKER FIRES.";:J=0
5210 J=J+1:PRINT@672,CHR$(131);:GOSUB1110:PRINT@672," ";
5220 IFRND(0)>G2!*SQR(DE!/(AK!+DE!))THEN5250
5230 PRINT@604,CHR$(160)CHR$(188)STRING$(5,191)CHR$(188)CHR$(144);:PRINT@668,CHR$(139)STRING$(7,191)CHR$(135);:PRINT@734,STRING$(5,131);:GOSUB1310:DE!=DE!-1:PRINT@503,DE!" ";
5235 PRINT@604,STRING$(9,32);:PRINT@668,STRING$(9,32);:PRINT@734,STRING$(5,32);
5240 IFDE!=0THENPRINT@723,CHR$(31);"ATTACKERS ARE VICTORIOUS!":WS(W)=AK!:WC(W)=FC(F):GOTO5300
5250 IFJ<AK!THEN5210
5260 PRINT:GOTO5080
5300 FC(F)=0
5310 GOSUB1910
5990 FORI=1TO50:NEXT
5999 RETURN
6010 FORW=1TOWN
6020 IFAB=0ANDWC(W)=0THEN6040
6030 WS(W)=WS(W)+WI(W)
6040 NEXT
6999 RETURN
7010 FORF=1TO50
7020 IFFC(F)=0THEN7040
7030 IFFT(F)=TTHENGOSUB5010
7040 NEXT
7999 RETURN
8010 CLS:PRINT@15,"R E S U L T S  F O R  Y E A R  "T
8400 QQ=896
8500 IFWN>30THEN8750
8510 IN$="WRLD CONT PROD SHPS"+CHR$(170)+" "
8520 PRINTIN$;IN$;LEFT$(IN$,19)
8530 Z=0:FORW=1TOWN
8540 PRINT" ";MID$(SN$,W,1);"   ";:PRINTUSING"%  %";NA$(WC(W));:PRINT;" ";:GOSUB1960
8550 IFWC(W)>0PRINTUSING"###";WI(W);:PRINT"  ";:PRINTUSING"####";W2(W);ELSEPRINTSTRING$(9,32);
8560 Z=Z+1:IFZ=3THENPRINT:Z=0ELSEPRINTCHR$(170);" ";
8570 NEXT:IFZ=1THENPRINTTAB(40);CHR$(170);
8580 GOTO8820
8750 IN$="WD CONT PD SHPS"+CHR$(170)
8760 PRINTIN$;IN$;IN$;LEFT$(IN$,15)
8770 Z=0:FORW=1TOWN
8780 PRINT" ";MID$(SN$,W,1);" ";:PRINTUSING"%  %";NA$(WC(W));:PRINT" ";:GOSUB1960
8790 IFWC(W)>0PRINTUSING"##";WI(W);:PRINT" ";:PRINTUSING"####";W2(W);ELSEPRINTSTRING$(7,32);
8800 Z=Z+1:IFZ=4THENPRINT:Z=0ELSEPRINTCHR$(170);
8810 NEXT:IFZ=1THENPRINTTAB(31);CHR$(170);TAB(47);CHR$(170);
8815 IFZ%=2THENPRINTTAB(47);CHR$(170);
8820 IFT<TNPRINT@778,"F L E E T  O R D E R S  F O R  Y E A R "T+1:RETURNELSERETURN
9010 CLS:IN$="TURN OFF":IFSO=0THENIN$="TURN ON"
9011 PRINT@202,"G = GO BACK TO REGULAR TURN":PRINT@266,"M = DRAW STAR MAP":PRINT@330,"T = TIME / DISTANCE CALCULATOR":PRINT@394,"N = "IN$" BATTLE SOUND EFFECTS":PRINT@458,"S = SAVE CURRENT GAME"
9020 PRINT@522,"Q = QUIT (DROP OUT OF GAME)":PRINT@586,"E = END THE GAME FOR EVERYONE":PRINT@650,"C = CHANGE THE NUMBER OF YEARS IN THE GAME"
9035 PRINT@847,CHR$(31);"WHAT IS YOUR CHOICE?";:GOSUB510:IFKY=71THENT=T-1:GOSUB8010:T=T+1:RETURNELSEIFKY=77GOSUB30010:GOTO9400ELSEIFKY=84GOSUB35010:GOTO9010ELSEIFKY=78IFSOTHENSO=0:GOTO9010ELSESO=1:GOTO9010ELSEIFKY<>83GOTO9250
9245 PRINT@911,"SAVE GAME";:GOSUB9810:IFIN$="Y"THENPRINT@975,"THE GAME WILL BE SAVED AT THE END OF THIS YEAR.";:SF=-1:FORI=1TO2000:NEXT:GOTO9035ELSEFORI=1TO800:NEXT:GOTO9035
9250 IFKY=81THENGOTO9510ELSEIFKY=69THENGOSUB9710:RETURNELSEIFKY=67THENGOSUB9920:GOTO9010ELSEPRINT@911,"I DON'T UNDERSTAND THAT.":GOSUB1610:GOTO9035
9400 PRINT@53,"HIT ANY KEY";:PRINT@117,"TO CONTINUE";:GOSUB510:GOTO9010
9510 PRINT@911,"QUIT";:GOSUB9810:IFIN$="N"THENFORI=1TO1000:NEXT:GOTO9035ELSEFORI=1TO50:IFFC(I)=PTHENFC(I)=0
9520 NEXT:FORI=1TOWN:IFWC(I)=PTHENWC(I)=0
9530 NEXT:FORI=1TO1000:NEXT:T=T-1:GOSUB8010:T=T+1:RETURN
9710 PRINT@911,"END GAME";:GOSUB9810:IFIN$="Y"THENFORI=1TO50:FC(I)=0:NEXT:FC(0)=-7:P(0)=PN:TN=T:FORI=1TO600:NEXT:RETURNELSEFORI=1TO600:NEXT:GOTO9035
9810 PRINT": ARE YOU SURE? ";
9820 GOSUB510:IFIN$="Y"THENPRINT"YES";:RETURNELSEIFIN$="N"THENPRINT"NO";:RETURNELSEGOSUB1610:GOTO9820
9920 CLS:PRINT"THIS IS NOW GAME YEAR"T".":PRINT"THERE ARE"TN"YEARS IN THE GAME.":GOSUB1510:PRINT"HOW MANY YEARS DO YOU WANT IN THE GAME NOW";:INPUTV$:V=VAL(V$):IFT>VORV>100PRINT"THAT ISN'T POSSIBLE.":GOSUB1610:GOTO9920ELSETN=V:RETURN
10010 PRINT@896,"THE WAR IS OVER! HIT <ENTER> FOR FINAL STATISTICS.":IFFC(0)=-7GOSUB510:GOTO10015ELSEPRINT"HIT <C> TO CONTINUE FOR ONE MORE YEAR.":GOSUB510:IFKY=67TN=T:T=T-1:GOSUB8010:T=T+1:GOTO10999
10015 IFKY=13THENN=0:GOTO10030
10020 GOSUB1610:GOTO10010
10030 CLS:FORP=1TOPN:V=0
10050 FORW=1TOWN:IFWC(W)=PTHENV=V+1
10070 NEXT
10080 PRINTP$NA$(P)" HAS"V"PLANETS."
10090 IFV>NTHENN=V
10100 NEXT:PRINT
10200 IFPN=1THEN10400
10300 FORP=1TOPN:V=0
10320 FORW=1TOWN:IFWC(W)=PTHENV=V+1
10340 NEXT
10350 IFV=NPRINT"CONGRATULATIONS TO "P$NA$(P)"!"
10360 NEXT
10370 GOSUB1660
10390 GOTO10500
10400 IFV=WNPRINT"CONGRATULATIONS!":GOSUB1410:GOTO10500
10410 PRINT"BETTER LUCK NEXT TIME."
10500 PRINT:PRINT
10610 FORI=1TO36:PRINTMID$("THIS IS CENTRAL CONTROL SIGNING OFF.",I,1);:GOSUB1760:NEXTI
10630 FORI=1TO2000:NEXTI:GOSUB1760:CLS
10999 RETURN
20010 GOSUB21010:GOSUB32000
20060 GOSUB25010
20061 ONERRORGOTO0:PRINT@960,"DO YOU WANT TO FINISH AN OLD GAME OR START A NEW ONE (O/N)? ";
20062 GOSUB510
20070 IFKY=78THENPRINT"NEW";:FORI=1TO400:NEXT:GOTO20130
20080 IFKY=79THENPRINT"OLD";:FORI=1TO400:NEXT:GOTO20100
20090 GOSUB1610:GOTO20062
20100 CLS:IFNOTDSKTHEN20110ELSEPRINT"WOULD YOU LIKE TO LOAD FROM TAPE OR DISK (T/D)? ";:GOSUB510:IFKY=84PRINT"TAPE":GOTO20110ELSEIFKY=68PRINT"DISK"ELSEGOSUB1610:GOTO20100
20102 PRINT"PREPARE DISK; PRESS <ENTER> WHEN READY, OR <A> TO ABORT.":GOSUB510:IFKY=13THENONERRORGOTO20103:PRINT"READING OLD GAME FROM DISK.":OPEN"I",1,"GXOLD":GOTO20104ELSEIFKY=65THENCLS:PRINT"GAME LOAD ABORTED.":GOTO20061ELSEGOSUB1610:GOTO20102
20103 PRINT"?CANNOT FIND OLD GAME FILE. ERROR CODE"ERR/2+1".":CLOSE1:GOSUB1610:RESUME20061
20104 ONERRORGOTO20109:INPUT#1,PN,WN,T,TN,FC(0),AB:FORI=1TOWN:INPUT#1,WS(I),WC(I),WX(I),WY(I),WI(I),W2(I):NEXT:FORI=0TOPN:INPUT#1,NA$(I):NEXT:FORI=1TO50:INPUT#1,FS(I),FD(I),FC(I),FT(I):NEXT
20108 CLOSE1:GOTO20116
20109 PRINT"?ERROR ENCOUNTERED IN READING FILE. ERROR CODE"ERR/2+1".":GOSUB1610:IFDSKCLOSE1:RESUME20061ELSERESUME20061
20110 PRINT"PREPARE DATA TAPE; PRESS <ENTER> WHEN READY, <A> TO ABORT.":GOSUB510:IFKY=13THENONERRORGOTO20109ELSEIFKY=65CLS:PRINT"GAME LOAD ABORTED.":GOTO20061ELSEGOSUB1610:GOTO20110
20111 PRINT"READING OLD GAME FROM TAPE.":INPUT#-1,PN,WN,T,TN,FC(0),AB:FORI=1TOWN:INPUT#-1,WS(I),WC(I),WX(I),WY(I),WI(I),W2(I):NEXT:FORI=0TOPN:INPUT#-1,NA$(I):NEXT:FORI=1TO46STEP5:I1=I+1:I2=I+2:I3=I+3:I4=I+4
20112 INPUT#-1,FS(I),FS(I1),FS(I2),FS(I3),FS(I4),FD(I),FD(I1),FD(I2),FD(I3),FD(I4),FC(I),FC(I1),FC(I2),FC(I3),FC(I4),FT(I),FT(I1),FT(I2),FT(I3),FT(I4):NEXT
20116 ONERRORGOTO0:T=T-1:GOSUB8010:T=T+1:GOTO20265
20130 GOSUB28010
20140 GOSUB33010:GOSUB29010:GOSUB30010
20160 PRINT@116,"NEW SETUP? ";:P=0:GOSUB510
20170 IFKY=89PRINT@183,"YES.";:PRINT@244,"PLEASE WAIT";:PRINT@308,"UNTIL I";:PRINT@372,"REARRANGE";:PRINT@436,"THE STARS.";:GOTO20140
20180 IFKY=78PRINT@183,"NO.";:PRINT@244,"PLEASE WAIT";:PRINT@308,"WHILE I SET";:PRINT@372,"UP THE GAME.";:GOTO20200
20190 GOSUB1610:GOTO20160
20200 GOSUB31010:GOSUB34040:GOSUB8010:T=1
20265 FORP=0TOPN:P(P)=0:NEXT
20270 P=RND(PN):IFP(P)=1THEN20270
20275 P(P)=1:P(0)=P(0)+1
20280 GOSUB3010
20290 IFP(0)<PNTHEN20270
20295 CLS
20300 GOSUB6010
20310 GOSUB7010
20320 FORW=1TOWN:W2(W)=WS(W):NEXT
20325 IFSFTHEN20340
20330 GOSUB8010
20340 T=T+1
20345 IFNOTSFTHEN20440
20350 CLS:ONERRORGOTO0:IFNOTDSKTHEN20380ELSEPRINT"SAVE GAME TO DISK OR TAPE (D/T)? ";:GOSUB510:IFKY=68PRINT"DISK"ELSEIFKY=84PRINT"TAPE":GOTO20380ELSEGOSUB1610:GOTO20350
20351 PRINT"PREPARE DISK; HIT <ENTER> WHEN READY, OR <A> TO ABORT.":GOSUB510:IFKY=65PRINT"GAME SAVE ABORTED.":GOTO20400ELSEIFKY<>13GOSUB1610:GOTO20351ELSEONERRORGOTO20352:PRINT"SAVING GAME TO DISK.":OPEN "O",1,"GXOLD":GOTO20355
20352 PRINT"?GAME FILE NOT ACCESSIBLE. ERROR CODE"ERR/2+1".":CLOSE1:RESUME 20351
20355 ONERRORGOTO20358:PRINT#1,PN,WN,T,TN,FC(0),AB:FORI=1TOWN:PRINT#1,WS(I),WC(I),WX(I),WY(I),WI(I),W2(I):NEXT:FORI=0TOPN:PRINT#1,NA$(I):NEXT:FORI=1TO50:PRINT#1,FS(I),FD(I),FC(I),FT(I):NEXT:CLOSE1:GOTO20399
20358 PRINT"?GAME FILE NOT WRITEABLE. ERROR CODE"ERR/2+1".":CLOSE1:RESUME20351
20380 PRINT"PREPARE TAPE; HIT <ENTER> WHEN READY, OR <A> TO ABORT.":GOSUB510:IFKY=65PRINT"GAME SAVE ABORTED.":GOTO20400ELSEIFKY<>13GOSUB1610:GOTO20380
20391 PRINT"SAVING GAME TO TAPE.":PRINT#-1,PN,WN,T,TN,FC(0),AB:FORI=1TOWN:PRINT#-1,WS(I),WC(I),WX(I),WY(I),WI(I),W2(I):NEXT:FORI=0TOPN:PRINT#-1,NA$(I):NEXT:FORI=1TO46STEP5:I1=I+1:I2=I+2:I3=I+3:I4=I+4
20392 PRINT#-1,FS(I),FS(I1),FS(I2),FS(I3),FS(I4),FD(I),FD(I1),FD(I2),FD(I3),FD(I4),FC(I),FC(I1),FC(I2),FC(I3),FC(I4),FT(I),FT(I1),FT(I2),FT(I3),FT(I4):NEXT
20399 PRINT"GAME SAVED."
20400 ONERRORGOTO0:SF=0:PRINT:PRINT"DO YOU WANT TO CONTINUE THE GAME? ";:GOSUB510
20410 IFKY=89THENPRINT"YES":FORI=1TO400:NEXT:T=T-1:GOSUB8010:T=T+1:GOTO20440
20420 IFKY=78THENCLS:GOTO20999
20430 GOSUB1610:GOTO20400
20440 IFT<=TNTHEN20265
20450 GOSUB10010
20500 IFT=TNTHEN20265
20999 END
21010 CLS:EP=0:DR=0:ML=0:SP=0:FR=0:Z=0:W=0:I=0:P=0
22010 P$="FLEET ADMIRAL ":SN$="ABCDEFGHIJKLMNOPQRSTUVWXYZ*#$%&+<=>?()@/"
23010 DIMWS(40),WX(40),WY(40),WI(40),WC(40),W2(40),FS(50),FD(50),FC(50),FT(50),P(20),NA$(20):RETURN
25010 GOSUB1410:FORI=1TO200:NEXT:PRINT@535,;:FORI=1TO13:PRINTMID$("G A L A X Y !",I,1);:GOSUB1760:NEXT
25020 RETURN
28010 CLS:PRINT@25,"> NEW GAME <":PRINT:PRINT
28020 GOSUB1510:INPUT"HOW MANY PLAYERS (1-20)";PN$:PN=INT(VAL(PN$)):IFPN<1ORPN>20THENGOSUB1610:GOTO28020ELSEPRINT
28040 GOSUB1510:INPUT"HOW MANY WORLDS (5-40)";WN$:WN=CINT(VAL(WN$))
28050 IFWN<=PNPRINT"THERE MUST BE MORE STARS THAN PLAYERS.":GOSUB1610:GOTO28040
28055 IFWN>40PRINTWN"IS TOO MANY STARS.":GOSUB1610:GOTO28040
28060 IFWN<5THENPRINTWN"IS TOO FEW STARS.":GOSUB1610:GOTO28040ELSEPRINT
28070 GOSUB1510:INPUT"HOW MANY YEARS (TURNS) ARE IN THE GAME (1-100)";TN$:TN=INT(VAL(TN$))
28080 IFTN<1ORTN>100THENGOSUB1610:GOTO28070
28100 PRINT:PRINT"DO YOU WANT THE NEUTRAL WORLDS TO BUILD DEFENSIVE SHIPS? ";:GOSUB510
28110 IFKY=89LETAB=1:PRINT"YES":GOTO28190
28120 IFKY=78LETAB=0:PRINT"NO":GOTO28190
28130 GOSUB1610:GOTO28100
28190 FORI=1TO400:NEXT:NA$(0)="TRS - 80"
28200 FORI=1TO130:NEXT:CLS
28210 FORP=1TOPN
28220 PRINT:PRINTP$;P;" WILL BEGIN THE GAME CONTROLLING WORLD "MID$(SN$,P,1)"."
28240 GOSUB1510:IN$="BOZO":INPUT"WHAT IS THE FLEET ADMIRAL'S NAME (1-4 CHARACTERS)";IN$
28250 IFLEN(IN$)>4THENGOSUB1610:GOTO28240
28260 NA$(P)=IN$
28300 NEXT
28800 PRINT:PRINT"PLEASE WAIT WHILE I CREATE THE UNIVERSE.";
28999 RETURN
29010 FORW=1TOWN
29020 WX(W)=RND(25):WY(W)=RND(15)
29030 FORI=0TOW-1
29040 IFWX(W)=WX(I)IFWY(W)=WY(I)THENI=W+1:FL=-1
29050 NEXT
29060 IFFLTHENFL=0:GOTO29020
29070 NEXT
29999 RETURN
30010 CLS:PRINTSTRING$(16,42)"  S T A R  M A P  "STRING$(17,42);
30020 FORI=1TO15:PRINT:FORJ=1TO26:PRINT". ";:NEXT:NEXT
30300 FORW=1TOWN
30310 DI=15360+WX(W)*2+WY(W)*64
30311 IFWC(W)=0THEN30320ELSEIFWC(W)=PTHENPOKEDI+1,138ELSEPOKEDI+1,93
30320 POKE DI,ASC(MID$(SN$,W,1)):GOSUB1960
30360 NEXT
30999 RETURN
31010 FORP=1TOPN:WI(P)=10:NEXT
31020 FORW=1+PNTOWN
31030 IFRND(0)<.25THEN31060
31040 WI(W)=WI(W)+1
31045 IFWI(W)>99THENWI(W)=99
31050 IFRND(0)<.67THEN31040
31060 NEXT
31999 RETURN
32000 MC$=STRING$(160,32):Q0=PEEK(VARPTR(MC$)+1):Q1=PEEK(VARPTR(MC$)+2):Q9=256:MO!=Q0+Q1*Q9:AR!=MO!:IFAR!>32767THENAR!=AR!-65536
32010 ONERRORGOTO32020:FORI=AR!+5TOAR!+154:READ MC:POKEI,MC:NEXTI:ONERRORGOTO0:GOTO32030
32020 CLS:PRINT"?BAD MEM SIZE":RESUME32021
32021 ONERRORGOTO0:END
32030 S1=(MO!+38)/Q9:S0=MO!+38-S1*Q9:S3=(MO!+56)/Q9:S2=MO!+56-S3*Q9:S5=(MO!+101)/Q9:S4=MO!+101-S5*Q9
32050 DATA 213,122,254,0,32,1,20,62,2,211,255,062,01,32,253,211,255,67,122,61,32,253,16,250,125,146,111,124,222,0,103,209,201
32060 DATA237,87,243,245,42,0,0,237,91,0,0,205,0,0,48,251,24,95
32070 DATA237,87,243,245,42,0,0,237,91,0,0,122,189,56,14,229,205,0,0,48,251,225,122,189,40,17,21,24,242,229,205,0,0,48,251,225,122,189,40,3,20,24,242,24,50
32080 DATA237,87,243,245,58,0,0,50,0,0,42,0,0,237,91,0,0,221,33,0,0,221,126,0,24,14,203,63,203,63,203,63,203,63,203,63,203,63,203,63,87,205,0,0,56,4,221,35,24,227
32090 DATA 241,224,251,201
32095 SO=-1:EP=AR!:DR=EP+1:ML=EP+2:SP=EP+3:FR=EP+4
32100 POKEEP+43,Q0:POKEEP+44,Q1:POKEEP+61,Q0:POKEEP+62,Q1:POKEEP+112,Q0:POKEEP+113,Q1
32110 Q1=(MO!+2)/Q9:Q0=MO!+2-Q1*Q9:POKEEP+47,Q0:POKEEP+48,Q1:POKEEP+65,Q0:POKEEP+66,Q1:POKEEP+116,Q0:POKEEP+117,Q1
32120 Q1=(MO!+5)/Q9:Q0=MO!+5-Q1*Q9:POKEEP+50,Q0:POKEEP+51,Q1:POKEEP+73,Q0:POKEEP+74,Q1:POKEEP+87,Q0:POKEEP+88,Q1:POKEEP+143,Q0:POKEEP+144,Q1:Q1=(MO!+4)/Q9:Q0=MO!+4-Q1*Q9:POKEEP+106,Q0:POKEEP+107,Q1
32125 Q1=(MO!+126)/Q9:Q0=MO!+126-Q1*Q9:POKEEP+109,Q0:POKEEP+110,Q1
32130 ON ERROR GOTO 32170
32140 CMD"T":DEFUSR0=EP+38:DEFUSR1=EP+56:DEFUSR2=EP+101
32150 DSK=-1
32160 GOTO 32190
32170 RESUME 32180
32180 DSK=0
32190 ON ERROR GOTO 0
32530 RETURN
33010 FORP=1TOPN:WC(P)=P:NEXT:RETURN
34040 FORP=1TOPN:WS(P)=12*WN-15*PN:FORI=1TOPN:WS(P)=WS(P)+300/(SQR((WX(P)-WX(I))[2+(WY(P)-WY(I))[2)+6):NEXT:FORW=PN+1TOWN:WS(P)=WS(P)-100/(SQR((WX(P)-WX(W))[2+(WY(P)-WY(W))[2)+3):NEXT:IFWS(P)<50THENWS(P)=50:NEXTELSENEXT
34120 IFPN=1THENW2(1)=WS(1):GOTO34200ELSEFORP=1TOPN:WS(0)=WS(0)+WS(P):NEXT:FORP=1TOPN:WS(P)=WS(P)*PN*100/WS(0):W2(P)=WS(P):NEXT:IFPN=WNRETURN
34200 FORW=1+PNTOWN
34210 WS(W)=WS(W)+(WI(W)+2.2)*RND(0):IFRND(0)<.67THEN34210ELSEW2(W)=WS(W):NEXT:RETURN
35010 CLS:PRINT@72,"TIME / DISTANCE CALCULATOR":PRINT@202,"HIT <ENTER> TO EXIT."
35030 PRINT@320,CHR$(30);"FROM WORLD:  ";:GOSUB2010:IFV=0THENRETURNELSEIFV<1ORV>WNGOSUB1610:GOTO35030ELSEWR=V:PRINT"TO WORLD:":GOSUB2010:IFV=0THENRETURNELSEIFV<1ORV>WNGOSUB1610:GOTO35030
35200 WD=V:DI!=SQR((WX(WR)-WX(WD))[2+(WY(WR)-WY(WD))[2):TI=(DI!+.99)/3:DI!=INT(DI!*100)/100:PRINT@458,CHR$(31)"DISTANCE:"DI!"LIGHT YEARS":PRINT@519,"TRAVEL TIME:"TI"YEARS":PRINT@582,"CURRENT YEAR:"T:PRINT@643,"YEAR OF ARRIVAL:";T+TI:GOTO35030
