0 GOSUB4000:X=32768:M=16384:HI=1000:Z=RND(-(PEEK(9)*256+PEEK(10))):GOSUB3000:GOSUB4140
10 S=1:Z=USR(0)
15 CLS:PRINT@0,"STAGE:";:PRINT@33,S;
20 FORI=0TO15
30 PRINT@8+I+32*0,"#";:PRINT@8+I+32*15,"#";:PRINT@25,"TIME:";
50 PRINT@8+32*I,"#";:PRINT@23+32*I,"#";
70 NEXT
80 Z=0:FORI=1TOS*3
90 X=RND(12)+9:Y=RND(11)+2:IFPEEK(M+X+32*Y)<>96THEN90
100 PRINT@X+32*Y,;:IF(I/3)-INT(I/3)THENPRINT"#";:GOTO110
102 PRINTMID$(STR$(I/3),2,1);:Z=Z+1:N(Z)=X+32*Y
110 NEXT:FORC=1TOZ
111 IF(PEEK(M+N(C)-1)=96ANDPEEK(M+N(C)+1)=96)OR(PEEK(M+N(C)-32)=96ANDPEEK(M+N(C)+32)=96)THENNEXT:GOTO120
112 X=RND(12)+9:Y=RND(11)+2:IFPEEK(M+X+32*Y)<>96THEN112
113 PRINT@N(C)," ";:PRINT@X+32*Y,MID$(STR$(C),2,1);:N(C)=X+32*Y:GOTO111
120 X=15:Y=14:V=16:W=14:C=1:POKE8,0
121 SOUND134,2:SOUND154,2:SOUND119,4:FORZ=1TO100:NEXT
122 SOUND73,4:SOUND134,2:SOUND154,2:SOUND134,2:SOUND154,2
123 SOUND134,2:SOUND154,2:SOUND119,4:FORZ=1TO150:NEXT:POKE8,8
125 K=0:K$=INKEY$:IFLEN(K$)THENK=ASC(K$)
130 A=-(K=83)+(K=65):B=-(K=90)+(K=87):IFPEEK(M+X+A+32*(Y+B))=96THENPRINT@X+32*Y," ";:X=X+A:Y=Y+B
150 IFPEEK(M+V-A+32*(W+B))=96THENPRINT@V+32*W," ";:V=V-A:W=W+B
160 PRINT@X+32*Y,"A";:PRINT@V+32*W,"B";
165 P=INT((X+V)/2)+32*INT((Y+W)/2):PRINT@P,;:IFPEEK(M+P)=C+112THEN180
170 T=INT(USR(1)/60):PRINT@25+32,T;:IFT<1000THEN125
175 PRINT@256,"TIME'S";:PRINT@256+32,"UP!";:GOTO340
180 IFY=WANDABS(X-V)=2ORX=VANDABS(Y-W)=2THENPRINT" ";:POKE8,0:SOUND212,1:POKE8,8:C=C+1:IFC=S+1THEN200
190 GOTO170
200 PRINT@480,"CLEAR!";
205 POKE8,0:SOUND5,2:SOUND1,2:SOUND11,2:SOUND93,4:SOUND73,4:SOUND93,4:SOUND73,4
206 FORZ=1TO2500:NEXT:IFS=9THEN300
210 S=S+1:GOTO15
300 POKE8,0:SOUND176,4:SOUND176,4:SOUND102,4:SOUND119,4:SOUND119,4:SOUND102,4
310 SOUND176,4:SOUND166,4:SOUND154,4:SOUND154,4:SOUND166,4:SOUND176,4:SOUND166,4
320 FORZ=1TO150:NEXT:SOUND154,8:SOUND154,4:POKE8,8
325 IFT<HITHENHI=T
330 PRINT@256,"DONE!";:PRINT@256+25,"HIGH:":PRINT@256+25+32,HI;
340 PRINT@480+25,"AGAIN?";
350 K$=INKEY$:IFK$=""THEN350
360 IFK$="Y"THEN10
370 IFK$="N"THENEND
380 GOTO350
3000 Z=16896:GOSUB3001:Z=X:GOSUB3001:EXECX:RETURN
3001 READ U$:FOR Y=1 TO LEN(U$) STEP 2:POKE Z,(ASC(MID$(U$,Y,1))-65)*16+ASC(MID$(U$,Y+1,1))-65:Z=Z+1:NEXT:RETURN
3002 DATA HPAAAIDLDLDLPOECBGKGACEKCGPCCBDLGOAD
3003 DATA INCFCABAABNMALMDDKEDJFAINNALNOPNAINPPNDLLNOLMHCGAEEPFPNNPNNMPNNNMKMGJAANHOPCMHDIPPECBGIGAIJHAIAODJ
4000 CLS:DIMA,B,X,Y,V,W,C,P,K,K$,T,N(10):PRINT
4010 PRINT"    THIS IS THE PUZZLE GAME"
4030 PRINT"  'BINARY ROOM' BASED ON THE"
4040 PRINT"   PUZZLE GAME 'BINARY LAND.'"
4050 PRINT"THE GAME CHARACTERS 'A' AND 'B'"
4060 PRINT" ARE CONTROLLED BY USING aswz."
4070 PRINT" INTERPOSE THE NUMBERS BETWEEN"
4080 PRINT" THE CHARACTERS TO GET THEM IN"
4090 PRINT"        ASCENDING ORDER."
4100 PRINT
4110 PRINT"   YOU HAVE 1000 SECONDS TO"
4120 PRINT"    COMPLETE THE 9 STAGES."
4130 PRINT:RETURN
4140 PRINT"    PRESS ANY KEY TO BEGIN"
4200 K$=INKEY$:IFK$=""THEN4200
4210 RETURN
5000 REM taisuke fukuno
5010 REM http://fukuno.jig.jp/1701
5020 REM MC-10 VER. JIM GERRIE
