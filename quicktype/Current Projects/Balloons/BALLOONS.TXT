RANDOMIZE TIMER
REM WASHAW GAMES
DIM PYL(1),PYR(1),PCL(1),PCR(1),PXB(1)
PYL(0)=1
PYR(0)=-1
PYL(1)=-1
PYR(1)=1
PCL(0)=12
PCL(1)=9
PCR(0)=9
PCR(1)=12
PXB(0)=-1
PXB(1)=1
PX=16
A=1
DIM POPS(5,15)
BBX=14
BBV=1

NEWGAME:
ILEVEL=-1
ILIVES=2

NEXTLEVEL:
ILEVEL=ILEVEL+1
ILIVES=ILIVES+1
IF ILEVEL=6 THEN GOTO GAMEWON
FOR I=0 TO ILEVEL
FOR P=0 TO 15
    POPS(I,P)=1
NEXT P
NEXT I
GOSUB DOMUSIC
CLS
COLOR 1
TEXT 16,12,"LEVEL "+ILEVEL,1
WAIT 0.01
WHILE SOUND LEN(0)>0
WEND
CLS

NEWBALL:
  BX=16
  BY=6
  DX=(RND-RND)
  DY=0.1
  SOUND 0,80,1,1



DO
  LAYER 0
  CLS 
  COLOR 15
  FOR I=1 TO ILIVES
      PLOT I*2,31
  NEXT I
  REM BOUNCERS
  BBX=BBX+BBV
  IF BBX=24 THEN BBV=-1
  IF BBX=4 THEN BBV=1
  COLOR 1
  LINE 0,16 TO 3,16
  LINE 28,16 TO 31,16
  LINE BBX,16 TO BBX+3,16
  
  REM POPS
  FOR I=0 TO ILEVEL
  FOR P=1 TO 15
      COLOR P
      X=(P*2+I+ISCROLL) MOD 32
      IF POPS(I,P)>0 THEN PLOT X,I
  NEXT P
  NEXT I
  ISCROLL=ISCROLL+0.1
  
  REM PADDLE
    IF BUTTON(0,1) AND A=1 THEN 
       A=0
       SOUND 0,80,1,3
    END IF
    IF BUTTON(0,2) AND A=0 THEN 
       A=1
       SOUND 0,80,1,3
    END IF
    IF LEFT(0) AND PX>1 THEN PX=PX-1
    IF RIGHT(0) AND PX<30 THEN PX=PX+1
    COLOR 2
    PLOT PX,30
    COLOR PCL(A)
    PLOT PX-1,30+PYL(A)
    COLOR PCR(A)
    PLOT PX+1,30+PYR(A)
    COLOR 1
    PLOT PX+PXB(A),30
  REM BALL
    COLOR 1
    IF DX<-0.5 THEN DX=-0.5
    IF DX>0.5 THEN DX=0.5
    BX=BX+DX
    IF BX<=0 THEN 
       DX=ABS(DX)
       SOUND 0,90,1,1
    END IF
    IF BX>=31 THEN 
       DX=-ABS(DX)
       SOUND 0,90,1,1
    END IF
    DY=DY+0.01
    IF DY>1 THEN DY=1
    IF DY<-1 THEN DY=-1
    BY=BY+DY
    IF BY<=0 THEN
       SOUND 0,90,1,1
       DY=ABS(DY)
    END IF
    IF BY>=31 THEN
       COLOR 4
       PLOT BX,BY
       SOUND 0,90,1,3
       WAIT 1
       ILIVES=ILIVES-1
       IF ILIVES=0 THEN GOTO GAMEOVER
       GOTO NEWBALL
    END IF
    IF BY<6 THEN
       ICH=POINT(BX,BY)
       IF ICH>0 THEN
          POPS(BY,ICH)=0
          SOUND 0,30,3,0
          BY=BY+1
          DY=ABS(DY)
          GOSUB LEVELCHECK
       ELSE
       COLOR 1
       PLOT BX,BY
       END IF
    END IF
    IBB=0
    IF INT(BY)=16 AND POINT(BX,BY)>0 THEN
       DY=-1
       BY=BY-1
       SOUND 0,80,1,1
       PLOT BX,BY
       IBB=1
       IF BX>16 THEN DX=DX-0.1 ELSE DX=DX+0.1
    END IF
    IF BY>6 THEN
    IHIT=0
    FOR I=-2 TO 2
        IF POINT(BX+I,BY)=2 OR POINT(BX+I,BY+1)=2 THEN IHIT=1
    NEXT I
    FOR I=-1 TO 1
        IF POINT(BX+I,BY)=9 OR POINT(BX+I,BY+1)=9 THEN IHIT=2
    NEXT I
    COLOR 1
    IF IHIT=0 THEN
       PLOT BX,BY
    ELSE IF IHIT>0 AND IBB=0 THEN
       SOUND 0,70+(IHIT*10),1,2
       A=(A+1) MOD 2
       DY=-0.5
       IF IHIT=2 THEN DY=-ABS(DY)-0.5
       BY=BY-2
       BX=PX+PXB(A)
       IF A=0 THEN DX=DX-0.5
       IF A=1 THEN DX=DX+0.5
       PLOT BX,BY
    END IF
    END IF
  WAIT 0.01
LOOP

LEVELCHECK:
  INEXT=1
  FOR I=0 TO ILEVEL
  FOR P=1 TO 15
      IF POPS(I,P)>0 THEN INEXT=0
  NEXT P
  NEXT I
  IF INEXT=1 THEN GOTO NEXTLEVEL
RETURN

DOMUSIC:
  SOUND OFF
  SOUND 0,72,6,1
  SOUND 0,0,3,1
  SOUND 0,71,6,1
  SOUND 0,0,3,1
  SOUND 0,69,3,1
  SOUND 0,0,2,1
  SOUND 0,70,3,1
  SOUND 0,0,2,1
  SOUND 0,69,3,1
  SOUND 0,0,2,1
  SOUND 0,70,3,1
  SOUND 0,0,2,1
  SOUND 0,69,6,1
  SOUND 0,0,3,1
  SOUND 0,68,6,1
  SOUND 0,0,3,1
  SOUND 0,66,6,1
  SOUND 0,0,3,1
  SOUND 0,67,6,1
  SOUND 0,0,12,1
  SOUND 0,0,1,1
RETURN

GAMEOVER: 
   GAMEPAD 0
   TEXT 16,12,"LEVEL "+ILEVEL,1
   SOUND 0,20,48,0
   WAIT 3
   SOUND 0,20,3,0
   WHILE NOT BUTTON(0,1)
   WEND
   GAMEPAD 1
   GOTO NEWGAME
   
GAMEWON:
   GAMEPAD 0
   COLOR 1
   TEXT 16,12,"YOU WIN",1
   SOUND 0,60,48,0
   SOUND 1,63,48,0
   SOUND 2,72,48,0
   WAIT 3
   SOUND 0,60,3,0
   WHILE NOT BUTTON(0,1)
   WEND
   GAMEPAD 1
   GOTO NEWGAME