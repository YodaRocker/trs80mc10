DISPLAY 4
LAYER OPEN 0,128,128,0
RANDOMIZE TIMER
ON PAUSE GOTO DEB
DIM MAP$(20,20)
DIM VIS(20,20)
ST:
CLS
COLOR 1
HP=10
GUN=FALSE
BUL=0
PLX=8
PLY=8
GOSUB MAKEMAP
GOSUB DEFINEMAP
WATER=20
MEAT=5
STP=0
GAMEPAD 1
INPUT "SEE THE INTRO?(Y/N)"; TMP$
IF TMP$="Y" THEN GOSUB INTRO
GAMEB:
CLS
KEYBOARD OFF
DO
  CLS 0
  GOSUB CHKWIN
  GOSUB DF
  GOSUB DISP
  IF UP(0) AND PLY>1 THEN
    PLY=PLY-1
    STP=STP+1
    WATER=WATER-1
    WAIT 0.2
    GOSUB CALCU
  ELSE IF DOWN(0) AND PLY<15 THEN 
    PLY=PLY+1
    STP=STP+1
    WATER=WATER-1
    WAIT 0.2
    GOSUB CALCU
  ELSE IF LEFT(0) AND PLX>1 THEN 
    PLX=PLX-1
    STP=STP+1
    WATER=WATER-1
    WAIT 0.2
    GOSUB CALCU
  ELSE IF RIGHT(0) AND PLX<15 THEN 
    PLX=PLX+1
    STP=STP+1
    WATER=WATER-1
    WAIT 0.2
    GOSUB CALCU
  END IF
  IF BUTTON(0) THEN GOSUB CHECK
  WAIT 0
LOOP




MAKEMAP:
FOR I=0 TO 20
  FOR J=0 TO 20
    TEMP=INT(RND*30)+1
    TEMP$="."
    IF TEMP=2 THEN 
      TEMP$="H"
      ITEM=ITEM+1
    ELSE IF TEMP=3 THEN 
      TEMP$="C"
      ITEM=ITEM+1
    ELSE IF TEMP=4 THEN 
      TEMP$="V"
      ITEM=ITEM+1
    ELSE IF TEMP=5 THEN 
      TEMP$="R"
    END IF
    MAP$(I,J)=TEMP$
  NEXT J
NEXT I
MAP$(8,8)="B"
RETURN

DEFINEMAP:
FOR I=0 TO 20
  FOR J=0 TO 20
    VIS(I,J)=0
  NEXT J
NEXT I
RETURN


DISP:
FOR I=1 TO 15
  FOR J=1 TO 15
    XDISP=(J-1)*7
    YDISP=(I-1)*7
    IF PLY=I AND PLX=J THEN COLOR 4 ELSE COLOR 1
    IF VIS(I,J)=1 THEN TEXT XDISP,YDISP,MAP$(I,J)
  NEXT J
NEXT I
COLOR 8
TEXT 63,110,"WATER: "+WATER,1
IF STP=5 THEN
  MEAT=MEAT-1
  STP=0
END IF
TEXT 63,120,"MEAT: "+MEAT,1
IF WATER<0 THEN 
  REASON=1
  GOTO DIE
ELSE IF MEAT<0 THEN
  REASON=2
  GOTO DIE
END IF
RETURN

DF:
VIS(PLY,PLX)=1
VIS(PLY-1,PLX)=1
VIS(PLY+1,PLX)=1
VIS(PLY,PLX-1)=1
VIS(PLY,PLX+1)=1
RETURN

CHECK:
IF MAP$(PLY,PLX)="B" THEN GOSUB BASE
IF MAP$(PLY,PLX)="V" THEN GOSUB VILL
IF MAP$(PLY,PLX)="R" THEN GOSUB ROAD
IF MAP$(PLY,PLX)="H" THEN GOSUB HOUSE
IF MAP$(PLY,PLX)="C" THEN GOSUB RCITY
RETURN

HOUSE:
CLS 1
COLOR 3
TEXT 63,54,"A HOUSE WITH THE DOOR OPEN.",1
TEXT 63,74,"YOU GO IN AND HAVE A LOOK.",1
TMP=INT(RND*5)+1
WAIT 2
IF TMP=5 THEN
  GOSUB FIGHT_ZOMBIE
  KEYBOARD OFF
  RETURN
END IF
CLS 1
TEXT 63,54,"A LADY WELCOMED YOU AND HANDED",1
TEXT 63,64,"YOU A GLASS OF WATER.",1
WATER=WATER+5
TEXT 63,74,"SHE WOULD LIKE TO TRADE SOMETHING.",1
WAIT 2
GOSUB TRADE
ITEM=ITEM-1
MAP$(PLY,PLX)="."
KEYBOARD OFF
RETURN

ROAD:
CLS 1
COLOR 3
TEXT 63,64,"A DUSTY ROAD LEADS TO SOMEWHERE.",1
WAIT 2
IF WATER>=10 THEN
  CLS 0
  COLOR 1
  TEXT 63,54,"A TRUCK BESIDES YOU.",1
  TEXT 63,64,"WHICH CAN HELP YOU",1
  TEXT 63,74,"TO POP BACK TO THE BASE QUICKLY.",1
  INPUT "USE 10 WATER TO RETURN BASE?(Y/N)"; TMP$
  IF TMP$="Y" THEN
    PLX=8
    PLY=8
    WATER=WATER-10
  END IF
END IF
RETURN

VILL:
CLS 3
COLOR 1
TEXT 63,0,"A SMALL VILLEGE STANDS",1
TEXT 63,10,"IN YOUR VIEW",1
WAIT 2
IF INT(RND*2)+1=1 THEN GOSUB FIGHT_MAN_STICK
KEYBOARD OFF
CLS 3
COLOR 1
TEXT 63,0,"YOU FIND SOME LOOT:",1
TEXT 63,10,"A LITTLE WATER,",1
TEXT 63,20,"A LITTLE MEAT,",1
TEXT 63,30,"2 BULLETS.",1
WATER=WATER+5
MEAT=MEAT+2
BUL=BUL+2
WAIT 3
MAP$(PLY,PLX)="."
ITEM=ITEM-1
RETURN

BASE:
CLS 11
COLOR 1
TEXT 63,0,"BASE, SWEET HOME.",1
WAIT 1
COLOR 12
TEXT 63,53,"REFILLING WATER...",1
WAIT 1
COLOR 8
TEXT 63,73,"TAKING MEAT...",1
WAIT 1
WATER=WATER+10
MEAT=MEAT+5
CLS 0
COLOR 1
INPUT "HAVE A GOOD SLEEP?(Y/N)";TMP$
KEYBOARD OFF
IF TMP$="Y" THEN
  CLS 0
  TEXT 63,63,"ZZZ...",1
  HP=HP+5
  WAIT 5
END IF
RETURN

RCITY:
CLS 10
COLOR 1
TEXT 64,54,"A HIGHWAY LEADS YOU TO",1
TEXT 64,64,"A RUINED CITY.",1
TEXT 64,74,"MAYBE THERE ARE THINGS YOU NEED.",1
WAIT 3
CLS 10
TMP=INT(RND*2)+1
IF TMP=1 THEN GOSUB FIGHT_MAN_STICK
CLS 10
COLOR 1
TEXT 63,54,"A SCHOOL WITH THE GATE OPEN.",1
TEXT 63,74,"YOU GO IN AND HAVE A LOOK.",1
WAIT 2
CLS 0
COLOR 4
TEXT 63,54,"DEAD STUDENTS ARE EVERYWHERE...",1
WAIT 0.5
TEXT 63,74,"WHO KNOWS WHAT HAPPENED...",1
WAIT 1.5
TMP=INT(RND*3)+1
IF TMP<>3 THEN GOSUB FIGHT_TEACHER
CLS 10
COLOR 1
TEXT 64,63,"YOU RUN OUT WITH A BAG OF MEAT.",1
MEAT=MEAT+5
WAIT 1.5
CLS 10
COLOR 1
PRINT
PRINT
PRINT
PRINT
PRINT
TEXT 63,10,"YOU ARE QUITE TIRED.",1
TEXT 63,20,"HAVE A SLEEP?",1
INPUT "(Y/N)>"; TMP$
IF TMP$="Y" THEN 
  GOSUB SLEEP_AT_WILD
ELSE 
  CLS 10
  COLOR 1
  TEXT 63,64,"MAYBE YOU SHOULD BACK TO BASE.",1
  WAIT 2
END IF
MAP$(PLY,PLX)="."
ITEM=ITEM-1
RETURN

SLEEP_AT_WILD:
CLS 0
TEXT 63,64,"ZZZ...",1
WAIT 2
CLS 13
COLOR 2
TEXT 63,0,"THE NIGHT...",1
WAIT 0.5
TEXT 63,54,"YOU SHOULD NOT SLEEP",1
TEXT 63,64,"BECAUSE WOLVES ARE EVERYWHERE!",1
TEXT 63,74,"NOW KILL! FIGHT TO THE EXIT!",1
WAIT 3
FOR I=1 TO INT(RND*4)+1
  GOSUB FIGHT_WOLF
NEXT I
KEYBOARD OFF
RETURN

INTRO:
RESTORE WORD
CLS 0
COLOR 11
DO
READ WD$
IF WD$="END" THEN EXIT
FOR I=1 TO LEN(WD$)
  TEXT 0,POS,LEFT$(WD$,I)
  IF MID$(WD$,I,1) <> " " THEN SOUND 1,INT(RND*30)+60,1,2 ELSE SOUND 1,INT(RND*30)+60,1,3
  WAIT 0.15
NEXT I
POS=POS+7
LOOP
WAIT 2
CLS 0
COLOR 4
FONT 3
TEXT 63,63,"THE RUSTYBLADE",1,1
WAIT 2
FONT 0
RETURN

DIE:
IF REASON=1 THEN TXT$="OUT OF WATER"
IF REASON=2 THEN TXT$="OUT OF MEAT"
IF REASON=3 THEN TXT$="LOSE TOO MUCH BLOOD"
CLS 4
FONT 3
COLOR 0
TEXT 63,53,"YOU DIED",1
FONT 0
TEXT 63,73,TXT$,1
END

WORD:
DATA "YOU."
DATA "YOU WOKE UP."
DATA "YOU'RE WONDERING WHERE ARE YOU."
DATA "YOU'RE FINDING THINGS"
DATA "TO SURVIVE."
DATA "NEED TO BUILD A BASE."
DATA "ALSO NEED SOME WATER."
DATA "DEAD ANIMALS IS FOOD."
DATA "YOU HAVE TO EXPLORER"
DATA "THE WHOLE LAND."
DATA "AND THE THING IN YOUR HAND"
DATA "IS"
DATA "END"


FIGHT_MAN_STICK:
CLS 0
COLOR 4
TEXT 63,54,"A MAN APPEARS.",1
TEXT 63,64,"A STICK IN HIS HAND",1
TEXT 63,74,"AND A CRIZY LOOK IN HIS EYES.",1
WAIT 3
EHP=5
EDMG=1
GOTO FIGHT

FIGHT_WOLF:
CLS 0
COLOR 4
TEXT 63,64,"A WOLF BARKING AT YOU.",1
WAIT 1
EHP=2
EDMG=2
GOTO FIGHT

FIGHT_ZOMBIE:
CLS 0
COLOR 4
TEXT 63,54,"A ZOMBIE APPEARS WITH A SCARY.",1
TEXT 63,64,"SOUND COMING FROM IT'S MOUTH.",1
TEXT 63,74,"IT ALSO HAS A SHARP BLADE.",1
WAIT 3
EHP=3
EDMG=2
GOTO FIGHT

FIGHT_TEACHER:
CLS 0
COLOR 4
TEXT 63,54,"A TEACHER WITH HIS EYES BLODDY RED",1
TEXT 63,64,"YOU DID THAT TO ME!!! HE SHOUTS",1
TEXT 63,74,"DOES HE NEEDS A LESSON?",1
WAIT 3
EHP=3
EDMG=1
GOTO FIGHT

FIGHT:
DO
  TMP=INT(RND*10)+1
  GOSUB CHOOSE
  CLS 0
  IF TMP/4=INT(TMP/4) THEN
    TEXT 63,0,"YOU AVOID THE ATTACK!",1
  ELSE
    TEXT 63,0,"THE ENEMY HIT YOU!",1
    TEXT 63,10,"YOU LOSE "+EDMG+" HP.",1
    HP=HP-EDMG
  END IF
  TEXT 63,20,"YOUR HP: "+HP,1
  IF TMP/5=INT(TMP/5) THEN
    TEXT 63,30,"THE ENEMY AVOID YOUR ATTACK!",1
  ELSE IF DMG<>0 THEN
    TEXT 63,30,"THE ENEMY GOT HIT!",1
    TEXT 63,40,"HE LOSE "+DMG+" HP.",1
    EHP=EHP-DMG
  END IF
  IF DMG=0 THEN
    TEXT 63,40,"YOU ATE A PIECE OF MEAT.",1
    HP=HP+2
    TEXT 63,50,"YOUR HP: "+HP+" .",1
  END IF
  TEXT 63,60,"ENEMY HP: "+EHP,1
  WAIT 3
  IF HP<=0 THEN
    REASON=3
    KEYBOARD OFF
    GOTO DIE
  ELSE IF EHP<=0 THEN
    CLS 0
    TEXT 63,63,"YOU WIN!",1
    WAIT 2
    KEYBOARD OFF
    RETURN
  END IF
LOOP



CHOOSE:
CLS 0
COLOR 1
PRINT
PRINT
PRINT
PRINT
PRINT
PRINT
PRINT
PRINT
PRINT
PRINT
PRINT
TEXT 63,0,"1.USE BLADE TO SWING",1
IF MEAT>0 THEN
  TEXT 63,10,"2.EAT MEAT(+2 HP)",1
END IF
TEXT 63,30,"HP: "+HP,1
IF GUN THEN 
  TEXT 63,20,"3.SHOOT(NEED 1 BULLET)",1
  TEXT 63,40,"BULLET: "+BUL,1
END IF
INPUT ">";TMP
IF TMP=1 THEN 
  DMG=1
  RETURN
ELSE IF TMP=2 AND MEAT>0 THEN
  DMG=0
  RETURN
ELSE IF TMP=3 AND GUN THEN
  IF BUL=0 THEN
    CLS 0
    COLOR 4
    TEXT 63,63,"NOT ENOUGH BULLET",1
    WAIT 1
    GOTO CHOOSE
  END IF
  BUL=BUL-1
  DMG=3
  RETURN
ELSE
  CLS 0
  COLOR 4
  TEXT 63,63,"INVALID CHOICE",1
  WAIT 1
  GOTO CHOOSE
END IF


CALCU:
TMP=INT(RND*25)+1
IF TMP=12 THEN
  CLS 1
  COLOR 4
  TEXT 63,54,"THE SOUND OF GUNFIRE APPEARS.",1
  TEXT 63,64,"A SNIPER RIGHT IN FRONT OF YOU",1
  TEXT 63,74,"AND A RIFLE IN HIS HAND.",1
  WAIT 3
  EHP=3
  EDMG=3
  GOSUB FIGHT
ELSE IF TMP=5 OR TMP=10 OR TMP=15 THEN
  CLS 1
  COLOR 4
  TEXT 63,54,"THE GRASS BESIDE YOU MOVES.",1
  TEXT 63,64,"AND A BEAST SUDDENLY RUNS OUT",1
  TEXT 63,74,"IT'S AS SCARY AS A GHOST.",1
  WAIT 3
  EHP=2
  EDMG=1
  GOSUB FIGHT
END IF
RETURN

CHKWIN:
IF ITEM<=0 THEN
  DO
  CLS 11
  FOR A=1 TO 200
    COLOR RND*16
    PLOT RND*128,RND*128
  NEXT A
  COLOR RND*16
  TEXT 63,61,"CONGRATS, YOU WIN!",1
  WAIT 0.05
  LOOP
END IF
RETURN

TRADE:
DO
CLS 0
COLOR 11
PRINT
PRINT
PRINT
PRINT
PRINT
PRINT
PRINT
PRINT
PRINT
PRINT
IF WATER>=20 THEN TEXT 63,0,"1.10 WATER->5 MEAT",1
IF MEAT>=10 THEN TEXT 63,10,"2.5 MEAT->10 WATER",1
IF MEAT>=20 THEN TEXT 63,20,"3.10 MEAT->AN OLD HUNTING GUN",1
IF WATER>=5 THEN TEXT 63,30,"4.2 WATER->1 BULLET",1
TEXT 63,40,"0.EXIT HOUSE",1
WAIT 0
INPUT ">";TMP
IF TMP=1 AND WATER>=20 THEN 
  WATER=WATER-10
  MEAT=MEAT+5
ELSE IF TMP=2 AND MEAT>=10 THEN
  WATER=WATER+10
  MEAT=MEAT-5
ELSE IF TMP=3 AND MEAT>20 THEN
  IF GUN THEN
    CLS 0
    COLOR 8
    TEXT 63,63,"YOU ALREADY HAVE!",1
    WAIT 1
  ELSE
    MEAT=MEAT-10
    GUN=TRUE
  END IF
ELSE IF TMP=0 THEN
  RETURN
ELSE IF TMP=4 AND WATER >= 5 THEN
  BUL=BUL+1
  WATER=WATER-1
ELSE
  CLS 0
  COLOR 4
  TEXT 63,63,"INVALID CHOICE",1
  WAIT 1
END IF
LOOP



DEB:
CLS 0
COLOR 1
PRINT "WELCOME TO DEBUG CONSOLE."
PRINT "INPUT RESET TO RESTART GAME."
PRINT "INPUT RETURN TO RETURN."
PRINT
COLOR 11
DO
  INPUT "DBG CONS>"; TMP$
  IF TMP$="RESET" THEN
    GOTO ST
  ELSE IF TMP$="CLS" THEN
    CLS 0
  ELSE IF TMP$="SET" THEN
    INPUT "OBJ>"; OBJ$
    INPUT "VAR>"; V
    IF OBJ$="HP" THEN
      HP=V
      COLOR 8
      PRINT "DONE!"
    ELSE IF OBJ$="MEAT" THEN
      MEAT=V
      COLOR 8
      PRINT "DONE!"
    ELSE IF OBJ$="WATER" THEN
      WATER=V
      COLOR 8
      PRINT "DONE!"
    ELSE IF OBJ$="GUN" THEN
      GUN=V
      COLOR 8
      PRINT "DONE!"
    ELSE IF OBJ$="BULLET" THEN
      BUL=V
      COLOR 8
      PRINT "DONE!"
    ELSE 
      COLOR 4
      PRINT "INVALID OBJ!"
    END IF
    COLOR 11
  ELSE IF TMP$="RETURN" THEN
    CLS
    KEYBOARD OFF
    GOTO GAMEB
  ELSE
    COLOR 4
    PRINT "INVALID!"
    COLOR 11
  END IF
LOOP