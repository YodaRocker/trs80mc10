10 PMODE0:PCLEAR1
20 CLEAR1000,29463
30 LOADM"PAULS2"
40 DEFUSR0=&H7318
50 CLS
60 OPEN"I",#1,"CHAR.DAT":INPUT#1,NAME$:INPUT#1,HP,FH,ST,IN,WI,DE,CO,CH,GP,EX,LVL:INPUT#1,W$,ARM$,SHI$:LVL=1:CLOSE#1:GOSUB2540
70 'W$="2SW":SHI$="3":GOSUB2290
80 GOSUB4080
90 DIMCAVE(26,26):PX=3:PY=25:F=4:A=1024:RF=0:LF=0:FF=0:PLAY"T255O1":TV=3:DIMBG(7,7):MLVL=1
100 LE=400
110 PRINT@198,"LOADING LEVEL DATA..."
120 MNAME$="LEVEL"+STR$(MLVL)+".MAP":OPEN"I",#1,MNAME$:FORX=0TO25:FORY=0TO25:INPUT#1,CAVE(X,Y):NEXTY,X:CLOSE#1:CLS
130 '
140 GOSUB1560
150 GOSUB1220
160 PRINT@463,STRING$(17,131);
170 LI$="FOOL!! EVIL AWAITS YOU...":GOSUB2070:GOSUB2090
180 '
190 GOSUB340
200 IFCAVE(PX,PY)=5THENU=USR(18):GOSUB4240:GOSUB340
210 I$=INKEY$:IFI$=""THEN210
220 IFI$="Q"THENGOSUB4020
230 IFI$="S"THENGOSUB4680
240 IFI$="C"THENGOSUB3020:GOSUB1350
250 IFI$=CHR$(94)ORI$="W"THENGOSUB1670:IFRND(50)=1THENGOSUB340:GOSUB1810
260 IFI$=CHR$(10)THENGOSUB1740
270 IFI$=CHR$(9)ORI$="D"THENF=F+1
280 IFF>4THENF=1
290 IFI$=CHR$(8)ORI$="A"THENF=F-1
300 IFF<1THENF=4
310 IFI$=CHR$(32)THENGOSUB1220:GOTO210
320 IFI$="M"THENGOSUB1480
330 I$="":GOTO180
340 '
350 IFF=1THENPRINT@7,"n";
360 IFF=2THENPRINT@7,"e";
370 IFF=3THENPRINT@7,"s";
380 IFF=4THENPRINT@7,"w";
390 IFCAVE(PX,PY)=3THENGOSUB3790
400 IFCAVE(PX,PY)=4THENGOSUB3950
410 IFF<>1THEN620
420 '
430 '
440 IFCAVE(PX-1,PY)=1ORCAVE(PX-1,PY)=5THENU=USR(7):LF=1
450 IFCAVE(PX-1,PY)=2THENLF=1:U=USR(12)
460 IFCAVE(PX-1,PY)=0ANDLF=1THENLF=0:U=USR(8)
470 IFCAVE(PX+1,PY)=1ORCAVE(PX+1,PY)=5THENU=USR(4):RF=1
480 IFCAVE(PX+1,PY)=0ANDRF=1THENRF=0:U=USR(5)
490 IFCAVE(PX+1,PY)=2THENRF=1:U=USR(13)
500 IFCAVE(PX,PY-1)=0THENU=USR(3):GOTO610
510 IFCAVE(PX,PY-1)=2THENRF=1:U=USR(11):RETURN
520 IFCAVE(PX,PY-1)=3ORCAVE(PX,PY-1)=4THENU=USR(16):RETURN
530 U=USR(1)
540 IFCAVE(PX-1,PY-1)=1ORCAVE(PX-1,PY-1)=5THENU=USR(6)
550 IFCAVE(PX-1,PY-1)=2THENU=USR(15)
560 IFCAVE(PX+1,PY-1)=1ORCAVE(PX+1,PY-1)=5THENU=USR(2)
570 IFCAVE(PX+1,PY-1)=2THENU=USR(14)
580 IFCAVE(PX,PY-2)=0THENU=USR(9)
590 IFCAVE(PX,PY-2)=2THENU=USR(10):RETURN
600 IFCAVE(PX,PY-2)=3ORCAVE(PX,PY-2)=4THENU=USR(17):RETURN
610 RETURN
620 '
630 IFF<>2THEN820
640 IFCAVE(PX,PY-1)=1ORCAVE(PX,PY-1)=5THENLF=1:U=USR(7)
650 IFCAVE(PX,PY-1)=2THENLF=1:U=USR(12)
660 IFCAVE(PX,PY-1)=0ANDLF=1THENLF=0:U=USR(8)
670 IFCAVE(PX,PY+1)=1ORCAVE(PX,PY+1)=5THENRF=1:U=USR(4)
680 IFCAVE(PX,PY+1)=2THENRF=1:U=USR(13)
690 IFCAVE(PX,PY+1)=0ANDRF=1THENRF=0:U=USR(5)
700 IFCAVE(PX+1,PY)=0THENU=USR(3):GOTO810
710 IFCAVE(PX+1,PY)=2THENU=USR(11):RETURN
720 IFCAVE(PX+1,PY)=3ORCAVE(PX+1,PY)=4THENU=USR(16):RETURN
730 U=USR(1)
740 IFCAVE(PX+1,PY-1)=1ORCAVE(PX+1,PY-1)=5THENU=USR(6)
750 IFCAVE(PX+1,PY-1)=2THENU=USR(15)
760 IFCAVE(PX+1,PY+1)=1ORCAVE(PX+1,PY+1)=5THENU=USR(2)
770 IFCAVE(PX+1,PY+1)=2THENU=USR(14)
780 IFCAVE(PX+2,PY)=2THENU=USR(10):RETURN
790 IFCAVE(PX+2,PY)=3ORCAVE(PX+2,PY)=4THENU=USR(17):RETURN
800 IFCAVE(PX+2,PY)=0THENU=USR(9)
810 RETURN
820 '
830 IFF<>3THEN1020
840 IFCAVE(PX+1,PY)=1ORCAVE(PX+1,PY)=5THENLF=1:U=USR(7)
850 IFCAVE(PX+1,PY)=2THENLF=1:U=USR(12)
860 IFCAVE(PX+1,PY)=0ANDLF=1THENLF=0:U=USR(8)
870 IFCAVE(PX-1,PY)=1ORCAVE(PX-1,PY)=5THENU=USR(4):RF=1
880 IFCAVE(PX-1,PY)=0ANDRF=1THENRF=0:U=USR(5)
890 IFCAVE(PX-1,PY)=2THENRF=1:U=USR(13)
900 IFCAVE(PX,PY+1)=0THENU=USR(3):GOTO1010
910 IFCAVE(PX,PY+1)=2THENU=USR(11):RETURN
920 IFCAVE(PX,PY+1)=3ORCAVE(PX,PY+1)=4THENU=USR(16):RETURN
930 U=USR(1)
940 IFCAVE(PX+1,PY+1)=1ORCAVE(PX+1,PY+1)=5THENU=USR(6)
950 IFCAVE(PX+1,PY+1)=2THENU=USR(15)
960 IFCAVE(PX-1,PY+1)=1ORCAVE(PX-1,PY+1)=5THENU=USR(2)
970 IFCAVE(PX-1,PY+1)=2THENU=USR(14)
980 IFCAVE(PX,PY+2)=0THENU=USR(9)
990 IFCAVE(PX,PY+2)=2THENU=USR(10):RETURN
1000 IFCAVE(PX,PY+2)=3ORCAVE(PX,PY+2)=4THENU=USR(17):RETURN
1010 RETURN
1020 '
1030 IFF<>4THENRETURN
1040 IFCAVE(PX,PY+1)=1ORCAVE(PX,PY+1)=5THENU=USR(7):LF=1
1050 IFCAVE(PX,PY+1)=0ANDLF=1THENLF=0:U=USR(8)
1060 IFCAVE(PX,PY+1)=2THENLF=1:U=USR(12)
1070 IFCAVE(PX,PY-1)=1ORCAVE(PX,PY-1)=5THENU=USR(4):RF=1
1080 IFCAVE(PX,PY-1)=0ANDRF=1THENRF=0:U=USR(5)
1090 IFCAVE(PX,PY-1)=2THENRF=1:U=USR(13)
1100 IFCAVE(PX-1,PY)=0THENU=USR(3):GOTO1210
1110 IFCAVE(PX-1,PY)=2THENU=USR(11):RETURN
1120 IFCAVE(PX-1,PY)=3ORCAVE(PX-1,PY)=4THENU=USR(16):RETURN
1130 U=USR(1)
1140 IFCAVE(PX-1,PY+1)=1ORCAVE(PX-1,PY+1)=5THENU=USR(6)
1150 IFCAVE(PX-1,PY+1)=2THENU=USR(15)
1160 IFCAVE(PX-1,PY-1)=1ORCAVE(PX-1,PY-1)=5THENU=USR(2)
1170 IFCAVE(PX-1,PY-1)=2THENU=USR(14)
1180 IFCAVE(PX-2,PY)=0THENU=USR(9)
1190 IFCAVE(PX-2,PY)=2THENU=USR(10):RETURN
1200 IFCAVE(PX-2,PY)=3ORCAVE(PX-2,PY)=4THENU=USR(17):RETURN
1210 RETURN
1220 '
1230 '
1240 TV=TV+1:IFTV>3THENTV=1
1250 I$=""
1260 IFTV<>1THEN1340
1270 '
1280 FORY=15TO431STEP32:PRINT@Y,STRING$(17,143);:NEXTY
1290 PRINT@18,CHR$(128);"commands";CHR$(128);:PRINT@79,"<SPACE> TO TOGGLE";:PRINT@113,"DISPLAY";
1300 PRINT@143,"<A-W-D> CLUSTER";:PRINT@175,"<K>ICK DOORS";:PRINT@207,"<M>AP DISPLAY";:PRINT@207,"<C>AST SPELLS";
1310 PRINT@239,"<M>AP DISPLAY";:PRINT@271,"<S>PEED";
1320 PRINT@335,"<Q>UIT GAME";
1330 RETURN
1340 IFTV<>2THEN1420
1350 '
1360 FORY=15TO431STEP32:PRINT@Y,STRING$(17,143);:NEXTY
1370 PRINT@(15+((17-LEN(NAME$))/2)),NAME$;
1380 PRINT@80,"STR";ST;:PRINT@89,"INT";IN;:PRINT@112,"WIS";WI;:PRINT@121,"DEX";DE;:PRINT@144,"CON";CO;:PRINT@153,"CHR";CH;
1390 PRINT@207,"LEVEL:";LVL;:PRINT@218,"AC";AC;:PRINT@239,"EXP:";EX;:PRINT@303,"HIT POINTS:";HP;:PRINT@335,"GOLD:";GP;
1400 PRINT@432,S1;S2;S3;S4;S5;
1410 RETURN
1420 IFTV<>3THENRETURN
1430 FORY=15TO431STEP32:PRINT@Y,STRING$(17,143);:NEXTY:PRINT@19,"inventory";:Y=47
1440 GOSUB2740:Y=Y+32
1450 GOSUB2890:Y=Y+32
1460 GOSUB2970:Y=Y+32
1470 RETURN
1480 '
1490 FORY=15TO431STEP32:PRINT@Y,STRING$(17,128);:NEXTY
1500 PRINT@19,"level";CHR$(128);"map";
1510 FORX=0TO25:FORY=0TO25
1520 IFCAVE(X,Y)<>0THENSET(X+32,Y+2,2)
1530 NEXTY,X
1540 SET(PX+32,PY+2,3)
1550 RETURN
1560 '
1570 PRINT@0,STRING$(15,128);
1580 PRINT@32,CHR$(128);CHR$(214);STRING$(11,223);CHR$(217);CHR$(128);
1590 PRINT@64,CHR$(128);CHR$(223);CHR$(214);STRING$(9,223);CHR$(217);CHR$(223);CHR$(128);
1600 PRINT@96,CHR$(128);STRING$(2,223);CHR$(214);STRING$(7,220);CHR$(217);STRING$(2,223);CHR$(128);
1610 FORX=128TO320STEP32:PRINT@X,CHR$(128);STRING$(2,223);CHR$(218);STRING$(7,175);CHR$(213);STRING$(2,223);CHR$(128);:NEXTX
1620 PRINT@352,CHR$(128);CHR$(223);CHR$(222);CHR$(177);STRING$(7,179);CHR$(178);CHR$(221);CHR$(223);CHR$(128);
1630 PRINT@384,CHR$(128);CHR$(222);CHR$(177);STRING$(9,191);CHR$(178);CHR$(221);CHR$(128);
1640 PRINT@416,CHR$(128);CHR$(177);STRING$(11,191);CHR$(178);CHR$(128);
1650 PRINT@448,STRING$(15,131);
1660 RETURN
1670 '
1680 IFF=1ANDCAVE(PX,PY-1)<>0THENPY=PY-1:PLAY"E":RETURN
1690 IFF=2ANDCAVE(PX+1,PY)<>0THENPX=PX+1:PLAY"E":RETURN
1700 IFF=3ANDCAVE(PX,PY+1)<>0THENPY=PY+1:PLAY"E":RETURN
1710 IFF=4ANDCAVE(PX-1,PY)<>0THENPX=PX-1:PLAY"E":RETURN
1720 PLAY"T255O1C1"
1730 RETURN
1740 '
1750 IFF=1ANDCAVE(PX,PY+1)<>0THENPY=PY+1:RETURN
1760 IFF=2ANDCAVE(PX-1,PY)<>0THENPX=PX-1:RETURN
1770 IFF=3ANDCAVE(PX,PY-1)<>0THENPY=PY-1:RETURN
1780 IFF=4ANDCAVE(PX+1,PY)<>0THENPX=PX+1:RETURN
1790 PLAY"T255O1C1"
1800 RETURN
1810 '
1820 GOSUB1350
1830 LI$="YOU ENCOUNTER A MONSTER!!":GOSUB2070:GOSUB2090
1840 GM=RND(16):IFGM=1THENMO=1ELSEIFGM=2THENMO=2ELSEIFGM=3THENMO=6ELSEIFGM=4THENMO=12ELSEIFGM=5THENMO=14ELSEIFGM=6THENMO=15ELSEIFGM=7THENMO=3ELSEIFGM=8THENMO=7
1850 IFGM=9THENMO=5ELSEIFGM=10THENMO=8ELSEIFGM=11THENMO=11ELSEIFGM=12THENMO=9ELSEIFGM=13THENMO=10ELSEIFGM=14THENMO=4ELSEIFGM=15THENMO=13ELSEIFGM=16THENMO=16ELSEIF GM=17THEN MO=17ELSEIF GM=18THEN MO=18
1860 IFMLVL<=2ANDGM>7THEN1840ELSEIF(MLVL>2ANDMLVL<=5)ANDGM>18THEN1840
1870 IFMO=1THENM$="SKELETON"ELSEIFMO=2THENM$="TROGLODYTE"ELSEIFMO=3THENM$="CAECILIA"ELSEIFMO=4THENM$="VAMPIRE"
1880 IFMO=5THENM$="WRAITH"ELSEIFMO=6THENM$="GIANT SPIDER"ELSEIFMO=7THENM$="DWARF"ELSEIFMO=8THENM$="WIZARD"ELSEIFMO=9THENM$="TROLL"ELSEIFMO=10THENM$="LESSER DEMON"
1890 IFMO=11THENM$="EVIL KNIGHT"ELSEIFMO=12THENM$="GREEN SLIME"ELSEIFMO=13THENM$="RED DRAGON"ELSEIFMO=14THENM$="PIXIE"ELSEIFMO=15THENM$="KOBOLD"ELSEIFMO=16THENM$="GUIDE GUY"
1900 IFMO=17THEN M$="MANTIS WARRIOR"ELSEIF MO=18THEN M$="GIANT SLUG"
1910 MO$=M$:IFMO<>1THENMO=(MO-1)*7+1
1920 OPEN"D",#1,"MONSTER.FIL"
1930 GET#1,MO:INPUT#1,A$:MD$=MID$(A$,1,2):MHP$=MID$(A$,3,2):DAM$=MID$(A$,5,2):MA$=MID$(A$,7,2):MG$=MID$(A$,9,2):MO=MO+1
1940 X=0:Y=0
1950 FORQ=MO TO MO+5:GET#1,Q:INPUT#1,A$:L=LEN(A$)
1960 FOR FP=1 TO L STEP3:P$=MID$(A$,FP,3):P=VAL(P$):PRINT@X+Y,CHR$(P);
1970 X=X+1:IFX>14THENY=Y+32:X=0
1980 NEXTFP,Q
1990 MD=VAL(MD$):MHP=VAL(MHP$):DAM=VAL(DAM$):MA=VAL(MA$):MG=VAL(MG$):MG=RND(MG)
2000 CLOSE
2010 SEED=MHP
2020 MHP=RND(SEED):IFMHP<MD THEN2020
2030 LI$="A "+MO$+"!":GOSUB2070:GOSUB2090
2040 IFRND(6)=1THENGOSUB2380
2050 GOSUB2130
2060 RETURN
2070 '
2080 PRINT@480+((32-LEN(LI$))/2),LI$;:RETURN
2090 '
2100 FORDLAY=1TOLE:NEXTDLAY
2110 PRINT@480,STRING$(31,143);
2120 RETURN
2130 '
2140 IFMHP<=0THENGOSUB2540:RETURN
2150 LI$="<F>IGHT, <C>AST SPELL OR <R>UN":GOSUB2070
2160 I$=INKEY$:IFI$=""THEN2160
2170 IFMHP<=0THENRETURN
2180 IFI$="F"THEN2230
2190 IFI$="C"THENGOSUB3020:GOSUB1350:IFMHP>0THENGOSUB2380:GOTO2130
2200 IFI$="R"THEN2470
2210 IFMHP<=0THENGOSUB2540:RETURN
2220 I$="":GOTO2160
2230 '
2240 GOSUB2090
2250 IF(LVL>=1ANDLVL<=3)THENCA=(19-MA)
2260 IF(LVL>=4ANDLVL<=6)THENCA=(17-MA)
2270 IF(LVL>=7ANDLVL<=9)THENCA=(14-MA)
2280 IF(LVL>=10ANDLVL<=12)THENCA=(12-MA)
2290 IFLVL>12THENCA=(10-MA)
2300 IFCA>20THENCA=20
2310 IFCA=1THENCA=2
2320 IFRND(20)>=(CA-DB)THENLI$="YOU HIT FOR":GOTO2340
2330 LI$="YOU MISS!!":GOSUB2070:GOSUB2090:GOTO2360
2340 '
2350 DTM=RND(PD)+SB:LI$=LI$+STR$(DTM)+" POINTS!":GOSUB2070:GOSUB2090:GOSUB2440:IFMHP<=0THENRETURN
2360 GOSUB2380
2370 GOTO2130
2380 '
2390 MTH=(19-AC-MD)
2400 IFRND(20)>=MTH THENLI$="YOU'RE HIT!!!":GOSUB2070:GOSUB2090:GOTO2420
2410 LI$="IT MISSES!!":GOSUB2070:GOSUB2090:RETURN
2420 HP=HP-RND(DAM):PRINT@303,"HIT POINTS:";HP;:IFHP<=0THENRUN"SKULL.BAS"
2430 RETURN
2440 '
2450 MHP=MHP-DTM:IFMHP<=0THENLI$="YOU DEFEAT THE "+MO$+"!":GOSUB2070:GOSUB2090:I$="":MG=RND(MG):EX=EX+(10*MD)+MG:GP=GP+MG:GOSUB1560:GOSUB2540:GOSUB1350
2460 RETURN
2470 '
2480 IFRND(10)>8THENGOSUB2090:LI$="YOU CANNOT ESCAPE!":GOSUB2070:GOSUB2090:GOTO2360
2490 GOSUB2090:LI$="YOU RUN AWAY!":GOSUB2070:GOSUB2090
2500 IFCAVE(PX+1,PY)<>0THENPX=PX+1:GOSUB2540:GOSUB1350:GOSUB1560:RETURN
2510 IFCAVE(PX-1,PY)<>0THENPX=PX-1:GOSUB2540:GOSUB1350:GOSUB1560:RETURN
2520 IFCAVE(PX,PY+1)<>0THENPY=PY+1:GOSUB2540:GOSUB1350:GOSUB1560:RETURN
2530 IFCAVE(PX,PY-1)<>0THENPY=PY-1:GOSUB2540:GOSUB1350:GOSUB1560:RETURN
2540 '
2550 AC=9
2560 IFVAL(MID$(ARM$,1,1))=0THEN2580
2570 IFMID$(ARM$,2,1)="L"THENAC=7-VAL(MID$(ARM$,1,1))ELSEIFMID$(ARM$,2,1)="C"THENAC=5-VAL(MID$(ARM$,1,1))ELSEIFMID$(ARM$,2,1)="P"THENAC=3-VAL(MID$(ARM$,1,1))
2580 IFARM$="LEA"THENAC=7ELSEIFARM$="CHA"THENAC=5ELSEIFARM$="PLA"THENAC=3
2590 IFSHI$<>"NONE"THENAC=AC-VAL(SHI$)-1
2600 IFDE=3THENAC=AC+3ELSEIF(DE>=4ANDDE<=5)THENAC=AC+2ELSEIF(DE>=6ANDDE<=8)THENAC=AC+1ELSEIF(DE>=13ANDDE<=15)THENAC=AC-1ELSEIF(DE>=16ANDDE<=17)THENAC=AC-2ELSEIFDE=18THENAC=AC-3
2610 IFW$="HAN"THENPD=2ELSEIFW$="DAG"THENPD=3ELSEIFW$="CLU"THENPD=4ELSEIFW$="AXE"THENPD=5ELSEIFW$="MAC"THENPD=6ELSEIFW$="SWO"THENPD=8
2620 IFVAL(MID$(W$,1,1))=0THEN2680
2630 IFMID$(W$,2,1)="S"THENPD=8
2640 IFMID$(W$,2,1)="D"THENPD=3
2650 IFMID$(W$,2,1)="C"THENPD=4
2660 IFMID$(W$,2,1)="A"THENPD=5
2670 IFMID$(W$,2,1)="M"THENPD=6
2680 IFST=3THENSB=-3ELSEIF(ST>=4ANDST<=5)THENSB=-2ELSEIF(ST>=6ANDST<=8)THENSB=-1ELSEIF(ST>=13ANDST<=15)THENSB=1ELSEIF(ST>=16ANDST<=17)THENSB=2ELSEIFST=18THENSB=3
2690 IFDE=3THENDB=-3ELSEIF(DE>=4ANDDE<=5)THENDB=-2ELSEIF(DE>=6ANDDE<=8)THENDB=-1ELSEIF(DE>=13ANDDE<=15)THENDB=1ELSEIF(DE>=16ANDDE<=17)THENDB=2ELSEIFDE=18THENDB=3
2700 IFVAL(MID$(W$,1,1))<>0THENDB=DB+VAL(MID$(W$,1,1)):SB=SB+VAL(MID$(W$,1,1))
2710 IFVAL(MID$(ARM$,1,1))<>0THENAC=AC-VAL(MID$(ARM$,1,1))
2720 IFHP>FH THENHP=FH
2730 RETURN
2740 '
2750 IFVAL(MID$(W$,1,1))<>0THEN2830
2760 IFW$="HAN"THENIW$="BARE HANDS"
2770 IFW$="DAG"THENIW$="DAGGER"
2780 IFW$="CLU"THENIW$="WOODEN CLUB"
2790 IFW$="AXE"THENIW$="HAND AXE"
2800 IFW$="MAC"THENIW$="MACE"
2810 IFW$="SWO"THENIW$="SWORD"
2820 GOTO2880
2830 IFMID$(W$,2,1)="D"THENIW$="DAGGER +"+MID$(W$,1,1)
2840 IFMID$(W$,2,1)="C"THENIW$="CLUB +"+MID$(W$,1,1)
2850 IFMID$(W$,2,1)="A"THENIW$="HAND AXE +"+MID$(W$,1,1)
2860 IFMID$(W$,2,1)="M"THENIW$="MACE +"+MID$(W$,1,1)
2870 IFMID$(W$,2,1)="S"THENIW$="SWORD +"+MID$(W$,1,1)
2880 PRINT@Y,IW$;:RETURN
2890 '
2900 IFVAL(MID$(ARM$,1,1))<>0THEN2950
2910 IFARM$="NONE"THENIW$="ROBES"
2920 IFARM$="LEA"THENIW$="LEATHER ARMOUR"
2930 IFARM$="CHA"THENIW$="CHAINMAIL"
2940 IFARM$="PLA"THENIW$="PLATEMAIL":GOTO2960ELSE2960
2950 IFMID$(ARM$,2,1)="L"THENIW$="LEATHER +"+MID$(ARM$,1,1)ELSEIFMID$(ARM$,2,1)="C"THENIW$="CHAINMAIL +"+MID$(ARM$,1,1)ELSEIFMID$(ARM$,2,1)="P"THENIW$="PLATEMAIL +"+MID$(ARM$,1,1)
2960 PRINT@Y,IW$;:RETURN
2970 '
2980 IFSHI$="NONE"THENRETURN
2990 IW$="SHIELD"
3000 IFSHI$<>"0"THENIS=(VAL(SHI$)-1):IW$=IW$+" +"+SHI$
3010 PRINT@Y,IW$:RETURN
3020 '
3030 SL=1
3040 FORY=15TO431STEP32:PRINT@Y,STRING$(17,143);:NEXTY
3050 PRINT@18,"spell book";
3060 ON SL GOSUB3160,3180,3200
3070 PRINT@303,"<#> TO CAST SPELL";:PRINT@335,"<0> TO ABORT";:PRINT@367,"<L> NEXT LEVEL";
3080 PRINT@432,S1;S2;S3;S4;S5;
3090 I$=INKEY$:IFI$=""THEN3090
3100 IFI$="0"THENFORY=15TO431STEP32:PRINT@Y,STRING$(17,143);:NEXTY:GOSUB2090:RETURN
3110 IFI$="L"THENSL=SL+1:IFSL>3THENSL=1
3120 IFI$="L"THENGOTO3060
3130 IFI$>="1"ANDI$<="5"THEN3220
3140 GOTO3090
3150 '
3160 FORY=47TO431STEP32:PRINT@Y,STRING$(17,143);:NEXTY:PRINT@83,"LEVEL 1";:PRINT@111,"<1>MAGIC MISSILE";:PRINT@143,"<2>SHIELD";:PRINT@175,"<3>SLEEP";:PRINT@207,"<4>CURE LIGHT";:PRINT@239,"<5>TURN UNDEAD";
3170 RETURN
3180 FORY=47TO431STEP32:PRINT@Y,STRING$(17,143);:NEXTY:PRINT@83,"LEVEL 2";:PRINT@111,"<1>KNOCK";:PRINT@143,"<2>WEB";:PRINT@175,"<3>BLESS";:PRINT@207,"<4>LESSER HOLD";
3190 RETURN
3200 FORY=47TO431STEP32:PRINT@Y,STRING$(17,143);:NEXTY:PRINT@83,"LEVEL 3";:PRINT@111,"<1>FIRE BALL";:PRINT@143,"<2>LIGHTNING BOLT";:PRINT@175,"<3>STRIKING";:PRINT@207,"<4>LOCATE EXIT";
3210 RETURN
3220 IFSL=1AND(S1<>0)THENONVAL(I$)GOSUB3260,3300,3330,3390,3430:S1=S1-1:RETURN
3230 IFSL=2AND(S2<>0)THENONVAL(I$)GOSUB3500,3520,3560,3590:S2=S2-1:RETURN
3240 IFSL=3AND(S3<>0)THENONVAL(I$)GOSUB3650,3700,3750,3780:S3=S3-1:RETURN
3250 GOSUB2090:LI$="YOU CANNOT CAST!":GOSUB2070:GOSUB2090:RETURN
3260 '
3270 GOSUB4750
3280 IFMHP>0THENDTM=(RND(6)+1)*(1+INT(LVL/5)):GOSUB2090:LI$="THE MISSILE SLAMS THE CREATURE!":GOSUB2070:GOSUB2090:LI$="THE "+MO$+" TAKES"+STR$(DTM)+" DAMAGE!":GOSUB2070:GOSUB2090:GOSUB2440
3290 RETURN
3300 '
3310 IFMHP>0THENAC=AC-2:GOSUB2090:LI$="YOUR ARMOUR GLOWS BRIEFLY...":GOSUB2070:GOSUB2090
3320 RETURN
3330 '
3340 IFMHP<=0THENRETURN
3350 GOSUB4750
3360 IFMD-1>LVLTHENGOSUB218:LI$="THE "+MO$+" LAUGHS!":GOSUB2070:GOSUB2090:RETURN
3370 GOSUB2090:LI$="THE MONSTER SLEEPS...":GOSUB2070:DTM=RND(PD+1)*5+SB:GOSUB2090:LI$="YOU SWING A MIGHTY BLOW...":GOSUB2070:GOSUB2090:GOSUB2440
3380 RETURN
3390 '
3400 HP=HP+RND(9)+1:IFHP>FH THENHP=FH
3410 GOSUB2090:LI$="YOU FEEL BETTER...":GOSUB2070:GOSUB2090:IFMHP<=0THENGOSUB1350
3420 RETURN
3430 '
3440 IFMHP<=0THENRETURN
3450 GOSUB4750
3460 IFMO<>1ANDMO<>4ANDMO<>5ANDMO<>10THEN3490
3470 IFMD-1>LVLTHENGOSUB218:LI$="NO EFFECT...":GOSUB2070:GOSUB2090:RETURN
3480 IFRND(20)>((3+MD)-LVL)THENGOSUB2090:LI$="THE UNDEAD DISINTEGRATES!":GOSUB2070:GOSUB2090:MHP=0:GOSUB1560:RETURN
3490 GOSUB2090:LI$="THE "+MO$+" LAUGHS...":GOSUB2070:GOSUB2090:RETURN
3500 '
3510 GOSUB2090:LI$="CANNOT BE CAST AT THIS TIME!":GOSUB2070:GOSUB2090:RETURN
3520 '
3530 GOSUB4750
3540 IFRND(100)>80THENGOSUB2090:LI$="THE "+MO$+" DODGES THE WEB!":GOSUB2070:GOSUB2090:RETURN
3550 GOSUB2090:LI$="THE "+MO$+" IS CAUGHT!":GOSUB2070:GOSUB2090:LI$="YOU ESCAPE THE "+MO$+"!!":GOSUB2070:GOSUB2090:MHP=0:GOSUB1560:RETURN
3560 '
3570 IFMHP<=0THENRETURN
3580 HP=HP+RND(4):DB=DB+1:SB=SB+1:GOSUB2090:LI$="YOU ARE BLESSED BY THE GODS...":GOSUB2070:GOSUB2090:RETURN
3590 '
3600 IFMHP<=0THENRETURN
3610 GOSUB4750
3620 IFMD>2THENGOSUB2090:LI$="THE SPELL HAS NO EFFECT!":GOSUB2070:GOSUB2090:RETURN
3630 GOSUB2090:LI$="THE "+MO$+" IS HELD!!":GOSUB2070:GOSUB2090:LI$="YOU KILL THE "+MO$+"!":GOSUB2070:GOSUB2090
3640 MHP=0:MG=RND(MG):EX=EX+(10*MD)+M:GP=GP+MG:GOSUB1560:RETURN
3650 '
3660 IFMHP<=0THENRETURN
3670 GOSUB4750
3680 DTM=0:FORX=1TOLVL:DTM=DTM+RND(6):NEXTX:GOSUB2090:LI$="THE FIRE BALL EXPLODES!!":GOSUB2070:GOSUB2090:LI$="THE "+MO$+" TAKES"+STR$(DTM)+" DAMAGE!":GOSUB2070:GOSUB2090
3690 GOSUB2440:RETURN
3700 '
3710 IFMHP<=0THENRETURN
3720 GOSUB4750
3730 DTM=0:FORX=1TOLVL:DTM=DTM+RND(6):NEXTX:GOSUB2090:LI$="YOUR HANDS SPEW LIGHTNING!!":GOSUB2070:GOSUB2090:LI$="IT FRIES FOR"+STR$(DTM)+" DAMAGE!!":GOSUB2070:GOSUB2090
3740 GOSUB2440:RETURN
3750 '
3760 IFMHP<=0THENRETURN
3770 SB=SB+10:GOSUB2090:LI$="YOUR WEAPON GLOWS BRIGHTLY!":GOSUB2070:GOSUB2090:RETURN
3780 '
3790 '
3800 GOSUB2090:LI$="TAKE THE ELEVATOR up? (Y/N)":GOSUB2070
3810 I$=INKEY$:IFI$=""THEN3810
3820 IFI$<>"Y"ANDI$<>"N"THEN3810
3830 IFI$="N"THENGOSUB2090:GOTO3900
3840 IFI$="Y"ANDMLVL=1THENOPEN"O",#1,"CHAR.DAT":WRITE#1,NAME$:WRITE#1,HP,FH,ST,IN,WI,DE,CO,CH,GP,EX,LVL:WRITE#1,W$,ARM$,SHI$:CLOSE#1:RUN"INN"
3850 IFI$="Y"ANDMLVL<>1THENFORX=0TO416STEP32:PRINT@X,STRING$(15,128);:NEXTX:MLVL=MLVL-1:GOSUB2090:LI$="THE ELEVATOR RISES...":GOSUB2070:MNAME$="LEVEL"+STR$(MLVL)+".MAP":OPEN"I",#1,MNAME$
3860 FORX=0TO25:FORY=0TO25:INPUT#1,CAVE(X,Y):NEXTY,X:CLOSE#1:GOSUB2090:GOSUB1560
3870 RETURN
3880 I$=INKEY$:IFI$=""THEN3880
3890 GOSUB4080:GOTO130
3900 IFCAVE(PX-1,PY)=1THENPX=PX-1:RETURN
3910 IFCAVE(PX+1,PY)=1THENPX=PX+1:RETURN
3920 IFCAVE(PX,PY-1)=1THENPY=PY-1:RETURN
3930 IFCAVE(PX,PY+1)=1THENPY=PY+1:RETURN
3940 STOP
3950 '
3960 GOSUB2090:LI$="TAKE THE ELEVATOR down? (Y/N)":GOSUB2070
3970 I$=INKEY$:IFI$=""THEN3970
3980 IFI$<>"Y"ANDI$<>"N"THEN3970
3990 IFI$="N"THENGOSUB2090:GOTO3900
4000 IFI$="Y"THENFORX=0TO416STEP32:PRINT@X,STRING$(15,128);:NEXTX:GOSUB2090:LI$="YOU DESCEND SLOWLY...":GOSUB2070:MLVL=MLVL+1:MNAME$="LEVEL"+STR$(MLVL)+".MAP":OPEN"I",#1,MNAME$:FORX=0TO25:FORY=0TO25:INPUT#1,CAVE(X,Y):NEXTY,X:CLOSE#1:GOSUB2090
4010 GOSUB1560:RETURN
4020 '
4030 GOSUB2090:LI$="QUIT? (Y/N)":GOSUB2070
4040 I$=INKEY$:IFI$=""THEN4040
4050 IFI$="Y"THENCLS:END
4060 IFI$="N"THENRETURN
4070 GOTO4040
4080 '
4090 S1=0:S2=0:S3=0:S4=0:S5=0
4100 IFEX<1500THENLVL=1:S1=1:S2=0
4110 IF(EX>=1500)AND(EX<=2999)THENLVL=2:S1=2:S2=0
4120 IF(EX>=3000)AND(EX<=5999)THENLVL=3:S1=2:S2=1:S3=0
4130 IF(EX>=6000)AND(EX<=11999)THENLVL=4:S1=2:S2=2:S3=0
4140 IF(EX>=12000)AND(EX<=24999)THENLVL=5:S1=2:S2=2:S3=1
4150 IF(EX>=25000)AND(EX<=49999)THENLVL=6:S1=2:S2=2:S3=2
4160 IF(EX>=50000)AND(EX<=99999)THENLVL=7:S1=3:S2=2:S3=2:S4=1
4170 IF(EX>=100000)AND(EX<=199999)THENLVL=8:S1=3:S2=3:S3=2:S4=2
4180 IF(EX>=200000)AND(EX<=299999)THENLVL=9:S1=3:S2=3:S3=3:S4=2:S5=1
4190 IF(EX>=300000)AND(EX<=399999)THENLVL=10:S1=3:S2=3:S3=3:S4=3:S5=2
4200 IF(EX>=400000)AND(EX<=499999)THENLVL=11:S1=3:S2=3:S3=3:S4=3:S5=3
4210 IF(EX>=500000)AND(EX<=599999)THENLVL=12:S1=4:S2=4:S3=4:S4=4:S5=4
4220 IFEX>=600000THENLVL=13:S1=4:S2=4:S3=5:S4=5:S5=5
4230 RETURN
4240 '
4250 LI$="YOU FIND A CHEST!!":GOSUB 2070:GOSUB2090
4260 LI$="WILL YOU OPEN IT? (Y/N)":GOSUB2070
4270 I$=INKEY$:IFI$=""THEN4270
4280 IFI$="N"THENCAVE(PX,PY)=1:GOSUB1560:GOSUB2090:RETURN
4290 IFI$="Y"THENGOSUB2090:GOTO4310
4300 GOTO4270
4310 RC=RND(10)
4320 IFRC=1THENLI$="IT'S EMPTY...":GOSUB2070:GOSUB2090:GOSUB1560:CAVE(PX,PY)=1:RETURN
4330 IFRC=2ORRC=3THENCD=INT(RND(5*(LVL*.5))):LI$="THE CHEST EXPLODES!!!":GOSUB2070:GOSUB2090:LI$="YOU TAKE"+STR$(CD)+" POINTS DAMAGE!":GOSUB2070:GOSUB2090:HP=HP-CD:IFHP<0THENRUN"SKULL.BAS"
4340 IFRC=4THEN4360
4350 CG=RND((RC+10+MLVL)*10):LI$="YOU FIND"+STR$(CG)+" GOLD!":GOSUB2070:GOSUB2090:CAVE(PX,PY)=1:GP=GP+CG:EX=EX+CG:GOSUB1350:GOSUB1560:RETURN
4360 IFRND(2)=1THEN4530ELSETW$=W$:IFRND(100)>40THEN4410
4370 RC=RND(2+(INT(MLV/4))):W$=STR$(RC)
4380 RC=RND(5):IFRC=1THENW$=W$+"DA"ELSEIFRC=2THENW$=W$+"CL"ELSEIFRC=3THENW$=W$+"AX"ELSEIFRC=4THENW$=W$+"MA"ELSEIFRC=5THENW$=W$+"SW"
4390 TEMP$=RIGHT$(W$,3):W$=TEMP$
4400 GOTO4420
4410 RC=RND(5):IFRC=1THENW$="DAG"ELSEIFRC=2THENW$="CLU"ELSEIFRC=3THENW$="AXE"ELSEIFRC=4THENW$="MAC"ELSEIFRC=5THENW$="SWO"
4420 IFVAL(MID$(W$,1,1))<>0THEN4440
4430 IFW$="DAG"THENIW$="DAGGER"ELSEIFW$="CLU"THENIW$="CLUB"ELSEIFW$="AXE"THENIW$="HAND AXE"ELSEIFW$="MAC"THENIW$="MACE"ELSEIFW$="SWO"THENIW$="SWORD"
4440 IFVAL(MID$(W$,1,1))=0THEN4470
4450 IFMID$(W$,2,1)="D"THENIW$="DAGGER"ELSEIFMID$(W$,2,1)="C"THENIW$="CLUB"ELSEIFMID$(W$,2,1)="A"THENIW$="HAND AXE"ELSEIFMID$(W$,2,1)="M"THENIW$="MACE"ELSEIFMID$(W$,2,1)="S"THENIW$="SWORD"
4460 IW$=IW$+"+"+MID$(W$,1,1)
4470 GOSUB2090:LI$="YOU FIND A "+IW$+"!":GOSUB2070:GOSUB2090
4480 LI$="DO YOU EQUIP THE WEAPON? (Y/N)":GOSUB2070
4490 I$=INKEY$:IFI$=""THEN4490
4500 IFI$="N"THENW$=TW$:GOSUB2090:GOSUB1560:CAVE(PX,PY)=1:RETURN
4510 IFI$="Y"THENGOSUB2090:GOSUB1560:CAVE(PX,PY)=1:GOSUB2540:RETURN
4520 GOTO4490
4530 '
4540 TARM$=ARM$:IFRND(100)>40THEN4570
4550 RC=RND(2+(INT(MLVL/5))):ARM$=STR$(RC):RC=RND(3):IFRC=1THEN ARM$=ARM$+"LE"ELSEIFRC=2THEN ARM$=ARM$+"CH"ELSEIFRC=3THEN ARM$=ARM$+"PL"
4560 TEMP$=RIGHT$(ARM$,3):ARM$=TEMP$:GOTO4580
4570 RC=RND(3):IFRC=1THENARM$="LEA"ELSEIFRC=2THENARM$="CHA"ELSEIFRC=3THENARM$="PLA"
4580 IFVAL(MID$(ARM$,1,1))<>0THEN4600
4590 IFARM$="LEA"THENIARM$="LEATHER"ELSEIFARM$="CHA"THENIARM$="CHAINMAIL"ELSEIFARM$="PLA"THENIARM$="PLATEMAIL"
4600 IFVAL(MID$(ARM$,1,1))=0THEN4630
4610 IFMID$(ARM$,2,1)="L"THENIARM$="LEATHER"ELSEIFMID$(ARM$,2,1)="C"THENIARM$="CHAINMAIL"ELSEIFMID$(ARM$,2,1)="P"THENIARM$="PLATEMAIL"
4620 IARM$=IARM$+"+"+MID$(ARM$,1,1)
4630 GOSUB2100:LI$="YOU FIND "+IARM$+" ARMOUR!":GOSUB2080:GOSUB2100
4640 LI$="DO YOU EQUIP THE ARMOUR? (Y/N)":GOSUB2080
4650 I$=INKEY$:IFI$=""THEN4650
4660 IFI$="N"THENARM$=TARM$:GOSUB2100:GOSUB1570:CAVE(PX,PY)=1:RETURN
4670 IFI$="Y"THENGOSUB2100:GOSUB1570:CAVE(PX,PY)=1:GOSUB2540:RETURN
4680 '
4690 GOSUB2090:LI$="INCREASE, DECREASE SPEED? (I/D)":GOSUB2070
4700 I$=INKEY$:IFI$=""THEN4700
4710 IFI$="I"THENLE=LE+50:GOSUB2090:GOTO4740
4720 IFI$="D"THENLE=LE-50:GOSUB2090:GOTO4740
4730 GOTO4700
4740 LI$="DELAY LENGTH ="+STR$(LE):GOSUB2070:GOSUB2090:I$="":RETURN
4750 '
4760 FORY=0TO5:FORX=0TO6:BG(X,Y)=PEEK(1024+(X+132+(Y*32))):NEXTX,Y
4770 U=USR(19)
4780 FORY=0TO5:FORX=0TO6:HY=Y*32:HX=X+132:P=BG(X,Y):POKE1024+HX+HY,P:NEXTX,Y
4790 RETURN
