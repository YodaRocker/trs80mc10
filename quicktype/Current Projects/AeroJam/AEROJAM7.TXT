10 CLS0:GOSUB3600
20 DATA HPAAAIDLDLDLPOECBGKGACEKCGPCCBDLGOAD
30 DATA INCFCABAABNMALMDDKEDJFAINNALNOPNAINPPNDLLNOLMHCGAEEPFPNNPNNMPNNNMKMGJAANHOPCMHDIPPECBGIGAIJHAIAODJ
100 REM AEROJAM
110 REM COPYRIGHT 1984 DILITHIUM PRESS
120 REM BY PHIL FELDMAN AND TOM RUGG
130 CLS:RESTORE:READR$:READR$
140 GOTO230
150 PRINT@0,;:IFCD=0THEN170
160 PRINT@32*CD,;
170 IFCR=0THEN190
180 PRINT@32*CD+CR,;
190 RETURN
200 CR=XP(Q):CD=YP(Q):GOSUB150:RETURN
210 CR=20:FORCD=13TO14:GOSUB150
220 PRINTLEFT$(SP$,12);:NEXT:PRINT@32*15+20,LEFT$(SP$,11);:POKEMC+511,96:PRINT@32*13+20,;:RETURN
230 J=0:K=0:Q=0:M=0:W=0:N=0:ND=0:TF=USR(1):CD=0:CR=0
240 NM=0:NV=0:EV=0:NB=0:P=1:R$="A"
250 MX=30:MC=16384
260 DIMG(72),CG(MX),GD(MX),TV(MX),DK(MX),TE(MX),DM(MX),XP(72),YP(72),CV$(4),T$(4,4,4),B$(4,4,4)
261 T$(2,3,1)="±≤":T$(2,1,1)="Ä≥":T$(2,2,1)="Ä≤":T$(2,4,1)="±±":T$(1,3,1)="±≤":T$(1,1,1)="Ä≥":T$(1,2,1)="Ä≤":T$(1,4,1)="±≥"
262 B$(2,3,1)="±æ":B$(2,1,1)="¥ª":B$(2,2,1)="µΩ":B$(2,4,1)="¥æ":B$(1,3,1)="µæ":B$(1,1,1)="¥ø":B$(1,2,1)="µø":B$(1,4,1)="¥æ"
263 T$(2,3,3)="ëí":T$(2,1,3)="Äì":T$(2,2,3)="Äí":T$(2,4,3)="ëë":T$(1,3,3)="ëí":T$(1,1,3)="Äì":T$(1,2,3)="Äí":T$(1,4,3)="ëì"
264 B$(2,3,3)="ëû":B$(2,1,3)="îõ":B$(2,2,3)="ïù":B$(2,4,3)="îû":B$(1,3,3)="ïû":B$(1,1,3)="îü":B$(1,2,3)="ïü":B$(1,4,3)="îû"
265 T$(2,3,2)="°¢":T$(2,1,2)="Ä£":T$(2,2,2)="†¢":T$(2,4,2)="°°":T$(1,3,2)="°¢":T$(1,1,2)="Ä£":T$(1,2,2)="Ä¢":T$(1,4,2)="°£"
266 B$(2,3,2)="°Æ":B$(2,1,2)="§´":B$(2,2,2)="•≠":B$(2,4,2)="§Æ":B$(1,3,2)="•Æ":B$(1,1,2)="§Ø":B$(1,2,2)="•Ø":B$(1,4,2)="§Æ"
267 T$(2,3,4)="ÅÇ":T$(2,1,4)="ÄÉ":T$(2,2,4)="ÄÇ":T$(2,4,4)="ÅÅ":T$(1,3,4)="ÅÇ":T$(1,1,4)="ÄÉ":T$(1,2,4)="ÄÇ":T$(1,4,4)="ÅÉ"
268 B$(2,3,4)="Åé":B$(2,1,4)="Ñã":B$(2,2,4)="Öç":B$(2,4,4)="Ñé":B$(1,3,4)="Öé":B$(1,1,4)="Ñè":B$(1,2,4)="Öè":B$(1,4,4)="Ñé"
269 DB$="ŸŸ":D2$="ŸŸ":CL$="ÄÄ":C2$="ÄÄ"
300 SP$="                                "
530 FORJ=1TO12:FORK=0TO5
540 XP(J+K*12)=(J*2)-1
550 YP(J+K*12)=K*2:NEXT:NEXT
560 DIMPA(6),PB(6)
570 FORK=1TO6:READPA(K),PB(K):NEXT
580 DATA1,12,13,24,25,36,37,48,49,60,61,72
590 DA=1:DB=0.5:DC=0.5
600 TF=60
610 MD=30:VD=30:BD=35
620 EV=600
630 GOSUB3460
640 CLS
650 PRINT"A E R O J A M   OPTION MENU":PRINT
660 PRINT"1 - PLAY EASY GAME":PRINT
670 PRINT"2 - PLAY MEDIUM GAME":PRINT
680 PRINT"3 - PLAY HARD GAME":PRINT
690 PRINT"4 - END PROGRAM"
700 PRINT:PRINT"PRESS 1, 2, 3, OR 4"
710 R$=INKEY$:IFLEN(R$)=0THEN710
720 IFR$<"1"ORR$>"4"THEN710
730 Q=VAL(R$):ONQGOTO740,780,760,3580
740 VD=50:MD=40:BD=50
750 DA=1:DB=1:DC=0.7:GOTO 780
760 VD=20:MD=25:BD=25
770 DA=1:DB=0.5:DC=0.2:GOTO 780
780 MD=MD*TF:VD=VD*TF:BD=BD*TF:EV=EV*TF
790 NM=MD:NV=VD:NB=BD
800 CLS0:GOSUB801:GOTO900
801 PRINT@0*32,"øÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ•™";
802 PRINT@1*32,"øÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ•™";
803 PRINT@2*32,"üÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÖä";
804 PRINT@3*32,"üÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÖä";
805 PRINT@4*32,"øÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ•™";
806 PRINT@5*32,"øÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ•™";
807 PRINT@6*32,"üÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÖä";
808 PRINT@7*32,"üÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÖä";
809 PRINT@8*32,"øÄÄÄÄÄÄÄÄÄÄ¡À√ ÄÄÄÄÄÄÄÄÄÄ•™";
810 PRINT@9*32,"øÄÄÄÄÄÄÄÄÄÄÕÄÄŒÄÄÄÄÄÄÄÄÄÄ•™";
811 PRINT@10*32,"üÄÄÄÄÄÄÄÄÄÄ≈ÄÄ ÄÄÄÄÄÄÄÄÄÄÖä";
812 PRINT@11*32,"üÄÄÄÄÄÄÄÄÄÄÕ√√ŒÄÄÄÄÄÄÄÄÄÄÖä";:RETURN
900 FORJ=12TO14STEP32:PRINT@J*32,SP$;:NEXT:PRINT@480,LEFT$(SP$,31);:POKEMC+511,96
1060 CD=12:CR=0:GOSUB150:PRINT"TIME IS"
1070 PRINT"NEXT MOVE";INT(NM/TF)
1080 PRINT"NEXT SHIP";INT(NV/TF)
1090 PRINT"LAST SHIP";INT(EV/TF);
1100 CD=9:CR=27:GOSUB150:PRINT"mind";
1110 CD=10:CR=27:GOSUB150:PRINT"moves";:CD=11:CR=28:GOSUB150:PRINT"dp";
1120 FORJ=1TO4:CD=J:CR=27+J:GOSUB150
1130 PRINTMID$("aero",J,1);:NEXT
1140 FORJ=1TO3:CD=3+J:CR=27+J:GOSUB150
1150 PRINTMID$("jam",J,1);:NEXT
1160 CD=12:CR=16:GOSUB150:PRINT"N";
1170 CD=13:CR=15:GOSUB150:PRINT"W+E";
1180 CD=14:CR=16:GOSUB150:PRINT"S";
1190 CD=12:CR=20:GOSUB150:PRINT"SCORE";:GOSUB3370
1240 C=USR(0):REM RESET TIMER
1250 FORC=1TO4
1260 J=INT(6*RND(0))+1:IFRND(0)>0.5THEN1290
1270 IFG(PA(J))<>0THEN1260
1280 Q=PA(J):N=N+1:TV(N)=2+2*INT(2*RND(0)):DM(N)=3:GOTO1310
1290 IFG(PB(J))<>0THEN1260
1300 Q=PB(J):N=N+1:TV(N)=1+2*INT(2*RND(0)):DM(N)=1
1310 DK(N)=2:CG(N)=Q:G(Q)=N:TE(N)=USR(1):GOSUB200
1320 PRINTT$(DK(N),DM(N),TV(N));:PRINT@32*CD+32+CR,B$(DK(N),DM(N),TV(N));:NEXT
1330 R=PEEK(2)ANDPEEK(17023):PRINT@391,INT(USR(1)/TF);:J=MC+XP(P)+32*YP(P):K=PEEK(J):POKEJ,42
1350 FORD=1TO50:NEXT:POKEJ,K:IFUSR(1)>=NMTHEN1810
1370 IFUSR(1)>=NVANDUSR(1)<EVTHEN3140
1380 IFUSR(1)>=NBTHEN3020
1390 IF(N=0ANDUSR(1)>EV)ORUSR(1)>64800THEN3380
1410 IFR=65THEN1520
1420 IFR=87THEN1560
1430 IFR=90THEN1610
1440 IFR=32THEN1660
1450 IFR=68THEN1720
1460 IFR=13THEN1830
1470 IFR<>83THEN1330
1480 P=P+1:IFP=54THENP=56:GOTO1330
1490 IFP=66THENP=68:GOTO1330
1500 IFP=73THENP=1:GOTO1330
1510 GOTO1330
1520 P=P-1:IFP=55THENP=53:GOTO1330
1530 IFP=67THENP=65:GOTO1330
1540 IFP=0THENP=72:GOTO1330
1550 GOTO1330
1560 P=P-12:IFP=-11THENP=72:GOTO1330
1570 IFP<1THENP=P+71
1580 IFP=66THENP=42:GOTO1330
1590 IFP=67THENP=43:GOTO1330
1600 GOTO1330
1610 P=P+12:IFP=84THENP=1:GOTO1330
1620 IFP>72THENP=P-71:GOTO1330
1630 IFP=54THENP=7:GOTO1330
1640 IFP=55THENP=8:GOTO1330
1650 GOTO1330
1660 M=G(P):IFM<=0THEN1710
1670 Q=P:GOSUB200
1680 DM(M)=DM(M)+1:IFDM(M)=5THENDM(M)=1
1690 Q=DK(M):IFQ=3THENQ=2
1700 PRINTT$(Q,DM(M),TV(M));:PRINT@32*CD+32+CR,B$(Q,DM(M),TV(M));
1710 GOTO1330
1720 J=G(P):IFJ<=0THEN1800
1730 CD=14:CR=20:GOSUB150:PRINTLEFT$(SP$,12);:GOSUB150
1740 PRINT"BYE SHIP!";
1750 GOSUB2930
1760 Q=P:GOSUB200:PRINTDB$;:PRINT@32*CD+32+CR,D2$;
1770 W=W-30:IFTV(J)>2THENW=W-30
1780 G(Q)=-2:GOSUB3330:GOSUB3370
1790 ND=ND+1:GD(ND)=Q
1800 GOTO1330
1810 GOSUB210:CD=13:CR=20:GOSUB150
1820 PRINT"MOVE FORCED";:GOTO1850
1830 GOSUB210:CD=13:CR=20:GOSUB150
1840 PRINT"MOVE ORDERED";
1850 FORK=1TO49STEP24
1860 J=G(K):IFJ<=0THEN1880
1870 IFTV(J)=1ANDDM(J)=1ANDDK(J)=1THENGOSUB2890
1880 NEXT
1890 FORK=13TO61STEP24:J=G(K):IFJ<=0THEN1910
1900 IFTV(J)=3ANDDM(J)=1ANDDK(J)=1THENGOSUB2890
1910 NEXT
1920 FORK=12TO60STEP24:J=G(K):IFJ<=0THEN1940
1930 IFTV(J)=2ANDDM(J)=3ANDDK(J)=1THENGOSUB2890
1940 NEXT
1950 FORK=24TO72STEP24:J=G(K):IFJ<=0THEN1970
1960 IFTV(J)=4ANDDM(J)=3ANDDK(J)=1THENGOSUB2890
1970 NEXT
1980 IFND=0THEN2000
1990 FORK=1TOND:G(GD(K))=-1:NEXT
2000 FORK=2TO11:M=G(K):IFM<=0THEN2050
2010 ONDM(M)GOTO2020,2050,2030,2040
2020 CG(M)=CG(M)-1:GOTO2050
2030 CG(M)=CG(M)+1:GOTO2050
2040 CG(M)=CG(M)+12
2050 NEXT
2060 FORK=1TO61STEP12:M=G(K):IFM<=0THEN2120
2070 ONDM(M)GOTO2120,2080,2100,2110
2080 IFK<>1THENCG(M)=CG(M)-12
2090 GOTO2120
2100 CG(M)=CG(M)+1:GOTO2120
2110 IFK<>61THENCG(M)=CG(M)+12
2120 NEXT
2130 FORK=12TO72STEP12:M=G(K):IFM<=0THEN2190
2140 ONDM(M)GOTO2150,2160,2190,2180
2150 CG(M)=CG(M)-1:GOTO2190
2160 IFK<>12THENCG(M)=CG(M)-12
2170 GOTO2190
2180 IFK<>72THENCG(M)=CG(M)+12
2190 NEXT
2200 FORK=62TO71:IFK>=65ANDK<=68THEN2260
2210 M=G(K):IFM<=0THEN2260
2220 ONDM(M)GOTO2230,2240,2250,2260
2230 CG(M)=CG(M)-1:GOTO2260
2240 CG(M)=CG(M)-12:GOTO2260
2250 CG(M)=CG(M)+1
2260 NEXT
2270 FORK=14TO23:GOSUB2960:NEXT
2280 FORK=26TO35:GOSUB2960:NEXT
2290 FORK=38TO41:GOSUB2960:NEXT
2300 FORK=44TO47:GOSUB2960:NEXT
2310 FORK=50TO52:GOSUB2960:NEXT
2320 FORK=57TO59:GOSUB2960:NEXT
2330 FORK=42TO43:M=G(K):IFM<=0THEN2400
2340 ONDM(M)GOTO2350,2360,2370,2380
2350 CG(M)=CG(M)-1:GOTO2400
2360 CG(M)=CG(M)-12:GOTO2400
2370 CG(M)=CG(M)+1:GOTO2400
2380 IFRND(0)>DATHEN2400
2390 DK(M)=1
2400 NEXT
2410 FORK=53TO65STEP12:M=G(K):IFM<=0THEN2480
2420 ONDM(M)GOTO2430,2440,2450,2470
2430 CG(M)=CG(M)-1:GOTO2480
2440 CG(M)=CG(M)-12:GOTO2480
2450 IFRND(0)>DBTHEN2480
2460 DK(M)=1:GOTO2480
2470 IFK=53THENCG(M)=CG(M)+12
2480 NEXT
2490 FORK=56TO68STEP12:M=G(K):IFM<=0THEN2560
2500 ONDM(M)GOTO2510,2530,2540,2550
2510 IFRND(0)>DBTHEN2560
2520 DK(M)=1:GOTO2560
2530 CG(M)=CG(M)-12:GOTO2560
2540 CG(M)=CG(M)+1:GOTO2560
2550 IFK=56THENCG(M)=CG(M)+12
2560 NEXT
2570 FORK=1TO72:G(K)=0:NEXT
2580 IFND=0THEN2600
2590 FORK=1TOND:G(GD(K))=-1:NEXT
2600 CD=1:CR=0:GOSUB150:GOSUB801
2630 IFND=0THEN2650
2640 FORK=1TOND:Q=GD(K):GOSUB200:PRINTDB$;:PRINT@32*CD+32+CR,D2$;:NEXT
2650 J=0
2660 J=J+1
2670 IFJ>NTHEN2800
2680 Q=CG(J):IFG(Q)=0THEN2780
2690 IFG(Q)>0THEN2750
2700 W=W-30:IFTV(J)>2THENW=W-30
2710 GOSUB3350:G(Q)=-2:GOSUB3370
2720 CD=14:CR=20:GOSUB150:PRINTLEFT$(SP$,12);:GOSUB150
2730 PRINT"DEBRIS HIT!";
2740 GOSUB2930:GOTO2670
2750 W=W-75:GOSUB3350:GOSUB3370:CR=20:CD=14:GOSUB150
2760 PRINTLEFT$(SP$,12);:GOSUB150
2770 PRINT"COLLISION!";:GOSUB2930:GOTO2670
2780 GOSUB200:PRINTT$(DK(J),DM(J),TV(J));:PRINT@32*CD+32+CR,B$(DK(J),DM(J),TV(J));
2790 G(Q)=J:GOTO2660
2800 K=0
2810 K=K+1
2820 IFK>NDTHEN2870
2830 Q=GD(K):IFG(Q)=-2THEN2810
2840 IFRND(0)>DCTHEN2810
2850 GD(K)=GD(ND):G(Q)=0:ND=ND-1
2860 GOSUB200:PRINTCL$;:PRINT@32*CD+32+CR,C2$;:GOTO2820
2870 NM=USR(1)+MD:CD=13:CR=9:GOSUB150:PRINTINT(NM/TF);
2880 GOTO1330
2890 Q=1:IFTV(J)=3ORTV(J)=4THENQ=3
2900 W=W+INT(Q*2000*TF/(USR(1)-TE(J)))
2910 Q=K:GOSUB200:PRINTCL$;:PRINT@32*CD+32+CR,C2$;
2920 G(Q)=0:GOSUB3330:GOSUB3370:RETURN
2930 FORC=1TO4:POKE49151,0
2940 FORD=1TO50:NEXT:POKE49151,64
2950 FORD=1TO50:NEXT:NEXT:RETURN
2960 M=G(K):IFM<=0THENRETURN
2970 ONDM(M)GOTO2980,2990,3000,3010
2980 CG(M)=CG(M)-1:RETURN
2990 CG(M)=CG(M)-12:RETURN
3000 CG(M)=CG(M)+1:RETURN
3010 CG(M)=CG(M)+12:RETURN
3020 TB=USR(1)+60
3030 IFUSR(1)>TBTHEN3130
3040 Q=RND(72)
3050 IFQ=54ORQ=55ORQ=66ORQ=67THEN3030
3060 IFG(Q)<>0THEN3030
3070 IFND>=MXTHEN3130
3080 ND=ND+1:GD(ND)=Q:G(Q)=-2
3090 GOSUB200:PRINTDB$;:PRINT@32*CD+32+CR,D2$;
3100 CR=20:CD=14:GOSUB150:PRINTLEFT$(SP$,12);:GOSUB150
3110 PRINT"NEW DEBRIS";
3120 NB=USR(1)+BD
3130 GOTO1330
3140 IFN>=MXTHEN3310
3150 J=INT(6*RND(0))+1:K=J:IFRND(0)>0.5THEN3220
3160 K=K+1:IFK=7THENK=1
3170 IFG(PA(K))=0THEN3200
3180 IFK=JTHEN3310
3190 GOTO3160
3200 Q=PA(K):N=N+1:TV(N)=2+2*INT(2*RND(0)):DM(N)=3
3210 GOTO3270
3220 K=K+1:IFK=7THENK=1
3230 IFG(PB(K))=0THEN3260
3240 IFK=JTHEN3310
3250 GOTO3220
3260 Q=PB(K):N=N+1:TV(N)=1+2*INT(2*RND(0)):DM(N)=1
3270 DK(N)=2:CG(N)=Q:G(Q)=N:TE(N)=USR(1)
3280 CR=20:CD=15:GOSUB150:PRINTLEFT$(SP$,11);:GOSUB150
3290 PRINT"NEW SHIP";
3300 GOSUB200:PRINTT$(DK(N),DM(N),TV(N));:PRINT@32*CD+32+CR,B$(DK(N),DM(N),TV(N));
3310 NV=USR(1)+VD:CD=14:CR=9:GOSUB150:PRINTINT(NV/TF);
3320 GOTO1330
3330 TV(J)=TV(N):TE(J)=TE(N):DK(J)=DK(N)
3340 CG(J)=CG(N):DM(J)=DM(N):G(CG(N))=J:N=N-1:RETURN
3350 TV(J)=TV(N):TE(J)=TE(N):DK(J)=DK(N):CG(J)=CG(N)
3360 DM(J)=DM(N):N=N-1:RETURN
3370 CD=12:CR=25:GOSUB150:PRINTW;:RETURN
3380 GOSUB2930:GOSUB2930:GOSUB2930
3390 GOSUB210:CD=13:CR=20:GOSUB150
3400 PRINT"RANK";INT(W*100*TF/USR(1))
3410 R$=INKEY$:IFLEN(R$)>0THEN3410
3420 CD=14:CR=20:GOSUB150:PRINT"GAME IS OVER";
3430 CD=15:GOSUB150:PRINT"HIT A KEY  ";:POKEMC+511,96
3440 R$=INKEY$:IFLEN(R$)=0THEN3440
3450 CLEAR:GOTO130
3460 POKE49151,64:CLS
3470 PRINT@32*3,;
3490 PRINTTAB(10)"A E R O J A M":PRINT
3500 PRINT" COPYRIGHT 1984 DILITHIUM PRESS"
3510 PRINT:PRINT:PRINT
3520 PRINTTAB(3)"FROM THE BOOK 'MIND MOVES'":PRINT
3530 PRINTTAB(2);"BY TOM RUGG AND PHIL FELDMAN"
3540 FORJ=1TO3:PRINT:NEXT
3550 PRINTTAB(6)"PRESS A KEY TO BEGIN";
3560 R$=INKEY$:R=RND(0):IFLEN(R$)=0THEN3560
3570 RETURN
3580 POKE49151,0:POKE8,0:END
2999 END
3600 R=16896:GOSUB3601:R=32768:GOSUB3601:EXEC32768:RETURN
3601 READR$:FORJ=1TOLEN(R$)STEP2:POKER,(ASC(MID$(R$,J,1))-65)*16+ASC(MID$(R$,J+1,1))-65:R=R+1:NEXT:RETURN
4000 REM ASWZ=MOVE CURSOR
4010 REM SPACE=ROTATE SHIPS
4020 REM D=DESTROY SHIP
4030 REM ENTER=ORDER MOVE
4040 REM MC-10 VER JIM GERRIE
4050 REM TIMER DARREN ATKINSON 
