40 MODE 1:BORDER 8:INK 0,1:INK 1,24:INK 2,20:INK 3,6
50 DIM N$(30),B(30),G$(29),X$(45),Q$(158),S(158,4),V$(5)
60 PEN 1:LOCATE 9,2:PRINT"THE SPY FROM THE NORTH"
70 PEN 2:LOCATE 5,8:PRINT"<C>  STEVE LUCAS   NOVEMBER 1984 "
80 PRINT:PRINT:PRINT"I HAVE BEEN CHOSEN BY 'M' FOR A SECRET  AND PROBABLY DANGEROUS MISSION."
90 PRINT:PRINT"THE SECRET PLANS FOR THE NUCLEAR        SUBMARINE 'LOBINAR' HAVE BEEN STOLEN BY THAT DANGEROUS TERRORIST 'XOLTAN'."
100 PRINT:PRINT"MY TASK IS TO FIND AND DESTROY HIM      BEFORE HE CAN THREATEN THE WORLD."
110 RESTORE
120 FOR X=1 TO 158: FOR Y=1 TO 4: READ S(X,Y): NEXT: READ Q$(X): NEXT
130 FOR X=1 TO 41: READ X$(X): NEXT:FOR X=1 TO 29: READ G$(X),B(X),N$(X): NEXT
140 SC=0:P=2:X$="":SCORE=0
150 CLS
160 WHILE P<158
170 PEN 1:PRINT:PRINT "I AM :-":PEN 2:PRINT Q$(P):PEN 1:PRINT:A$="":IF S(P,1)>0 THEN A$="NORTH"
180 IF P=27 AND JL=1 THEN PEN 3:PRINT"THERE IS A LEVER BEHIND AN OIL DRUM":PEN 1:PRINT
190 IF P=2 THEN MOVE 0,0:DRAW 1000,0
200 IF S(P,2)>0 AND LEN(A$)>0 THEN A$=A$+",SOUTH" ELSE IF S(P,2)>0 THEN A$="SOUTH"
210 IF S(P,3)>0 AND LEN(A$)>0 THEN A$=A$+",EAST" ELSE IF S(P,3)>0 THEN A$="EAST"
220 IF S(P,4)>0 AND LEN(A$)>0 THEN A$=A$+",WEST" ELSE IF S(P,4)>0 THEN A$="WEST"
230 IF P=124 OR P=141 OR P=17 THEN A$=A$+",DOWN"
240 IF P=12 OR P=1 OR P=21 OR P=140 OR P=122 OR P=90 OR P=99 OR P=122 THEN A$=A$+",UP"
250 IF P=101 THEN A$="DOWN" ELSE IF A$="" THEN A$="NOWHERE OBVIOUS!"
260 PRINT X$(19);" :-":PEN 3:PRINT A$:PEN 1:PRINT
270 E=0:FOR T=1 TO 29:PP=0:IF B(T)=P THEN PP=1
280 IF PP=1 THEN 310
290 NEXT:IF E=0 THEN PEN 2:PRINT "I SEE NOTHING SPECIAL":PEN 1
300 GOTO 330
310 PEN 1:PRINT "I CAN SEE :-":PEN 2
320 PRINT G$(T):E=1:GOTO 290
330 PEN 1:PRINT:PRINT X$(18);:PEN 3:INPUT Z$
340 Z$=LOWER$(Z$):B$=LEFT$(Z$,2):C$=LEFT$(Z$,3):D$=LEFT$(Z$,4):PEN 1:CLS
350 SC=INT(P/158*100):IF SC>SCORE THEN SCORE=SC
360 K=0
370 PRINT CHR$(7)
380 IF C$="SCO" THEN PRINT"YOU HAVE SCORED ";SCORE;"":K=1 ELSE IF C$="LOO" THEN PRINT"I CAN'T SEE ANYTHING SPECIAL":K=1 ELSE IF C$="EAT" THEN PRINT X$(26):K=1 ELSE IF  D$="DRIN" THEN PRINT X$(27):K=1
390 IF (B$="N" OR D$="GO N") AND S(P,1)>0 THEN P=S(P,1):K=1
400 IF (B$="S" OR D$="GO S") AND S(P,2)>0 THEN P=S(P,2):K=1
410 IF (B$="E" OR D$="GO E") AND S(P,3)>0 THEN P=S(P,3):K=1
420 IF (B$="W" OR D$="GO W") AND S(P,4)>0 THEN P=S(P,4):K=1
430 IF (B$="N" OR B$="S" OR B$="E" OR B$="W") AND K=0 THEN K=1:PRINT"I CAN'T GO THAT WAY!"
440 IF B$="FU" OR B$="PI" THEN PRINT X$(30):K=1 ELSE IF B$="UP" OR B$="CL" OR D$="GO U" THEN K=1:GOSUB 620
450 IF B$="DO" OR D$="GO D" THEN K=1:GOSUB 650 ELSE IF B$="JU" THEN K=1:GOSUB 670
460 IF B$="WA" THEN K=1:GOSUB 770 ELSE IF C$="IN" OR D$="GO I" THEN K=1:GOSUB 690 ELSE IF C$="OUT" OR D$="GO O" THEN K=1:GOSUB 720 ELSE IF C$="CUT" THEN K=1:GOSUB 1280
470 IF C$="GET" OR C$="TAK" OR C$="GRA" THEN K=1:GOSUB 810 ELSE IF C$="DRO" OR C$="LEA" THEN K=1:GOSUB 970
480 IF D$="DRIV" THEN K=1:GOSUB 1300 ELSE IF C$="INV" THEN K=1:GOSUB 930 ELSE IF C$="SWI" THEN K=1:GOSUB 1010
490 IF C$="EXA" THEN GOSUB 1030 ELSE IF C$="SEA" THEN K=1:GOSUB 1340 ELSE IF C$="RID" OR D$="GO T" THEN K=1:GOSUB 1080 ELSE IF C$="PAY" THEN K=1:GOSUB 1100
500 IF C$="HEL" THEN K=1:GOSUB 1150 ELSE IF C$="PRA" THEN K=1:PRINT"THAT MADE ME FEEL BETTER BUT IT DIDN'T  SOLVE THE PROBLEM!"
510 IF C$="USE" OR C$="INS" OR C$="UNL" THEN K=1:GOSUB 1380 ELSE IF C$="FLY" THEN K=1:GOSUB 1180 ELSE IF C$="LAN" THEN K=1:GOSUB 1210
520 IF C$="PUL" THEN K=1:GOSUB 1350 ELSE IF C$="KIL" THEN PRINT"BY WHAT MEANS":K=1 ELSE IF C$="SHO" THEN GOSUB 1430:K=1 ELSE IF C$="STA" THEN K=1:GOSUB 1400
530 IF P=32 AND O<>1 THEN J=J+1:IF J>2 THEN X$="AAAGGHHH HE STABBED ME":GOSUB 750
540 IF C$="SAY" THEN GOSUB 1390:K=1 ELSE IF P=77 THEN S=S+1:IF S>1 THEN X$="HE ATTACKS ME!":GOSUB 750
550 IF P=148 THEN T=T+1:IF T>2 THEN X$="SHE STABS ME.":GOSUB 750
560 IF C$="MEN" OR C$="REP" THEN K=1:GOSUB 1490
570 IF C$="PUS" OR C$="PRE" THEN K=1:GOSUB 1520 ELSE IF C$="REA" THEN PRINT"I FORGOT MY GLASSES!":K=1
580 IF P=84 THEN U=U+1:IF U>2 THEN X$="IT DRIVES STRAIGHT AT ME":GOSUB 750
590 IF K=0 THEN PRINT"I'M SORRY I CAN'T DO THAT HERE!"
600 WEND
610 CLS:PEN 1:PRINT"WELL DONE. YOU HAVE SOLVED THIS         ADVENTURE AND KILLED XOLTAN. THE WORLD  IS A SAFER PLACE TO LIVE!":END
620 X$=X$(3):IF P=99 THEN P=101:X$=X$(10) ELSE IF P=12 THEN P=17:X$=X$(10) ELSE IF P=1 THEN P=21:X$="I CLIMB OVER" ELSE IF P=21 THEN P=1:X$="I CLIMB OVER" ELSE IF P=140 THEN P=141:X$=X$(10) ELSE IF P=122 THEN P=124:X$=X$(10)
630 IF P=90 THEN P=91:X$=X$(10)
640 PRINT X$:RETURN
650 X$=X$(3):IF P=124 THEN P=122:X$=X$(10) ELSE IF P=101 THEN P=99:X$=X$(10) ELSE IF P=141 THEN P=140:X$=X$(10) ELSE IF P=17 THEN P=12:X$=X$(10)
660 PRINT X$:RETURN
670 X$=X$(3):IF P=147 THEN P=148:X$="SPLASH" ELSE IF P=91 THEN P=90:X$=X$(10) ELSE IF P=92 THEN P=93:X$=X$(10) ELSE IF P=141 THEN X$=X$(39):GOSUB 750
680 PRINT X$:RETURN
690 X$=X$(3):IF P=9 THEN P=10:X$=X$(10) ELSE IF P=102 THEN P=105:X$=X$(10) ELSE IF P=121 THEN P=120:X$=X$(10) ELSE IF P=55 THEN P=59:X$=X$(9)
700 IF P=62 THEN P=61:X$=X$(10) ELSE IF P=25 AND AA=1 THEN P=34:X$=X$(10) ELSE IF P=25 THEN X$="IT'S LOCKED!"
710 PRINT X$:RETURN
720 X$=X$(5):IF P=10 THEN P=9:X$=X$(10) ELSE IF P=34 THEN P=25:X$=X$(10) ELSE IF P=105 THEN P=102:X$=X$(10) ELSE IF P=120 THEN P=121:X$=X$(10)
730 IF P=59 THEN P=55:X$=X$(10) ELSE IF P=61 THEN P=62:X$=X$(10)
740 PRINT X$:RETURN
750 CLS:PEN 3:LOCATE 15,1:PRINT"OH DEAR":PEN 2:LOCATE 1,5:PRINT X$:PEN 1:LOCATE 1,20:PRINT X$(16)
760 F$=INKEY$:F$=LOWER$(F$):IF F$="Y" THEN RUN ELSE IF F$="N" THEN CLS:LOCATE 1,15:PEN 3:PRINT"GOODBYE. THANK YOU FOR PLAYING.":END ELSE GOTO 760
770 PRINT X$(10):FOR F=1 TO 1000:NEXT: IF (P>39 AND P<47) THEN PRINT"THE SUBMARINE DOCKS":P=47
780 IF P=33 THEN P=40:PRINT"THE SUBMARINE SUBMERGES" ELSE IF P=86 THEN PRINT"THE DRIVER HOLDS HIS HAND OUT AND       MUTTERS SOMETHING!":P=87
790 IF P=139 THEN P=143:PRINT"GOING UP!" ELSE IF P=129 THEN P=130:PRINT"THE TRAIN STOPS AND EVERYONE GETS OFF!"
800 RETURN
810 GOSUB 890:IF L<>1 THEN RETURN
820 E=0:FOR H=1 TO 29:IF B(H)=P AND B(R)=P THEN E=1
830 NEXT:IF E=0 THEN PRINT"IT'S NOT HERE!":RETURN
840 IF R=6 OR R=10 OR R=12 OR R=14 OR R=16 OR R=19 OR R=22 OR R=25 OR R=23 THEN PRINT X$(32):RETURN
850 IF R=21 AND P=142 AND N<>1 THEN PRINT"IT'S IN A LOCKED CASE!":RETURN ELSE IF R=27 AND O<>1 THEN PRINT X$(32):RETURN
860 E=0:FOR D=1 TO 5:IF V$(D)="" THEN V$(D)=G$(R):E=1:D=9
870 NEXT:IF E=0 THEN PRINT"SORRY. MY HANDS ARE FULL!":RETURN
880 B(R)=0:RETURN
890 L$="":FOR H=1 TO LEN(Z$):IF MID$(Z$,H,1)=" " THEN L$=RIGHT$(Z$,(LEN(Z$)-H)):H=200
900 NEXT:R=0:L=0:IF LEN(L$)<2 THEN RETURN
910 FOR H=1 TO 29:IF LEFT$(N$(H),LEN(L$))=L$ THEN L=1:R=H
920 NEXT:RETURN
930 X$="":F=0:PEN 1:PRINT"I HAVE :-":PEN 2:PRINT"A JET PACK STRAPPED TO MY BACK. TO USE  IT I MUST SAY 'FIRE' INTO MY RADIO/WATCHAND :-"
940 PEN 3:FOR X=1 TO 5:IF V$(X)<>"" THEN PRINT V$(X):F=1
950 NEXT:IF F=0 THEN PRINT"NOTHING ELSE"
960 RETURN
970 GOSUB 890:IF L<>1 THEN PRINT"I DON'T SEE A ";L$:RETURN
980 E=0:FOR D=1 TO 5:IF V$(D)=G$(R) THEN V$(D)="":E=1
990 NEXT:IF E=0 THEN PRINT"I'M NOT CARRYING IT!":RETURN
1000 B(R)=P:RETURN
1010 IF P=48 OR P=49 OR P=148 THEN PRINT X$(36) ELSE IF P=32 THEN PRINT X$(6) ELSE IF P=147 THEN P=148:PRINT X$(10) ELSE PRINT X$(32)
1020 RETURN
1030 IF P=48 THEN PRINT"WOW!!!!":K=1 ELSE IF P=32 THEN PRINT"THE FISHERMAN IS CARRYING A SHARP KNIFE!":K=1 ELSE IF P=101 THEN PRINT"IT LOOKS LIKE AN EPROM HAS BLOWN!!":K=1
1040 GOSUB 890:IF R=2 THEN JJ=2:GOSUB 1260:IF E=1 THEN PRINT"THIS IS NO ORDINARY BRUSH. IT'S ONE OF  Q'S PATENT WIRE CUTTERS!":K=1
1050 IF R=2 AND E=0 THEN PRINT"I'M NOT CARRYING IT!":K=1
1060 IF K=0 THEN PRINT X$(9)
1070 RETURN
1080 X$=X$(5):IF P=83 THEN P=86:X$=X$(10) ELSE IF P=127 THEN P=129:X$=X$(10)
1090 PRINT X$:RETURN
1100 IF P=87 OR P=72 OR P=71 OR P=70 THEN 1110 ELSE PRINT X$(32):RETURN
1110 E=0:FOR X=1 TO 5:IF V$(X)=G$(26) THEN E=1
1120 NEXT:IF E=0 THEN PRINT"BUT I HAVE NO MONEY!!!!":RETURN
1130 IF P=87 THEN P=88:PRINT"THE DRIVER THANKS ME ":RETURN
1140 PRINT X$(10):RETURN
1150 IF P=69 THEN PRINT"YOU ARE A QUALIFIED PILOT, BUT THIS     PLANE IS A LITTLE BEFORE YOUR TIME!":RETURN
1160 IF P=91 THEN PRINT"I THINK YOU COULD JUMP!":RETURN ELSE IF P=24 THEN PRINT"WIRE CUTTERS COULD BE USEFUL!":RETURN
1170 PRINT"I'M SORRY I HAVEN'T A CLUE!":RETURN
1180 IF P=69 OR P=73 THEN 1190 ELSE PRINT X$(7):RETURN
1190 IF P=73 THEN PRINT X$(10):PRINT"I'D TRY LANDING IT!":RETURN ELSE JJ=7:GOSUB 1260:IF E<>1 THEN PRINT"I NEED A HELMET!":RETURN
1200 P=73:PRINT X$(10):RETURN
1210 IF P<>73 THEN PRINT X$(7):RETURN ELSE PRINT"I SEE THREE LEVERS":PRINT"RED","GREEN","BLUE":PRINT"THEY AREN'T LABELLED"
1220 PRINT"WHICH ONE SHOULD I PULL ?"
1230 AA$=INKEY$:AA$=LOWER$(AA$):IF AA$="R" OR AA$="G" OR AA$="B" THEN 1240 ELSE 1230
1240 IF AA$<>"B" THEN X$="THE ENGINE EXPLODES IN A BALL OF FLAMES!":GOSUB 750
1250 CLS:P=74:PRINT"GOOD CHOICE....I LANDED SAFELY":RETURN
1260 E=0:FOR X=1 TO 5:IF V$(X)=G$(JJ) THEN E=1
1270 NEXT:RETURN
1280 IF P<>24 THEN PRINT X$(1):RETURN ELSE JJ=2:GOSUB 1260
1290 IF E<>1 THEN PRINT X$(3):RETURN ELSE PRINT X$(10):S(24,2)=26:Q$(24)="BY A BROKEN FENCE":RETURN
1300 IF P<59 OR P>61 THEN PRINT X$(32):RETURN ELSE IF P=60 THEN M=M+1:IF M>2 THEN P=61:M=0:PRINT"OUT OF GAS!":RETURN
1310 JJ=1:GOSUB 1260: IF E<>1 THEN PRINT"I HAVEN'T GOT THE CAR KEY!":RETURN
1320 IF P=59 THEN PRINT"THE CAR STARTS!":P=60:RETURN
1330 RETURN
1340 IF P<>27 THEN PRINT"THERE'S NOTHING HERE!":RETURN ELSE PRINT"I HAVE FOUND SOMETHING!":JL=1:RETURN
1350 GOSUB 890: IF P=27 AND R=15 THEN X$="AAAGGGGHHHH. A  HOLE OPENS BENEATH MY   FEET AND I FALL INTO A POOL OF SHARKS":GOSUB 750
1360 IF P=105 THEN P=106:PRINT"THE ENGINE STARTS":RETURN
1370 PRINT X$(3):RETURN
1380 JJ=1:GOSUB 1260:IF E<>1 THEN PRINT"I HAVE NO KEY!":RETURN ELSE IF P<>142 THEN PRINT X$(7):RETURN ELSE N=1:PRINT X$(10):RETURN
1390 GOSUB 890:IF L$="FIRE" OR L$="FIRE" THEN PRINT"THE JETPACK TRANSPORTS ME":P=INT(RND(1)*120)+1: RETURN ELSE PRINT X$(10):RETURN
1400 JJ=27:GOSUB 1260: IF E<>1 THEN PRINT"I HAVE NO KNIFE!":RETURN
1410 IF P=108 OR P=144 OR P=54 OR P=77 THEN X$="HE ATTACKS ME":GOSUB 750 ELSE IF P<>157 THEN PRINT X$(7):RETURN
1420 P=158:RETURN
1430 JJ=18:GOSUB 1260:IF E=0 THEN PRINT"I HAVE NO GUN!":RETURN
1440 JJ=21:GOSUB 1260:IF E=0 THEN PRINT"I HAVE NO BULLET!":RETURN
1450 IF P=157 THEN X$="TOO SLOW. HE ATTACKS FIRST!":GOSUB 750
1460 IF P<>32 THEN PRINT X$(41):RETURN
1470 IF O=0 THEN PRINT"HE'S DEAD. A KNIFE FALLS TO THE FLOOR.":O=1:G$(27)="A KNIFE":N$(27)="KNIFE":RETURN
1480 RETURN
1490 JJ=1:GOSUB 1260:IF E<>1 THEN PRINT"I FORGOT THE MANUAL!":RETURN
1500 IF P<>101 THEN PRINT X$(7):RETURN
1510 PRINT"I REPAIR THE COMPUTER AND A MESSAGE     APPEARS ON THE SCREEN..KILL XOLTAN!":RETURN
1520 IF P<>41 THEN PRINT X$(7):RETURN ELSE X$="THERE IS AN EXPLOSION!":GOSUB 750
1530 DATA 2,0,0,0,STANDING NEXT TO AN ENORMOUS METAL GATE.IT IS LOCKED
,0,1,3,0,IN A SMALL CLEARING IN THE MIDDLE OF A  DENSE FOREST
,5,0,4,2,IN A DARK AND GLOOMY FOREST
,6,0,0,3,IN THE MIDDLE OF THE FOREST. A LARGE    BOULDER PREVENTS ME GOING EAST
,7,3,6,0,IN A DARK FOREST. I HEAR THE SOUND OF   WOLVES IN THE DISTANCE
,0,4,0,5,IN A DENSE FOREST. THERE IS A SMALL     STREAM NEARBY
1550 DATA 0,5,0,8,ON A NARROW FOOTPATH. THERE IS AN OPEN  GARDEN GATE HERE
,0,0,7,9,IN A SMALL BUT WELL TENDED GARDEN. A    DOG SNORES LOUDLY AS IT BASKS IN THE    SUNSHINE
1560 DATA 0,0,8,0,STANDING IN THE DOORWAY OF AN OLD HOUSE.THERE'S A LARGE BRASS KNOCKER ON THE OAKDOOR
,0,0,11,0,BY THE FRONT DOOR,0,0,12,10,IN A LONG NARROW HALLWAY. THE WALLPAPER HANGS IN SHREDS FROM THE WALLS
1570 DATA 0,0,13,11,AT THE BOTTOM OF A FLIGHT OF STAIRS
,14,15,16,12,AT THE END OF THE HALLWAY. THERE ARE    DOORS ON EITHER SIDE
,0,13,0,0,IN A LARGE LIBRARY. THE FURNITURE IS    COVERED IN DUST. A BOOK LIES OPEN ON THESIDE OF A CHAIR
1580 DATA 13,0,0,0,IN A LARGE EMPTY ROOM. A BARE LIGHT BULBHANGS FROM THE CEILING
,0,0,0,13,IN A SMALL BROOM CUPBOARD
,20,0,18,19,AT THE TOP OF THE STAIRS. THERE ARE     THREE DOORS HERE
,0,0,0,17,IN A SMALL BATHROOM. AN EMPTY CABINET ISFASTENED TO THE WALL
1590 DATA 0,0,17,0,IN A SMALL BEDROOM. A PILE OF DIRTY     CLOTHES LIES ON THE FLOOR
,0,17,0,0,IN THE MASTER BEDROOM. I CAN HEAR       SNORING COMING FROM THE FOUR POSTER BED
,0,24,22,23,BY A LOCKED GATE. A SIGN HERE READS     'TRESPASSERS WILL BE PROSECUTED'
,0,0,0,21,BY THE MAIN ENTRANCE TO A LARGE CHEMICALPLANT
,0,25,21,0,NEAR TO THE PERIMETER FENCE OF THE      CHEMICAL PLANT. THERE IS A SMALL HUT    HERE BUT IT IS LOCKED
1610 DATA 21,0,0,0,STANDING NEXT TO A BARBED WIRE FENCE
,23,0,0,0,BY THE MAIN ENTRANCE TO AN OFFICE BLOCK.THE STEEL DOOR IS FIRMLY LOCKED
,24,28,27,0,STANDING ON A FOOTPATH AT THE SIDE OF A BARBED WIRE FENCE
,0,0,29,26
1620 DATA ON A NARROW FOOTPATH BETWEEN TWO TALL   BRICK BUILDINGS
,26,0,0,0,IN AN ENORMOUS TIMBER YARD
,30,32,31,27,IN A TOP SECURITY NAVAL DOCKYARD
,0,29,0,0,IN A DOCKYARD. THERE IS A TALL CRANE    HERE
,0,0,33,29,STANDING NEXT TO A NUCLEAR SUBMARINE
,29,0,0,0,ON THE QUAYSIDE
,0,0,0,0,IN A SUBMARINE HATCH
,25,35,0,0,INSIDE A DISUSED FACTORY. A PILE OF     EMPTY CARDBOARD BOXES IS PILED UP       AGAINST THE WALL
,34,0,0,36,NEXT TO A LARGE MACHINE. I HEAR A LOUD  NOISE
,0,37,35,0,IN A WIDE PASSAGEWAY. THE WALLS ARE     COVERED IN CALENDARS
,36,39,0,0,STANDING NEXT TO SOME ENORMOUS PIPES
1650 DATA 0,0,0,39,IN A SMALL CONTROL ROOM. A LARGE DIAL   POINTS TO RED
,37,0,38,0,IN A NARROW CORRIDOR LIT BY FLUORESCENT TUBES
,0,41,0,0,IN THE HATCH OF A SUBMERGED SUBMARINE
,40,0,42,0,BY A CONTROL PANEL FULL OF BUTTONS AND  DIALS
,0,43,0,41,BY A TORPEDO
1660 DATA 42,0,44,0,STANDING NEXT TO A BALLAST TANK
,0,46,45,43,IN THE ENGINE ROOM
,0,0,0,44,IN THE CARGO HOLD. IT'S FULL OF BOXES OFAMMUNITION
,44,0,0,0,BY SOME STRANGE LOOKING MACHINERY
,0,48,0,0,STANDING NEXT TO AN OPEN HATCH
,47,0,49,50,STANDING ON A SANDY BEACH. THERE IS A   SUBMARINE TO THE NORTH
,0,51,0,48,ON A SANDY BEACH. AN ICY WIND BLOWS FROMTHE NORTH AND THERE IS NOBODY TO BE SEEN
,0,52,48,53,STANDING OUTSIDE A SMALL BEACH HUT
1680 DATA 49,0,54,0,WALKING ACROSS SOME SAND DUNES. THE WINDBLOWS SAND INTO MY EYES
,50,0,0,0,INSIDE A BEACH HUT. AN OLD NEWSPAPER    LIES ON THE SMALL TABLE
,0,0,50,0,BY A SMALL CAFE SELLING ICE CREAM. THEY AREN'T DOING MUCH BUSINESS TODAY
1690 DATA 0,55,56,51,ON A GARAGE FORECOURT
,54,0,0,0,BY A PARKED CAR. THE DOOR IS UNLOCKED
,57,0,0,54,BY SOME PETROL PUMPS
,0,56,58,0,ON A LONG STRAIGHT ROAD
,0,0,58,57,ON A LONG ROAD STRETCHING EAST
,0,0,0,0,SAT IN A PARKED CAR
,0,0,0,0,DRIVING THE CAR ALONG A NARROW WINDING  ROAD
,0,0,0,0,AT A SET OF TRAFFIC LIGHTS. I SEE A TOWNTO THE EAST
,0,63,64,0,ON THE OUTSKIRTS OF A TOWN. THERE IS A  CAR HERE
,62,63,0,0,ON A LONG ROAD
,0,65,66,62,BY A SET OF TRAFFIC LIGHTS
,64,67,0,0,OUTSIDE A SMALL AIRFIELD
,0,0,68,64,OUTSIDE A SHOPPING ARCADE
,65,0,69,0,ON A GRASS RUNWAY. THERE IS A TIGER MOTHPLANE TO THE EAST
,72,70,71,66,IN A SMALL SHOPPING ARCADE
,0,0,0,67,SAT IN THE TIGER MOTH PLANE
,68,0,0,0,IN A SMALL SHOP SELLING MOTORBIKE SPARES
,0,0,0,68,IN A CAKE SHOP. THEY SEEM TO HAVE ALMOSTSOLD OUT OF CAKES
,0,68,0,0,IN A SHOE SHOP. THE ASSISTANT ASKS ME   WHAT I WANT
,0,0,0,0,FLYING HIGH ABOVE THE CLOUDS....HELP!!!
,0,79,0,0,IN THE COCKPIT OF THE PLANE
,0,0,0,79,BY A LOCKED HANGER
,79,0,77,0,IN THE AIRPORT BUILDING. IT IS VERY BUSY
,0,78,80,76,IN THE CUSTOMS HALL. A NOTICE READS     'NOTHING TO DECLARE..EAST'
,77,81,0,0,BY A DESK. THE CUSTOMS OFFICER KNOWS ME AND LETS ME THROUGH
,74,76,75,0,ON A RUNWAY AT THE SIDE OF A TIGER MOTH,0,0,0,0
,LOCKED IN A CELL. A CUSTOMS OFFICER HAS ARRESTED ME FOR ATTEMPTED SMUGGLING
,78,82,0,0,BY AN AIRPORT BOOKSTALL
,81,83,0,0,IN THE AIRPORT FOYER
,82,0,84,85,AT A TAXI RANK. A DRIVER WALKS OVER AND ASKS ME SOMETHING BUT I DON'T RECOGNISE THE LANGUAGE
,0,0,0,83,IN A VAST CARPARK
,0,0,83,0,BY A LOCKED BARRIER
,0,0,0,0,RIDING IN A TAXI
,0,0,0,0,IN A STATIONARY TAXI
,0,0,89,0,IN A TAXI. THE DRIVER HAS OPENED THE    DOOR
,0,0,90,88,OUTSIDE A MILITARY BASE. THE GATE IS    LOCKED
,0,0,0,89,BY THE PERIMETER FENCE. THERE IS A TALL TREE HERE
,0,0,0,92,IN THE BRANCHES OF A TALL TREE
,0,0,91,0,ON A BRANCH WHICH OVERHANGS THE MILITARYBASE
,0,0,95,94,INSIDE THE PERIMETER FENCE OF THE BASE
,0,0,93,0,BY A LOCKED GATE. I SEE SOLDIERS IN THE DISTANCE
1780 DATA 0,96,0,93,BY SOME LARGE FUEL TANKS
,95,0,97,0,AT THE ENTRANCE TO A LARGE BUILDING
,0,98,0,0,IN A LARGE STOREROOM FULL OF BOTTLES OF STRANGE LIQUIDS
,97,99,100,0,IN A LARGE ROOM FULL OF BOXES LABELLED  'MADE IN JAPAN'
,98,0,0,0,AT THE BOTTOM OF A FLIGHT OF STAIRS
,0,0,102,98,IN AN OPEN DOORWAY
,0,0,0,0,IN A SMALL OFFICE. THE DOOR SLAMS SHUT  BEHIND ME
,0,0,103,100,BY A SMALL AMPHIBIOUS VEHICLE
,0,0,104,102,ON A NARROW ROAD
,0,0,0,103,AT THE SIDE OF A DEEP DARK LAKE
,0,0,0,0,SAT AT THE CONTROLS OF AN AMPHIBIOUS    VEHICLE
,0,107,108,0,DRIVING THE VEHICLE ACROSS A LARGE YARD
,106,0,0,0,BY A BRICK WALL. I THINK I'LL HAVE TO GOBACK
,0,0,109,106,DRIVING ALONG A ROAD
,0,0,110,108,DRIVING INTO A DEEP LAKE
,0,112,111,109,IN THE DARK DEPTHS OF THE LAKE
,0,113,0,110,IN THE DEEP LAKE
,110,0,113,0,IN THE DEEP LAKE. A STRANGE FISH SWIMS  AROUND MY CRAFT
,111,114,113,112,IN THE DEEP LAKE. THERE IS A LARGE ROCK BELOW ME
,113,115,114,118,IN THE DEEP LAKE
1820 DATA 114,116,115,117,IN THE DEEP LAKE
,115,119,0,0,AT THE ENTRANCE TO A GLOOMY UNDERWATER  CAVERN
,118,117,115,117,IN THE DEEP LAKE
,0,117,114,0,NEXT TO A LARGE BOULDER WHICH BEARS THE STRANGE INSCRIPTION 'FOLNIA'
,116,120,0,0,DRIVING BETWEEN TWO LARGE COLUMNS IN THECAVE ENTRANCE
,0,0,0,0,PARKED IN A SMALL AIRLOCK. THE STEEL    DOORS HAVE CLOSED BEHIND ME AND IT HAS  FILLED WITH AIR
,0,123,0,122,IN AN AIRLOCK. A SMALL AMPHIBIOUS CRAFT IS PARKED HERE
1840 DATA 0,0,121,0,IN A SMALL GLASS CAGE. STAIRS LEAD UP   FROM HERE
,121,0,0,0,IN A ROOM FULL OF DIALS AND SWITCHES
,0,0,0,125,AT THE TOP OF A FLIGHT OF STAIRS. A     NARROW GLASS PASSAGE LEADS WEST ACROSS  AN ENORMOUS WORKSHOP
,126,128,124,0,AT A JUNCTION IN THE GLASS CORRIDOR
,0,125,127,0,IN A GLASS CORRIDOR. THERE IS A SMALL   DOOR HERE
,0,0,127,126,ON A SMALL RAILWAY PLATFORM. A TRAIN IS HERE WITH ITS DOORS OPEN. PEOPLE ARE    GETTING ON,125,0,0,0,AT THE TOP OF A DEEP WELL
1860 DATA 0,0,0,0,RIDING IN A SMALL COMPARTMENT ON THE    TRAIN. THE MAN IN THE CORNER IS CARRYINGA REVOLVER
,0,0,0,131,IN A STATIONARY CARRIAGE. THE DOOR IS   OPEN
,0,132,130,0,ON A WIDE PLATFORM. THE TRAIN IS STILL  HERE BUT ITS DOORS ARE CLOSED
1870 DATA 131,133,0,0,ON A NARROW STAIRCASE
,132,134,0,0,AT THE TOP OF A FLIGHT OF STAIRS
,133,0,135,0,AT THE WEST END OF A VAST CHAMBER
,0,136,0,134,AT THE EAST END OF A VAST CHAMBER
,135,137,0,0,IN A LOW PASSAGEWAY LIT BY TORCHES IN   ALCOVES
,136,138,0,0,IN A WIDE PASSAGE
,137,140,139,0,IN A BRIGHTLY LIT CAVERN FULL OF MEN IN UNIFORMS
,0,0,0,138,IN A CROWDED LIFT. THE DOORS ARE OPEN   AND A MAN STANDS NEXT TO THE BUTTONS
,138,0,0,0,BY A SHEER ROCK FACE. A ROPE HANGS FROM ABOVE
,0,0,142,0,IN A SMALL CAVERN. THERE IS A SHEER DROPTO THE NORTH. A ROPE IS ATTACHED TO A   RING IN THE WALL
,0,0,0,141,IN A LABORATORY. A FLASK OF BLUE LIQUID BOILS OVER A LOW HEAT
,0,144,0,0,IN A CROWDED LIFT
,143,145,146,0,IN A HOTEL FOYER
1900 DATA 144,147,0,0,IN A CROWDED BAR. THE BARMAN IS BUSY    SERVING A GROUP OF WORKMEN,0,0,149,144,BY THE HOTEL RECEPTION DESK,145,0,0,0,BY A SWIMMING POOL. THERE IS A SMALL    QUEUE WAITING AT AN ICE CREAM KIOSK,147,148,148,148,SWIMMING IN THE POOL
1910 DATA 0,150,0,146,AT THE REAR ENTRANCE TO THE HOTEL. A    PATH LEADS SOUTH THROUGH SOME BUSHES,149,151,0,0,AT THE ENTANCE TO A CAMPSITE,150,153,152,0,BY THE CAMP SHOP,0,0,0,151,INSIDE A BUSY CAMPSHOP. THERE IS A LARGEQUEUE WAITING AT THE CHECKOUT
1920 DATA 151,154,0,0,ON A NARROW FOOTPATH BETWEEN ROWS OF    TENTS,153,0,155,0,ON A NARROW FOOTPATH. THERE IS A LARGE  GLOOMY FOREST TO THE EAST,0,156,0,154,IN A DARK AND MENACING FOREST,155,0,0,157,IN A FOREST. THERE IS A CAVE TO THE EAST,0,0,156,0
1930 DATA IN THE SECRET HEADQUARTERS OF XOLTAN,0,0,0,0,AT JOURNEY'S END,NOT LIKELY!,SORRY !, HOW ?,NO !,I WOULD IF I COULD,THAT'S FAR TOO DANGEROUS!,NOT HERE!,NOTHING HAPPENS,I CAN'T SEE ANYTHING SPECIAL,O.K.,I SEE SOMETHING,I HAVEN'T GOT IT!
1940 DATA I FIRE THE GUN!,IT IS DEAD,HE SHOOTS FIRST!,I AM DEAD. WOULD YOU LIKE ANOTHER GAME ?,IT GLISTENS,WHAT DO I DO NOW,I CAN GO, NORTH,SOUTH,EAST,WEST,I CAN SEE,I FORGOT MY GLASSES!,I'M NOT HUNGRY!,I NEVER DRINK ALCOHOL !
1950 DATA THAT'S NOT MUCH USE !,I'M SORRY I HAVEN'T A CLUE,DON'T SWEAR AT ME !!,H E L P,DON'T BE SUCH A SILLY BILLY!,HE REFUSES,SHE DOESN'T LIKE THAT,I'VE NEVER DONE THIS BEFORE!,THIS IS FUN !,WHOOPS!,I SLIPPED AND BROKE MY NECK!,SPLAT,ZAP,BANG
1960 DATA A SMALL KEY,20,KEY,A STIFF BRUSH,16,BRUSH,SOME MUSHROOMS,6,MUSHROOMS,AN OIL DRUM,27,DRUM,A COMPUTER REPAIR MANUAL,38,MANUAL,AN ARMED GUARD,77,GUARD,A CRASH HELMET,70,HELMET,A PAIR OF SHOES,72,SHOES,A CAKE,71,CAKE,A TORPEDO,43,TORPEDO
1970 DATA A CAN OF SOUP,45,SOUP,SOME SUNBATHERS,48,SUNBATHERS,A BATH TOWEL,52,TOWEL,AN ATTENDANT,54,ATTENDANT,A RED LEVER,105,LEVER,AN OLD TRAMP,108,TRAMP,A SMALL FISH,112,FISH,A REVOLVER,97,REVOLVER,XOLTAN HIMSELF,157,XOLTAN,SOME FIRELIGHTERS,152
1980 DATA FIRELIGHTER,A GOLDEN BULLET,142,BULLET,A BEAUTIFUL LADY WHO IS SWIMMING,148,LADY,A BLUE BUTTON,41,BUTTON,A MAGAZINE,81,MAGAZINE,A ROLLS ROYCE,84,ROLLS,A CREDIT CARD,14,CARD,A FISHERMAN,32,FISHERMAN,A WAITER,144,WAITER,A BROKEN COMPUTER,101
1990 DATA COMPUTER
