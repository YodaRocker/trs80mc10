0 CLEAR1500:DIMCC,ZZ,C1,C2,M$,I$:MC=16384:GOTO100
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 POKECC,138:POKECC+1,A(21,CC):POKECC+2,A(22,CC):POKECC+3,A(23,CC):POKECC+32,138:RETURN
3 PRINTPOKECC,138:POKECC+1,A(21,CC):POKECC+2,A(22,CC):POKECC+3,A(23,CC):POKECC+32,138:RETURN
4 PRINTTAB(14)"Š"CHR$(A(21,CC))CHR$(A(22,CC))CHR$(A(23,CC)):PRINTTAB(14)"Š":RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORC1=CC-LEN(M$)+1TOCC:POKEC1,PEEK(C1)-64:NEXT:RETURN
8 FORCC=1TO32:PRINTCHR$(8);:NEXT:RETURN
9 CC=ASC(MID$("¯¿Ïßÿ",RND(5),1)):RETURN
10 IFABS(VAL(M$))<1000THENRETURN
11 ZZ=LEN(M$):FORCC=1TOZZ:ON-(ASC(MID$(M$,CC,1))>=ASC("1")ANDASC(MID$(M$,CC,1))<=ASC("9"))GOSUB12:NEXT:RETURN
12 FORC1=CC+1TOZZ+1:IFC1>ZZTHEN14
13 IF(ASC(MID$(M$,C1,1))>=ASC("0")ORASC(MID$(M$,C1,1))<=ASC("9"))THENNEXT:CC=ZZ:RETURN
14 FORC2=C1-4TOCCSTEP-3:M$=MID$(M$,1,C2)+","+MID$(M$,C2+1,ZZ-C2+1):ZZ=LEN(M$):NEXT:C1=ZZ+1:NEXT:CC=ZZ:RETURN
100 DIMLA(8),SA(8),VW(8),MN(8),MO(8),BV(8),WI(8),VS(8),SF(8),AR(8),SF$(4),MR(8),GR(8),NA$(8),A(23,8)
110 CLS:PRINT:PRINT" CONTINUE OLD GAME?":PRINT:PRINT" (yES OR nO)"
111 A$=INKEY$:IFA$="Y"THEN53000
112 IFA$<>"N"THEN111
120 CLS:PRINT:INPUT" NUMBER OF PLAYERS (2-8)";AZ:PRINT
122 IFAZ>8ORAZ<2THEN120
124 FORI=1TOAZ
126 PRINT" PLAYER NAME";STR$(I);:INPUTNA$(I):NA$(I)=LEFT$(NA$(I),8)
128 LA(I)=340000000/AZ
130 SA(I)=5:VW(I)=4
132 MN(I)=100:MO(I)=200000000
134 BV(I)=10000000:WI(I)=100
136 VS(I)=.00001:SF(I)=1
138 MR(I)=100:AR(I)=100000
140 GR(I)=1:FORC1=21TO23:GOSUB9:A(C1,I)=CC:NEXT
160 NEXT:SP=0
162 SF$(2)="DEMOCRACY, MARKET"
164 SF$(3)="DEMOCRACY, PLANNED"
166 SF$(1)="MONARCHY, MARKET"
168 SF$(4)="DICTATORSHIP"
200 SP=SP+1:IFSP>AZTHENSP=1
201 IFLA(SP)<0THENLA(SP)=0
202 IFMO(SP)<0THENMO(SP)=0
203 IFBV(SP)<0THENBV(SP)=0
204 VS(SP)=VS(SP)-RND(0)/10:IFVS(SP)<0THENVS(SP)=.00001
205 CLS:PRINT:PRINT" SAVE GAME?":PRINT:PRINT" (yES OR nO)"
206 A$=INKEY$:IFA$="Y"THEN53500
207 IFA$<>"N"THEN206
208 IFWI(SP)>240THENWI(SP)=240
210 CLS:M$="STATE OF "+NA$(SP):GOSUB1:PRINT:CC=SP:GOSUB4:PRINT
212 MN(SP)=MN(SP)-RND(8)
214 MR(SP)=MR(SP)-SF(SP):IFMR(SP)<4THENMR(SP)=4
220 M$="GOV'T TYPE: "+SF$(SF(SP)):GOSUB1
222 PRINT
230 PRINT"LAND:      ";:M$=STR$(INT(LA(SP))):GOSUB10:PRINTRIGHT$("              "+M$,14)" SQ.KM"
240 PRINT"TREASURY  $";:M$=STR$(INT(MO(SP))):GOSUB10:PRINTRIGHT$("              "+M$,14)
250 PRINT"POPULATION:";:M$=STR$(INT(BV(SP))):GOSUB10:PRINTRIGHT$("              "+M$,14)
260 B1=25-WI(SP)/10+7.5*VS(SP)
270 IFB1<0THENB1=1
280 BD=BV(SP)*INT(B1)
290 PRINT"WELFARE:  $";:M$=STR$(INT(BD)):GOSUB10:PRINTRIGHT$("              "+M$,14)
300 PRINT
310 INPUT"WELFARE SPENDING ";AB
320 INPUT"ECONOMIC STIMULUS";AW
330 IFAB+AW>MO(SP)THENGOSUB8:GOSUB8:GOTO310
340 CLS
350 T1=INT((BD-AB)/B1)
355 IFT1<0THENTD=0:GOTO357
356 TD=T1
357 T2=INT(RND(VS(SP)*100)/100*BV(SP)):TD=TD+T2
360 PRINT"MORTALITY:  ";:M$=STR$(INT(TD)):GOSUB10:PRINTM$
370 GB=RND(((BV(SP)/10)-BV(SP)*VS)/100)*10
380 PRINT"BIRTHS:     ";:M$=STR$(INT(GB)):GOSUB10:PRINTM$
390 BV(SP)=BV(SP)-TD+GB
400 IFT1>0THENWI(SP)=WI(SP)-RND(ABS(T1/100000))/2+AW/10000000
402 IFT1<0THENWI(SP)=WI(SP)+RND(ABS(T1/100000))/2+AW/10000000
406 IFT1>0THENMR(SP)=MR(SP)-RND(ABS(T1/100000))/2
407 IFT1<0THENMR(SP)=MR(SP)+RND(ABS(T1/100000))/2
410 PRINT
415 IFWI(SP)>200THENWI(SP)=200
416 IFWI(SP)<10THENWI(SP)=10
420 WE=(1+INT(SF(SP))/8)*(1+RND(0)/2)*WI(SP)*1000000:WE=INT(WE)
430 PRINT"TAX REVENUE:   $";:M$=STR$(INT(WE)):GOSUB10:PRINTRIGHT$("              "+M$,14)
440 MO(SP)=MO(SP)+WE-AB-AW
450 WS=AR(SP)*10:WW=SA(SP)*5+VW(SP)*4+AR(SP)*3
460 PRINT"MILITARY SALES:$";:M$=STR$(INT(WS)):GOSUB10:PRINTRIGHT$("              "+M$,14)
470 PRINT"MILITARY SPEND:$";:M$=STR$(INT(WW)):GOSUB10:PRINTRIGHT$("              "+M$,14)
480 MO(SP)=MO(SP)-WW-WS
485 GOSUB62000
490 IFRND(0)>.1THEN512
492 V=RND(10000):K=RND(0)*10*V
494 CLS:M$="A FOREIGN COMPANY OFFERS TO ARTIFICIALLY EXPAND YOUR LAND AREA BY APPROXIMATELY:":GOSUB1
500 M$=STR$(INT(V)):GOSUB10:PRINTMID$(M$,2)" SQ.KM.":PRINT"COST: $";
502 M$=STR$(INT(K)):GOSUB10:PRINTMID$(M$,2):PRINT"ACCEPT OFFER?"
504 GOSUB60000:IFTA=-1THEN1000
506 MO(SP)=MO(SP)-K:LA(SP)=LA(SP)+V:MR(SP)=MR(SP)+RND(0)*3
508 V=RND(AZ):IFV=AZTHEN508
510 MO(V)=MO(V)+K:GOTO1000
512 IFRND(0)>.1THEN560
514 CLS:M$="ONE OF YOUR COMPANIES HAS DEVELOPED A NEW MEDICINE THAT COULD BENEFIT SIX THOUSAND PEOPLE IN THE COUNTRY. "
515 M$=M$+"ALLOW THE MEDICINE TO BE MARKETED?":GOSUB1:PRINT
526 GOSUB60000
528 IFTA=1THEN540
530 IFRND(0)>.4THEN1000
532 ML=INT(RND(0)/10*BV(SP)):BV(SP)=BV(SP)-ML
534 PRINT:PRINT"THE DOSE COSTS $";:M$=STR$(ML):GOSUB10:PRINTM$
535 MR(SP)=MR(SP)-RND(10)
536 PRINT"PEOPLE LIVE!":GOSUB62000:GOTO1000
540 IFRND(0)<.4THEN550
542 PRINT:M$="THE MEDICINE PROVES SUCCESSFUL. IT CURES ALL THE PEOPLE IN HOSPITAL.":GOSUB1
543 MR(SP)=MR(SP)+RND(9)
546 WI(SP)=WI(SP)+3*RND(0):GOSUB62000:GOTO1000
550 PRINT:PRINT"THE MEDICINE WAS A FLOP."
552 MR(SP)=MR(SP)-RND(4):ML=INT(RND(0)/10*BV(SP))
554 M$=STR$(ML):GOSUB10:PRINTM$" PEOPLE DIE.":BV(SP)=BV(SP)-ML
556 GOSUB62000:GOTO1000
560 IFRND(0)>.05THEN600
562 CLS:M$="ONE OF YOUR ATOMIC REACTORS IS OUT OF CONTROL. "
563 M$=M$+"A FOREIGN GROUP OF EXPERTS OFFERS HELP. THE COST IS ONLY $10 MILLION!":GOSUB1
565 PRINT"ACCEPT THEIR OFFER?":PRINT:GOSUB60000
574 IFTA=1THEN586
576 IFRND(0)>.3THEN582
578 PRINT
579 MR(SP)=MR(SP)+5+RND(10)
580 M$="YOU'RE IN LUCK! YOUR OWN EXPERTS ARE ABLE TO HANDLE THE PROBLEM.":GOSUB1:GOSUB62000:GOTO1000
581 ML=INT(RND(0)/50*BV(SP))
582 PRINT:M$=STR$(INT(ML)):GOSUB10:PRINTMID$(M$,2)" PEOPLE KILLED"
583 MR(SP)=MR(SP)-6:VS(SP)=VS(SP)+RND(0)/100
584 PRINT"AS A RESULT OF THE ACCIDENT.":GOSUB62000:GOTO1000
586 V=RND(AZ):IFV=SPTHEN586
588 MO(V)=MO(V)+10000000:MO(SP)=MO(SP)-10000000
590 IFRND(0)<.1THEN594
592 PRINT"THE TEAM CAN HELP YOU!":GOSUB62000:GOTO1000
594 PRINT:PRINT"IT DOES NOT WORK!":ML=INT(RND(0)/50*BV(SP))
596 PRINTM$=STR$(INT(ML)):GOSUB10:PRINTMID$(M$,2)" DEAD!":BV(SP)=BV(SP)-ML
598 GOSUB62000:GOTO1000
600 IFRND(0)<.4ANDGR(SP)=-1THEN602
601 IFRND(0)>.1ORSF(SP)<3THEN700
602 CLS:M$="AN UPRISING BREAKS OUT IN YOUR STATE. WANT TO SEND IN THE ARMY?":GOSUB1:PRINT:GOSUB60000
608 IFTA=1THEN630
610 IFRND(0)<.5THEN620
612 TD=RND(500):PRINT:M$="THE REBELS KILL"+STR$(TD)+" MEMBERS OF THE GOVERNMENT":GOSUB1
616 PRINT"BEFORE GIVING UP.":BV(SP)=BV(SP)-TD:GOSUB62000:GOTO1000
620 PRINT:M$="YOU NEGOTIATE WITH THE REBELS TO LAY DOWN THEIR ARMS.":GOSUB1
622 GOSUB62000:GOTO1000
630 PRINT:M$="THE ARMY STRIKES A BLOODY BLOW AGAINST THE REBELS.":GOSUB1:PRINT"THEY LOSE ";
634 ML=INT(RND(0)/10*BV(SP)):M$=STR$(INT(ML)):GOSUB10
636 PRINTM$" PEOPLE."
638 BV(SP)=BV(SP)-ML:GOSUB62000:GOTO1000
700 IFRND(0)>.1THEN720
702 CLS:M$="A SPECIAL OFFER: 3 NUCLEAR MISSILES FOR ONLY $40 MILLION!":GOSUB1
706 PRINT"BUY THEM?":PRINT:GOSUB60000:IFTA=-1THEN1000
708 MO(SP)=MO(SP)-40000000:SA(SP)=SA(SP)+3
710 GOTO1000
720 IFRND(0)>.1THEN740
722 CLS:M$="A SPECIAL OFFER: 5000 SOLDIERS FOR ONLY $7.5 MILLION!":GOSUB1
726 PRINT"BUY THEM?":PRINT:GOSUB60000:IFTA=-1THEN1000
728 MO(SP)=MO(SP)-7500000:AR(SP)=AR(SP)+5000:GOTO1000
740 IFRND(0)>.1THEN820
742 CLS:M$="A SPY IS CAPTURED. DO YOU ATTEMPT TO TURN HIM?":GOSUB1:PRINT:GOSUB60000:PRINT
746 IFTA=-1THEN780
748 IFRND(0)<.5THEN760
750 PRINT"HE BETRAYS YOU! A SERIOUS BLOW TO YOUR ECONOMY.":GOSUB62000
754 WI(SP)=WI(SP)-RND(20):GOTO1000
760 PRINT"HE WORKS SUCCESSFULLY FOR YOU!":GOSUB62000
762 WI(SP)=WI(SP)+RND(20):GOTO1000
780 IFRND(0)<.2THEN790
782 PRINT"OK. HE IS EXECUTED.":GOSUB62000:GOTO1000
790 PRINT"THE SPY ESCAPES!":WI(SP)=WI(SP)-RND(15)
792 GOTO1000
820 IFGR(SP)=-1ANDRND(0)<.4THEN822
821 IFMR(SP)>15THEN900
822 CLS:M$="THE PEOPLE ARE VERY DISSATISFIED. A REBELLION BREAKS OUT AND ALMOST THE ENTIRE GOVERNMENT IS KILLED."
823 GOSUB1:M$="SHOULD THE TROUBLEMAKERS BE EXECUTED?":GOSUB1:PRINT:GOSUB60000:PRINT
832 BV(SP)=BV(SP)-INT(BV(SP)/125)
834 IFTA=1THEN1000
836 M$="THERE ARE STILL MANY CASUALTIES...":GOSUB1
838 BV(SP)=BV(SP)-INT(BV(SP)/100):GOTO1000
900 IFMR(SP)>50ORRND(0)>.3THEN950
902 CLS:ML=INT(BV(SP)/(RND(15)+10))
904 M$=STR$(INT(ML)):GOSUB10:PRINT"EMIGRATION APPLICATIONS: "M$
906 M$="'DISSATISFACTION WITH THE SYSTEM' CAUSES MANY TO WANT TO LEAVE YOUR COUNTRY. LET THE PEOPLE GO?":GOSUB1
910 PRINT:GOSUB60000
912 IFTA=1THENBV(SP)=BV(SP)-ML:MR(SP)=MR(SP)+RND(5)
914 IFTA=-1THENMR(SP)=MR(SP)-RND(5)
916 GOTO1000
950 IFMR(SP)<130ORRND(0)>.3THEN1000
952 CLS:ML=INT(BV(SP)/(RND(15)+5))
954 M$=STR$(INT(ML)):GOSUB10:PRINT"VISA APPLICATIONS: "M$
956 M$="MANY FOREIGNERS WANT TO ENTER YOUR COUNTRY. DO YOU LET THEM IN?":GOSUB1:PRINT
958 GOSUB60000:IFTA=1THENBV(SP)=BV(SP)+ML:MR(SP)=MR(SP)+RND(9)
960 IFTA=-1THENMR(SP)=MR(SP)-RND(5)
1000 IFMR(SP)<5THENMR(SP)=5
1005 CLS:M$="CONDITIONS FOR YEAR IN THE STATE OF "+NA$(SP):GOSUB1
1010 FORI=1TO32:PRINT"=";:NEXT
1020 PRINT:PRINT" 1) INTELLIGENCE"
1022 PRINT" 2) ECONOMY"
1024 PRINT" 3) PEOPLE"
1026 PRINT" 4) FORM OF GOV'T"
1028 PRINT" 5) MILITARY"
1030 PRINT" 6) INTERNAL SECURITY"
1048 PRINT" 7) CONTINUE"
1050 PRINT:PRINT" PLEASE CHOOSE"
1052 A$=INKEY$:ON-(A$="")GOTO1052:A=VAL(A$)
1054 IFA<1ORA>7THEN1052
1056 IFA=7THEN10000
1058 IFA<>1THEN1100
1060 CLS:PRINT"REPORT":PRINT"======"
1062 PRINT:
1063 PRINT"LAND:          ";:M$=STR$(INT(LA(SP))):GOSUB10:PRINTRIGHT$("              "+M$,14)
1064 PRINT"TREASURY:     $";:M$=STR$(INT(MO(SP))):GOSUB10:PRINTRIGHT$("              "+M$,14)
1066 PRINT"POP.:          ";:M$=STR$(INT(BV(SP))):GOSUB10:PRINTRIGHT$("              "+M$,14)
1068 PRINT"ARMY:          ";:M$=STR$(INT(AR(SP))):GOSUB10:PRINTRIGHT$("              "+M$,14)
1070 PRINT"STRAT. NUKES:  ";:M$=STR$(INT(SA(SP))):GOSUB10:PRINTRIGHT$("              "+M$,14)
1072 PRINT"DEFENSE FORCES:";:M$=STR$(INT(VW(SP))):GOSUB10:PRINTRIGHT$("              "+M$,14)
1073 PRINT"MILITARY READINESS:"STR$(INT(MN(SP)))"%"
1074 PRINT"ECONOMIC GROWTH:   "STR$(INT(WI(SP)))"%"
1078 PRINT"CIVILIAN MORALE:   "STR$(INT(MR(SP)))"%"
1080 PRINT:M$="FORM OF GOV'T: "+SF$(SF(SP)):GOSUB1
1082 GOSUB62000:GOTO1000
1100 IFA<>2THEN1200
1102 CLS:PRINT"ECONOMIC PRIORITIES":PRINT"===================":PRINT
1104 PRINT" 1) ENVIRONMENTAL PROTECTION"
1106 PRINT" 2) IMPROVE PRODUCTIVITY"
1108 PRINT" 3) MONOPOLIZTION"
1110 PRINT" 4) RATIONALIZATION"
1112 PRINT" 5) DO NOTHING"
1114 PRINT:PRINT" YOUR CHOICE?":PRINT
1116 A$=INKEY$:ON-(A$="")GOTO1116:A=VAL(A$)
1118 IFA<1ORA>5THEN1116
1120 IFA=5THEN1000
1122 IFA<>1THEN1132
1123 QQ=RND(10)+5
1124 PRINT" COST: $";:M$=STR$(INT(MO(SP)/QQ)):GOSUB10:PRINTM$
1126 PRINT" OK? ":PRINT:GOSUB60000:IFTA=-1THEN1000
1128 MO(SP)=MO(SP)-INT(MO(SP)/QQ):MR(SP)=MR(SP)+4
1130 WI(SP)=WI(SP)-5:GOTO1000
1132 IFA<>2THEN1136
1133 SOUND31,1:MR(SP)=MR(SP)-15:IFMR(SP)<5THENMR(SP)=5
1134 WI(SP)=WI(SP)+10:IFWI(SP)>200THENWI(SP)=200
1135 GOTO1000
1136 IFA<>3THEN1140
1138 MR(SP)=MR(SP)+2:WI(SP)=WI(SP)+5:GOTO1133
1140 MR(SP)=MR(SP)-10:WI(SP)=WI(SP)+2:GOTO1133
1200 IFA<>3THEN1300
1201 CLS:PRINT"PEOPLE":PRINT"======":PRINT
1202 PRINT "1) BUILD SCHOOL"
1204 PRINT "2) BUILD A BANK"
1206 PRINT "3) BUILD A SHOPPING CENTRE"
1208 PRINT "4) BUILD A FOOTBALL STADIUM"
1210 PRINT "5) BUILD ROADS"
1212 PRINT "6) DO NOTHING"
1214 PRINT:PRINT" YOUR CHOICE?":PRINT
1216 A$=INKEY$:ON-(A$="")GOTO1216:A=VAL(A$)
1218 IFA<1ORA>6THEN1216
1220 IFA=6THEN1000
1222 K=MO(SP)/(RND(10)+5):PRINT" COST: $";:M$=STR$(INT(K)):GOSUB10:PRINTM$
1224 PRINT" OK? ":PRINT:GOSUB60000
1226 IFTA=-1THEN1000
1228 MO(SP)=MO(SP)-INT(K)
1230 MR(SP)=MR(SP)+5:WI(SP)=WI(SP)+5:SOUND31,1:GOTO1000
1300 IFA<>4THEN1350
1302 CLS:PRINT"GOV'T TYPE":PRINT"==========":PRINT
1304 FORI=1TO4:PRINTSTR$(I)") ";SF$(I):NEXT
1306 PRINT:PRINT" CURRENTLY: "SF$(SF(SP)):PRINT
1308 PRINT" YOUR CHOICE"
1310 A$=INKEY$:ON-(A$="")GOTO1310:A=VAL(A$):IFA<1ORA>4THEN1310
1312 IFA=SF(SP)THEN1000
1314 MR(SP)=MR(SP)-10*A:SF(SP)=INT(A):IFMR(SP)<5THENMR(SP)=5
1315 SOUND31,1
1316 GOTO1000
1350 IFA<>6THEN1500
1352 CLS:PRINT"INTERNAL SECURITY":PRINT"=================":PRINT
1354 M$="1) DISSOLVE ANTI-GOVERNMENT GROUPS":GOSUB1
1356 M$="2) LAYOFF ANTI-GOVERNMENT CIVIL SERVANTS":GOSUB1
1360 M$="3) RESTRICT FUNDAMENTAL RIGHTS":GOSUB1
1362 M$="4) ALLOW FUNDAMENTAL RIGHTS":GOSUB1
1364 M$="5) DO NOTHING":GOSUB1
1366 PRINT:PRINT" YOUR CHOICE"
1368 A$=INKEY$:ON-(A$="")GOTO1368:A=VAL(A$):IFA<1ORA>5THEN1368
1369 IFA<>5THENSOUND31,1
1370 IFA=5ORA<3THEN1000
1372 IFA=3ANDGR(SP)=1THENGR(SP)=-1:SOUND31,1:MR(SP)=MR(SP)-15
1374 IFA=4ANDGR(SP)=-1THENGR(SP)=1:SOUND31,1:MR(SP)=MR(SP)+10
1376 GOTO1000
1500 CLS:PRINT"MILITARY":PRINT"========":PRINT
1502 PRINT"1) NUCLEAR MISSILE ($20 MIL.)
1504 PRINT"2) DEFENSIVE WEAPONS ($15 MIL.)"
1506 PRINT"3) 1000 SOLDIERS ($5 MIL.)"
1508 PRINT"4) MANEUVERS (APPROX $40 MIL.)"
1510 PRINT"5) WAR"
1512 PRINT"6) DO NOTHING"
1514 PRINT:PRINT" YOUR CHOICE"
1516 A$=INKEY$:ON-(A$="")GOTO1516:A=VAL(A$):IFA<1ORA>6THEN1516
1517 IFA=6THEN1000
1518 IFA=1THENMO(SP)=MO(SP)-20000000:SOUND31,1:SA(SP)=SA(SP)+1
1520 IFA=1THEN1516
1522 IFA=2THENMO(SP)=MO(SP)-15000000:SOUND31,1:VW(SP)=VW(SP)+1
1524 IFA=2THEN1516
1526 IFA=3THENMO(SP)=MO(SP)-5000000:SOUND31,1:AR(SP)=AR(SP)+1000
1528 IFA=3THEN1516
1530 IFA=5THEN2000
1532 CLS:MO(SP)=MO(SP)-40000000:SOUND31,1:PRINT"MANEUVERS"
1534 PRINT"=========":K=RND(0)*10000000:PRINT
1536 PRINT"COST:       $";:M$=STR$(INT(40000000+K)):GOSUB10:PRINTRIGHT$("              "+M$,14)
1538 F=RND(0)*5000000
1540 PRINT"MIN COST: $";:M$=STR$(INT(F)):GOSUB10:PRINTRIGHT$("              "+M$,14)
1542 MN(SP)=MN(SP)+RND(10)
1545 MO(SP)=MO(SP)-K-F:GOSUB62000:GOTO1500
2000 CLS:PRINT"WAR":PRINT"===":PRINT
2002 FORI=1TOAZ:IFI<>SPTHENPRINTI;") ";NA$(I)
2004 NEXT:PRINTAZ+1") NOBODY":PRINT
2006 PRINT" WHO DO YOU WANT TO ATTACK?"
2008 A$=INKEY$:ON-(A$="")GOTO2008:A=VAL(A$):IFA<1ORA>AZ+1THEN2008
2010 IFA=AZ+1THEN1000
2012 IFA=SPTHEN2008
2020 CLS:PRINTNA$(SP)" HAS"
2022 PRINT"NUCLEAR MISSILES";SA(SP)
2024 PRINT"DEFENSIVE WEAPONS";VW(SP)
2026 PRINT"TROOPS";AR(SP):PRINT
2028 PRINT"HOW MUCH DO YOU USE?":PRINT
2030 INPUT"NUCLEAR MISSILES";A1:IFA1<0ORA1>SA(SP)THENGOSUB8:GOTO2030
2032 INPUT"DEFENSIVE WEAPONS";V1:IFV1<0ORV1>VW(SP)THENGOSUB8:GOTO2032
2034 INPUT"TROOPS";S1:IFS1<0ORS1>AR(SP)THENGOSUB8:GOTO2034
2036 CLS:PRINTNA$(A)" HAS"
2038 PRINT"NUCLEAR MISSILES";SA(A)
2040 PRINT"DEFENSIVE WEAPONS";INT(VW(A))
2042 PRINT"TROOPS";AR(A):PRINT
2044 PRINT"HOW MUCH DO YOU USE?":PRINT
2046 INPUT"NUCLEAR MISSILES";A2:IFA2<0ORA2>SA(A)THENGOSUB8:GOTO2046
2048 INPUT"DEFENSIVE WEAPONS";V2:IFV2<0ORV2>VW(A)THENGOSUB8:GOTO2048
2050 INPUT"TROOPS";S2:IFS2<0ORS2>AR(A)THENGOSUB8:GOTO2050
2052 CLS:PRINT"THE WAR BEGINS":PRINT"=============="
2054 TA=0:TV=0:LA=0:LV=0
2056 FORI=1TOA1
2058 Q1=RND(MN(SP)):Q2=RND(MN(A))
2060 IFQ1>Q2THENTA=TA+1:VS(A)=VS(A)+RND(0)/5
2062 NEXT
2064 FORI=1TOA2
2066 Q1=RND(MN(A)):Q2=RND(MN(SP))
2068 IFQ1>Q2THENTV=TV+1:VS(SP)=VS(SP)+RND(0)/5
2070 NEXT:GOSUB63000
2072 SA(SP)=SA(SP)-A1:SA(A)=SA(A)-A2
2073 WI(SP)=WI(SP)-TV:WI(A)=WI(A)-TA
2074 IFA1+A2<>0THENLA=LA(A)/(A1+A2)*TA
2076 IFA1+A2<>0THENLV=LA(SP)/(A1+A2)*TV
2078 SS=MN(SP)+MN(A)-10+RND(20)
2080 SA=INT(MN(SP)/SS*S1):SV=INT(MN(A)/SS*S2)
2086 PRINT"        ATTACKER      DEFENDER"
2088 PRINT"LAND  ";:M$=STR$(INT(LA)):GOSUB10:PRINTRIGHT$("             "+M$,13);
2089 M$=STR$(INT(LV)):GOSUB10:PRINTRIGHT$("          "+M$,10)
2090 PRINT"TROOPS";:M$=STR$(INT(SA)):GOSUB10:PRINTRIGHT$("             "+M$,13);
2089 M$=STR$(INT(SV)):GOSUB10:PRINTRIGHT$("          "+M$,10)
2092 FORI=1TO32:PRINT"=";:NEXT
2094 M$="THE WINNER IS "
2096 IFLA>LVTHENM$=M$+NA$(SP):MR(SP)=MR(SP)+A1+A2:MR(A)=MR(A)-A1-A2
2098 IFLA<LVTHENM$=M$+NA$(A):MR(A)=MR(A)+A1+A2:MR(SP)=MR(SP)-A1-A2
2100 IFLA=LVTHENM$=M$+"NO ONE"
2101 GOSUB1
2110 AR(SP)=AR(SP)-SA:AR(A)=AR(A)-SV
2120 LA(SP)=LA(SP)-LV+LA
2122 LA(A)=LA(A)-LA+LV
2124 MO(SP)=MO(SP)-A1*20
2126 MO(A)=MO(A)-A2*20
2128 VW(A)=VW(A)-V2:VW(SP)=VW(SP)-V1
2200 GOSUB62000
10000 CLS
10005 IFLA(SP)>300000000THEN40000
10010 IFMO(SP)>0ANDBV(SP)>1000ANDAR(SP)>0THEN200
10020 IFMO(SP)>0THEN10100
10022 M$=NA$(SP)+", YOUR TREASURY IS EMPTY!":GOSUB1:PRINT
10024 M$="THE U.N. IS READY TO HELP YOU. BUT IT WILL COST YOU HALF YOUR LAND!":GOSUB1
10030 LA(SP)=LA(SP)/2:FORI=1TOAZ
10032 IFI<>SPTHENLA(I)=LA(I)+LA(SP)/(AZ-1)
10034 NEXT:MO(SP)=200000000:GOSUB62000:GOTO10000
10100 IFBV(SP)>1000THEN10200
10102 M$=NA$(SP)+", YOUR PEOPLE ARE ERADICATED!":GOSUB1
10106 G1=0:G2=0:FORI=1TOAZ:IFI=SPTHEN10110
10108 IFAR(I)>G1THENG1=AR(I):G2=I
10110 NEXT
10112 M$=NA$(G2)+"-DAY PARADE IN YOUR COUNTRY! YOU ARE DECORATED.":GOSUB1
10117 LA(G2)=LA(G2)+LA(SP)
10118 FORI=SPTOAZ
10120 LA(I)=LA(I+1):SA(I)=SA(I+1):VW(I)=VW(I+1):MN(I)=MN(I+1)
10122 MO(I)=MO(I+1):BV(I)=BV(I+1):WI(I)=WI(I+1):VS(I)=VS(I+1)
10124 SF(I)=SF(I+1):NA$(I)=NA$(I+1):AR(I)=AR(I+1):MR(I)=MR(I+1)
10125 GR(I)=GR(I+1)
10126 NEXT
10127 GOSUB62000
10128 AZ=AZ-1:SP=SP-1:IFAZ=1THENSP=G2-1:GOTO40000
10130 GOTO200
10200 M$=NA$(I)+", YOU NO LONGER HAVE ANY TROOPS TO DEFEND YOUR COUNTRY!":GOSUB1:GOTO10106
40000 CLS:PRINT:PRINT"  THE WINNER OF THE GAME"
40002 PRINTTAB(14-LEN(NA$(SP))/2);"IS ";NA$(SP):PRINT
40004 PRINT:M$="  HE HAS CONQURED THE BIGGEST PART OF THE WORLD!":GOSUB1:PRINT:PRINT
40010 PRINT"  PLAY AGAIN?  (Y/N)"
40012 FORI=0TO31:POKEMC+I,107:POKEMC+480+I,107
40014 POKEMC+INT(I/2)*32,107:POKEMC+31+INT(I/2)*32,107:NEXT
40016 A$=INKEY$:ON-(A$="")GOTO40016:IFA$="Y"THENRUN
40017 IFA$<>"N"THEN40016
50000 END
53000 GOSUB55000:SP=SP-1:GOTO162
53500 GOSUB54000:GOTO210
54000 CLS:PRINT:PRINT"ready recorder hit enter"
54004 A$=INKEY$:IFA$<>CHR$(13)THEN54004
54010 CLS
54020 FORI=1TOAZ
54030 A(1,I)=GR(I):A(2,I)=LA(I):A(3,I)=SA(I):A(4,I)=VW(I):A(5,I)=MN(I):A(6,I)=MO(I):A(7,I)=BV(I)
54040 A(8,I)=WI(I):A(9,I)=VS(I):A(10,I)=AR(I):A(11,I)=MR(I):A(12,I)=SF(I)
54045 FORCC=1TOLEN(NA$(I)):I$=MID$(NA$(I),CC,1)+CHR$(0):A(12+CC,I)=ASC(I$):NEXT
54050 NEXT:A(0,0)=AZ:A(1,0)=SP:PRINT"recording":CSAVE*A,"WORLDATA":RETURN
55000 CLS:PRINT:PRINT"ready recorder"
55010 CLOAD*A,"WORLDATA":AZ=A(0,0):SP=A(1,0)
55020 FORI=1TOAZ
55030 GR(I)=A(1,I):LA(I)=A(2,I):SA(I)=A(3,I):VW(I)=A(4,I):MN(I)=A(5,I):MO(I)=A(6,I):BV(I)=A(7,I)
55040 WI(I)=A(8,I):VS(I)=A(9,I):AR(I)=A(10,I):MR(I)=A(11,I):SF(I)=A(12,I)
55045 NA$(I)="":FORCC=1TO8:IFA(12+CC,I)>0THENNA$(I)=NA$(I)+CHR$(A(12+CC,I))
55046 NEXT:RETURN
60000 PRINT" (yES OR nO)":TA=0
60010 A$=INKEY$:IFA$=""THEN60010
60020 IFA$="Y"THENTA=1
60030 IFA$="N"THENTA=-1
60040 IFTA=0THEN60010
60050 RETURN
62000 PRINT@480,;:M$="PRESS ANY KEY TO CONTINUE":GOSUB7
62002 A$=INKEY$:IFA$=""THEN62002
62004 RETURN
63000 FORI=1TOV1
63002 Q1=RND(MN(SP)*2):Q2=RND(MN(A)*2)
63004 IFQ1>Q2THENTV=TV-1
63006 NEXT:IFTV<0THENTV=0
63008 FORI=1TOV2
63010 Q1=RND(MN(A)*2):Q2=RND(MN(SP)*2)
63012 IFQ1>Q2THENTA=TA-1
63014 NEXT:IFTA<0THENTA=0
63016 RETURN
REM ¯¿Ïßÿ
REM Š 138
