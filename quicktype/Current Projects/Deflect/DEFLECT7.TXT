0 CLS:DIMK(255):K(87)=1:K(83)=2:K(90)=3:K(65)=4:PX=RND(-(PEEK(9)*256+PEEK(10)))
1 REM
2 REM oldgame by tinyan
3 REM
4 REM MC-10 EDITS JIM GERRIE
10 DIMHX(4),HY(4)
11 DIMB(16,16),CH$(6)
12 DIMEX(4),EY(4),EH(4)
13 DIMNH(8)
14 DIMSN$(5)
20 FORI=0TO3:READHX(I),HY(I):NEXT
21 FORI=0TO6:READCH$(I):IFCH$(I)="2"THENCH$(I)=CHR$(92)
22 NEXT
23 FORI=0TO7:READNH(I):NEXT
40 REM MAIN
50 GOSUB10000
60 GOSUB11000
70 GOSUB30000
100 C=C+1:SD=0:S=K(PEEK(2)ANDPEEK(17023)):IFS<1THEN200
110 S=S-1:NX=PX+HX(S):NY=PY+HY(S):B=B(NX,NY):IFB=0THENGOSUB2000:GOTO190
140 IFB=2ORB=3THENGOSUB3000:GOTO190
150 IFB=4THENPRINT@32*PY+PX," ";:PX=NX:PY=NY:PRINT@32*PY+PX,"@";:GOTO400
190 PRINT@32*PY+PX,"@";
200 GM=0:FORI=0TOEN
220 X1=EX(I):Y1=EY(I):H1=EH(I):D1=B(X1,Y1):IFD1=2ORD1=3THENH1=NH((D1-2)*4+H1):SD=2:EH(I)=H1
230 VX=HX(H1):VY=HY(H1):X2=X1+VX:Y2=Y1+VY:D2=B(X2,Y2):IFX2=PXANDY2=PYTHENGM=1
240 IFD2=1THENGOSUB7000:EH(I)=H1:GOTO290
250 IFD2=4THENKS=KS-1:SD=3:SC=SC+INT(1000/C)+1:B(X2,Y2)=0
260 PRINT@32*Y1+X1,CH$(D1);:PRINT@32*Y2+X2,"O";
280 EX(I)=X2:EY(I)=Y2
290 NEXT:IFGM=1THEN400
300 IFKS=0THEN500
310 ONSDGOTO320,330,340:GOTO100
320 SOUND50,1:GOTO100
330 SOUND102,1:GOTO100
340 SOUND102,1:SOUND134,1:SOUND154,1:PRINT@232+(16-LEN(STR$(SC))/2),SC:GOTO100
400 REM
401 REM GAME OVER
402 REM
410 PRINT@232+(16-LEN(STR$(SC))/2),SC:SOUND93,1:SOUND154,1:SOUND141,1:SOUND134,1:SOUND154,1
420 SOUND141,1:SOUND134,1:SOUND119,1:SOUND141,1:SOUND134,1:SOUND119,1:SOUND102,4
430 FORI=32TO127:PRINT@32*PY+PX,CHR$(I);:NEXT
450 PRINT@32*5+5,"#############";
451 PRINT@32*6+5,"#           #";
452 PRINT@32*7+5,"# GAME OVER #";
453 PRINT@32*8+5,"#           #";
454 PRINT@32*9+5,"# HIT SPACE #";
455 PRINT@32*10+5,"#           #";
456 PRINT@32*11+5,"#############";
460 IFSC=>HSTHENHS=SC
470 PRINT@328+(16-LEN(STR$(HS))/2),HS
480 IFINKEY$<>" "THEN480
499 GOTO50
500 REM
501 REM CLEAR!
502 REM
510 PRINT@232+(16-LEN(STR$(SC))/2),SC
520 SOUND180,2:SOUND196,2:SOUND206,2:SOUND196,2:SOUND206,2:FORI=1TO50:NEXT:SOUND219,4
530 FORJ=1TO10:PRINT@32*8+5,"CLEAR!";:FORI=1TO100:NEXT:PRINT@32*8+5,"clear";:POKE16650,33
540 FORI=1TO100:NEXT:NEXT
550 C=0:ST=ST+1:IFST>=12THENST=0
560 IFSC=>HSTHENHS=SC
570 PRINT@328+(16-LEN(STR$(HS))/2),HS:PRINT@32*15+19,"HIT SPACE";
580 IFINKEY$<>" "THEN580
599 GOTO60
2000 PRINT@32*PY+PX," ";
2010 PX=NX:PY=NY:RETURN
3000 LX=NX+HX(S):LY=NY+HY(S)
3010 B2=B(LX,LY)
3020 IFB2<>0THENRETURN
3030 B(LX,LY)=B
3040 B(NX,NY)=0
3060 PRINT@32*PY+PX," ";
3070 PRINT@32*LY+LX,CH$(B);
3080 PX=NX:PY=NY
3090 SD=1:RETURN
7000 H1=H1+2:IFH1>3THENH1=H1-4
7099 RETURN
10000 REM
10001 REM INIT NEW GAME
10002 REM
10010 ST=0:SC=0:C=0
10999 RETURN
11000 REM
11001 REM INIT NEW STAGE
11002 REM
11010 GOSUB20000
11100 PX=7:PY=7
11110 EN=ST-INT((ST/4)*4)
11200 FORJ=0TO1
11210 FORI=0TO1
11220 EX(I+J*2)=13*I+1
11230 EY(I+J*2)=13*J+1
11240 NEXT
11250 NEXT
11260 EH(0)=1:EH(1)=2:EH(2)=0:EH(3)=3:RETURN
20000 REM
20001 REM CREATE STAGE
20002 REM
20005 CLS:PRINT@32*1+1,"PLEASE WAIT..."
20010 FORI=0TO15:B(I,0)=1:B(I,15)=1:NEXT:FORJ=0TO15:B(0,J)=1:B(15,J)=1:NEXT
20020 FORJ=1TO14:FORI=1TO14:B(I,J)=0:NEXT:NEXT
20100 K=ST*8+30:FORI=0TOK:RX=RND(14):RY=RND(14):D=RND(2)+1:B(RX,RY)=D
20160 NEXT:FORJ=6TO9:FORI=6TO9:B(I,J)=1:NEXT:NEXT
20170 FORJ=0TO1:FORI=0TO1:B(1+I*13,1+J*13)=0:NEXT:NEXT
20200 K=ST*2+2:KS=0:FORI=0TOK
20230 RX=RND(12)+1:RY=RND(12)+1:IFB(RX,RY)<>0THEN20230
20260 B(RX,RY)=4:KS=KS+1:NEXT
20270 FORI=1TO9:B(I,1)=0:NEXT
20300 FORJ=6TO9:FORI=6TO9:B(I,J)=0:NEXT:NEXT
20999 RETURN
30000 REM
30001 REM PRINT START SCREEN
30002 REM
30010 CLS
30020 FORJ=0TO15:FORI=0TO15:D=B(I,J):IFD=0THEN30090
30040 PRINT@32*J+I,CH$(D);
30090 NEXT:NEXT
30100 FORI=0TOEN
30200 PRINT@32*PY+PX,CHR$(96);:PRINT@32*EY(I)+EX(I),"o";:FORJ=1TO100:NEXT
30210 PRINT@32*PY+PX,"@";:PRINT@32*EY(I)+EX(I),"O";:FORJ=1TO100:NEXT
30230 NEXT
30300 PRINT@32*3+22,"STAGE":PRINT@32*6+22,"SCORE":PRINT@32*9+22,"HIGH"
30310 PRINT@32*4+23,ST+1:PRINT@232+(16-LEN(STR$(SC))/2),SC:PRINT@328+(16-LEN(STR$(HS))/2),HS
30999 RETURN
50000 DATA 0,-1,1,0,0,1,-1,0
50100 DATA " ","#","/","2","*"
50110 DATA "O","@"
50200 DATA 1,0,3,2,3,2,1,0
