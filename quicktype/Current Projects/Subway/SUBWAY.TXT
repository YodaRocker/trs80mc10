100 CLS : KEY OFF
110 LOCATE 10, 1 : X$ = "SUBWAY SCAVENGER" : GOSUB 5530
120 LOCATE 13, 1 : X$ = "(C) BY DAVID H. AHL, 1986" : GOSUB 5530
130 LOCATE 23, 1 : GOSUB 5500 : GOSUB 1650
140 LOCATE 21, 1 : X$ = "(INITIALIZING DATA — PLEASE BE PATIENT)" : GOSUB 5530
150 DIM PN(20), PKGDES$(20), PKSTNU(20), PKGSTA(20, 5), PKSTDS(20, 5), LGPKG(20)
160 DIM DORP(20), STATION$(300), STANU(300), STATR(300, 6)
170 DIM TRAIN$(12), TRSTOP(12), TRSTA(12, 50), TRDES$(12, 2), DUM(20)
180 PS = 20 : STNS = 264 : TRNS = 11 : 'FOR READING DATA: PACKAGES, STATIONS, TRAINS
190 GOSUB 5660 : LGMAX = 10 : TKMAX = 20 : 'PACKAGES TO START
200 GOSUB 1780 : GOSUB 2050 : GOSUB 4760 : 'READ DATA INTO VARIABLES
210 WHILE RN > 32767 : RN = RN - 65635! : WEND : RANDOMIZE RN
220 GOSUB 5560 : STA = 21 : 'STARTING STATION
230 LOCATE 21, 1 : PRINT TAB(60) " " : GOSUB 5500 : PRINT
240 INPUT "DO YOU WANT TO BE ABLE TO DELIVER AFTER 5:00 PM (EASIER)";A$
250 GOSUB 5300 : IF A$ = "Y" THEN TM = 540 : TM$ = "6:00" : GOTO 270
260 TM = 480 : TM$ = "5 : 00"
270 FOR I = 1 TO 5 : 'INFORMATION ABOUT DELIVERIES
280 DORP(I) = 1 : NEXT I
290 FOR I = 6 TO 10 : 'INFORMATION ABOUT PICK-UPS
300 DORP(I) = 2 : NEXT I
310 CLS : PRINT "YOU MAY WANT TO PRINT OR COPY THIS SCREEN FOR LATER REFERENCE."
320 PRINT : GOSUB 5390 : 'PRINT PACKAGE LOG
330 '
340 'ARRIVE AT STATION ROUTINE
350 GOSUB 5140 : 'PRINT TIME
360 PRINT "YOU HAVE ARRIVED AT " STATION$(STA) "STATION."
370 PRINT "TRAINS THAT STOP AT THIS STATION:"
380 FOR I = 1 TO STANU(STA)
390 PRINT " " TRAIN$(STATR(STA, I)) : NEXT I
400 IF PERS = 0 THEN GOSUB 1100 : GOTO 530 : 'IF ON FOOT, BUY TOKEN
410 IF STA< >TRSTA(TR, 1) AND STA< >TRSTA(TR, TRSTOP(TR)) THEN 430
420 PRINT "END OF THE LINE. YOU'LL HAVE TO GET OFF." : GOTO 450
430 INPUT "DO YOU WANT TO GET OFF";A$ : GOSUB 5300
440 IF A$ = "N" THEN 690 : 'IF YOU WANT TO STAY ON TRAIN, BRANCH TO TRAIN TRAVEL
450 PERS = 0 : PRINT "DO YOU WANT TO:" : PRINT " MAKE A PICKUP (P)"
460 PRINT " MAKE A DELIVERY (D)" : PRINT " CHECK YOUR LOGBOOK (C)"
470 PRINT " GET ANOTHER TRAIN (T)"
480 INPUT "YOUR CHOICE PLEASE (P, D, C, OR T)";A$
490 GOSUB 5350 : IF A$ = "P" OR A$ = "D" THEN 750 ELSE IF A$ = "T" THEN 530
500 IF A$ = "C" THEN GOSUB 5390 : GOTO 450
510 INPUT "NOT A VALID CHOICE. ENTER P, D, C, OR T PLEASE.";A$ : GOTO 490
520 '
530 'TRAINS COMING ROUTINE
540 GOSUB 5140 : 'PRINT TIME
550 RN = INT(1 + STANU(STA) * RND(1)) : 'WHICH TRAIN IS COMING?
560 TR = STATR(STA, RN)
570 IF STA = TRSTA(TR, 1) THEN DES = 2 : GOTO 600 : 'AT ONE END OF LINE?
580 IF STA = TRSTA(TR, TRSTOP(TR)) THEN DES = 1 : GOTO 600 : 'OR THE OTHER?
590 DES = INT(1 + 2 * RND(1)) : 'DESTINATION?
600 PRINT "HERE COMES THE " TRAIN$(TR) " TRAIN TO" TRDES$(TR, DES)
610 MIN = MIN + 1
620 INPUT "DO YOU WANT TO GET ON";A$ : GOSUB 5300
630 IF A$ = "N" THEN 550 : 'IF DON'T GET ON TRAIN, WAIT FOR NEXT ONE
640 FOR I = 1 TO TRSTOP(TR) : 'FIND OUT WHERE TRAIN IS
650 IF TRSTA(TR, I)< >STA THEN NEXT I : PRINT "ERROR AT LINE 465"
660 TRSTX = I : 'TRAIN STATION IDENTIFICATION INDEX
670 IF DES = 1 THEN TRX = -1 ELSE TRX = 1
680 '
690 'TRAIN TRAVEL ROUTINE
700 PERS = 1 : PRINT "YOU ARE ON THE " TRAIN$(TR) " TRAIN TO " TRDES$(TR, DES)
710 GOSUB 1210 : 'POSSIBLE TRIP HAZARDS
720 TRSTX = TRSTX + TRX : STA = TRSTA(TR, TRSTX) : MIN = MIN + INT(2 + 1.3 * RND(1))
730 GOTO 340 : 'GO TO NEXT STATION ROUTINE
740 '
750 'PICKUP AND DELIVERY ROUTINE
760 IF A$ = "P" THEN X$ = "PICKUP" ELSE X$ = "DELIVERY"
770 PRINT "WHICH " X$; : INPUT " DO YOU WANT TO MAKE (BY LOGBOOK NUMBER)";A
780 IF DORP(A)< >0 THEN 820
790 INPUT "THAT NUMBER SEEMS TO BE IN ERROR. WANT TO CHECK YOUR LOGBOOK";A$
800 GOSUB 5300 : IF A$ = "Y" THEN GOSUB 5390
810 GOTO 450
820 PRINT "THAT " X$ " IS AT " PKGDES$(A)
830 FOR I = 1 TO PKSTNU(A)
840 IF PKGSTA(A, I) = STA THEN 870
850 NEXT I : PRINT "WHICH IS TOO FAR TO WALK FROM THIS STATION."
860 PRINT "PERHAPS YOU SHOULD TRY SOMETHING ELSE." : GOTO 450
870 IF PKSTDS(A, I) > 1 THEN X$ = "S" ELSE X$ = ""
880 PRINT "WHICH IS" PKSTDS(A, I) "BLOCK" X$ " FROM HERE. OFF YOU GO."
890 '
900 'SUCCESSFUL PICKUP OR DELIVERY
910 MIN = MIN + 2 * PKSTDS(A, I) + 6 : 'ADD TO TIME (2 MIN PER BLOCK, 6 AT DESTINATION)
920 DELTOT = DELTOT + 1 : IF DORP(A) = 2 THEN 950 : 'IS THIS A PICK-UP?
930 PRINT : PRINT "YOU FIND SOMEONE TO SIGN FOR THE PACKAGE."
940 DORP(A) = 0 : GOTO 990 : 'MARK DELIVERY COMPLETED
950 LGMAX = LGMAX + 1 : PRINT "YOU PICK UP A PACKAGE AND LOG IT IN AS NO." LGMAX
960 PRINT "THE ADDRESS ON IT IS " PKGDES$(LGMAX)
970 DORP(A) = 0 : DORP(LGMAX) = 1 : GOTO 1020
980 '
990 'CHECK IF ALL PICKUPS AND DELIVERIES MADE
1000 IF DELTOT = 15 THEN 1580 : 'HAVE ALL DELIVERIES BEEN MADE?
1010 IF PKSTNU(A) = 1 THEN X$ = "" ELSE X$ = "S"
1020 PRINT : "FROM HERE YOU CAN WALK TO THE FOLLOWING SUBWAY STATION" X$ " : "
1030 IF PKSTNU(A) = 1 THEN PRINT " " STATION$(PKGSTA(A, 1)) : GOTO 340
1040 FOR I = 1 TO PKSTNU(A) : 'ITERATE THROUGH POSSIBLE STATIONS
1050 PRINT " " I " — " STATION$(PKGSTA(A, I)) : NEXT I
1060 INPUT "WHICH STATION DO YOU WANT TO GO TO (BY NUMBER)";B
1070 IF B < 1 OR B > PKSTNU(A) THEN PRINT "NOT A VALID RESPONSE." : GOTO 1060
1080 STA = PKGSTA(A, B) : MIN = MIN + 3 + PKSTDS(A, B) : GOTO 340
1090 '
1100 'BUY TOKEN SUBROUTINE
1110 TOKEN = TOKEN + 1 : IF TOKEN <= TKMAX THEN 1190
1120 PRINT : PRINT "YOU HAVE SPENT THE ENTIRE $20 YOUR BOSS GAVE YOU ON TOKENS."
1130 IF TK = 0 THEN 1150 : 'USED OWN MONEY YET?
1140 PRINT "MOREOVER, YOU HAVE USED UP YOUR OWN MONEY AS WELL." : GOTO 1520
1150 TK = 1 : INPUT "DO YOU WANT TO BUY TOKENS WITH YOUR OWN MONEY";A$ : GOSUB 5300
1160 IF A$"N" THEN PRINT "OKAY, THAT'S IT THEN." : GOTO 1520
1170 RN = INT(300 + 600 * RND(1)) / 100 : PRINT "YOU HAVE EXACTLY $" RN;
1180 PRINT "SO YOU CAN BUY" INT(RN) "MORE TOKENS." : TKMAX = TKMAX + INT(RN)
1190 RETURN
1200 '
1210 'TRIP HAZARDS SUBROUTINE
1220 'DOOR REFUSES TO CLOSE
1230 IF RND(1) > .05 THEN 1290 : '5% CHANCE OF A STICKY DOOR
1240 PRINT "ONE OF THE CAR DOORS REFUSES TO CLOSE AND THE TRAIN CAN'T MOVE."
1250 RN = INT(1 + 2.5 * RND(1)) : MIN = MIN + RN : IF RN > 1 THEN X$ = "S" ELSE X$ = ""
1260 PRINT "YOU'RE STUCK HERE FOR" RN "MINUTE" X$ "."
1270 '
1280 'POSSIBLE MUGGING
1290 IF RND(1) > .35 THEN 1430 : '35 - 65% CHANCE OF MUGGING OR FIRE ON THE TRACKS
1300 IF RND(1) > .05 THEN RETURN : '5% CHANCE OF TOUGH CHARACTERS
1310 PRINT "SOME REAL UNSAVORY TYPES ARE WHOOPING IT UP IN THE CAR ACROSS FROM"
1320 INPUT "YOUR SEAT. DO YOU WANT TO MOVE TO ANOTHER CAR";A$ : GOSUB 5300
1330 IF A$ = "Y" THEN IF RND(1) > .05 THEN 1350 ELSE GOTO 1370
1340 IF RND(1) > .05 THEN 1360 ELSE GOTO 1380
1350 PRINT "THEY JEER AT YOU BUT LET YOU PASS. ALL IS OKAY...FOR NOW." : RETURN
1360 PRINT "THEY LOOK AT YOU AND TRY TO BAIT YOU, BUT YOU AVOID THEM." : RETURN
1370 PRINT "UH OH. TWO OF THEM GET UP AND BLOCK YOUR WAY."
1380 PRINT "OH MY, OH MY. THEY'RE ALL MOVING TO SURROUND YOU."
1390 PRINT "THEY PULL KNIVES AND DEMAND YOUR MONEY." : GOSUB 5470
1400 PRINT "YOU, DECIDING THAT DISCRETION IS THE BETTER PART OF VALOR, GIVE"
1410 PRINT "THEM ALL YOUR MONEY AND CALL IT QUITS FOR THE DAY." : GOTO 1520
1420 '
1430 'FIRE ON THE TRACK
1440 IF RND(1) > 8.000001E - 03 THEN RETURN : '0.8% CHANCE OF FIRE ON THE TRACKS
1450 PRINT "UH OH. THE TRAIN IS SLOWING DOWN AND SEEMS TO BE STOPPING."
1460 GOSUB 5470 : PRINT "YOU'RE STUCK HERE IN THE TUNNEL." : GOSUB 5470
1470 PRINT "A TRAINMAN FINALLY COMES THROUGH AND ANNOUNCES, 'IT'S JUST A"
1480 RN = INT(10 + 35 * RND(1)) : MIN = MIN + RN
1490 PRINT "FIRE ON THE TRACKS FOLKS. WE'LL BE UNDER WAY IN A FEW MINUTES.'"
1500 PRINT "IN FACT, THE DELAY IS MORE LIKE" RN "MINUTES!" : RETURN
1510 '
1520 'END OF GAME ROUTINE
1530 IF DELTOT = 15 THEN 1580 : 'WERE ALL DELIVERIES MADE?
1540 PRINT : PRINT "YOU MADE IT TO" DELTOT "LOCATIONS, BUT"
1550 PRINT "YOUR LOG STILL SHOWS THE FOLLOWING ITEMS:" : GOSUB 5390
1560 GOSUB 5250 : PRINT "PERHAPS YOU'LL BE ABLE TO DO BETTER TOMORROW."
1570 GOTO 1610
1580 GOSUB 5250 : PRINT : PRINT TAB(25) "CONGRATULATIONS !" : PRINT
1590 PRINT "YOU MADE ALL YOUR DELIVERIES AND PICK-UPS SUCCESSFULLY IN THE"
1600 PRINT "LARGEST CITY IN THE WORLD. VERY GOOD!"
1610 PRINT "YOU USED $" TOKEN " FOR TOKENS."
1620 PRINT : INPUT "WOULD YOU LIKE TO TRY AGAIN";A$ : GOSUB 5300
1630 IF A$ = "Y" THEN RUN ELSE CLS : KEY ON : PRINT "BYE FOR NOW." : END
1640 '
1650 CLS : X$ = "SUBWAY SCAVENGER" : GOSUB 5530 : PRINT : PRINT
1660 PRINT " YOU HAVE A JOB WITH A MESSENGER/COURIER SERVICE LOCATED IN"
1670 PRINT "MID-TOWN MANHATTAN. TODAY, YOU HAVE FIVE PACKAGES TO DELIVER AND"
1680 PRINT "FIVE PACKAGES TO PICK UP FOR DELIVERY TO OTHER LOCATIONS IN THE"
1690 PRINT "CITY. SO, IN TOTAL YOU MUST VISIT 15 DIFFERENT LOCATIONS." : PRINT
1700 PRINT " YOU CAN USE 264 STATIONS OF THE NEW YORK SUBWAY SYSTEM WHICH"
1710 PRINT "ARE SERVICED BY THE FOLLOWING 11 TRAINS: A, B, CG, D, E, F, N, 1,"
1720 PRINT "2, 4, AND 7." : PRINT
1730 PRINT " YOU MUST COMPLETE ALL YOUR DELIVERIES AND PICKUPS BY 5:00 PM."
1740 PRINT "YOUR BOSS HAS GIVEN YOU $20 FOR TOKENS (WHICH WILL ALLOW FOR A FEW"
1750 PRINT "WRONG TRAINS). ANY MONEY THAT YOU DON'T USE ON TOKENS IS YOURS TO"
1760 PRINT "KEEP. GOOD LUCK! (YOU'LL NEED IT.)" : RETURN
1770 '
1780 'SUBROUTINE TO READ DATA ABOUT PACKAGE DELIVERIES
1790 FOR I = 1 TO PS
1800 READ PN(I), PKGDES$(I), PKSTNU(I)
1810 FOR J = 1 TO PKSTNU(I)
1820 READ PKGSTA(I, J), PKSTDS(I, J)
1830 NEXT J : NEXT I : RETURN
1840 DATA 1, "CURATOR, MUSEUM OF NATURAL HISTORY", 1, 17, 1
1850 DATA 2, "GEORGE WASHINGTON BRIDGE BUS TERMINAL", 1, 5, 1
1860 DATA 3, "WEST SIDE TENNIS CLUB, FOREST HILLS", 1, 75, 4
1870 DATA 4, "NATHAN'S AT CONEY ISLAND AMUSEMENT PARK", 1, 95, 2
1880 DATA 5, "BIG AL'S DISCOUNT MART, ROCKAWAY BLVD, WOODHAVEN", 3, 50, 1, 49, 9, 51, 9
1890 DATA 6, "APOLLO THEATER, 125TH ST, HARLEM", 2, 11, 1, 12, 9
1900 DATA 7, "MET'S DUGOUT, SHEA STADIUM", 1, 260, 3
1910 DATA 8, "PRESS ROOM, YANKEE STADIUM", 1, 246, 3
1920 DATA 9, "LION KEEPER, BRONX ZOO", 2, 204, 5, 205, 8
1930 DATA 10, "BOROUGH HALL, BROOKLYN", 2, 32, 1, 221, 2
1940 DATA 11, "BROOKLYN ACADEMY OF MUSIC", 1, 67, 2
1950 DATA 12, "REGISTRAR, BROOKLYN COLLEGE, FLATBUSH", 1, 234, 1
1960 DATA 13, "COMPUTER SCIENCE DEPT, NYU, WASHINGTON SQ", 1, 25, 3
1970 DATA 14, "NY BOTANICAL GARDENS", 1, 55, 4
1980 DATA 15, "WINDOWS ON THE WORLD, WORLD TRADE CENTER", 3, 29, 1, 28, 5, 191, 1
1990 DATA 16, "METROPOLITAN MUSEUM OF ART", 1, 249, 1
2000 DATA 17, "COMPUTER EDUCATION DEPT, COLUMBIA UNIV.", 2, 174, 2, 175, 8
2010 DATA 18, "ALICE TULLY HALL, LINCOLN CENTER", 1, 181, 2
2020 DATA 19, "NEW YORK STOCK EXCHANGE", 2, 219, 2, 252, 2
2030 DATA 20, "LIN CHOWS, MOTT ST, CHINATOWN", 2, 65, 4, 146, 4
2040 '
2050 'SUBROUTINE TO READ DATA ABOUT SUBWAY STATIONS
2060 FOR I = 1 TO STNS : 'STNS = NUMBER OF SUBWAY STATIONS
2070 READ STA, STATION$(I), STANU(I)
2080 FOR J = 1 TO STANU(I)
2090 READ STATR(I, J) : 'READ TRAIN NUMBERS THAT STOP AT STATION
2100 NEXT J : NEXT I : RETURN
2110 DATA 1, "207 ST/BDWY/WASH HTS (MANHATTAN)", 1, 1
2120 DATA 2, "DYCKMAN ST/BDWY", 1, 1
2130 DATA 3, "190 ST/FT WASH AV", 1, 1
2140 DATA 4, "181 ST/FT WASH AV", 1, 1
2150 DATA 5, "175 ST/GW BRIDGE", 1, 1
2160 DATA 6, "168 ST/BDWY (MANHATTAN)", 3, 1, 3, 6
2170 DATA 7, "163 ST/AMSTERDAM AV", 1, 3
2180 DATA 8, "155 ST/ST NICHOLAS AV", 1, 3
2190 DATA 9, "145 ST/ST NICHOLAS AV", 3, 1, 3, 4
2200 DATA 10, "135 ST/ST NICHOLAS AV", 1, 3
2210 DATA 11, "125 ST/ST NICHOLAS AV", 3, 1, 3, 4
2220 DATA 12, "116 ST/8 AV", 1, 3
2230 DATA 13, "110 ST/CATHEDRAL PKWY", 1, 3
2240 DATA 14, "103 ST/CENTRAL PK W", 1, 3
2250 DATA 15, "96 ST/CENTRAL PK W", 1, 3
2260 DATA 16, "86 ST/CENTRAL PK W", 1, 3
2270 DATA 17, "81 ST/MUSEUM NATL HISTORY", 1, 3
2280 DATA 18, "72 ST/CENTRAL PK W", 1, 3
2290 DATA 19, "59 ST/COLUMBUS CIRCLE", 4, 1, 3, 4, 6
2300 DATA 20, "50 ST/8 AV", 1, 2
2310 DATA 21, "42 ST/8 AV", 2, 1, 2
2320 DATA 22, "34 ST/PENN STATION", 2, 1, 2
2330 DATA 23, "23 ST/8 AV", 1, 2
2340 DATA 24, "14 ST/8 AV", 2, 1, 2
2350 DATA 25, "W 4 ST/WASHINGTON SQ", 5, 1, 2, 3, 4, 5
2360 DATA 26, "SPRING ST/6 AV", 1, 2
2370 DATA 27, "CANAL ST/6 AV", 2, 1, 2
2380 DATA 28, "CHAMBERS ST/CHURCH ST", 3, 1, 2, 7
2390 DATA 29, "WORLD TRADE CENTER", 1, 2
2400 DATA 30, "BDWY/NASSAU ST/FULTON ST (MANHATTAN)", 3, 1, 7, 8
2410 DATA 31, "HIGH ST/BROOKLYN BR (BKLYN)", 1, 1
2420 DATA 32, "JAY ST/BOROUGH HALL", 2, 1, 5
2430 DATA 33, "HOYT ST", 2, 1, 11
2440 DATA 34, "LAFAYETTE AV", 1, 11
2450 DATA 35, "CLINTON AV", 1, 11
2460 DATA 36, "FRANKLIN AV", 1, 11
2470 DATA 37, "NOSTRAND AV", 1, 1
2480 DATA 38, "KINGSTON AV", 1, 11
2490 DATA 39, "UTICA AV", 1, 1
2500 DATA 40, "RALPH AV", 1, 11
2510 DATA 41, "ROCKAWAY AV", 1, 11
2520 DATA 42, "BDWY, E NY", 1, 11
2530 DATA 43, "LIBERTY AV", 1, 11
2540 DATA 44, "VAN SICIEN AV", 1, 11
2550 DATA 45, "SHEPHERD AV", 1, 11
2560 DATA 46, "EUCLID AV", 1, 1
2570 DATA 47, "GRANT AV (BROOKLYN)", 1, 1
2580 DATA 48, "80 ST/LIBERTY AV (QUEENS)", 1, 1
2590 DATA 49, "88 ST/LIBERTY AV", 1, 1
2600 DATA 50, "ROCKAWAY BLVD", 1, 1
2610 DATA 51, "104 ST/LIBERTY AV", 1, 1
2620 DATA 52, "111 ST/LIBERTY AV", 1, 1
2630 DATA 53, "LEFFERTS BLVD (QUEENS)", 1, 1
2640 DATA 54, "205 ST/BAINBRIDGE AV (BRONX)", 1, 4
2650 DATA 55, "BEDFORD PK BLVD (NY BOTANICAL GARDEN)", 1, 4
2660 DATA 56, "KINGSBRIDGE RD", 1, 4
2670 DATA 57, "FORDHAM RD", 1, 4
2680 DATA 58, "TREMONT AV", 1, 4
2690 DATA 59, "47-50 ST/ROCKEFELLER CENTER", 3, 3, 4, 5
2700 DATA 60, "42 ST/AV AMERICAS", 4, 3, 4, 5, 9
2710 DATA 61, "34 ST/HERALD SQ", 4, 3, 4, 5, 10
2720 DATA 62, "23 ST/AV AMERICAS", 1, 5
2730 DATA 63, "14 ST/AV AMERICAS", 3, 5, 6, 7
2740 DATA 64, "BDWY/LAFAYETTE ST", 4, 3, 4, 5, 8
2750 DATA 65, "GRAND ST (MANHATTAN)", 2, 3, 4
2760 DATA 66, "DEKALB AV/FLATBUSH AV (BKLYN)", 2, 4, 10
2770 DATA 67, "ATLANTIC AV/PACIFIC ST/BAM", 5, 3, 4, 7, 8, 10
2780 DATA 68, "179 ST/HILLSIDE AV (QUEENS)", 2, 2, 5
2790 DATA 69, "169 ST", 1, 2
2800 DATA 70, "PARSONS BLVD", 2, 2, 5
2810 DATA 71, "SUTPHIN AV", 1, 2
2820 DATA 72, "VAN WYCK BLVD", 1, 2
2830 DATA 73, "UNION TPK", 2, 2, 5
2840 DATA 74, "75 AV", 1, 2
2850 DATA 75, "71 AV/CONTINENTAL AV/FOREST HILLS", 4, 2, 5, 10, 11
2860 DATA 76, "ROOSEVELT AV", 5, 2, 5, 9, 10, 11
2870 DATA 77, "QUEENS PLAZA", 4, 2, 5, 10, 11
2880 DATA 78, "23 ST/ELY AV (QUEENS)", 2, 2, 5
2890 DATA 79, "LEXINGTON AV (MANHATTAN)", 2, 2, 5
2900 DATA 80, "5TH AV/53 ST", 2, 2, 5
2910 DATA 81, "7TH AV/53 ST", 3, 2, 3, 4
2920 DATA 82, "36 ST/4 AV", 2, 3, 10
2930 DATA 83, "9 AV/39 ST", 1, 3
2940 DATA 84, "FT HAMILTON PKY", 1, 3
2950 DATA 85, "50 ST/NEW UTRECHT AV", 1, 3
2960 DATA 86, "55 ST/NEW UTRECHT AV", 1, 3
2970 DATA 87, "62 ST/NEW UTRECHT AV", 2, 3, 10
2980 DATA 88, "71 ST/NEW UTRECHT AV", 1, 3
2990 DATA 89, "79 ST/NEW UTRECHT AV", 1, 3
3000 DATA 90, "18 AV/NEW UTRECHT AV", 1, 3
3010 DATA 91, "20 AV/86 ST", 1, 3
3020 DATA 92, "BAY PKY/86 ST", 1, 3
3030 DATA 93, "25 AV/86 ST", 1, 3
3040 DATA 94, "BAY 50 ST", 1, 3
3050 DATA 95, "CONEY ISLAND/SURF AV (BKLYN)", 4, 3, 4, 5, 10
3060 DATA 96, "67 AV/QUEENS BLVD", 2, 10, 11
3070 DATA 97, "63 DR/QUEENS BLVD", 2, 10, 11
3080 DATA 98, "WOODHAVEN BLVD", 2, 10, 11
3090 DATA 99, "GRAND AV/QUEENS BLVD", 2, 10, 11
3100 DATA 100, "ELMHURST AV", 2, 10, 11
3110 DATA 101, "65 ST/BDWY", 2, 10, 11
3120 DATA 102, "NORTHERN BLVD", 2, 10, 11
3130 DATA 103, "46 ST/BDWY", 2, 10, 11
3140 DATA 104, "STEINWAY ST", 2, 10, 11
3150 DATA 105, "2 AV/HOUSTON ST", 1, 5
3160 DATA 106, "DELANCEY ST", 1, 5
3170 DATA 107, "EAST BDWY (MANHATTAN)", 1, 5
3180 DATA 108, "YORK ST/JAY ST (BROOKLYN)", 1, 5
3190 DATA 109, "BERGEN ST", 2, 5, 11
3200 DATA 110, "CARROLL ST", 2, 5, 11
3210 DATA 111, "SMITH ST", 2, 5, 11
3220 DATA 112, "4 AV/9 ST", 2, 5, 10
3230 DATA 113, "7 AV/9 ST", 1, 5
3240 DATA 114, "15 ST/PROSPECT PARK", 1, 5
3250 DATA 115, "FT HAMILTON PWY", 1, 5
3260 DATA 116, "CHURCH AV", 1, 5
3270 DATA 117, "DITMAS AV", 1, 5
3280 DATA 118, "18 AV/MCDONALD AV", 1, 5
3290 DATA 119, "KINGS HWY", 1, 5
3300 DATA 120, "AVENUE U", 1, 5
3310 DATA 121, "AVENUE X", 1, 5
3320 DATA 122, "NEPTUNE AV", 1, 5
3330 DATA 123, "W 8TH/NY AQUARIUM", 1, 5
3340 DATA 124, "7 AV/FLATBUSH AV", 1, 4
3350 DATA 125, "PROSPECT PARK", 1, 4
3360 DATA 126, "CHURCH AV/E 18 ST", 1, 4
3370 DATA 127, "NEWKIRK AV", 1, 4
3380 DATA 128, "KINGS HWY/E 16 ST", 1, 4
3390 DATA 129, "SHEEPSHEAD BAY", 1, 4
3400 DATA 130, "BRIGHTON BEACH", 1, 4
3410 DATA 131, "COURT SQUARE", 1, 11
3420 DATA 132, "21 ST/JACKSON AV (QUEENS)", 1, 11
3430 DATA 133, "GREENPOINT AV (BKLYN)", 1, 11
3440 DATA 134, "NASSAU AV", 1, 11
3450 DATA 135, "METROPOLITAN AV", 1, 11
3460 DATA 136, "BROADWAY/UNION AV", 1, 11
3470 DATA 137, "FLUSHING-MARCY AVS", 1, 11
3480 DATA 138, "MYRTLE-WILLOUGHBY AVS", 1, 11
3490 DATA 139, "BEDFORD-NOSTRAND AVS", 1, 11
3500 DATA 140, "36 ST/NORTHERN BLVD", 2, 10, 11
3510 DATA 141, "LEXINGTON AV/59-60 STS (MANHATTAN)", 2, 8, 10
3520 DATA 142, "5TH AV/59-60 STS", 1, 10
3530 DATA 143, "57 ST/7 AV", 1, 10
3540 DATA 144, "TIMES SQ/42 ST/BDWY", 4, 6, 7, 9, 10
3550 DATA 145, "UNION SQ/14 ST", 2, 8, 10
3560 DATA 146, "CANAL ST/BDWY (MANHATTAN)", 1, 10
3570 DATA 147, "UNION ST/4 AV", 1, 10
3580 DATA 148, "PROSPECT AV", 1, 10
3590 DATA 149, "25 ST/4 AV", 1, 10
3600 DATA 150, "45 ST/4 AV", 1, 10
3610 DATA 151, "53 ST/4 AV", 1, 10
3620 DATA 152, "59 ST/4 AV", 1, 10
3630 DATA 153, "8 AV/62 ST", 1, 10
3640 DATA 154, "FT HAMILTON PWY", 1, 10
3650 DATA 155, "18 AV/64 ST", 1, 10
3660 DATA 156, "20 AV/64 ST", 1, 10
3670 DATA 157, "BAY PWY/AV O", 1, 10
3680 DATA 158, "KINGS HWY/W 7 ST", 1, 10
3690 DATA 159, "AVENUE U/W 7 ST", 1, 10
3700 DATA 160, "86 ST/W 7 ST", 1, 10
3710 DATA 161, "242 ST/VAN CORTLANDT PARK", 1, 6
3720 DATA 162, "238 ST/BDWY", 1, 6
3730 DATA 163, "231 ST/BDWY (BRONX)", 1, 6
3740 DATA 164, "225 ST/BDWY (MANHATTAN)", 1, 6
3750 DATA 165, "215 ST/10 AV", 1, 6
3760 DATA 166, "207 ST/10 AV", 1, 6
3770 DATA 167, "DYCKMAN AV", 1, 6
3780 DATA 168, "191 ST/ST NICHOLAS AV", 1, 6
3790 DATA 169, "181 ST/ST NICHOLAS AV", 1, 6
3800 DATA 170, "157 ST/BDWY", 1, 6
3810 DATA 171, "145 ST/BDWY", 1, 6
3820 DATA 172, "137 ST/BDWY", 1, 6
3830 DATA 173, "125 ST/BDWY", 1, 6
3840 DATA 174, "116 ST/BDWY/COLUMBIA UNIV", 1, 6
3850 DATA 175, "110 ST/CATHEDRAL PKWY", 1, 6
3860 DATA 176, "103 ST/BDWY", 1, 6
3870 DATA 177, "96 ST/BDWY", 2, 6, 7
3880 DATA 178, "86 ST/BDWY", 1, 6
3890 DATA 179, "79 ST/BDWY", 1, 6
3900 DATA 180, "72 ST/BDWY", 2, 6, 7
3910 DATA 181, "66 ST/BDWY/LINCOLN CENTER", 1, 6
3920 DATA 182, "50 ST/BDWY", 1, 6
3930 DATA 183, "PENN STATION/34 ST/7 AV", 2, 6, 7
3940 DATA 184, "28 ST/7 AV", 1, 6
3950 DATA 185, "23 ST/7 AV", 1, 6
3960 DATA 186, "18 ST/7 AV", 1, 6
3970 DATA 187, "CHRISTOPHER ST", 1, 6
3980 DATA 188, "HOUSTON ST", 1, 6
3990 DATA 189, "CANAL & VARICK STS", 1, 6
4000 DATA 190, "FRANKLIN ST", 1, 6
4010 DATA 191, "CHAMBERS ST/W BDWY", 2, 6, 7
4020 DATA 192, "CORTLANDT ST/WORLD TRADE CENTER", 1, 6
4030 DATA 193, "RECTOR ST/GREENWICH ST", 1, 6
4040 DATA 194, "SOUTH FERRY/BATTERY PARK", 1, 6
4050 DATA 195, "241 ST/WHITE PLAINS RD (BRONX)", 1, 7
4060 DATA 196, "238 ST/WHITE PLAINS RD", 1, 7
4070 DATA 197, "233 ST/WHITE PLAINS RD", 1, 7
4080 DATA 198, "225 ST/WHITE PLAINS RD", 1, 7
4090 DATA 199, "219 ST/WHITE PLAINS RD", 1, 7
4100 DATA 200, "GUN HILL RD/WHITE PLAINS RD", 1, 7
4110 DATA 201, "BURKE AV/WHITE PLAINS RD", 1, 7
4120 DATA 202, "ALLERTON AV/WHITE PLAINS RD", 1, 7
4130 DATA 203, "PELHAM PKWY/WHITE PLAINS RD", 1, 7
4140 DATA 204, "BRONX PK E/WHITE PLAINS RD", 1, 7
4150 DATA 205, "E 180 ST/BRONX ZOO", 1, 7
4160 DATA 206, "E TREMONT AV/BOSTON RD", 1, 7
4170 DATA 207, "174 ST/SOUTHERN BLVD", 1, 7
4180 DATA 208, "FREEMAN ST", 1, 7
4190 DATA 209, "SIMPSON ST", 1, 7
4200 DATA 210, "INTERVALE AV", 1, 7
4210 DATA 211, "PROSPECT AV", 1, 7
4220 DATA 212, "JACKSON AV", 1, 7
4230 DATA 213, "3 AV/149 ST", 1, 7
4240 DATA 214, "149 ST/GRAND CONCOURSE (BRONX)", 2, 7, 8
4250 DATA 215, "135 ST/LENOX AV (MANHATTAN)", 1, 7
4260 DATA 216, "125 ST/LENOX AV", 1, 7
4270 DATA 217, "116 ST/LENOX AV", 1, 7
4280 DATA 218, "110 ST/LENOX AV", 1, 7
4290 DATA 219, "WALL ST (MANHATTAN)", 1, 7
4300 DATA 220, "CLARK ST (BROOKLYN)", 1, 7
4310 DATA 221, "BOROUGH HALL/COURT ST (BKLYN)", 2, 7, 8
4320 DATA 222, "HOYT ST/FULTON ST", 1, 7
4330 DATA 223, "NEVINS ST", 2, 7, 8
4340 DATA 224, "BERGEN ST", 1, 7
4350 DATA 225, "GRAND ARMY PLAZA, PROSPECT PARK", 1, 7
4360 DATA 226, "EASTERN PKWY/BROOKLYN MUSEUM", 1, 7
4370 DATA 227, "FRANKLIN AV/EASTERN PKWY", 1, 7
4380 DATA 228, "PRESIDENT ST", 1, 7
4390 DATA 229, "STERLING ST/NOSTRAND AV", 1, 7
4400 DATA 230, "WINTHROP ST/NOSTRAND AV", 1, 7
4410 DATA 231, "CHURCH AV/NOSTRAND AV", 1, 7
4420 DATA 232, "BEVERLEY RD/NOSTRAND AV", 1, 7
4430 DATA 233, "NEWKIRK AV/NOSTRAND AV", 1, 7
4440 DATA 234, "FLATBUSH AV/BKLYN COLLEGE", 1, 7
4450 DATA 235, "WOODLAWN/JEROME AV (BRONX)", 1, 8
4460 DATA 236, "MOSHOLU PKWY", 1, 8
4470 DATA 237, "BEDFORD PARK BLVD", 1, 8
4480 DATA 238, "KINGSBRIDGE RD", 1, 8
4490 DATA 239, "FORDHAM RD/JEROME AV", 1, 8
4500 DATA 240, "183 ST/JEROME AV", 1, 8
4510 DATA 241, "BURNSIDE AV/JEROME AV", 1, 8
4520 DATA 242, "176 ST/JEROME AV", 1, 8
4530 DATA 243, "MT EDEN AV/JEROME AV", 1, 8
4540 DATA 244, "170 ST/JEROME AV", 1, 8
4550 DATA 245, "167 ST/RIVER AV", 1, 8
4560 DATA 246, "161 ST/YANKEE STADIUM (BRONX)", 2, 4, 8
4570 DATA 247, "", 1, 11
4580 DATA 248, "125 ST/LEXINGTON AV (MANHATTAN)", 1, 8
4590 DATA 249, "86 ST/LEXINGTON AV/METROPOLITAN MUSEUM", 1, 8
4600 DATA 250, "42 ST/GRAND CENTRAL STA", 2, 8, 9
4610 DATA 251, "BKLYN BRIDGE/WORTH ST", 1, 8
4620 DATA 252, "WALL ST/BDWY", 1, 8
4630 DATA 253, "BOWLING GREEN (MANHATTAN)", 1, 8
4640 DATA 254, "NOSTRAND AV/EASTERN PKWY", 1, 8
4650 DATA 255, "ROCKAWAY AV/LIVONIA AV", 1, 8
4660 DATA 256, "NEW LOTS AV (BROOKLYN)", 1, 8
4670 DATA 257, "QUEENSBORO PLAZA (QUEENS)", 1, 9
4680 DATA 258, "61 ST/ROOSEVELT AV", 1, 9
4690 DATA 259, "JUNCTION BLVD", 1, 9
4700 DATA 260, "WILLETS POINT/SHEA STADIUM", 1, 9
4710 DATA 261, "MAIN ST/FLUSHING (QUEENS)", 1, 9
4720 DATA 262, "CLASSON AV", 1, 11
4730 DATA 263, "CLINTON-WASHINGTON AVS", 1, 11
4740 DATA 264, "FULTON ST/LAFAYETTE AV", 1, 11
4750 '
4760 'SUBROUTINE TO READ DATA ABOUT SUBWAY TRAINS
4770 FOR I = 1 TO TRNS : 'TRNS = NUMBER OF SUBWAY TRAINS
4780 READ TR, TRAIN$(I), TRSTOP(I)
4790 FOR J = 1 TO TRSTOP(I)
4800 READ TRSTA(I, J) : 'READ STATION NUMBERS FOR A TRAIN
4810 NEXT J
4820 TRDES$(TR, 1) = STATION$(TRSTA(TR, 1)) : 'STATION NAME AT NORTH OR WEST END
4830 TRDES$(TR, 2) = STATION$(TRSTA(TR, TRSTOP(TR))) : 'STATION AT SOUTH OR EAST END
4840 NEXT I : RETURN
4850 DATA 1, "A - 8 AV EXPRESS", 29, 1, 2, 3, 4, 5, 6, 9, 11, 19, 21, 22, 24, 25, 27, 28
4860 DATA 30, 31, 32, 33, 37, 39, 46, 47, 48, 49, 50, 51, 52, 53
4870 DATA 2, "E - 8 AV LOCAL", 24, 68, 69, 70, 71, 72, 73, 74, 75, 76, 77, 78, 79, 80, 81, 20, 21
4880 DATA 22, 23, 24, 25, 26, 27, 28, 29
4890 DATA 3, "B - AV AMERICAS EXPRESS", 36, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15, 16, 17, 18
4900 DATA 19, 81, 59, 60, 61, 25, 64, 65, 67, 82, 83, 84, 85, 86, 87, 88, 89, 90, 91, 92, 93, 94, 95
4910 DATA 4, "D - AV AMERICAS EXPRESS", 26, 54, 55, 56, 57, 58, 246, 9, 11, 19, 81, 59, 60
4920 DATA 61, 25, 64, 65, 66, 67, 124, 125, 126, 127, 128, 129, 130, 95
4930 DATA 5, "F - AV AMERICAS LOCAL", 37, 68, 70, 73, 75, 76, 77, 78, 79, 80
4940 DATA 59, 60, 61, 62, 63, 25, 64, 105, 106, 107, 108, 32, 109, 110
4950 DATA 111, 112, 113, 114, 115, 116, 117, 118, 119, 120, 121, 122, 123, 95
4960 DATA 6, "1 - BDWY-7TH AV LOCAL", 38, 161, 162, 163, 164, 165, 166, 167, 168, 169, 6
4970 DATA 170, 171, 172, 173, 174, 175, 176, 177, 178, 179, 180, 181, 19, 182, 144, 183, 184
4980 DATA 185, 186, 63, 187, 188, 189, 190, 191, 192, 193, 194
4990 DATA 7, "2 - 7TH AV EXPRESS", 49, 195, 196, 197, 198, 199, 200, 201, 202, 203, 204
5000 DATA 205, 206, 207, 208, 209, 210, 211, 212, 213, 214, 215, 216, 217, 218, 177, 180, 144
5010 DATA 183, 63, 191, 28, 30, 219, 220, 221, 222, 223, 67, 224, 225, 226, 227, 228, 229, 230
5020 DATA 231, 232, 233, 234
5030 DATA 8, "4 - LEXINGTON AV EXPRESS", 29, 235, 236, 237, 238, 239, 240, 241, 242, 243
5040 DATA 244, 245, 246, 214, 248, 249, 141, 250, 145, 64, 251, 30, 252, 253, 221, 223, 67, 254
5050 DATA 255, 256
5060 DATA 9, "7 - FLUSHING EXPRESS", 9, 144, 60, 250, 257, 258, 76, 259, 260, 261
5070 DATA 10, "N - BROADWAY EXPRESS", 40, 75, 96, 97, 98, 99, 100, 76, 101, 102, 103, 104
5080 DATA 140, 77, 141, 142, 143, 144, 61, 145, 146, 66, 67, 147, 112, 148, 149, 82, 150, 151
5090 DATA 152, 153, 154, 87, 155, 156, 157, 158, 159, 160, 95
5100 DATA 11, "CG - BKLYN/QUEENS CROSSTOWN", 29, 75, 96, 97, 98, 99, 100, 76, 101, 102,
5110 DATA 103, 104, 140, 77, 131, 132, 133, 134, 135, 136, 137, 138, 139, 262, 263, 264, 33,
5120 DATA 109, 110, 111
5130 '
5140 'SUBROUTINE TO CHECK FOR LUNCH AND END OF WORKDAY
5150 IF MIN > TM THEN 5220 : 'AFTER 5 PM?
5160 GOSUB 5250
5170 IF LUN = 1 THEN RETURN : 'HAD LUNCH ALREADY?
5180 IF MIN < 180 THEN RETURN : 'BEFORE 12 NOON?
5190 IF PERS = 1 THEN RETURN : 'ON A TRAIN?
5200 PRINT : PRINT "TIME FOR A LUNCH BREAK. CHILI DOG AND COLA. BURP!"
5210 PRINT : MIN = MIN + INT(24 + 20 * RND(1)) : LUN = 1 : RETURN
5220 PRINT : PRINT "SO SORRY, IT IS AFTER " TM$ "PM AND THE PLACES TO WHICH"
5230 PRINT "YOU WANT TO GO WILL BE CLOSED." : GOTO 1520
5240 '
5250 'SUBROUTINE TO PRINT THE TIME
5260 HR = INT(MIN / 60) : MN = MIN - 60 * HR : IF HR < 4 THEN HP = 9 + HR ELSE HP = HR - 3
5270 HP = 100 * HP + MN + 10000 : H$ = STR$(HP)
5280 PRINT : PRINT "TIME " MID$(H$, 3, 2) ":" RIGHT$(H$, 2) : RETURN
5290 '
5300 'SUBROUTINE TO READ YES/NO ANSWER
5310 IF A$ = "" THEN A$ = "Y" : RETURN
5320 GOSUB 5350 : IF A$ = "Y" OR A$ = "N" THEN RETURN
5330 INPUT "DON'T UNDERSTAND YOUR ANSWER. ENTER 'Y' OR 'N' PLEASE";A$ : GOTO 5320
5340 '
5350 'SUBROUTINE TO READ FIRST LETTER OF ANSWER
5360 A$ = LEFT$(A$, 1) : IF A$ >= "A" AND A$ <= "Z" THEN RETURN
5370 A$ = CHR$(ASC(A$) - 32) : RETURN
5380 '
5390 'SUBROUTINE TO PRINT DELIVERY/PICK-UP LOG
5400 PRINT : PRINT TAB(20) "DELIVERY - PICK-UP LOG" : PRINT
5410 FOR I = 1 TO 15
5420 IF DORP(I) = 0 OR DORP(I) = 3 THEN 5450
5430 IF DORP(I) = 1 THEN X$ = "DELIVERY" ELSE X$ = "PICK-UP"
5440 PRINT I, X$, PKGDES$(I)
5450 NEXT I : PRINT : GOSUB 5500 : RETURN
5460 '
5470 'SUBROUTINE TO MAKE A SHORT PAUSE
5480 FOR I = 1 TO 1200 : NEXT I : RETURN
5490 '
5500 X$ = "PRESS ANY KEY TO CONTINUE." : GOSUB 5530
5510 WHILE LEN(INKEY$) = 0 : RN = RN + 1 : WEND : PRINT : RETURN
5520 '
5530 'SUBROUTINE TO PRINT CENTERED LINES
5540 PRINT TAB((70 - LEN(X$)) / 2) X$; : RETURN
5550 '
5560 'SUBROUTINE TO SHUFFLE THE LIST OF PACKAGES
5570 FOR I = 1 TO PS : DUM(I) = I : NEXT I
5580 FOR I = 1 TO PS - 1
5590 K = I + INT((PS + 1 - I) * RND(I)) : J = PN(I) : PN(I) = PN(K) : PN(K) = J
5600 X$ = PKGDES$(I) : PKGDES$(I) = PKGDES$(K) : PKGDES$(K) = X$
5610 A = PKSTNU(I) : PKSTNU(I) = PKSTNU(K) : PKSTNU(K) = A
5620 FOR J = 1 TO 3
5630 A = PKGSTA(I, J) : PKGSTA(I, J) = PKGSTA(K, J) : PKGSTA(K, J) = A
5640 A = PKSTDS(I, J) : PKSTDS(I, J) = PKSTDS(K, J) : PKSTDS(K, J) = A
5650 NEXT J : NEXT I : RETURN
5660 '
5670 'SUBROUTINE TO PLAY A SHORT MELODY
5680 SOUND 262, 4 : SOUND 294, 6 : SOUND 330, 4 : SOUND 349, 6
5690 SOUND 392, 6 : SOUND 349, 10 : SOUND 330, 12 : FOR K = 1 TO 1200 : NEXT
5700 SOUND 330, 4 : FOR K = 1 TO 4 : SOUND 294, 4 : NEXT
5710 SOUND 330, 6 : SOUND 330, 6 : SOUND 262, 12 : RETURN
