0 GOTO120
1 W=VARPTR(A$(K)):POKEPEEK(W+2)*L+PEEK(W+3)+J-1,T:RETURN
2 A=1:IFK=49ORMID$(A$(K+1),J,1)<>"€"THENRETURN
3 T=B:GOSUB1:K=K+1:T=Y:GOSUB1:GOSUB20:GOSUB85:RETURN
4 A=2:IFK=2ORMID$(A$(K-1),J,1)<>"€"THENRETURN
5 T=B:GOSUB1:K=K-1:T=Y:GOSUB1:GOSUB20:GOSUB85:RETURN
6 A=3:IFJ=98ORMID$(A$(K),J+1,1)<>"€"THENRETURN
7 T=B:GOSUB1:J=J+1:T=Y:GOSUB1:GOSUB20:GOSUB85:RETURN
8 A=4:IFJ=1ORMID$(A$(K),J-1,1)<>"€"THENRETURN
9 T=B:GOSUB1:J=J-1:T=Y:GOSUB1:GOSUB20:GOSUB85:RETURN
10 IFX>1THENX=X-1:GOTO20
11 RETURN
12 IFX<36THENX=X+1:GOTO20
13 RETURN
14 IFV<67THENV=V+1:GOTO20
15 RETURN
16 IFV>1THENV=V-1:GOTO20
17 RETURN
20 ?@.,MID$(A$(X),V,Z)MID$(A$(X+1),V,Z)MID$(A$(X+2),V,Z)MID$(A$(X+3),V,Z)MID$(A$(X+4),V,Z)MID$(A$(X+5),V,Z);
21 ?MID$(A$(X+6),V,Z)MID$(A$(X+7),V,Z)MID$(A$(X+8),V,Z)MID$(A$(X+9),V,Z)MID$(A$(X+10),V,Z)MID$(A$(X+11),V,Z);
22 ?MID$(A$(X+12),V,Z)MID$(A$(X+13),V,Z)MID$(A$(X+14),V,Z);:RETURN
30 ONK(PEEK(2)ANDPEEK(17023))GOSUB2,4,6,8,10,12,14,16,40,2010:GOTO30
40 IFBB<1THENRETURN
41 ONAGOSUB42,44,46,48:RETURN
42 IFMID$(A$(K+1),J,1)<>"¯"THENRETURN
43 K=K+1:T=B:GOSUB1:K=K-1:GOSUB20:GOTO50
44 IFMID$(A$(K-1),J,1)<>"¯"THENRETURN
45 K=K-1:T=B:GOSUB1:K=K+1:GOSUB20:GOTO50
46 IFMID$(A$(K),J+1,1)<>"¯"THENRETURN
47 J=J+1:T=B:GOSUB1:J=J-1:GOSUB20:GOTO50
48 IFMID$(A$(K),J-1,1)<>"¯"THENRETURN
49 J=J-1:T=B:GOSUB1:J=J+1:GOSUB20:GOTO50
50 BB=BB-1:GOSUB116:FORT=1TO5:SOUND1,1:SOUND2,1:NEXT:RETURN
51 IFY=253THENRETURN
52 W=VARPTR(A$(K+1)):POKEPEEK(W+2)*L+PEEK(W+3)+J-1,B:Y=253:GOTO100
53 IFY=253THENRETURN
54 W=VARPTR(A$(K-1)):POKEPEEK(W+2)*L+PEEK(W+3)+J-1,B:Y=253:GOTO100
55 IFY=253THENRETURN
56 W=VARPTR(A$(K)):POKEPEEK(W+2)*L+PEEK(W+3)+J,B:Y=253:GOTO100
57 IFY=253THENRETURN
58 W=VARPTR(A$(K)):POKEPEEK(W+2)*L+PEEK(W+3)+J-2,B:Y=253:GOTO100
61 IFY=237THENRETURN
62 W=VARPTR(A$(K+1)):POKEPEEK(W+2)*L+PEEK(W+3)+J-1,B:Y=237:GOTO100
63 IFY=237THENRETURN
64 W=VARPTR(A$(K-1)):POKEPEEK(W+2)*L+PEEK(W+3)+J-1,B:Y=237:GOTO100
65 IFY=237THENRETURN
66 W=VARPTR(A$(K)):POKEPEEK(W+2)*L+PEEK(W+3)+J,B:Y=237:GOTO100
67 IFY=237THENRETURN
68 W=VARPTR(A$(K)):POKEPEEK(W+2)*L+PEEK(W+3)+J-2,B:Y=237:GOTO100
85 ONK(ASC(MID$(A$(K+1),J,1)))GOSUB110,51,61:ONK(ASC(MID$(A$(K-1),J,1)))GOSUB110,53,63
87 ONK(ASC(MID$(A$(K),J+1,1)))GOSUB110,55,65:ONK(ASC(MID$(A$(K),J-1,1)))GOSUB110,57,67:RETURN
100 SS=SS+1:GOSUB116:FORT=1TO5:SOUND176,1:SOUND193,1:SOUND204,1:SOUND218,1:NEXT
101 IFSS=40THEN2050
103 W=VARPTR(A$(K)):POKEPEEK(W+2)*L+PEEK(W+3)+J-1,Y:GOSUB20:RETURN
110 IFRND(3)>1THENRETURN
111 PRINT@480,"LEVEL";NN;"FLOODED";:SOUND1,10
112 FORT=2TO98:IFMID$(A$(NN),T,1)="€"THENW=VARPTR(A$(NN)):POKEPEEK(W+2)*L+PEEK(W+3)+T-1,159
113 NEXT:GOSUB20
114 IFNN<=KORNN=2THEN2000
115 NN=NN-1:PRINT@480,"                               ";
116 PRINT@480,"BOMBS:"BB;:PRINT@498,"SCORE:"SS;:RETURN
120 CLS
130 CLEAR5200
140 PRINT@232,"PITS OF GANYMEDE";
150 J=RND(-(PEEK(9)*256+PEEK(10)))
200 DIMA$(50),W,K,X,Y,V,Z,A,B,T,K(255),C,L,M,NN,BB,SS
210 Z=32
220 Y=237
230 K(75)=1:K(73)=2:K(76)=3:K(74)=4:K(87)=5:K(90)=6:K(83)=7:K(65)=8:K(32)=9:K(82)=10:K(207)=1:K(239)=2:K(255)=3
260 NN=49:BB=5
270 PRINT@289,"ADVENTURE STARTS IN 45 SECONDS"
280 A$(0)="¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿"
300 FORB=1TO50:A$(B)="¿¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¿":NEXT
321 FORB=2TO49:A$(B)=A$(B)+"¿":NEXT:J=128
330 FORA=1TO5:B=1+RND(94):FORC=2TO49:W=VARPTR(A$(C)):T=PEEK(W+2)*256+PEEK(W+3)+B-1:POKET,J:POKET+1,J:POKET+2,J
331 IFRND(4)=1ANDB>2THENB=B-1:NEXT:NEXT:GOTO400
381 IFB<47THENB=B+1
390 NEXT:NEXT
400 FORA=1TO5:B=1+RND(46)
420 FORC=2TO96STEP2:FORL=BTOB+2:W=VARPTR(A$(L)):T=PEEK(W+2)*256+PEEK(W+3)+C-1:POKET,J:POKET+1,J:NEXT
421 IFRND(4)=1ANDB>1THENB=B-1:NEXT:NEXT:GOTO490
471 IFB<47THENB=B+1
480 NEXT:NEXT
490 X=1:PRINT@448,"...OR THEREABOUTS."
510 B=RND(20):FORJ=2TO83:IFMID$(A$(3),J,1)="€"THENL=L+1
540 IFL=BTHENW=VARPTR(A$(3)):POKEPEEK(W+2)*256+PEEK(W+3)+J-1,Y:T=J:J=83:NEXT:J=T:GOTO580
550 NEXT:L=0:GOTO510
580 IFJ<17THENV=1:GOTO590
581 V=J-16
590 M=J
600 FORT=2TO49
610 J=1+RND(96):K=1+RND(48):IFMID$(A$(K),J,1)="€"THENW=VARPTR(A$(K)):POKEPEEK(W+2)*256+PEEK(W+3)+J-1,207:NEXT:GOTO650
631 GOTO610
650 FORL=1TO2:IFL=1THENA=255:GOTO660
651 A=239
660 FORT=1TO20
670 J=1+RND(96):K=12+RND(37):IFMID$(A$(K),J,1)="€"THENW=VARPTR(A$(K)):POKEPEEK(W+2)*256+PEEK(W+3)+J-1,A:NEXT:NEXT:GOTO720
701 GOTO670
720 A$(1)=A$(.):A$(50)=A$(.):GOSUB20:K=3:J=M:A=0:B=128:L=256:GOSUB116:GOTO30
1290 END
2000 PRINT@32,"LOST IN THE MIASMIC MIST."
2010 PRINT@64,"FINAL SCORE";SS:PRINT"TAP y KEY TO TRY AGAIN":GOTO2060
2050 PRINT@Z,"YOU SAVED ALL THE TWIMULKETS!":FORT=1TO200STEP5:SOUNDT,1:NEXT:SOUND206,10:GOSUB2010
2060 SS$=INKEY$:ON-(SS$<>"Y"ANDSS$<>"N")GOTO2060:ON-(SS$="N")GOTO1290:RUN
2100 REM * PITS OF GANYMEDE *
2110 REM TRS-80 MC-10 + 16K
2120 REM ELMER'S ARCADE
2130 REM RICHARD RAMELLA
2140 REM hot COCO MAY 1984
2150 REM MC-10 VER. JIM GERRIE
2160 REM SAVE ALL THE PURPLE
2170 REM AND ORANGE TWIMULKETS.
2180 REM AVOID WHITE GAS
2190 REM MUSHROOMS. r=RESTART
2200 REM space=BOMBS(FOR WALLS)
2210 REM aswz= PAN MAP
2220 REM ijkl= MOVE SHIP
2230 REM 40=WINNING SCORE
