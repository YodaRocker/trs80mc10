0 GOTO120
1 W=VARPTR(A$(K)):POKE(PEEK(W+2)*256)+PEEK(W+3)+J-1,Y:RETURN
2 IFK=49ORMID$(A$(K+1),J,1)<>"€"THENRETURN
3 Y=128:GOSUB1:K=K+1:Y=ASC(Y$):GOSUB1:GOTO20
4 IFK=2ORMID$(A$(K-1),J,1)<>"€"THENRETURN
5 Y=128:GOSUB1:K=K-1:Y=ASC(Y$):GOSUB1:GOTO20
6 IFJ=98ORMID$(A$(K),J+1,1)<>"€"THENRETURN
7 Y=128:GOSUB1:J=J+1:Y=ASC(Y$):GOSUB1:GOTO20
8 IFJ=1ORMID$(A$(K),J-1,1)<>"€"THENRETURN
9 Y=128:GOSUB1:J=J-1:Y=ASC(Y$):GOSUB1:GOTO20
10 IFX>1THENX=X-1:GOTO20
11 RETURN
12 IFX<36THENX=X+1:GOTO20
13 RETURN
14 IFV<67THENV=V+1:GOTO20
15 RETURN
16 IFV>1THENV=V-1:GOTO20
17 RETURN
20 ?@0,MID$(A$(X),V,32)MID$(A$(X+1),V,32)MID$(A$(X+2),V,32)MID$(A$(X+3),V,32)MID$(A$(X+4),V,32)MID$(A$(X+5),V,32);
21 ?MID$(A$(X+6),V,32)MID$(A$(X+7),V,32)MID$(A$(X+8),V,32)MID$(A$(X+9),V,32)MID$(A$(X+10),V,32)MID$(A$(X+11),V,32);
22 ?MID$(A$(X+12),V,32)MID$(A$(X+13),V,32)MID$(A$(X+14),V,32);:RETURN
120 CLS
130 CLEAR5200
140 PRINT@232,"PITS OF GANYMEDE";
150 A=RND(-(PEEK(9)*256+PEEK(10)))*1000
170 FORB=1TOA
180 X=RND(20)
190 NEXT
200 DIMA$(50),Q(3),K(255),W,J,K,Y,H,P:Q(1)=207:Q(2)=255:Q(3)=239
210 Y$="í"
220 K(75)=1:K(73)=2:K(76)=3:K(74)=4:K(87)=5:K(90)=6:K(83)=7:K(65)=8
260 NN=49
270 PRINT@289,"ADVENTURE STARTS IN 45 SECONDS"
280 A$(1)="¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿"
290 A$(50)=A$(1)
300 FORB=2TO49
310 A$(B)="¿¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¿¿"
320 NEXT
330 FORA=1TO5:B=1+RND(96):FORC=2TO49:A$(C)=LEFT$(A$(C),B-1)+"€€€"+MID$(A$(C),B+3):E=RND(4):IFE=1ANDB>2THENB=B-1:GOTO390
381 IFB<47THENB=B+1
390 NEXT:NEXT
400 FORA=1TO5
410 B=1+RND(46)
420 FORC=2TO96STEP2:FORF=0TO2:A$(B+F)=LEFT$(A$(B+F),C-1)+"€€"+MID$(A$(B+F),C+2):NEXT:E=RND(4):IFE=1ANDB>1THENB=B-1:GOTO480
471 IFB<47THENB=B+1
480 NEXT:NEXT
490 X=1
500 PRINT@448,"...OR THEREABOUTS."
510 G=RND(20)
520 FORJ=2TO98
530 IFMID$(A$(3),J,1)="€"THENL=L+1
540 IFL=GTHENA$(3)=LEFT$(A$(3),J-1)+Y$+MID$(A$(3),J+1):GOTO580
550 NEXT
560 L=0
570 GOTO510
580 IFJ<17THENV=1:GOTO590
581 V=J-16
590 M=J
600 FORT=1TO50
610 J=RND(99)
620 K=RND(49)
630 IFMID$(A$(K),J,1)="€"THENA$(K)=LEFT$(A$(K),J-1)+"Ï"+MID$(A$(K),J+1):GOTO640
631 GOTO610
640 NEXT
650 FORL=1TO2
660 FORT=1TO20
670 J=RND(100)
680 K=12+RND(38)
690 IFL=1THENQ=255:GOTO700
691 Q=239
700 IFMID$(A$(K),J,1)="€"THENA$(K)=LEFT$(A$(K),J-1)+CHR$(Q)+MID$(A$(K),J+1):GOTO710
701 GOTO670
710 NEXT:NEXT:GOSUB20
720 K=3
730 J=M
740 H=K(PEEK(2)ANDPEEK(17023)):IFHTHENONHGOSUB2,4,6,8,10,12,14,16:GOSUB850
750 GOTO740
850 FORP=1TO3:H$=CHR$(Q(P)):IFMID$(A$(K+1),J,1)=H$ORMID$(A$(K-1),J,1)=H$ORMID$(A$(K),J+1,1)=H$ORMID$(A$(K),J-1,1)=H$THEN950
880 NEXT:RETURN
950 IFP=1THEN1160
960 IFP=2ANDY$="í"ORP=3ANDY$="ý"THEN740
970 A$(K+1)=LEFT$(A$(K+1),J-1)+"€"+MID$(A$(K+1),J+1)
980 A$(K-1)=LEFT$(A$(K-1),J-1)+"€"+MID$(A$(K-1),J+1)
990 A$(K)=LEFT$(A$(K),J)+"€"+MID$(A$(K),J+2)
1000 A$(K)=LEFT$(A$(K),J-2)+"€"+MID$(A$(K),J)
1010 IFP=2THENY$="í":GOTO1020
1011 Y$="ý"
1020 SS=SS+1
1030 PRINT@498,"SCORE:";SS;:GOSUB1080
1040 A$(K)=LEFT$(A$(K),J-1)+Y$+MID$(A$(K),J+1)
1050 GOSUB20
1060 GOSUB1080
1070 GOTO740
1080 FORT=1TO5
1090 SOUND176,1
1100 SOUND193,1
1110 SOUND204,1
1120 SOUND218,1
1130 NEXT
1140 RETURN
1150 GOTO740
1160 U=RND(30)
1170 IFU>1THEN740
1180 PRINT@480,"LEVEL";NN;"FLOODED";
1190 SOUND1,10
1200 FORN1=2TO98:IFMID$(A$(NN),N1,1)="€"THENW=VARPTR(A$(NN)):POKE(PEEK(W+2)*256)+PEEK(W+3)+N1-1,159
1220 NEXT
1230 GOSUB20
1240 IFNN<KORNN=2THENGOSUB2000:SS$=INKEY$:ON-(SS$="")GOTO1240:RUN
1250 NN=NN-1
1260 PRINT@480,"                               ";
1270 PRINT@498,"SCORE:";SS;
1280 GOTO740
1290 END
2000 PRINT@32,"LOST IN THE MIASMIC MIST.":PRINT"FINAL SCORE";SS:PRINT"TAP ANY KEY TO TRY AGAIN":RETURN
2100 REM*PITS OF GANYMEDE *TRS-80 16K EXT.CO1.BAS. *ELMER'S/RAMELLA
2110 REM W=VARPTR(A$(C)):C1=(PEEK(W+2)*256)+PEEK(W+3)+B-1:POKEC1,128:POKEC1+1,128:POKEC1+2,128
