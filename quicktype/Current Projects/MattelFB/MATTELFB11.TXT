0 CLS:DIMT,A,B,X(5),Y(5),O,K(255),M,MI,SE,BA,L,YT:GOTO900
1 IFBA=50THENPRINT@204,"  "BA"  ";:RETURN
2 IFS$="<"THENPRINT@204," <"BA"  ";:RETURN
3 PRINT@204,"  "BA"> ";:RETURN
6 IFDO=4ANDDE=1ANDS$=">"THENGOSUB500:RETURN
7 IFDO=4ANDDE=1ANDS$="<"THENGOSUB600:RETURN
8 IFDO=4ANDDE=2ANDS$="<"THENGOSUB500:RETURN
9 IFDO=4ANDDE=2ANDS$=">"THENGOSUB600:RETURN
10 RETURN
11 O=32*A+B+M:POKEO,128:A=A-1:IFA=10THENA=11
12 IFPEEK(O-32)=188THENPOKE32*A+B+M,252:GOTO180
13 POKE32*A+B+M,252:RETURN
14 O=32*A+B+M:POKEO,128:A=A+1:IFA=14THENA=13
15 IFPEEK(O+32)=188THENPOKE32*A+B+M,252:GOTO180
16 POKE32*A+B+M,252:RETURN
20 ONK(ASC(INKEY$+"@"))GOSUB40,60,11,14,6:IFBA<1THENGOSUB190:GOTO20
22 MI=MI-SE:PRINT@17,INT(MI);:IFMI<1THENPRINT@17,INT(0);:GOSUB800:GOTO20
24 FORT=1TO5:O=32*X(T)+Y(T)+M:ONRND(L)GOTO26,28,30,32:NEXT:GOTO20
26 ONK(PEEK(O-32))GOTO27,35:IFX(T)>ATHENPOKEO,128:X(T)=X(T)-1:POKEO-32,188:NEXT:GOTO20
27 NEXT:GOTO20
28 ONK(PEEK(O+32))GOTO27,36:IFX(T)<ATHENPOKEO,128:X(T)=X(T)+1:POKEO+32,188:NEXT:GOTO20
29 NEXT:GOTO20
30 ONK(PEEK(O-2))GOTO27,37:IFY(T)>BTHENPOKEO,128:Y(T)=Y(T)-2:POKEO-2,188:NEXT:GOTO20
31 GOTO20
32 ONK(PEEK(O+2))GOTO27,38:IFY(T)<BTHENPOKEO,128:Y(T)=Y(T)+2:POKEO+2,188:NEXT:GOTO20
33 NEXT:GOTO20
35 POKEO,128:POKEO-32,188:GOSUB180:T=5:NEXT:GOTO20
36 POKEO,128:POKEO+32,188:GOSUB180:T=5:NEXT:GOTO20
37 POKEO,128:POKEO-2,188:GOSUB180:T=5:NEXT:GOTO20
38 POKEO,128:POKEO+2,188:GOSUB180:T=5:NEXT:GOTO20
39 REM MOVE.L:
40 IFDE=1THENRETURN
41 O=32*A+B+M:POKEO,128:B=B-2:IFB=5THENB=23
42 IFPEEK(O-2)=188THENPOKE32*A+B+M,252:GOTO180
43 POKE32*A+B+M,252:IFS$<>"<"THEN48
44 IFDE=1THENBA=BA-1:YT=YT+1:GOTO46
45 BA=BA-1:YT=YT-1:IFYT<0THENYT=0
46 IFA=51THENS$=">":BA=49
47 GOSUB1:RETURN
48 IFDE=1THENBA=BA+1:YT=YT+1:GOTO50
49 BA=BA+1:YT=YT-1:IFYT<0THENYT=0
50 IFBA=51THENS$="<":BA=49
51 GOSUB1:RETURN
59 REM MOVE.R:
60 IFDE=2THENRETURN
61 O=32*A+B+M:POKEO,128:B=B+2:IFB=25THENB=7
62 IFPEEK(O+2)=188THENPOKE32*A+B+M,252:GOTO180
63 POKE32*A+B+M,252:IFS$<>">"THEN69
64 IFDE=2THENBA=BA-1:YT=YT+1:GOTO67
65 BA=BA-1:YT=YT-1:IFYT<0THENYT=0
67 IFBA=51THENS$="<":BA=49
68 GOSUB1:RETURN
69 IFDE=2THENBA=BA+1:YT=YT+1:GOTO73
71 BA=BA+1:YT=YT-1:IFYT<0THENYT=0
73 IFBA=51THENS$=">":BA=49
75 GOSUB1:RETURN
179 REM COLLISION.CHK:
180 DO=DO+1:NE=1:SOUND25,3:IFYT=0THENDO=1:YT=10:SOUND200,2:SOUND225,3
182 IFDO<5ANDDE=1THENGOSUB200:RETURN
184 IFDO<5ANDDE=2THENGOSUB300:RETURN
186 IFDO=5ANDDE=1THENDE=2:DO=1:YT=10:GOSUB300:RETURN
188 IFDO=5ANDDE=2THENDE=1:DO=1:YT=10:GOSUB200:RETURN
190 IFS$=">"ANDDE=1THENP1=P1+7:GOSUB410:FORT=1TO12:SOUND200,1:NEXT:DE=2:DO=1:DO$="1ST":YT=10:S$=">":BA=20:GOSUB300:RETURN
191 IFS$="<"ANDDE=2THENP2=P2+7:GOSUB410:FORT=1TO12:SOUND200,1:NEXT:DE=1:DO=1:DO$="1ST":YT=10:S$="<":BA=20:GOSUB200:RETURN
199 REM RESET.POSITIONS:
200 IFMI<1THENRETURN
210 IFK=1THENGOSUB224
212 FORT=1TO150:NEXT
216 POKE32*A+B+M,252
218 FORT=1TO150:NEXT
220 POKE32*A+B+M,128
222 IFINKEY$<>" "THEN212
224 A=12:B=7:X(1)=12:Y(1)=13:X(2)=11:Y(2)=13:X(3)=13:Y(3)=13:X(4)=12:Y(4)=17:X(5)=12:Y(5)=23:K=0:GOSUB400:RETURN
280 FORT=1TO100STEP10:SOUNDT,1:NEXT:FORT=100TO1STEP-10:SOUNDT,1:NEXT:RETURN
299 REM RESET.POSITIONS.2:
300 IFK=1THENGOSUB316
302 FORT=1TO150:NEXT
306 POKE32*A+B+M,252
308 FORT=1TO150:NEXT
312 POKE32*A+B+M,128
314 IFINKEY$<>" "THEN302
316 A=12:B=23:X(1)=12:Y(1)=17:X(2)=11:Y(2)=17:X(3)=13:Y(3)=17:X(4)=12:Y(4)=13:X(5)=12:Y(5)=7:K=0:GOSUB400:RETURN
399 REM PRINT.FRAME:
400 GOSUB1000:PRINT@11,LEFT$(" TIME:"+STR$(INT(MI))+"   ",10);:PRINT@43,LEFT$(" "+DO$(DO)+" &"+STR$(YT)+"  ",10);
401 IFHA=1THENPRINT@32*3+6,"     1ST  HALF     ";
402 IFHA=2THENPRINT@32*3+6,"     2ND  HALF     ";
403 GOSUB1
404 IFDO<>4THEN410
405 IFDE=1ANDS$=">"THENPRINT@269,"(K)ICK";
406 IFDE=1ANDS$="<"THENPRINT@269,"(P)UNT";
407 IFDE=2ANDS$="<"THENPRINT@269,"(K)ICK";
408 IFDE=2ANDS$=">"THENPRINT@269,"(P)UNT";
410 IFGA=1THENPRINT@259,MID$(STR$(P1)+"  ",2,3);:PRINT@282,MID$(STR$(P2)+"  ",2,3);
420 RETURN
499 REM KICK.IT:
500 K=1:PRINT@269,"€€€€€€";:N=INT(RND(0)*47)+2:GOSUB280
502 IFDE=1ANDBA-N<=0THENP1=P1+3:FORT=1TO6:SOUND200,1:NEXT:DE=2:DO=1:DO$="1ST":YT=10:SI$= ">":BA=20:GOSUB300:RETURN
504 IFDE=2ANDBA-N<=0THENP2=P2+3:FORT=1TO6:SOUND200,1:NEXT:DE=1:DO=1:DO$="1ST":YT=10:SI$= "<":BA=20:GOSUB200:RETURN
506 IFDE=1ANDBA-N>0THENSOUND200,3:DE=2:DO=1:DO$="1ST":YT=10:SI$=">":BA=20:GOSUB300:RETURN
508 IFDE=2ANDBA-N>0THENSOUND200,3:DE=1:DO=1:DO$="1ST":YT=10:SI$="<":BA=20:GOSUB200:RETURN
510 RETURN
599 REM PUNT.IT:
600 K=1:PRINT@269,"€€€€€€";:GOSUB280
602 IFDE=1THENDE=2:SI$=">":BA=INT(RND(0)*47)+2:DO=1:YT=10:SOUND25,3:GOSUB300:RETURN
604 IFDE=2THENDE=1:SI$="<":BA=INT(RND(0)*47)+2:DO=1:YT=10:SOUND25,3:GOSUB200:RETURN
606 RETURN
699 REM HALFTIME:
700 K=1
702 FORT=1TO150:NEXT:PRINT@32*3+6,"€€ H A L F T I M E €€";:PRINT@32*4+6,"€€(GO GRAB A BEER!)€€";
704 FORT=1TO150:NEXT:FORT=104+MTO120+M:POKET,PEEK(T)-64:NEXT
706 IFINKEY$<>" "THEN702
708 CLS:RETURN
799 REM GAME.END:
800 IFHA=1THENHA=2:GOSUB700:DE=2:MI=30:S$=">":BA=20:YT=10:DO=1:GOSUB300:RETURN
802 FORT=1TO150:NEXT:PRINT@32*3+6,"€€THAT'S IT, FOLKS!€€";
804 FORT=1TO150:NEXT:FORT=104+MTO120+M:POKET,PEEK(T)-64:NEXT
806 IFINKEY$<>" "THEN802
808 CLS:RUN
900 L=30:K(65)=1:K(83)=2:K(87)=3:K(90)=4:K(75)=5:K(80)=5
901 DO$(1)="1ST":DO$(2)="2ND":DO$(3)="3RD":DO$(4)="4TH":M=16384
902 K(128)=3:K(252)=2:K(188)=1
910 CLS:PRINT"BOMBERPUNK PRESENTS..."
920 PRINT"CLASSIC FOOTBALL!"
930 PRINT" (S)TART  (H)ELP "
940 A$=INKEY$:IFA$=""THEN940
950 IFA$="S"THEN980
960 IFA$="H"THEN8000
970 GOTO940
980 GA=1:A=12:B=7:X(1)=12:Y(1)=13:X(2)=11:Y(2)=13:X(3)=13:Y(3)=13:X(4)=12:Y(4)=17:X(5)=12:Y(5)=23
990 DO=1:DO$="1ST":YT=10:S$="<":BA=20:HA=1:MI=30:SE=.01:DE=1:P1=0:P2=0:GOSUB400:GOSUB280:GOTO20
999 REM PRINT.FIELD:
1000 CLS0
1010 PRINT@32*09+3,"ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ";
1020 PRINT@32*10+3,"€€€…€„€„€„€„€„€„€„€„€…€€€€";
1040 PRINT@32*11+3,"ÌÍÀ…€€€€€€€€€€€€€€€€€…ÀÀÎÌ";
1050 PRINT@32*12+3,"ÀÅÌ…€€€€€€€€€€€€€€€€€…ÄÌÊÀ";
1060 PRINT@32*13+3,"ÌÌ€…€€€€€€€€€€€€€€€€€…€€ÌÌ";
1080 PRINT@32*14+3,"ƒƒƒ‡ƒ‡ƒ‡ƒ‡ƒ‡ƒ‡ƒ‡ƒ‡ƒ‡ƒ‡ƒƒƒƒ";
1090 FORT=1TO5:POKE32*X(T)+Y(T)+M,188:NEXT:POKE32*A+B+M,252:RETURN
8000 CLS:PRINTTAB(4)"MATTEL CLASSIC FOOTBALL":PRINT
8001 PRINTTAB(4)"BY BOMBERPUNK@YAHOO.COM"
8002 PRINTTAB(4)"MC-10 EDITS JIM GERRIE":PRINT
8010 PRINT"PLAYS JUST LIKE THE 1970S"
8020 PRINT"HANDHELD GAME. THE ORANGE BLIP"
8021 PRINT"IS THE RUNNING BACK. THE RED"
8022 PRINT"BLIPS ARE DEFENSIVE TACKLERS."
8070 PRINT"USE AWSZ KEYS TO MOVE. P TO PUNT";
8080 PRINT"AND K TO KICK (WHEN PROMPTED).
8090 PRINT"FINALLY, HIT SPACE TO START THE"
8100 PRINT"NEXT PLAY. ":PRINT
8130 PRINT"PRESS SPACE TO CONTINUE...";
8140 A$=INKEY$:IFA$=""THEN8140
8150 IFA$=" "THEN910
8160 GOTO3140
