0 CLEAR700:DIMMC,C1$(4),C2$(4),CA$,C(51),X,Y,P,C,O,L,S,V,C1,CC,M$,K(255):MC=RND(-(PEEK(9)*256+PEEK(10))):GOSUB3000:GOTO300
1 S=INT(C(P)/13):V=C(P)-S*13:RETURN
2 IFS<4THENPRINT@L,MID$(CA$,V+1,1)C1$(S);:PRINT@L+32,C2$(S);:RETURN
3 RETURN
4 P=(X+(Y-1)*(Y/2))-1:RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORC1=CC-LEN(M$)+1TOCC:POKEC1,PEEK(C1)-64:NEXT:RETURN
10 PRINT@L,"ßßßß";:PRINT@L+32,"ßßßß";:C(P)=52:IFP=0ORL=46THENE=-1:RESTORE:FORC1=0TO27:READCC:SOUNDCC,1:NEXT
11 RETURN
20 I$=INKEY$:POKEL+MC,207
21 IFI$="A"THENIFPEEK(L+MC-34)<128THENPOKEL+MC,T:L=L-34:T=PEEK(L+MC):Y=Y-1:X=X-1:GOSUB4
22 IFI$="S"THENIFPEEK(L+MC-30)<128THENPOKEL+MC,T:L=L-30:T=PEEK(L+MC):Y=Y-1:GOSUB4
23 IFI$="Z"THENIFPEEK(L+MC+30)<128THENPOKEL+MC,T:L=L+30:T=PEEK(L+MC):Y=Y+1:GOSUB4
24 IFI$="X"THENIFPEEK(L+MC+34)<128THENPOKEL+MC,T:L=L+34:T=PEEK(L+MC):Y=Y+1:X=X+1:GOSUB4
25 IFI$="C"THENH=P:F=L:GOSUB550:P=H:L=F:IFR=3THENPOKEL+MC,T:GOTO200
26 IFI$="D"THENTT=T:TM=L:P=W:L=10*32+8:T=PEEK(L+MC):V=K(T):ON-(V+G=13)GOTO450:GOTO400
27 IFI$="W"THENTT=T:TM=L:P=Q:L=10*32+20:T=PEEK(L+MC):V=K(T):ON-(V+G=13)GOTO450:GOTO400
29 IFI$=CHR$(13)THENON-(V+G=13)GOTO150:GOTO100
30 POKEL+MC,T:V=K(T):PRINT@485,"CARD1:"V;:PRINT@497,"CARD2:"G;:IFI$<>"R"THEN20
40 PRINT@480,"DO YOU REALLY WANT TO QUIT?    ";:POKEMC+511,96
41 I$=INKEY$:IFI$=""THEN41
42 IFI$="Y"THENPRINT@480,"DO YOU WISH TO ";:LS=LS+1:GOTO50
43 IFI$="N"THENPRINT@480,"ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß";:POKEMC+511,223:GOTO20
44 GOTO41
50 PRINT@480+15,"PLAY AGAIN (Y/N)";:POKEMC+511,127:PRINT@0,"WINS:"STR$(WW);:PRINT@20,"LOSSES:"STR$(LS);
60 I$=INKEY$:IFI$=""THEN60
70 IFI$="Y"THEN300
80 IFI$="N"THENEND
90 GOTO60
100 IFPEEK(L+MC+64)<>223ORPEEK(L+MC+66)<>223THEN30
110 IFG=0ANDV<13THENT=T-64:SOUND100,1:G=V:H=P:F=L:GOTO30
120 SOUND1,1:GOTO340
150 IFPEEK(L+MC+64)<>223ORPEEK(L+MC+66)<>223THEN30
160 IFV=13THENGOSUB10:GOTO180
165 IFPEEK(F+MC+64)<>223ORPEEK(F+MC+66)<>223THEN30
170 GOSUB10:P=H:L=F:GOSUB10
180 IFNOT(E)THENSOUND200,1:GOTO340
190 PRINT@480,"YOU WIN!!!     ";:WW=WW+1:GOTO50
200 PRINT@480,"GAME OVER!     ";:LS=LS+1:GOTO50
300 CLS:PRINT@8,"pyramid solitaire":POKE16399,32:PRINT@73,"BY JAMES GERRIE"
310 FORI=0TO51:C(I)=I:NEXT:FORI=0TO51:PRINT@261+I-3*INT((I+1)/3),"  s h u f f l i n g  ";:R=RND(51)
320 QQ=PEEK(9)AND128:POKE49151,0+QQ:POKE49151,128-QQ:T=C(I):C(I)=C(R):C(R)=T:NEXT
330 CLS6:GOSUB700:P=28:W=28:E=0:R=0
340 GOSUB500:GOSUB555:L=32+14:T=PEEK(L+MC):V=K(T):X=1:Y=1:G=0:V=0:H=0:I$="":GOTO20
400 IFPEEK(L+MC)=223THENL=TM:T=TT:V=K(T):GOTO30
410 IFG=0ANDV<13THENT=T-64:POKEL+MC,T:SOUND100,1:G=V:H=P:F=L:L=TM:T=TT:V=K(T):GOTO30
420 SOUND1,1:GOTO340
450 IFPEEK(L+MC)=223THENL=TM:T=TT:V=K(T):GOTO30
460 IFV=13THENGOSUB10:GOTO180
465 IFPEEK(F+MC)=223THENL=TM:T=TT:V=K(T):GOTO30
470 GOSUB10:P=H:L=F:GOSUB10:GOTO180
500 P=0:C=1:O=10:FORY=1TO7:FORX=1TOC:L=32*Y+4*X+O:GOSUB1:GOSUB2:P=P+1:NEXT:C=C+1:O=O-2:NEXT:RETURN
550 W=W+1
555 P=W:IFW=52THENPRINT@10*32+8,"ßßßß";:PRINT@10*32+8+32,"ßßßß";:GOTO630
560 IFW>52THENP=28:W=28:R=R+1:ON-(R<4)GOTO570:RETURN
570 IFC(W)=52THEN550
580 PRINT@13*32+8,W+1;:PRINT@13*32+20,R+1;
600 L=10*32+8
620 GOSUB1:GOSUB2
630 L=10*32+20
640 P=P-1
650 IFP<28THENPRINT@L,"ßßßß";:PRINT@L+32,"ßßßß";:RETURN
660 IFC(P)=52THEN640
670 Q=P:GOSUB1:GOSUB2:RETURN
700 PRINT@11*32+4,"dß=";:PRINT@11*32+25,"=ßw";:PRINT@11*32+13,"cß=ß->";:RETURN
3000 GOSUB4000:C1$(0)="¥¯ "
3010 C2$(0)=" ¬®¨"
3020 C1$(1)="±»°"
3030 C2$(1)=" ½¿¸"
3040 C1$(2)="··²"
3050 C2$(2)=" ´¾°"
3060 C1$(3)="¡« "
3070 C2$(3)=" ¬®¨"
3080 C1$(4)="ÏÏÏ"
3090 C2$(4)=" ÏÏÏ"
3100 CA$="A23456789TJQK":MC=16384
3110 K(65)=1:K(114)=2:K(115)=3:K(116)=4:K(117)=5:K(118)=6:K(119)=7
3120 K(120)=8:K(121)=9:K(84)=10:K(74)=11:K(81)=12:K(75)=13
3130 K(65-64)=1:K(114-64)=2:K(115-64)=3:K(116-64)=4:K(117-64)=5:K(118-64)=6:K(119-64)=7
3140 K(120-64)=8:K(121-64)=9:K(84-64)=10:K(74-64)=11:K(81-64)=12:K(75-64)=13
3150 PRINT@486,;:M$="HIT ANY KEY TO BEGIN":GOSUB7
3160 I$=INKEY$:IFI$=""THEN3160
3170 RETURN
4000 CLS:PRINT@8,"pyramid solitaire":POKE16399,32
4010 PRINT"THE GOAL IS TO CLEAR AS MANY":PRINT"CARDS AS POSSIBLE BY SELECTING"
4020 PRINT"TWO THAT ADD TO 13. USE azsx TO"
4030 PRINT"MOVE YOUR CURSOR. USE enter TO"
4040 PRINT"TO SELECT A CARD OR d TO SELECT"
4050 PRINT"FROM THE DECK OR w FOR THE WASTE";
4060 PRINT"PILE. HIT c TO REVEAL THE NEXT"
4070 PRINT"CARD. YOU CAN EVEN MATCH THE"
4080 PRINT"DECK AND WASTE PILE CARDS. TAP"
4090 PRINT"r TO GET A NEW DEAL. YOU CAN"
4100 PRINT"CYCLE THROUGH THE DECK 3 TIMES."
4110 PRINT"THE CYCLE NUMBER IS LISTED UNDER";
4120 PRINT"THE WASTE PILE.":RETURN
5550 DATA 19,45,69,89,99,117
5560 DATA 133,140,153,165,176,180
5570 DATA 189,197,200,207,213,218
5580 DATA 221,225,229,231,234,237
5590 DATA 239,241,243,245
