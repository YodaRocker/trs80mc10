10 REM "ADVENT"
50 DIM V$(20),O$(25),D$(25),L(25),M(25),L$(30,2),N$(30)
55 O=0
60 GOSUB 1340:LET LC=1:LET NC=0:LET BF=0:LET DF=0:LET LF=0:LET GF=0:GOSUB 1190
70 GOSUB 930
80 IF V>6 THEN GOTO 130
90 IF DF=1 AND RND(1)<.6 THEN LET OB=8:GOSUB 1070:GOTO 150
100 LET NL=VAL(MID$(N$(LC),2*V-1,2))
110 IF NL>0 THEN LET LC=NL:PRINT "O.K.":GOSUB 1190:GOTO 150
120 PRINT "YOU CAN'T GO THAT WAY !":GOTO 150
130 ON V-6 GOSUB 360,540,1190,210,300,420,590,710,790,830,830,860,860
140 IF V=10 AND O=4 THEN GOTO 70
150 IF DF=1 AND RND(1)<.2 THEN GOSUB 1130:GOTO 70
160 IF LC>4 AND RND(1)<.2 AND DF=0 THEN GOSUB 1090
170 IF LC>4 AND DF=1 THEN LET L(8)=-LC
180 IF LC<5 THEN LET DF=0:LET L(8)=0
190 GOTO 70
200 REM ***** TAKE *****
210 IF L(O)=99 THEN PRINT "YOU'VE ALREADY GOT IT !":RETURN
220 IF ABS(L(O))<>LC THEN PRINT "IT ISN'T HERE !":RETURN
230 IF L(O)<0 THEN PRINT "YOU MUST BE JOKING !":RETURN
240 IF NC=5 THEN PRINT "YOU CAN'T CARRY ANY MORE":RETURN
250 IF O<11 OR O>15 THEN GOTO 280
260 IF L(6)=-LC THEN LET OB=6:GOSUB 1070:RETURN
270 IF L(7)=-LC THEN LET OB=7:GOSUB 1070:RETURN
280 PRINT "O.K.":LET NC=NC+1:LET L(O)=99:RETURN
290 REM ***** DROP *****
300 IF L(O)<>99 THEN PRINT "YOU HAVEN'T GOT IT !":RETURN
310 LET NC=NC-1
320 IF O<>14 OR L(15)=LC THEN PRINT "O.K.":LET L(O)=LC:RETURN
330 PRINT "IT CRASHES TO THE FLOOR !!":LET L(14)=0:LET L(16)=LC:LET MS=MS-30
340 RETURN
350 REM ***** LIST *****
360 LET CF=0:FOR K=1 TO NO:IF L(K)<>99 THEN GOTO 390
370 IF CF=0 THEN PRINT "YOU ARE CARRYING...":LET CF=1
380 PRINT D$(K)
390 NEXT K:IF CF=0 THEN PRINT "YOU AREN'T CARRYING ANYTHING !"
400 RETURN
410 REM ***** WAVE *****
420 IF L(O)<>99 THEN PRINT "YOU HAVEN'T GOT IT !":RETURN
430 IF O<>3 THEN PRINT "IF IT MAKES YOU HAPPY !":RETURN
440 IF LC<>7 AND LC<>14 THEN PRINT "NOTHING HAPPENS": RETURN
450 IF BF=1 THEN GOTO 480
460 PRINT "A CRYSTAL BRIDGE APPEARS !!"
470 LET L(10)=-7:LET M(10)=-14:LET A$="14":LET B$="07":GOTO 500
480 PRINT "THE BRIDGE VANISHES !!"
490 LET L(10)=0:LET M(10)=0:LET A$="00":LET B$="00"
500 LET N$(7)=LEFT$(N$(7),6)+A$+MID$(N$(7),9)
510 LET N$(14)=LEFT$(N$(14),2)+B$+MID$(N$(14),5)
520 LET BF=1-BF:RETURN
530 REM ***** SCORE *****
540 LET SC=0:FOR K=11 TO 15:IF L(K)=1 THEN LET SC=SC+30
550 IF L(K)=99 THEN LET SC=SC+10
560 NEXT K:PRINT "YOUR SCORE IS "; SC;" OUT OF ";MS:IF SC<MS THEN RETURN
570 PRINT:PRINT "YOU HAVE COMPLETED YOUR QUEST !!":PRINT:STOP
580 REM ***** THROW *****
590 IF L(O)<>99 THEN GOTO 290
600 IF O<>5 THEN GOTO 670
610 LET B=0:IF L(6)=-LC THEN LET B=6
620 IF L(7)=-LC THEN LET B=7
630 IF B=0 THEN GOTO 290
640 LET NC=NC-1:PRINT "THE BIRD ATTACKS THE ";O$(B)
650 IF B=7 THEN PRINT "AND DRIVES IT AWAY !!":LET L(7)=0:LET L(5)=LC:RETURN
660 IF B=6 THEN PRINT "AND GETS BURNED TO A CINDER !!":LET L(5)=0:RETURN
670 IF O<>4 OR DF=0 THEN GOTO 290
680 IF RND(1)<.1 THEN PRINT "THE DWARF DODGES IT !":GOTO 290
690 PRINT "THE DWARF VANISHES IN SMOKE":LET L(8)=0:LET DF=0:GOTO 290
700 REM ***** FIGHT *****
710 IF O<>6 THEN PRINT "DON'T BE SILLY !!":RETURN
720 PRINT "WHAT - WITH YOUR BARE HANDS ??!!"
730 LET G$=INKEY$(0):IF G$="" THEN GOTO 730
740 LET G=ASC(G$):IF G>96 THEN LET G=G-32
750 LET G$=CHR$(G):IF G$<>"Y" AND G$<>"N" THEN GOTO 730
760 IF G$="N" THEN RETURN
770 PRINT "YOU DEFEAT IT !!":PRINT "IT'S A STRANGE WORLD !":LET L(6)=0:RETURN
780 REM ***** SAY *****
790 IF (LC<>16 AND LC<>1) OR O<>17 THEN PRINT "O.K. '";B$;"'":RETURN
800 PRINT "YOU FEEL YOURSELF SPINNING ROUND ...":FOR Z=1 T0 400:NEXT Z
810 LET LC=17-LC:GOSUB 1190:RETURN
820 REM ***** LIGHT/EXTINGUISH *****
830 IF O<>1 THEN PRINT "DON'T BE SILLY !":RETURN
840 LET LF=V-16:RETURN
850 REM ***** LOCK/UNLOCK *****
860 IF O<>9 THEN PRINT "DON'T BE SILLY !":RETURN
870 IF L(2)<>99 THEN PRINT "YOU HAVEN'T ANY KEYS !":RETURN
880 LET GF=19-V:IF GF=0 THEN LET A$="00":LET B$="00":GOTO 900
890 LET A$="04":LET B$="03"
900 LET N$(3)=LEFT$(N$(3),8)+A$+MID$(N$(3),11):LET N$(4)=LEFT$(N$(4),10)+B$
910 RETURN
920 REM ***** DECODE COMMAND *****
930 LET A$="":LET B$="":PRINT "WHAT NOW ";:INPUT K$:LET LK=LEN(K$):LET K=0
940 LET K=K+1:IF K>LK THEN GOTO 960
950 IF MID$(K$,K,1)<>" " THEN GOTO 940
960 LET A$=LEFT$(K$,K-1):LET LA=LEN(A$)
970 LET V=0:FOR C=1 TO NV:IF A$=LEFT$(V$(C),LA) THEN LET V=C:LET C=99
980 NEXT C:IF V=0 THEN GOTO 1040
990 IF V<10 THEN RETURN
1000 IF K>=LK THEN PRINT A$;" WHAT ...";:INPUT B$:LET LB=LEN(B$):GOTO 1020
1010 LET B$=MID$(K$,K+1):LET LB=LEN(B$)
1020 LET O=0:FOR C=1 TO NO:IF B$=LEFT$(O$(C),LB) THEN LET O=C:LET C=99
1030 NEXT C:IF O>0 THEN RETURN
1040 PRINT:PRINT "SORRY - I DON'T UNDERSTAND"
1050 PRINT:GOTO 930
1060 REM ***** CAN'T DO *****
1070 PRINT "THE ";O$(OB);" STOPS YOU !!":RETURN
1080 REM ***** DWARF *****
1090 IF L(4)<>0 THEN PRINT "A DWARF COMES ROUND A CORNER !":LET DF=1:RETURN
1100 PRINT "SUDDENLY, A DWARF APPEARS. HE"
1110 PRINT "HURLS AN AXE AT YOU, CURSES"
1120 PRINT "WHEN IT MISSES, AND RUNS AWAY":LET L(4)=LC:RETURN
1130 PRINT "THE DWARF THROWS A KNIFE AT YOU ..."
1140 FOR Z=1 TO 400:NEXT Z
1150 IF RND(1)<.8 THEN PRINT "IT MISSES YOU !":RETURN
1160 PRINT "IT HITS YOU... YOU SINK":PRINT "TO THE FLOOR, MORTALLY WOUNDED !"
1170 GOSUB 530:PRINT:STOP
1180 REM ***** LOOK AROUND *****
1190 CLS:PRINT
1200 IF LF=1 AND (L(1)=LC OR L(1)=99) THEN GOTO 1220
1210 IF LC>4 THEN PRINT "IT IS TOO DARK TO SEE...":RETURN
1220 PRINT L$(LC,1):PRINT L$(LC,2)
1230 LET OF=0:FOR K=1 TO NO:IF ABS(L(K))<>LC AND ABS(M(K))<>LC THEN GOTO 1260
1240 IF OF=0 THEN PRINT "YOU SEE ...":LET OF=1
1250 PRINT D$(K)
1260 NEXT K
1270 IF LC<>3 AND LC<>4 THEN GOTO 1300
1280 PRINT "THE GRATING IS ";:IF GF=1 THEN PRINT "UN";
1290 PRINT "LOCKED"
1300 IF LC=16 THEN PRINT "A HOLLOW VOICE SAYS 'HELLO'"
1310 IF DF=1 AND LC>4 THEN PRINT "THERE IS A NASTY DWARF NEXT TO YOU..."
1320 RETURN
1330 REM ***** READ DATA *****
1340 READ NV:FOR K=1 TO NV:READ V$(K):NEXT K
1350 READ NO:FOR K=1 TO NO:READ O$(K),D$(K),L(K),M(K):NEXT K
1360 READ NL:FOR K=1 TO NL:READ L$(K,1),L$(K,2),N$(K):NEXT K
1370 LET MS=150:RETURN
1380 DATA 19,"NORTH","EAST","SOUTH","WEST","DOWN","UP","LIST","SCORE","LOOK"
1390 DATA "TAKE","DROP","WAVE","THROW","FIGHT","SAY","EXTINGUISH","LIGHT"
1400 DATA "UNLOCK","LOCK"
1410 DATA 17
1420 DATA "LANTERN","A BRASS LANTERN",1,0
1430 DATA "KEYS","A BUNCH OF KEYS",1,0
1440 DATA "WAND","A MAGIC WAND",4,0
1450 DATA "AXE","A SMALL GOLDEN AXE",0,0
1460 DATA "BIRD","A LITTLE SINGING BIRD",5,0
1470 DATA "DRAGON","A LARGE GREEN DRAGON !",-15,0
1480 DATA "SNAKE","A COILED HISSING SNAKE",-6,0
1490 DATA "DWARF","A NASTY DWARF WITH A SHARP KNIFE !",0,0
1500 DATA "GRATING","A GRATING SET IN CONCRETE",-3,-4
1510 DATA "BRIDGE","A CRYSTAL BRIDGE OVER THE CHASM",0,0
1520 DATA "SILVER","SOME BARS OF SILVER",6,0
1530 DATA "GOLD","A PILE OF GOLD PIECES",14,0
1540 DATA "JEWELS","A BAG OF JEWELS",13,0
1550 DATA "VASE","A DELICATE MING VASE",15,0
1560 DATA "CUSHION","AN ORNATE CUSHION",16,0
1570 DATA "","SOME WDRTHLESS BITS OF POTTERY",0,0
1580 DATA "HELLO","",0,0
1590 DATA 16
1600 DATA "YOU ARE INSIDE A STONE HUT"
1610 DATA "THERE IS A DOOR TO THE WEST"
1620 DATA "000000020000"
1630 DATA "A PATH LEADS SOUTH."
1640 DATA "THERE IS A HUT TO THE EAST"
1650 DATA "000103000000"
1660 DATA "YOU ARE IN A SHALLOW DEPRESSION"
1670 DATA "A PASSAGE LEADS UNDERGROUND"
1680 DATA "020000000000"
1690 DATA "YOU'RE IN AN UNDERGROUND PASSAGE"
1700 DATA "LIGHT FILTERS THROUGH A GRATING"
1710 DATA "000000050000"
1720 DATA "YOU ARE IN AN EAST-WEST PASSAGE"
1730 DATA "WITH A HOLE IN THE SOUTH WALL"
1740 DATA "000408060000"
1750 DATA "YOU ARE IN A GREAT HALLWAY, WITH"
1760 DATA "DOORS TO THE EAST AND WEST"
1770 DATA "000500070000"
1780 DATA "YOU ARE ON THE EAST BANK OF A"
1790 DATA "DEEP CHASM"
1800 DATA "000600000000"
1810 DATA "YOU ARE IN A MAZE OF TWISTY"
1820 DATA "PASSAGES, ALL ALIKE"
1830 DATA "050910110000"
1840 DATA "YOU ARE IN A MAZE OF TWISTY"
1850 DATA "PASSAGES, ALL ALIKE"
1860 DATA "110910080000"
1870 DATA "YOU ARE IN A MAZE OF TWISTY"
1880 DATA "PASSAGES, ALL ALIKE"
1890 DATA "080910120000"
1900 DATA "YOU ARE IN A MAZE OF TWISTY"
1910 DATA "PASSAGES, ALL ALIKE"
1920 DATA "090812110000"
1930 DATA "YOU ARE IN A MAZE OF TWISTY"
1940 DATA "PASSAGES, ALL ALIKE"
1950 DATA "111013120000"
1960 DATA "YOU HAVE REACHED A DEAD END"
1970 DATA ""
1980 DATA "120000000000"
1990 DATA "YOU ARE ON THE WEST BANK OF THE"
2000 DATA "CHASM. A PASSAGE LEADS NORTH"
2010 DATA "151300000000"
2020 DATA "YOU ARE IN A LARGE CAVERN WITH A"
2030 DATA "STAIRCASE SPIRALLING UPWARDS"
2040 DATA "000014000016"
2050 DATA "THE WAY IS BLOCKED BY A ROCKFALL"
2060 DATA "A STAIRCASE SPIRALS DOWNWARDS"
2070 DATA "000000001500"
