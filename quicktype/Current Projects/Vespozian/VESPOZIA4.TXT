0 GOSUB7000:CLEAR500:DIMCC,ZZ,M$,Z$,N(21):MC=16384:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:Z$=MID$(M$,CC,1):IFZ$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFZ$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
5 CC=1:FORZZ=1TO63STEP3:N(CC)=VAL(MID$("087103113129142149161171181185193200203209215219221223229231233",ZZ,3)):CC=CC+1
6 GOTO10
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORZZ=CC-LEN(M$)+1TOCC:POKEZZ,PEEK(ZZ)-64:NEXT:RETURN
8 FORCC=1TOLEN(M$)STEP2:SOUNDN(ASC(MID$(M$,CC,1))-64),ASC(MID$(M$,CC+1,1))-48:NEXT:RETURN
9 FORZZ=1TO2500:NEXT:RETURN
10 DIML$(15):FORI=1TO15:READL$(I):NEXT
11 DIMB$(15):FORI=1TO15:READB$(I):NEXT
12 DIMO$(19):FORI=1TO19:READO$(I):NEXT
13 DIMR$(12):FORI=1TO12:READR$(I):NEXT
14 DIME$(15):FORI=1TO15:READM$:E$(I)=LEFT$(M$+"    ",4):NEXT
15 DIMD$(15):FORI=1TO15:READM$:D$(I)=LEFT$(M$+"    ",4):NEXT
16 DIMP(19):FORI=1TO19:READP(I):NEXT
17 DIMC(19):FORI=1TO19:READC(I):NEXT
18 PQ=0:GR=0:DIMQ$(5):DIMU$(1)
20 A$="NOTHING":K1=1:K2=1
25 FORI=1TO4:Q$(I)="":NEXTI
30 IN=0
50 JE=0:PN=2:PL=PN:CT=0:JM=-1:PV=6:CY=143:PF=600:CD=2:K$="A THOUGHT IS FRAMED IN YOUR MIND":ST=1:PI=999:SF=1
60 V$="TAKDROLIFLOOEXAOPEUNLLOCCLOWEAREMINSRAIPREKILSLE"
70 N$="PORSTAFORAFTUP DOWOUTBUNSOUNOREASWESBULDOOCRAVES"
80 P$="PSFAUDOBTNEWLRCV"
90 T$="SHUDEVPOLCRAASBSPACONCYLTONBEZGRAMAC005001002003004KEYROCDISPILDOOBUTAROSCRBUNSHE":GOSUB7075
100 IFPN=6THENIFPL=10ORP(10)=99THENP(10)=99:P(11)=99:GOTO103
102 P(10)=6:P(11)=6
103 IFP(8)=10OR(P(7)=10ANDC(7)>2)THENSF=1:GOTO105
104 SF=0
105 IFPV=4ANDP(3)=99THENP(3)=5
106 IFPN=3ANDC(1)=-4ANDC(5)<5THENGOSUB3530:GOTO6050
107 IFC(7)=3ANDP(7)=10THENC(7)=4:PF=PF+2000:Q$(3)="NEW FUEL LOADED"
108 CT=CT+1:IFST=1THENJE=JE+1
109 IFJE<25THEN113
110 IFST=1THENJE=JE+1:IFJE>24THENJE=0:PV=PV+JM:IFPV=CDTHEN112
111 CLS:PRINT"VESPOZIAN PASSING ---":PRINT:PRINTR$(PV):GOSUB9:GOTO113
112 ST=2:PF=PF-100:CLS:PRINT"VESPOZIAN IN ---":PRINT:PRINTR$(PV);" ORBIT":GOSUB9
113 IFCT>200THENCT=1:CY=CY+1
114 IFCD=PVTHENST=2
115 IFST=2THENW$="ORBITING":GOTO117
116 W$="BEARING ON"
117 IFPN=10ANDC(5)<5THEN6010
118 IFPN=14ANDA$="GO NORTH"THEN6055
120 IFPV=3ANDPN=5ANDP(13)=99THENP(13)=5
125 IFP(3)=14THENP(3)=88:PI=CY:Q$(3)="POLYPS VANISH INTO ROCKS"
126 IFCY>PI+19THENP(19)=14:PI=CY
129 IFP(2)=PNAND(PN=9ORPN=14)AND(P(19)<>14ORP(7)<>14)THENQ$(3)="SWITCH STARTS MOTOR. DRILL":Q$(4)="ENGAGES AND SPRAYS DUST!"
130 IFP(19)=14ANDP(2)=14ANDP(7)=14ANDC(7)=2THEN134
131 GOTO135
134 P(19)=88:O$(7)="FUEL CONTAINER":O$(8)=" ":C(7)=3:Q$(3)="SWITCH STARTS MOTOR. DRILL":Q$(4)="ENGAGES AND FILLS CONTAINER"
135 IFP(18)=55THENC(4)=3
140 IFC(5)>4ANDC(5)<7AND(PN=2ORPN=7ORPN=8ORPN=11)THENIG=IG+1:GOTO145
141 IG=0
145 IFIG>0THENP(11)=P(1):IFIG=5THENGOSUB3530:GOTO6070
146 GOTO170
147 IFP(11)=PNTHENIFINT(RND(0)*7+1)=3THENQ$(1)="EVER GET THE FEELING...?"
150 IFP(5)<>88ANDP(5)<>PNANDP(5)<>55ANDP(5)<>12ANDP(5)<>66THENP(5)=88:Q$(1)="STRANGE FEELING...":GOTO160
155 IFP(6)<>PNANDP(6)<>88ANDP(6)<>55ANDP(6)<>12ANDP(6)<>66THENP(6)=88:Q$(1)="STRANGE FEELING..."
160 IFC(4)=2ANDP(18)<>55ANDP(18)<>66ANDP(18)<>99THENP(18)=88:Q$(1)="STRANGE, SOMETHING SEEMS MISSING":P(18)=3
170 IFP(11)=2ANDPN=2ANDK1=14THENGOSUB3530:GOTO6080
175 IFPN=7ORPN=8ORPN=11ORPN=2THENIFINT(RND(0)*3+1)=3THENP(11)=PN:GOTO180
176 IFPN=6ANDPL<>10THEN180
177 P(11)=88
180 IFP(18)=55ANDC(18)=4ANDP(11)=11AND(PN=12ORPN=11)THENGOSUB3530:GOTO6100
181 IFP(18)=55ANDC(18)=4ANDP(11)=11THENGOSUB3530:GOTO6100
185 IFC(1)=3ANDPN<>2ANDPN<>3THENGOSUB3530:GOTO6110
200 FORI=1TOLEN(E$(PN))
210 IFMID$(E$(PN),I,1)="P"THENS$=S$+"PORT, "
220 IFMID$(E$(PN),I,1)="S"THENS$=S$+"S'BOARD, "
230 IFMID$(E$(PN),I,1)="F"THENS$=S$+"FOR'D, "
240 IFMID$(E$(PN),I,1)="A"THENS$=S$+"AFT, "
250 IFMID$(E$(PN),I,1)="U"THENS$=S$+"UP, "
260 IFMID$(E$(PN),I,1)="D"THENS$=S$+"DOWN, "
270 IFMID$(E$(PN),I,1)="O"THENS$=S$+"OUT, "
280 NEXTI:IFS$<>""THENS$=LEFT$(S$,LEN(S$)-2)+"."
330 CLS:M$="I AM "+L$(PN):GOSUB1:M$=B$(PN):GOSUB1:PRINT"ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ";
331 IFLEN(S$)>0THENM$="EXITS: "+S$:GOSUB1
335 IFPN=9ORPN=14THENPRINT"WHICH WAY ?"
340 II=1:IFPN=15ANDK2<>21THEN346
342 M$="":FORI=1TO19:IFP(I)=PNTHENM$=M$+O$(I)+", "
344 NEXTI:IFM$<>""THENM$=LEFT$(M$,LEN(M$)-2)+"."
345 M$="VISIBLE: "+M$:GOSUB1
346 IFPN=5THENPRINT"VESPOZIAN"
360 PRINT"ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ";:IFA$<>""THENPRINT"-----YOU SAID ";A$
365 PRINT:FORI=1TO4:IFQ$(I)<>""THENPRINTQ$(I)
370 NEXTI
375 IFMID$(Q$(2),1,2)="OK"THENSOUND1,1
380 PRINT:PRINT"-----WHAT NOW";
390 PL=PN:FORI=1TO5:Q$(I)="":NEXTI:A$="":G$="":H$="":I$="":J$="":S$="":INPUTA$:IFASC(A$)>90THENGOSUB8050
400 J=1:IFLEN(A$)<3THEN4000
401 IFMID$(A$,1,1)<>" "THENJ=J+1:G$=G$+MID$(A$,1,1):A$=MID$(A$,2):ON-(A$="")GOTO403:GOTO401
402 I$=MID$(A$,2)
403 H$=MID$(G$+"   ",1,3)
404 J$=MID$(I$+"   ",1,3)
405 IFI$=""THENJ=0
406 A$=G$+" "+I$
410 IFH$="INV"THEN460
411 IFH$="WAI"THEN500
412 IFH$="QUI"ORH$="HEL"THEN600
414 IFG$="GO"ORH$="GET"THEN425
415 X$=V$:Y$=H$:GOSUB3500:IFJ=0THENQ$(2)="I DON'T KNOW HOW TO "+G$:GOTO100
416 K1=INT((J-1)/3+1)
417 X$=T$:Y$=J$:GOSUB3500
419 IFJ=0THENQ$(2)="WHAT IS A "+I$+"?":GOTO100
420 K2=INT((J-1)/3)+1
421 ONK1GOTO430,450,700,800,800,900,1000,1100,1200,1300,1400,1500,700,1600,1700,1800
425 X$=N$:Y$=J$:GOSUB3500:IFJ=0THEN4010
426 X$=E$(PN):Y$=MID$(P$,INT((J-1)/3+1),1):GOSUB3500:IFJ=0THEN4010
427 PN=ASC(MID$(D$(PN),J,1))-64:GOTO4020
430 IFK2=20THEN4030
431 IFP(11)=PNTHEN6080
432 IFK2>19THEN4070
433 IFP(K2)=55THEN4040
434 IFP(K2)<>PNTHEN4050
435 IFC(K2)=-2THEN4000
436 IFC(K2)=-1THEN4060
440 IF(K2=8OR(K2=7ANDC(7)=3))ANDP(9)<>55THENQ$(2)="TOO HOT TO HANDLE":GOTO100
441 IFIN>3THENQ$(2)="I'M OVERLOADED ALREADY!":GOTO100
442 IN=IN+1:P(K2)=55:GOTO4020
450 IFK2>19THEN4070
451 IFP(K2)<>55THEN4070
452 IFK2=2AND(PN<>9ANDPN<>14)THENGOSUB3530:GOTO6060
453 IN=IN-1:IFPN=15THENP(K2)=66:Q$(3)="SLIPPED IT UNDER PILLOW":GOTO4020
454 P(K2)=PN
455 GOTO4020
460 IFIN=0THENQ$(2)="NOTHING":GOTO100
462 J=1:M$="":FORI=1TO19:IFP(I)=55THENM$=M$+O$(I)+", "
470 NEXTI:IFM$<>""THENM$=LEFT$(M$,LEN(M$)-2)+"."
471 M$="I AM CARRYING: "+M$:GOSUB1
480 GOSUB9:GOSUB9:GOTO100
500 CT=CT+15:IFST=1THENJE=JE+15
510 GOTO4020
600 Q$(2)="LOOK AROUND AND":Q$(3)="EXAMINE THINGS. TRY WORDS":Q$(4)="LIKE INSERT, LIFT, WAIT ETC.":GOTO100
700 IFK2<>21ORPN=15THEN4000
701 Q$(2)="NOTHING!":FORI=1TO19:IFP(I)=66THEN P(I)=15:Q$(2)="IT'S STILL THERE!":GOTO100
702 NEXTI:GOTO100
800 IFK2<>24THEN812
801 IFPN=8THENQ$(2)="I SEE BUTTON MARKED":Q$(3)="-THINK CONTROL-":GOTO100
802 IFPN=11ANDMID$(E$(11),1,3)="PD "THENE$(11)="PDL"+MID$(E$(11),4):Q$(2)="WOW! A HIDDEN BULKHEAD":GOTO100
805 IFPN=10ANDE$(10)="S   "THENE$(10)="SC  ":P(4)=10:Q$(2)="AHA!":GOTO100
806 IFPN=7THENQ$(2)="COURSE DATA ON SCREEN":GOTO100
807 IFPN=3THENIFC(1)=-3THENQ$(3)="INNER DOOR OPEN.":Q$(4)="OUTER DOOR CLOSED.":GOTO4020
808 IFPN=3THENIFC(1)=-4THENQ$(3)="INNER DOOR CLOSED.":Q$(4)="OUTER DOOR OPEN.":GOTO4020
812 IFK2=20THEN4030
813 IFK2=2THENIFP(2)<>55ANDP(2)<>PNTHEN4030
814 IFK2=2THENQ$(2)="IT HAS HOLLOW CYLINDRICAL CUTTER": Q$(3)="AND SENSITIVE PRESSURE SWITCH":GOTO100
815 IF(PN=3ORPN=2)ANDK2=23 THENQ$(2)="SIGN -AIRLOCK CONTROL-":GOTO100
820 IFK2<18ANDK2>12THENIFP(K2)<>55THEN4070
821 IFK2<18ANDK2>12THENONK2-12GOTO870,871,872,873,874
825 IFK2=20ANDPN=15THENQ$(3)="LOOKS STRANGELY LUMPY...":GOTO4020
826 IFK2=25ANDPN=7THEN4500
830 Q$(3)="NOTHING SPECIAL":GOTO4020
870 Q$(2)="LABEL -YUSES-":GOTO100
871 Q$(2)="LABEL -POLYPS-":GOTO100
872 Q$(2)="LABEL -FUNUS-":GOTO100
873 Q$(2)="LABEL -CONFIDENTIAL-":GOTO100
874 Q$(2)="LABEL -BLOODLINE-":GOTO100
900 IFK2<>22THEN4000
901 IFPN<>11THEN4050
902 IFP(11)=11ANDPN=11 THEN6100
904 IFC(18)=2THENQ$(2)="LOCKED!":GOTO100
905 IFC(18)=4THENQ$(2)="ALREADY OPEN":GOTO100
906 IFP(8)=11THEN6000
910 C(18)=4:E$(11)="PD R":GOTO4020
1000 IFK2<>22THEN4000
1001 IFPN<>11THEN4050
1002 IFC(18)>2THEN4090
1003 IFP(18)<>55THEN4060
1004 C(18)=3:GOTO4020
1100 IFK2<>22THEN4000
1101 IFPN<>11THEN4050
1103 IFP(11)=11ANDPN=11THEN6080
1104 IFC(18)<>3ORP(18)<>55THEN4060
1106 C(18)=3:GOTO4020
1200 IFK2<>22THEN4000
1201 IFPN<>11THEN4050
1203 IFP(11)=11ANDPN=11THEN6080
1204 IFC(18)<>4THEN4060
1206 C(18)=3:E$(11)=LEFT$(E$(11),3):GOTO4020
1300 IFK2<>5ANDK2<>6THEN4000
1301 IFP(K2)<>55THEN4070
1302 IFC(5)=K2THENQ$(2)="WHAT'S THIS I'M WEARING THEN?":GOTO100
1303 IFC(5)>4THENQ$(2)="WEARING "+O$(K2):GOTO100
1304 C(5)=K2:O$(K2)=O$(K2)+" (WORN)":GOTO4020
1400 IF(K2<>5ANDK2<>6)ORC(5)=2THEN4080
1401 C(5)=2:O$(K2)=O$(K2):GOTO4020
1500 IFK2=20THEN4030
1501 IFK2<13ORK2>17THEN4000
1502 IFP(K2)<>55THEN4070
1503 IFPN<>7THENQ$(2)="NO COMPUTER TO TAKE IT":GOTO100
1505 IFP(11)=7ANDK2>12ANDK2<18THENGOSUB3530:GOTO6090
1510 CLS:PRINTO$(K2):
1511 PRINT"SPINS IN ITS DRIVE...":FORI=1TO1500:NEXTI:CLS:
1512 ONK2-12GOTO1550,1561,1563,1566,1570
1550 PRINT"-YUSES SYSTEM-": PRINT:PRINT"STAR SYSTEM ADJACENT TO FUNUS": PRINT "IT HAS 3 PLANETS.":PRINT:PRINTR$(11)
1551 PRINT"- BALL OF FIRE -":PRINT:PRINTR$(10):PRINT" - INHABITED BY MINING COLONY,":PRINT"NO FINDS.":PRINT 
1560 PRINTR$(9):PRINT" - REMOTE DEAD PLANET -":GOTO1569
1561 PRINT"-POLYPS-":PRINT"FREQUENT GALAXY IN MILLIONS":PRINT "AROUND ENERGY RICH PLANETS."
1562 PRINT"REGENERATE EVERY 20 YEARS.":PRINT"ACTIVE POLYPS CAN TURN DEAD":PRINT "PLANET INTO GREENHOUSE EFFECT"
1563 PRINT"PLANET, RICH IN STELLAR ENERGY"
1562 PRINT"AFTER 20 YEARS...":GOTO1569
1563 PRINT"-FUNUS SYSTEM-": PRINT "STAR WITH 7 PLANETS.":PRINT"IN ORDER FROM FUNUS -":PRINT:PRINTR$(2);" -MOLTEN ROCK"
1564 PRINT R$(3);" -UNINHABITED": PRINT R$(4);" -GR'NHOUSE EFFECT": PRINT R$(5);" -INHABITED"
1565 PRINT R$(6): PRINT R$(7): PRINT " - (BOTH ICY WASTES) -": PRINT R$(8): PRINT " - (LUMP OF ROCK) -":GOTO1569
1566 PRINT"-CAPTAIN'S LOG-":PRINT"BEARING TO DOCK WITH BLOODLINE":PRINT"STARGATE IN GYRATES ORBIT."
1567 PRINT"MISSION ACCOMPLISHED - D'TAAN'S":PRINT"EXPERIMENTS CONFIRM POLYP":PRINT"THEORY. PITY BLOODLINE WILL":PRINT "BENEFIT!"
1568 PRINT"GLAD TO GET SHOT OF THIS":PRINT"CREW! SUSPECT GRAKTA IS":PRINT"BLOODLINE SPY. MACHEN,"
1569 PRINT"THE TIAITHEN LIZARD GIVES ME":PRINT"THE CREEPS. CAN'T GET RID OF": PRINT "HIM - THE ONLY PILOT ABOARD!":GOTO1569
1570 PRINT"-BLOODLINE-":PRINT"TYRANNICAL EMPIRE KEEPING":PRINT"CONTROL ON ENERGY SUPPLIES":PRINT "FROM GREENHOUSE PLANETS -"
1571 PRINT"ON WHICH INTERSTELLAR TRAVEL":PRINT"DEPENDS":PRINT"STARGATE IN FUNUS SYSTEM"
1572 PRINT"CAN SUMMON CRUISERS TO": PRINT "QUELL REBELS."
1573 FORI=1TO2000:NEXTI:Q$(2)="YOU REMOVE THE DISK...":GOTO100
1600 IFK2<>23THEN4020
1602 IFP(11)=2ANDPN=2THEN6000
1603 IFPN=8THENIFP(11)=8THEN6000
1604 IFPN=8THENQ$(2)="MACHEN, THE LIZARD PILOT":Q$(3)="RESTRAINS YOU":GOTO100
1606 CLS:IFPN<>13THEN1661
1607 IFPF<1THEN6020
1608 GOSUB3515
1609 PRINT"HEADING,(H)":PRINT"CRYOGEN,(C)":PRINT"LASER,(L)":PRINT"END OF INPUT,(E)"
1612 C$=INKEY$:IFV$="H"THEN1618
1613 IFC$="C"THEN1633
1614 IFC$="L"THEN1639
1615 IFC$="E"THEN4020
1616 GOTO1612
1618 J=0:GOSUB3515:M$="CURRENTLY: "+W$:GOSUB1:PRINTR$(CD):PRINT:PRINT"NEW DESINATION?":INPUTU$(1)
1620 FOR I=1 TO 12: IF U$(1)=R$(I) THEN J=I
1621 NEXT I: IF J=0 THEN PRINT U$(1);" NOT ON DATABASE": GOSUB3527:GOTO1606
1624 IFJ=CDTHEN1606
1625 IFJ>PVTHENJM=1:GOTO1627
1626 JM=-1
1627 CLS:ST=1:JE=0:CD=J:PF=PF-100:PRINT:PRINT"COURSE CORRECTION APPLIED":PRINT:PRINT"NEW BEARING:":
1628 PRINTR$(CD):PRINT:PRINT"FUEL: ";PF;" PICORATH":GOSUB9:GOSUB9
1629 IFPF<1THEN6020
1630 GOTO1606
1633 GOSUB3515:PRINT:PRINT"CRYOGENIC CONTROL":PRINT "ENERGY TO FREEZE CREW - 10 PR/CY"
1634 PRINT:PRINT"HOW MANY CLONE-YEARS FREEZE?":INPUTF$
1635 IFVAL(F$)*10>PFTHENPRINT:PRINT"FUEL DEFICIENCY.":PRINT"INTERLOCK OVERRIDE":PRINT"* OPERATION ABORTED *":GOSUB3525:GOTO1606
1636 PF=PF-10*VAL(F$):CY=CY+VAL(F$):PRINT"stand by":GOSUB9:GOSUB3530:PN=15:Q$(2)="YAWN...!":GOTO100
1639 GOSUB3515: PRINT"LASER CONTROL": PRINT 
1642 PRINT"TARGET?": INPUT U$(1): IF U$(1)="STARGATE      " THEN1664
1643 J=0: FOR I=1 TO 12: IF R$(I)=U$(1) THEN J=1: I=12: NEXT I
1644 NEXT I
1645 IF J=0 THEN PRINT U$(1);" NOT ON DATABASE": GOSUB3527:GOTO1606
1646 PRINT "TARGETED ON ";U$(1): FOR I=1 TO 150: NEXT I: IF PF<1000 THEN PRINT FLASH 1;"FUEL DEFICIENCY"
1647 IF U$(1)="STARGATE      " THEN1650
1649 IF J<>PV THEN PRINT "OUT OF RANGE":GOTO1652
1650 IF U$(1)="STARGATE      " AND PV<>3 THEN PRINTU$(1);" OUT OF RANGE":GOTO1652
1651 GOTO1655
1652 PRINT:PRINT"COMPUTER OVERRIDE": PRINT "* OPERATION ABORTED *": GOSUB3525:GOTO1606
1655 GOSUB3540: IF U$(1)<>"STARGATE      " THEN6030
1656 GOTO6040
1661 IF PN=2 THEN1692
1664 IF PN=3 THEN1692
1665 IF PN<>4 THEN4050
1666 GOSUB3515:PRINT"THINK 'DESTINATION' OR 'END'":INPUT U$(1)
1667 IFU$(1)="VESPOZIAN    "THENIFP(1)=3THENPRINT"SHUTTLE AT ESPOZIAN":PRINT:PRINT"* MISSION ABORTED *":GOSUB3525:GOTO100
1668 IFU$(1)="VESPOZIAN     "THENPRINT"IN FLIGHT VESPOZIAN":GOSUB3520:PRINT"DOCKING":GOSUB3520:P(1)=3:D$(4)="C   ":GOTO100
1672 IFMID$(U$(1),1)="E"THEN4020
1675 J=0: FOR I=1 TO 12: IF U$(1)=R$(I) THEN J=I
1678 NEXT I: IF J=0 THEN PRINT F$;"NOT ON DATABASE": GOSUB3527:GOTO100
1681 IF J<>PV THEN PRINTU$(1);"OUT OF RANGE": GOSUB3520:GOTO100
1684 IF ST=1 THEN PRINT"VESPOZIAN NOT IN ": PRINTU$(1);" ORBIT": PRINT:PRINT "* OPERATION ABORTED *": GOSUB3525:GOTO100
1687 IFJ<>9THEN1689
1688 GOTO1690
1689 PRINT"CONDITIONS ON ";U$(1):PRINT"NOT SUITABLE FOR SHUTTLE":PRINT"LANDING":PRINT"* OPERATION ABORTED *":GOSUB3525:GOTO100
1690 IF C(1)<>-4 THEN PRINT"SHUTTLE BAY DOORS CLOSED": GOSUB3520: PRINT "* OPERATION ABORTED *": GOSUB3525:GOTO100
1691 PRINT"HEADING - ";U$(1): GOSUB3520: PRINT"LANDED ON ";U$(1): P(1)=9: D$(4)="I   ": GOSUB3520:GOTO100
1692 IF PN=2 THEN IF C(1)=-2 THEN C(1)=-3: B$(2)="BLUE BUTTON BY OPEN BULKHEAD":E$(2)="PFL ":GOTO4020
1693 IF PN=2 THEN IF C(1)=-3 THEN C(1)=-2: B$(2)="BLUE BUTTON BY CLOSED BULKHEAD":E$(2)="PF  ":GOTO4020
1694 IFPN=3AND C(1)=-3 THEN C(1)=-4: E$(3)="OT  ": D$(3)="ED  ": Q$(3)="INNER DOOR CLOSES": Q$(4)="OUTER DOOR OPENS":GOTO4020
1695 IFPN=3AND C(1)=-4 THEN C(1)=-3: E$(3)="TP  ": D$(3)="DB  ": Q$(3)="OUTER DOOR CLOSES": Q$(4)="INNER DOOR OPENS":GOTO4020
1698 GOTO4050
1700 Q$(2)="NO CHANCE-TOO RISKY":GOTO100
1800 Q$(2)="I'M NOT TIRED":GOTO100
3500 J=0:FORI=1TOLEN(X$)STEPLEN(Y$)
3505 IFY$=MID$(X$,I,LEN(Y$))THENJ=I:I=LEN(X$)+3
3510 NEXTI:RETURN
3515 CLS:PRINTK$:PRINT:RETURN
3520 FORI=1TO300:NEXTI:RETURN
3525 FORO=1TO6:SOUND183,1:SOUND150,1:NEXTO:GOSUB9:RETURN
3526 SOUND183,4:RETURN
3527 FORO=1TO10:SOUND100,1:NEXTO:RETURN
3530 FORI=1TO15:CLSRND(8):NEXTI:CLS:RETURN
3540 FORI=1TO10:POKE49151,64:FORZZ=1TO25:NEXT:POKE49151,0:FORZZ=1TO25:NEXT:NEXTI:CLS:RETURN
3550 RETURN 
3560 GOTO4020
4000 Q$(2)="IMPOSSIBLE!":GOTO100
4010 Q$(2)="I CAN'T GO "+I$:GOTO100
4020 Q$(2)="OK":GOTO100
4030 Q$(2)="REFERENCE DISC BY ITS NUMBER":GOTO100
4040 Q$(2)="I'M ALREADY CARRYING IT":GOTO100
4050 Q$(2)="I DON'T SEE IT HERE":GOTO100
4060 Q$(2)="I CAN'T YET!":GOTO100
4070 Q$(2)="I'M NOT CARRYING IT":GOTO100
4080 Q$(2)="YOU MUST BE JOKING!":GOTO100
4090 Q$(2)="OK-NOTHING HAPPENS":GOTO100
4100 Q$(2)="CAP'N WINKS AT ME AND": Q$(3)="GLANCES AT GRAKTA":GOTO100
4110 Q$(2)="HUH?":GOTO100
4500 CLS:PRINT"CLONE YEAR:";CY;"   ZONETIME:";CT:PRINT:PRINT"ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ";
4501 PRINT:PRINT"VESPOZIAN ";W$," ";TAB(20)R$(CD):IFST=1THENPRINT"ETA: ";CT+ABS(CD-PV)*25-JE
4505 PRINT:PRINT"NEAREST BODY: "TAB(9)R$(PV):IFPV=2THENPRINTTAB(9)"BLOODLINE STARGATE"
4510 PRINT:PRINT"FUEL: ";TAB(12)PF;" PICORATH":PRINT:PRINT"LASER: ";:IFPF<1000THENPRINT"INOPERATIVE":GOTO4520
4511 PRINT"OPERATIVE"
4520 PRINT:PRINT"ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ";
4521 PRINT:PRINT"THINK CONTROL REPORTING":M$="B4B4B4B4":GOSUB8:GOTO4020
4900 DATA"IN THE CREWS QUARTERS","IN THE RESEARCH LAB WITH","IN THE SHUTTLE BAY","IN A SHUTTLE"
4901 DATA"OUTSIDE THE HULL","IN THE CAPTAINS CABIN","IN THE NAVIGATION AND","ON THE BRIDGE"
4902 DATA"ON YUSES FAR UNDER","AS VESPOZIANS FUEL CORE"
4903 DATA"IN A METAL GANGWAY WITH A","IN A SUIT ROOM","IN A SECRET CONTROL ROOM","ON YUSES FAR","IN BY BUNK-IT HAS CLEAN"
4904 DATA"LINED WITH BUNKS","BLUE BUTTON BY A CLOSED BULKHEAD","BLUE BUTTON ON WALL","WITH-THINK CONTROL-BUTTON"
4905 DATA"OF THE VESPOZIAN","","COMPUTER ROOM","","A PURPLE SKY","I CAN FEEL HEAT"
4906 DATA"DOOR","","WITH DUPLICATE CONTROL","UNDER AN INDIGO SKY","SHEETS AND LARGE PILLOW"
4907 DATA"AUTO SHUTTLE","STRANGE DEVICE","*POLYPS*","CRAWLWAY","ASBESTOS SUIT","SPACE SUIT","METAL CONTAINER"
4908 DATA"FUEL CYLINDER","INSULATED TONGS","CAP'N BEZEL","GRAKTA","MACHEN","DISK 005","DISK 001","DISK 002","DISK 003"
4909 DATA"DISK 004","KEY","GLOWING ROCK"
5005 DATA "FUNUS","LITTLE FUNUS","GYRATES","ROTH","BULGEN","GREATER BULGEN","OUTER FUNUS","FAR FUNUS"
5006 DATA"YUSES FAR","MID YUSES","YUSES NEAR","YUSES","DSB","PF","P","O","V","OC","AFUP","AU","TE","S","PD","P","O","NEW","U"
5007 DATA "HBO","AGC","B","C","C","GJ","BHKF","GA","DN","KF","JGML","K","K","NNI","A"
5008 DATA 3,2,99,99,12,12,6,10,2,6,7,8,66,2,8,6,7,13,99
5009 DATA -2,2,2,-2,2,2,2,2,2,-2,-2,-2,2,2,2,2,2,2,-2
6000 CLS:PRINT "GRAKTA, BLOODLINE SPY, BLASTS": PRINT "YOU WITH HIS LASER": PRINT "YOU ARE NOW A PILE OF"
6001 PRINT"YUSES DUST!":GOTO6200
6010 CLS:PRINT "AARGH...!":PRINT "WENT INTO THE FUEL CORE WITHOUT": PRINT "PROTECTION - SEARING HEAT . .":GOTO6200
6020 CLS:PRINT "WITH NO FUEL, THE VESPOZIAN": PRINT "WANDERS AIMLESSLY OUT": PRINT "OF CONTROL, LOST IN SPACE":GOTO6200
6030 CLS:PRINT "TOO CLOSE PROXIMITY": PRINT "THE VESPOZIAN IS RIPPED APART": PRINT "BY THE EXPLOSION":GOTO6200
6040 CLS:PRINT"WHAM!":PRINT"THE BLOODLINE STARGATE IS":PRINT"VAPOURISED. YOU HAV SUCCEEDED"
6041 PRINT"IN YOUR MISSION! CONGRATULATIONS":GOTO6202
6050 CLS:PRINT "INNER DOOR CLOSES": PRINT "AIR EVACUATED": PRINT "OUTER DOOR OPENS - NO SPACE SUIT"
6051 PRINT"YOU PERISHED AND DRIFT INTO SPACE":GOTO6200
6055 CLS:PRINT"YOU HAVE STUMBLED BLINDLY ONTO": PRINT "RAZOR SHARP ROCK": PRINT
6056 PRINT"YOUR SUIT HAS RIPPED AND YOU": PRINT "DIE FROM LACK OF OXYGEN":GOTO6200
6060 CLS:PRINT "PRESSURE SWITCH OPERATES": PRINT "DRILL ENGAGES AND BORES THROUGH": PRINT "CRAFT."
6061 IF C(5)=6 THEN PRINT "YOU DRIFT OFF INTO SPACE": PRINT "AND PERISH WHEN OXYGEN RUNS OUT":GOTO6200
6065 PRINT "YOU BURST APART WITH THE": PRINT "DEPRESSURISATION!":GOTO6200
6070 CLS:PRINT "WALKING AROUND IN ";O$(K2): PRINT "HIGHLY SUSPICIOUS!": PRINT "GRAKTA, BLOODLINE SPY BLASTS YOU":GOTO6200
6080 CLS:PRINT "GRAKTA, BLOODLINE SPY, BLASTS": PRINT "YOU FOR TREACHERRY AGAINST THE": PRINT "BLOODLINE EMPIRE!":GOTO6200
6090 CLS:PRINT"GRAKTA VAPOURISES YOU ON":PRINT"SUSPICION OF SPYING BY":PRINT"RETRIEVING COMPUTERISE DATA"
6091 PRINT"YOU PERISH...":GOTO6200
6100 CLS:PRINT"GRAKTA NOTICES SUIT ROOM DOOR":PRINT"OPEN. FINDS KEY ON YOU AND"
6101 PRINT"BLASTS YOU TOWARDS FUNUS": PRINT "YOU PERISH...":GOTO6200
6110 CLS:PRINT"GRAKTA NOTICES SHUTTLE BAY":PRINT "DOOR OPEN, AND SUSPECTS A PLOT":PRINT"AGAINST THE BLOODLINE EMPIRE"
6111 PRINT"YOU AR EJECTED IN THE SHUTTLE":PRINT"AND FOREVER LOST IN SPACE"
6200 FORO=15TO1STEP-1:SOUNDO,1:NEXTO:PRINT:PRINT:PRINT"YOUR ADVENTURE IS OVER":PRINT "IF YOU WOULD LIKE ANOTHER"
6201 PRINT"GAME THEN PRESS 'Y'":GOTO6205
6202 GOSUB3525:GOSUB3526:GOSUB3527:PRINT:PRINT:PRINT"YOUR ADVENTURE IS OVER":PRINT"IF YOU WOULD LIKE ANOTHER"
6203 PRINT"GAME THEN PRESS 'Y'"
6205 M$=INKEY$:IFM$=""THEN6205
6210 IFM$="Y"THENCLS:RUN
6215 PRINT:PRINT"GOODBYE THEN":END
7000 CLS0
7005 PRINT"€€€€€€€€€€€€€€€€€€€€€Â€Óíÿÿþÿó€€";
7010 PRINT"€€ÁÀÄ€€€€€€€€€Â€€€€€€€Öáÿøÿþóÿû€";
7011 PRINT"€€€€€€€€€€€€€€€€€€€€€Õ€ïú÷úûõÿÿú";
7012 PRINT"€€€€€È€€€€€€€€€€€€€€€Úåÿûþù÷óÿÿÿ";
7013 PRINT"€€€€€€€€€€€€€Â€€€€€€€Úåþÿÿÿþþþýÿ";
7014 PRINT"€€€€€€€€€€€€€€€€€€€Â€Ùäÿÿû÷ÿÿÿÿþ";
7015 PRINT"€€ÁÀ€€€€€€€€€€€€€€€€ÀÔÒíÿûý÷ÿùÿø";
7016 PRINT"€€€€€€€€€Á€€€€€€€€€€€€ÔÓíÿó÷ûÿø€";
7017 PRINT"€€€€€€€€€€€ÄÍÏÃÃÏÃÃ€€€€€Üìüüü€€€";
7018 PRINT"€€€€€€”›üû»²ÃÍÏËËËÃÏÃÀ€€€€€€€€€€";
7019 PRINT"€€€€€€‘žóþ¾¸ÌÉÃÃÃÃÃÈ€€€€€€€€€ÈÀ€";
7020 PRINT"ÀÀ€€€€€€€€€€ÄÌÀ€€€€€€€€€€€€€€€€Á";
7021 PRINT"€È€€€€ÁÀ€€€€€€€€È€€€€€€€€À€€€€€€";
7022 PRINT"€€€€€€€€€€€€€€€€€€€€€€€€€Ä€€€€€€";:RETURN
7075 PRINT@448,;:M$="BY KEITH CAMPBELL. (C) C&VG 1983":GOSUB7
7080 PRINT@487,;:M$="PRESS 'P' TO PLAY":GOSUB7:PRINT@0,;:M$="THE VESPOZIAN AFFAIR":GOSUB7
7085 M$=INKEY$:IFM$=""THEN7085
7090 IFM$="P"THENRETURN
7095 GOTO7085
