0 CLEAR900:DIMCC,ZZ,MC,M$,I$:MC=16384:CC=RND(-(PEEK(9)*256+PEEK(10))):GOTO20
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORZZ=CC-LEN(M$)+1TOCC:POKEZZ,PEEK(ZZ)-64:NEXT:RETURN
20 GOSUB30:CLS0:PRINT@231,:M$="STELLAR  EMPIRES":GOSUB7:FORI=1TO2500:NEXT:CLS:GOTO50
30 Y1=0:Y2=31:X1=8:X2=55
40 CLS0:SET(X1,Y1,5):SET(X1,Y2,5):SET(X2,Y1,5):SET(X2,Y2,5):IFY1=15THENRETURN
41 Y1=Y1+1:Y2=Y2-1:X1=X1+2:X2=X2-2:GOTO40
50 X$="":INPUT"HOW MANY PLAYERS (2 TO 4)";X$:NP=VAL(X$):IFNP<2ORNP>4THEN50
60 X$="":INPUT"FOR HOW LONG DO YOU WANT TO     PLAY, IN TURNS";X$:MT=VAL(X$):IFMT<1THEN60
70 NS=7+RND(4+NP)+2*NP:NP=NP-1:T=0:C=RND(3)-1:D=RND(4):DIMS(NS,5+NP),N$(NS),E(NP,1),W(NP,9,2),F$(NP),B(NS,4+NP)
80 PRINT:PRINT"THERE ARE"NS+1"STAR SYSTEMS"
90 FORI=0TONS:READN$(I):FORJ=0TONP+5:S(I,J)=0:NEXT:NEXT
100 FORI=0TONP:FORJ=0TO9:FORK=0TO2:W(I,J,K)=0:NEXT:NEXT:NEXT
110 FORP=0TONP:I=RND(4)+P*4:E(P,0)=I:E(P,1)=0:S(I,0)=4:S(I,3)=P:S(I,4)=5:S(I,5+P)=1
120 ONP+1GOSUB130,140,150,160:GOTO170
130 X=0:Y=0:RETURN
140 Y=7:IFNP=2THENX=3:RETURN
141 X=7:RETURN
150 X=7:IFNP=2THENY=1:RETURN
151 Y=0:RETURN
160 X=0:Y=7:RETURN
170 S(I,1)=X+RND(3):S(I,2)=Y+RND(3):CLS:PRINT"PLAYER"P+1
180 INPUT"PLEASE ENTER YOUR NAME";F$(P):PRINT
190 IFLEN(F$(P))=INT(LEN(F$(P))/2)*2THENF$(P)=F$(P)+" "
200 M$="PRESS enter WHEN READY TO ENTER YOUR HOME STAR NAME.":GOSUB1
210 M$="OTHER PLAYERS MAY NOT WATCH!":GOSUB1:INPUTX$:CLS
220 M$="THE REGULAR NAME IS "+N$(I):GOSUB1:X$=LEFT$(N$(I),1):M$="THE NAME MUST START WITH "+X$+".":GOSUB1
230 INPUT"ENTER NEW NAME ";Y$:IFX$<>LEFT$(Y$,1)THENPRINT:GOTO220
231 N$(I)=Y$:CLS:PRINT"PLEASE WAIT...":NEXT
240 FORI=0TONS:IFS(I,0)=4THEN320
241 C=C+1:IFC=4THENC=1
250 D=D+1:IFD=5THEND=1
260 X=RND(5):Y=RND(5):OND-1GOSUB270,280,290:GOTO300
270 X=X+5:RETURN
280 Y=Y+5:RETURN
290 X=X+5:Y=Y+5:RETURN
300 FORJ=0TONS:IFX=S(J,1)ANDY=S(J,2)THEN260
301 NEXT
310 S(I,0)=C:S(I,1)=X:S(I,2)=Y:S(I,3)=-1:IFC=3THENS(I,4)=RND(3)
320 NEXT
330 PRINT"WORKING...":T=T+1:FORI=0TONS:FORJ=0TO4+NP:B(I,J)=0:NEXT:C=S(I,3):IFC=-2THENC=-1
340 B(I,4+C)=S(I,4):NEXT
350 FORP=0TONP:FORI=0TO9:IFW(P,I,2)<>TTHEN380
351 C=W(P,I,0)
360 S(C,5+P)=1:IFB(C,4+P)=0THENB(C,0)=1
370 B(C,4+P)=B(C,4+P)+W(P,I,1):FORJ=0TO2:W(P,I,J)=0:NEXT
380 NEXT:NEXT
390 FORI=0TONS:C=0:FORJ=3TO4+NP:IFB(I,J)>0THENC=C+1:K=J
400 NEXT:IFC=0THEN470
401 IFC=1THENB(I,1)=K-4:S(I,3)=K-4:S(I,4)=B(I,K):GOTO470
410 B(I,0)=2:X=0:Y=0:X1=0:FORJ=3TO4+NP:IFB(I,J)=XTHENY=Y+1:GOTO420
411 IFB(I,J)>XTHENX=B(I,J):X1=J:Y=1
420 NEXT:IFY>1THEN460
430 B(I,1)=X1-4:Y=0:FORJ=3TO4+NP:IFJ=X1THEN440
431 IFB(I,J)>YTHENY=B(I,J)
440 NEXT:B(I,2)=INT(Y/2+0.5):S(I,3)=B(I,1):S(I,4)=B(I,X1)-B(I,2):IFS(I,0)=3ANDS(I,3)<0THENS(I,3)=-2
450 GOTO470
460 B(I,1)=-2:S(I,3)=-1:S(I,4)=0:IFS(I,0)=3THENS(I,3)=-2:S(I,4)=RND(2)
470 NEXTI
480 FORP=0TONP:FORI=0TO9:IFW(P,I,2)=0THENW(P,I,2)=9999
490 NEXT
500 C=0:FORI=0TO8:IFW(P,I,2)<=W(P,I+1,2)THEN520
510 FORJ=0TO2:K=W(P,I,J):W(P,I,J)=W(P,I+1,J):W(P,I+1,J)=K:NEXT:C=C+1
520 NEXT:IFC>0THEN500
530 FORI=0TO9:IFW(P,I,2)=9999THENW(P,I,2)=0
540 NEXT:NEXT
550 FORP=0TONP:IFE(P,1)=-1THEN1470
551 GOSUB30:CLS:PRINTTAB(12)"TURN#"T:PRINT@64,"================================";
552 PRINT@172,"PLAYER"P+1:PRINT@265,"PRESS ANY KEY":PRINT@299,"WHEN READY":X$=INKEY$
560 PRINT@352,"================================";:PRINT@145-(4+LEN(F$(P)))/2*2,"EMPEROR "F$(P);
570 X$=INKEY$:IFX$=""THEN570
571 CLS:PRINT"PLAYER"P+1:GOSUB580:GOTO750
580 C=0:PRINT:PRINT"BATTLE RESULTS FOR TURN"T:PRINT
590 FORI=0TONS:IFB(I,0)=0ORB(I,4+P)=0THEN730
591 C=C+1
600 M$=N$(I)+" ("+MID$(STR$(S(I,1)),2)+","+MID$(STR$(S(I,2)),2)+") C:"+STR$(S(I,0)):GOSUB1:GOTO620
610 IFC>1THENX$="S":RETURN
611 X$="":RETURN
620 C=B(I,4+P):GOSUB610:IFB(I,0)=1THEN625
621 GOTO630
625 M$="YOUR"+STR$(C)+" STARSHIP"+X$+" ARRIVED AT THE STAR SYSTEM.":GOSUB1
626 M$="NO OPPOSITION WAS REPORTED. THE STAR SYSTEM OF "+N$(I)+" IS NOW PART OF YOUR EMPIRE.":GOSUB1:GOTO720
630 M$="YOUR"+STR$(C)+"STARSHIP"+X$+" WENT INTO BATTTLE AGAINST":GOSUB
640 IFB(I,3)>0THENC=B(I,3):GOSUB610:M$+STR$(C)+"NATIVE WARSHIP"+X$:GOSUB1
650 FORJ=0TONP:IFJ=PORB(I,4+J)=0THEN670
660 C=B(I,4+J):GOSUB610:M$=MID$(STR$(C),2)+"STARSHIP"+X$+" FROM YOUR ENEMY PLAYER"+STR$(J+1):GOSUB1
670 NEXT
680 IFB(I,1)=-2THENM$="ALL STARSHIPS CONCERNED WERE DESTROYED.":GOSUB1:GOTO720
690 C=B(I,2):GOSUB610
691 IFB(I,1)=-1THENM$="THE NATIVE FORCE WAS VICTORIOUS, BUT THEY LOST"+STR$(C)+"WARSHIP"+X$:GOSUB1:GOTO720
700 IFB(I,1)<>PTHENM$="PLAYER"+STR$(B(I,1)+1)+" WAS THE VICTOR, BUT HE LOST"+STR$(C)+"STARSHIP"+X$:GOSUB1:GOTO720
710 M$="YOUR FORCE WAS VICTORIOUS. THE STAR SYSTEM "+N$(I)+" IS NOW PART OF YOUR EMPIRE.":GOSUB1
711 M$="YOU DID LOSE"+STR$(C)" STARSHIP"+X$+".":GOSUB1
720 INPUT"PRESS enter TO CONTINUE";X$:PRINT
730 NEXT:IFC=0THENM$="THERE HAVE BEEN NO BATTLES OR NEW ARRIVALS THIS TURN":GOSUB1:PRINT
740 RETURN
750 FORI=0TONS:IFS(I,0)=4THEN1640
751 IFS(I,0)<>3ORS(I,3)<>PTHEN870
760 C=25-5*S(I,4):IFC<5THENC=5
770 X=RND(100):IFX>CTHEN870
780 M$="THERE HAS BEEN A REBELLION ON "+N$(I)+"!":GOSUB1
790 C=20*S(I,4):X=RND(100):IFX>CTHEN830
800 M$="WHICH WAS QUICKLY PUT DOWN BY YOUR LOYAL TROOPS.":GOSUB1
810 C=RND(3):IFC=>S(I,4)THENC=S(I,4)-1:IFC=0THEN860
820 GOSUB610:M$="BUT THE REBELS DID DESTROY"+STR$(C)" STARSHIP"+X$:GOSUB1:S(I,4)=S(I,4)-C:GOTO860
830 M$="WHICH COULD NOT BE CONTAINED BY YOUR FORCES!":GOSUB1
840 C=S(I,4):GOSUB610:M$="YOU HAD"+STR$(C)+" STARSHIP"+X$+" STATIONED AT "+N$(I)+", ALL WERE DESTROYED.":GOSUB1
850 M$="LONG LIVE THE REVOLUTION!":GOSUB1:S(I,3)=-2:S(I,4)=RND(2)
860 INPUT"PRESS enter TO CONTINUE";X$:PRINT
870 NEXTI
880 IFS(E(P,0),3)<>PTHENM$="NO HOME STAR - NO INCOME!":GOSUB1:GOTO1000
890 PRINT:PRINT"INCOME CALCULATION":C=0
900 FORI=0TONS:IFS(I,3)<>PTHEN970
910 ONS(I,0)GOSUB920,930,940,950:C=C+X:GOTO960
920 X=1:RETURN
930 X=RND(3):RETURN
940 X=2+RND(3):RETURN
950 X=5+RND(5):RETURN
960 PRINTLEFT$(N$(I),14)TAB(16)S(I,0)TAB(24)STR$(X)"RU."
970 NEXT:PRINT"TOTAL OUTPUT:"STR$(C);"RU.":E(P,1)=E(P,1)+C
980 PRINT"TOTAL RESOURCE UNITS:"E(P,1):C=E(P,1)/5:GOSUB610
990 E(P,1)=E(P,1)-5*C:M$="THIS ALLOWS PRODUCTION OF"+STR$(C)+" STARSHIP"+X$:GOSUB1:S(E(P,0),4)=S(E(P,0),4)+C
991 INPUT"PRESS enter TO CONTINUE";X$:PRINT
1000 X$="":M$=LEFT$("TURN"+STR$(T)+" ENTER COMMAND (0=LIST)      ",32):GOSUB7:INPUTX$:Z=VAL(X$)
1010 ONZ+1GOSUB1020,1090,1240,1280,580,1320,1370:IFZ=7THEN1470
1011 IFZ=8THEN1460
1012 GOTO1000
1020 PRINT"1. DISPLAY STAR-MAP"
1030 PRINT"2. DISTANCE BETWEEN TWO STARS"
1040 PRINT"3. LIST STARSHIPS IN HYPERSPACE"
1050 PRINT"4. REVIEW BATTLE DATA"
1060 PRINT"5. LIST DATA ON STAR SYSTEMS"
1070 PRINT"6. SEND STARSHIPS TO HYPERSPACE"
1080 PRINT"7. END TURN":PRINT"8. RESIGN FROM GAME":RETURN
1090 CLS:GOSUB1110:PRINT@0,:FORI=1TO10:PRINTTAB(3*I-1)STR$(I);:NEXT
1100 FORI=1TO10:PRINTMID$(STR$(I)+" ",2,2);:FORJ=1TO10:PRINTTAB(J*3-1)MID$("응횬쩜응응응응응응응응",RND(20),3);:NEXT:GOTO1200
1110 PRINT@416,"KEY: 1ST CHARACTER=CLASS"
1130 PRINT"     2ND CHARACTER=NAME"
1150 PRINT"     3RD "CHR$(168)"=YOU, "CHR$(184)"=SOMEONE ELSE";:RETURN
1200 NEXT:FORI=0TONS:IFS(I,5+P)=1THENX$=RIGHT$(STR$(S(I,0)),1):GOTO1210
1201 X$="?"
1210 X$=X$+CHR$(I+65):IFS(I,3)=PTHENC=168:GOTO1220
1211 C=184
1220 M$=X$:PRINT@(S(I,1)*32+S(I,2)*3)-1,;:GOSUB7:PRINTCHR$(C);:NEXT
1230 PRINT@352,:RETURN
1240 PRINT:PRINT"LETTERS OF THE TWO STAR NAMES?":INPUTX$,Y$:IFX$=""ORY$=""THENX$="Z":Y$="Z":GOTO1250
1241 X=ASC(X$)-65:Y=ASC(Y$)-65
1250 IFX<0ORX>NSORY<0ORY>NSTHENPRINT"YOU DONT MAKE SENSE!!!":RETURN
1260 GOSUB1270:GOSUB610:PRINT"THE DISTANCE IS"C"PARSEC"X$:PRINT:RETURN
1270 C=INT(SQR((S(X,1)-S(Y,1))^2+(S(X,2)-S(Y,2))^2)+0.5):RETURN
1280 PRINT:PRINT"STARSHIPS IN HYPERSPACE":IFW(P,0,2)=0THENPRINT"THERE ARE NONE":PRINT:RETURN
1290 FORI=0TO9:IFW(P,I,2)=0THEN1310
1300 C=W(P,I,1):GOSUB610:PRINTC"STARSHIP"X$" TO "N$(W(P,I,0))TAB(40)"  ARRIVE TURN"W(P,I,2)
1310 NEXT:PRINT:RETURN
1320 PRINT:PRINT"DATA ON STAR SYSTEMS":PRINT"SYSTEM"TAB(13)"LOC"TAB(18)"CLASS"TAB(25)"SHIPS"
1330 FORI=0TONS:IFS(I,3)<>PTHEN1360
1340 PRINTLEFT$(N$(I),11)TAB(12)STR$(S(I,1))","MID$(STR$(S(I,2)),2)TAB(19)STR$(S(I,0))TAB(25);
1350 IFS(I,3)=PTHENPRINTSTR$(S(I,4)):GOTO1360
1351 PRINT" 0"
1360 NEXT:PRINT:RETURN
1370 FORI=0TO9:IFW(P,I,2)=0THEN1380
1371 NEXT:PRINT"10 ALREADY FLEETS IN HYPERSPACE":RETURN
1380 PRINT:INPUT"FIRST LETTER OF START AND END   STAR SYSTEMS";X$,Y$:IFX$=""ORY$=""THENX$="Z":Y$="Z":GOTO1390
1381 X=ASC(X$)-65:Y=ASC(Y$)-65
1390 IFX<0ORX>NSORY<0ORY>NSTHEN1250
1400 IFX=YTHENPRINT"YOU DONT MAKE SENSE":RETURN
1410 IFS(X,3)<>PTHENM$="YOU HAVE NO STARSHIPS AT "+N$(X)+".":GOSUB1:RETURN
1420 C=S(X,4):GOSUB610:PRINT"THERE ARE"C"SHIP"X$" AT "N$(X)
1421 X$="":INPUT"HOW MANY ARE LEAVING";X$:K=VAL(X$):IFK<1THENPRINT"A GHOST FLEET EH?":RETURN
1430 IFK>S(X,4)THENPRINT"TOO MANY!":RETURN
1440 S(X,4)=C-K:IFS(X,4)=0THENS(X,3)=-1:IFS(X,0)=3THENS(X,3)=-2
1450 W(P,I,0)=Y:W(P,I,1)=K:GOSUB1270:W(P,I,2)=T+C:M$="THE FLEET WILL ARRIVE ON TURN"+STR$(T+C):GOSUB1:PRINT:RETURN
1460 E(P,1)=-1
1470 CLS:NEXTP
1480 FORI=0TONS:IFS(I,3)<>-2THEN1500
1490 IFS(I,0)=4THENS(I,4)=S(I,4)+1:GOTO1500
1491 IFRND(10)<7THENS(I,4)=S(I,4)+1
1500 NEXT:IFT=MTTHEN1560
1501 K=0
1510 FORP=0TONP:IFE(P,1)=-1THENK=K+1:GOTO1520
1511 C=P
1520 NEXT:IFK<>NPTHEN330
1521 GOSUB30
1530 PRINT"PLAYER"C+1"HAS WON!!"
1540 PRINT:INPUT"PLAY AGAIN (Y/N)";X$
1550 IFLEFT$(X$,1)="Y"THENRUN
1551 END
1560 FORP=0TONP:W(P,0,0)=0:NEXT
1570 FORI=0TONS:IFS(I,3)>-1THENW(S(I,3),0,0)=W(S(I,3),0,0)+S(I,0)
1580 NEXT:FORP=0TONP:IFE(P,1)=-1ORS(E(P,0),3)<>PTHENW(P,0,0)=0
1590 PRINT"PLAYER"P+1"HAS A SCORE OF"W(P,0,0):NEXT
1600 X=0:Y=0:C=0:FORP=0TONP:IFW(P,0,0)=XTHENY=Y+1:GOTO1610
1601 IFW(P,0,0)>XTHENX=W(P,0,0):Y=1:C=P
1610 NEXT:IFY>1THENPRINT"THERE IS NO DEFINITE WINNER":GOTO1540
1620 PRINT:PRINT"      PLAYER"C+1"HAS WON!!":GOTO1540
1630 DATAALTAIR,BETELGEUSE,CAPELLA,DENEB,ERIDANI,FORNAX,GEMINI,HADAR,INDI,JACARA,KATENOR,LACERTAE,MIZAR,NORYON,OPIUM
1631 DATAPROCYON,QUEATUS,RIGEL,SIRIUS,THORUS,UTOPIA,VENORA,WEREWORLD,XARNIA
1640 IFS(I,3)=PANDI<>E(P,0)THEN1650
1641 GOTO870
1650 C=50-S(I,4)*5:IFC<10THENC=10
1660 X=RND(100):IFX>CTHEN870
1670 M$="THERE HAS BEEN A REVOLT ON THE CLASS 4 SYSTEM OF "+N$(I):GOSUB1
1680 C=S(I,4)*10:X=RND(100):IFX>CTHEN1720
1690 M$="WHICH WAS PUT DOWN WITH SOME DIFFICULTY BY YOUR LOYAL TROOPS!":GOSUB1
1700 C=RND(5):IFC>=S(I,4)THENC=S(I,4)-1:IFC=0THEN860
1710 GOSUB610:M$="THE WELL-EQUIPPED REBELS DID DESTROY"+STR$(C)+" STARSHIP"+X$:GOSUB1:S(I,4)=S(I,4)-C:GOTO860
1720 C=S(I,4):GOSUB610:M$="YOUR FORCE OF"+STR$(C)" STARSHIP"+X$+" WAS VASTLY OVERWHELMED "
1721 M$=M$+"BY THE REBEL ARMY...  LONG LIVE THE REVOLUTION!":GOSUB1
1730 S(I,3)=-2:S(I,4)=RND(3)
1740 FORC=0TONP:IFE(C,0)=ITHENCC=C:C=NP:NEXT:C=CC:GOTO1750
1741 NEXTC
1750 IFE(C,1)<>-1THENS(I,3)=C
1760 GOTO860
