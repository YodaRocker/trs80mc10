0 REM01234567890123456789012345678901234567890123456
1 CLS:GOSUB990:GOSUB274:M=16384:GOTO54
2 IFC(T-1,U)>=0THENPOKEJ,Z:T=T-1:J=J-1:Z=PEEK(J):OD=1:POKEJ,66:RETURN
3 RETURN
4 IFC(T+1,U)>=0THENPOKEJ,Z:T=T+1:J=J+1:Z=PEEK(J):OD=2:POKEJ,66:RETURN
5 RETURN
6 IFC(T,U-1)>=0THENPOKEJ,Z:U=U-1:J=J-P:Z=PEEK(J):OD=3:POKEJ,66:RETURN
7 RETURN
8 IFC(T,U+1)>=0THENPOKEJ,Z:U=U+1:J=J+P:Z=PEEK(J):OD=4:POKEJ,66:RETURN
9 RETURN
10 ONDGOTO11,12,13:B=B-1:RETURN
11 A=A+1:RETURN
12 B=B+1:RETURN
13 A=A-1:RETURN
14 ONDGOTO15,16,17:LY=B:RY=B:LX=A-1:RX=A+1:GOTO18
15 LX=A:RX=A:LY=B-1:RY=B+1:GOTO18
16 LY=B:RY=B:LX=A+1:RX=A-1:GOTO18
17 LX=A:RX=A:LY=B+1:RY=B-1
18 Q=.:O=.:IFLX<.ORLX>=XSORLY<.ORLY>=YSTHEN20
19 Q=M(LX,LY)
20 IFRX<.ORRX>=XSORRY<.ORRY>=YSTHENRETURN
21 O=M(RX,RY):RETURN
26 N=.:IFLAND8THENN=1
28 E=.:IFLAND4THENE=1
30 S=.:IFLAND2THENS=1
32 W=.:IFLAND1THENW=1
34 ONDGOTO36,38,40:F=N:O=E:Q=W:RETURN
36 F=E:O=S:Q=N:RETURN
38 F=S:O=W:Q=E:RETURN
40 F=W:O=N:Q=S:RETURN
54 DIMA,B,M(16,14),A(2,2),V,X,Y,C,D,M$,B$,L,H,G,C(16,16),W$(6),D$(3),O,Q,P,K,N,S,E,W,LY,LX,SW,DM,XS,YS,SL,RX,RY:P=32
55 I=RND(-(PEEK(9)*256+PEEK(10)))
56 W$(5)="ßßßßßßßßßßßßßßßß":W$(6)="ïïïïïïïïïïïïïïïï"
58 D$(.)="NORTH":D$(1)="EAST ":D$(2)="SOUTH":D$(3)="WEST ":B$="€€€€€€€€€€€€€€€€":GOSUB1140:CLS0
60 X=.:Y=.:D=.:XS=16:YS=14:PRINT@17,:M$="SETTING UP MAP":GOSUB960:A=.:B=.
62 READL$:IFLEN(L$)=0THEN80
64 K=0:FORI=1TOLEN(L$):M$=MID$(L$,I,1)
66 IFM$="N"THENK=KOR8:NEXT:GOTO78
68 IFM$="E"THENK=KOR4:NEXT:GOTO78
70 IFM$="S"THENK=KOR2:NEXT:GOTO78
72 IFM$="W"THENK=KOR1:NEXT:GOTO78
74 IFM$=" "THENM(X,Y)=K:C(X,Y)=(K=0):PRINT@16+(Y+1)*P+X,CHR$(143-(K=0));:K=0:X=X+1
76 NEXT
78 M(X,Y)=K:X=0:Y=Y+1:GOTO62
80 CC=0:GOSUB700
90 GOSUB92:GOSUB142:GOTO90
92 PRINT@0,B$;:PRINT@P,B$B$B$;:PRINT@96,B$;:PRINT@128,B$;:PRINT@160,B$;:PRINT@192,B$;:PRINT@224,B$;:PRINT@256,B$;
94 PRINT@288,B$;:PRINT@320,B$;:PRINT@352,B$;:PRINT@384,B$;:PRINT@416,B$;:PRINT@448,B$B$B$;:A=X:B=Y:H=D+X+YAND1
96 FORI=0TO2:GOSUB14:A(1,I)=.:IFA>=.ANDB>=.ANDA<XSANDB<YSTHENA(1,I)=M(A,B):A(.,I)=Q:A(2,I)=O:GOSUB10:NEXT
98 L=A(1,2):GOSUB26:IFFTHENV=5-(H=1):M$=LEFT$(W$(V),6):FORA=165TO293STEPP:PRINT@A,M$;:NEXT
100 V=6+(H=1):M$=LEFT$(W$(V),5):FORG=0TO2STEP2:L=A(G,2):GOSUB26:IFFTHENA=G*5.5:FORB=A+160TOA+288STEPP:PRINT@B,M$;:NEXT
102 NEXT:L=A(1,2):GOSUB26:IFQTHENC=V*16+143:FORA=M+164TOM+292STEPP:POKEA,C:NEXT:POKEM+132,V*16+139:POKEM+324,V*16+142
104 IFOTHENC=V*16+143:FORA=M+171TOM+299STEPP:POKEA,C:NEXT:POKEM+139,V*16+135:POKEM+331,V*16+141
106 L=A(1,1):GOSUB26:C=V*16+143:IFFTHENM$=LEFT$(W$(V),8):FORA=132TO324STEPP:PRINT@A,M$;:NEXT
108 V=5-(H=1):M$=LEFT$(W$(V),4):FORG=0TO2STEP2:L=A(G,1):GOSUB26:IFFTHENA=G*6:FORB=4TO10:PRINT@A+B*P,M$;:NEXT
110 NEXT:V=5-(H=1):L=A(1,1):GOSUB26:IFQ=0THEN116
112 IFV=6THEN?@33,"ë";:?@65,"ïë";:?@97,"ïïë";:FORA=129TO321STEPP:?@A,"ïïï";:NEXT:?@353,"ïïî";:?@385,"ïî";:?@417,"î";:GOTO116
114 ?@33,"Û";:?@65,"ßÛ";:?@97,"ßßÛ";:FORA=129TO321STEPP:?@A,"ßßß";:NEXT:?@353,"ßßÞ";:?@385,"ßÞ";:?@417,"Þ";
116 IFO=.THEN122
118 IFV=6THEN?@46,"ç";:?@77,"çï";:?@108,"çïï";:FORA=140TO332STEPP:?@A,"ïïï";:NEXT:?@364,"íïï";:?@397,"íï";:?@430,"í";:GOTO122
120 ?@46,"×";:?@77,"×ß";:?@108,"×ßß";:FORA=140TO332STEPP:?@A,"ßßß";:NEXT:?@364,"Ýßß";:?@397,"Ýß";:?@430,"Ý";
122 L=A(1,.):GOSUB26:IFFTHENM$=LEFT$(W$(V),14):FORA=33TO417STEPP:PRINT@A,M$;:NEXT
124 V=6+(H=1):C=V*16+143:FORG=0TO2STEP2:L=A(G,.):GOSUB26:IFFTHENA=G*7.5:FORB=M+A+PTOM+A+416STEPP:POKEB,C:NEXT
126 NEXT:L=A(1,.):GOSUB26:IFQTHENPOKEM,V*16+139:POKEM+448,V*16+142:C=V*16+143:FORB=M+PTOM+416STEPP:POKEB,C:NEXT
128 IFOTHENPOKEM+15,V*16+135:POKEM+463,V*16+141:C=V*16+143:FORA=M+47TOM+431STEPP:POKEA,C:NEXT:RETURN
130 RETURN
132 PRINT@389,"‘““’";:PRINT@421,"•CCš";:PRINT@453,"•žžž’";
134 PRINT@486,"œ””˜";:SOUND200,1:SOUND225,2:SOUND175,2:SOUND100,1:C(X,Y)=2:SC=SC+1:CC=CC+1:PRINT@17,"SCORE"SC;
136 PRINT@389,"ÁÃÃÂ";:PRINT@421,"ÅMCÊ";:PRINT@453,"ÅÎÎÎÂ";:PRINT@486,"ÌÄÄÈ";:IFCC=10THEN270
140 GOTO144
142 POKEM+16+P*(Y+1)+X,106:IFC(X,Y)>0THENONC(X,Y)GOTO132,136
144 KK=ASC(INKEY$+" "):IFRND(25)=1THENON2+SGN(X-T)GOSUB2,9,4:ON2+SGN(Y-U)GOSUB6,9,8
146 ONRND(25)GOSUB2,4,6,8:IFRND(3)=1THENONODGOSUB2,4,6,8
148 IFT=XANDU=YTHEN800
150 IFKK=PTHEN144
152 POKEM+16+P*(Y+1)+X,110:IFKK=87THEN180
154 IFKK=83THEN200
156 IFKK=65THEND=(D-1)AND3:PRINT@497,"FACING "D$(D);:RETURN
158 IFKK=68THEND=(D+1)AND3:PRINT@497,"FACING "D$(D);:RETURN
160 GOTO144
180 K=M(X,Y):ONDGOTO186,190,194:IFKAND8THENRETURN
184 Y=Y-1:RETURN
186 IFKAND4THENRETURN
188 X=X+1:RETURN
190 IFKAND2THENRETURN
192 Y=Y+1:RETURN
194 IFKAND1THENRETURN
196 X=X-1:RETURN
200 K=M(X,Y):ONDGOTO204,208,212:IFKAND2THENRETURN
202 Y=Y+1:RETURN
204 IFKAND1THENRETURN
206 X=X-1:RETURN
208 IFKAND8THENRETURN
210 Y=Y-1:RETURN
212 IFKAND4THENRETURN
214 X=X+1:RETURN
256 POKEJ,66:PRINT@465,:M$="HE GOT YOU!":GOSUB960:SOUND1,10:IFSC>HSTHENHS=SC
257 PRINT@49,:M$="HIGH"+STR$(HS):GOSUB960
258 PRINT@497,"PLAY AGAIN?€€";
260 M$=INKEY$:IFM$=""THEN260
262 IFM$="Y"THEN268
264 IFM$="N"THENEND
266 GOTO260
268 SC=0
270 FORX=1TO10:SOUND225,1:NEXT:FORX=1TO14:FORY=1TO12:IFC(X,Y)>=0THENC(X,Y)=0:PRINT@16+(Y+1)*P+X," ";
272 NEXT:NEXT:LV=0:CC=0:GOSUB700:RETURN
274 FORA=0TO44:READB:POKE17227+A,B:NEXT:EXEC17227:RETURN
700 PRINT@17,"€€€€€€€€€€€€***";:LV=0:PRINT@28,LEFT$("€€€",LV);:PRINT@16+6*P+3," ";:C(3,5)=0:PRINT@17,"SCORE"SC;
710 FORI=1TO10
720 X=RND(14):Y=RND(12):IFC(X,Y)THEN720
730 C(X,Y)=1:NEXT
740 X=1:Y=1:PRINT@16+P*(Y+1)+X,"*";:T=14:U=12:J=M+16+P*(U+1)+T:Z=96:POKEJ,66:D=0:PRINT@497,"FACING "D$(D);:RETURN
800 GOSUB900:SOUND1,2:LV=LV+1:PRINT@28+LV," ";:IFLV<3THENFORA=1TO2500:NEXT
810 POKEJ,110:IFLV=3THEN256
820 GOSUB740:RETURN
900 PRINT@67,"¯¯¯¯¯¯¯¯";
901 PRINT@99,"¯¯¯¯¯¯¯¯";
902 PRINT@133,"¯¯";:PRINT@139,"¯¯";
903 PRINT@165,"¯¯";:PRINT@171,"¯¯";
904 PRINT@197,"¯¯";:PRINT@203,"¯¯";
905 PRINT@229,"¯¯";:PRINT@235,"¯¯";
906 PRINT@261,"¯¯¯¯¯¯";
907 PRINT@293,"¯¯¯¯¯¯";
908 PRINT@325,"¯¯";:PRINT@331,"¯¯";
909 PRINT@357,"¯¯";:PRINT@363,"¯¯";
910 PRINT@389,"¯¯";:PRINT@395,"¯¯";
911 PRINT@421,"¯¯";:PRINT@427,"¯¯";
912 PRINT@451,"¯¯¯¯¯¯¯¯";
913 PRINT@483,"¯¯¯¯¯¯¯¯";:RETURN
920 DATA60,54,55,150,246,129,126,38,25,220,247,195,1,5,131,1,1,221,252,206,67,113,236,1
922 DATA221,246,236,3,221,248,236,5,221,250,51,50,56,57,240,129,58,37,1,57,126
924 DATA".... .... .... .... .... .... .... .... .... .... .... .... .... .... .... .... "
926 DATA".... WN.. .NS. .NS. .N.. .NS. .NS. .NS. .N.. .NS. .NS. .NS. .NS. .NS. .N.E .... "
928 DATA".... W..E .... .... W..E .... .... .... W..E .... .... .... .... .... W..E .... "
930 DATA".... W..E .... WN.. ..S. .NS. .NS. .NS. ...E .... WN.. .NS. .NS. .NS. ...E .... "
932 DATA".... W..E .... W..E .... .... .... .... W..E .... W..E .... .... .... W..E .... "
934 DATA".... W... .NS. .... .NS. .NS. .NS. .NS. ...E .... W... .NS. .NS. .NS. ...E .... "
936 DATA".... W..E .... W..E .... .... .... .... W..E .... W..E .... .... .... W..E .... "
938 DATA".... W... .NS. ..S. .N.. .NS. .NS. .NS. ...E .... W..E .... WN.. .NS. ...E .... "
940 DATA".... W..E .... .... W..E .... .... .... W..E .... W.S. .NS. ...E .... W..E .... "
942 DATA".... W..E .... WN.. ..S. .N.E .... WN.. ..S. .N.E .... .... W..E .... W..E .... "
944 DATA".... W..E .... W..E .... W..E .... W..E .... W.S. .N.E .... W..E .... W..E .... "
946 DATA".... W..E .... W..E .... W..E .... W..E .... .... W..E .... W..E .... W..E .... "
948 DATA".... W.S. .NS. ..S. .NS. ..S. .NS. ..S. .NS. .NS. ..S. .NS. ..S. .NS. ..SE .... "
950 DATA".... .... .... .... .... .... .... .... .... .... .... .... .... .... .... .... "
952 DATA""
960 PRINTM$;:B=((PEEK(17024)AND1)*256+PEEK(17025))-1+M:FORA=B-LEN(M$)+1TOB:POKEA,PEEK(A)-64:NEXT:RETURN
990 PRINTTAB(12)"mc-chase"
1000 PRINT"  YOU ARE AT THE HERON POINT"
1010 PRINT"CONVENTION CENTRE. IT IS"
1020 PRINT"COCOFEST. THE CONVENTION HAS"
1030 PRINT"BEEN TAKEN OVER BY RETROBRITE-"
1040 PRINT"NEEDING COCOS. YOU MUST SWAP"
1050 PRINT"OUT 10 YELLOWING COCOS FOR 10"
1060 PRINT"BRIGHT & SHINY MC-10S."
1070 PRINT"  HOWEVER, YOU ARE BEING CHASED"
1080 PRINT"BY THE 'B' WHO IS FILLED WITH"
1090 PRINT"RIGHTEOUS INDIGNATION AT THE"
1100 PRINT"MC-10 (FOR RUINING THE CHANCES"
1110 PRINT"OF THE DELUXE COCO). HE WILL"
1120 PRINT"TRY TO STOP YOU. YOU HAVE 3"
1130 PRINT"LIVES PER LEVEL. TO MOVE: wasd.":RETURN
1140 PRINTTAB(5);:M$="HIT ANY KEY TO BEGIN":GOSUB960
1150 M$=INKEY$:IFM$=""THEN1150
1160 RETURN
