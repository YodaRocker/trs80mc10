0 DIM A(100),A0(100),A1(7),A2(7),A3(6),A4(6),AY(7)
1 CLS:CLEAR150:FOR XX=1 TO 6:PRINT:NEXT
2 PRINT CHR$(23);"A SPACECRAFT SIMULATOR":FOR XX=1TO700:NEXT
3 PRINT "BY GARY SIVAK":FOR XX=1 TO 250:NEXT
4 PRINT:PRINT"TRS-80 ADAPTED BY:":FORXX=1TO250:NEXT
5 PRINT"HERBERT BERRY AND ROBERT HEWETT"
6 FOR XX=1 TO 1500:NEXT:CLS
7 PRINT"THIS PROGRAM ENABLES THE PLAYER TO DESIGN & PUT INTO"
8 PRINT"ORBIT A MULTISTAGE SPACECRAFT LAUNCHED FROM EARTH-"
9 PRINT"BASED CONDITIONS.  BY ASKING FOR ENGINE THROTTLE"
10 PRINT"SETTINGS, THRUST ANGLES, AND FIRING TIMES, YOUR"
11 PRINT"TRS-80 PUTS YOU AT THE CONTROLS OF A MULTISTAGE"
12 PRINT"SPACECRAFT OF YOUR OWN DESIGN AS YOU PILOT IT FROM"
13 PRINT"THE EARTH'S SURFACE INTO ORBIT.":PRINT
14 PRINT"CONTINUOUS DATA DISPLAYS OF THE USER'S STATUS ARE"
15 PRINT"PRESENTED, AS WELL AS ARRAYS OF ALTITUDE & RANGE"
16 PRINT"INFORMATION FOR POSSIBLE PLOTTING AT MISSION'S END."
17 PRINT:PRINT:FORXX=1TO1000:NEXT:PRINTTAB(25);"PRESS ENTER TO CONTINUE"
18 AB$=INKEY$:IF LEN(AB$)=0 THEN18 ELSEIF ASC(AB$)<>13 THEN18
19 CLS:PRINT"THE PROGRAM FIRST ASKS FOR AND VERIFIES ALL SHIP"
20 PRINT"DESIGN PARAMETERS; OVERALL (AS IN NUMBER OF STAGES,"
21 PRINT"ITERATION TIME, ORBITAL HEIGHT, AND SHIP'S PAYLOAD"
22 PRINT"WEIGHT) AND INDIVIDUAL STAGE PARAMETERS (AS IN"
23 PRINT"STAGE X'S FUEL AND HULL WEIGHTS, THRUST, AND SPEC-"
24 PRINT"IFIC IMPULSE).":PRINT
25 PRINT"A COMPLETE PROGRAM DESCRIPTION IS PROVIDED IN THE"
26 PRINT"ARTICLE: A SPACECRAFT SIMULATOR -- NOV. 79 ISSUE OF"
27 PRINT"BYTE MAGAZINE.":PRINT:PRINT:FOR XX=1TO1000:NEXT
28 PRINTTAB(25)"PRESS ENTER TO TAKE CONTROL."
29 AB$=INKEY$:IF LEN(AB$)=0 THEN29 ELSEIF ASC(AB$)<>13 THEN29
30 CLS:FORXX=1TO350:NEXT:AA$="SECTION(1): SPACECRAFT DESIGN."
31 PRINTAA$:FOR XX=1TO50:NEXT:PRINT
35 PRINT:PRINT"ENTER:   * 1). NUMBER OF ROCKET STAGES (6 MAX.)"
40 PRINT"           2). ITERATION TIME (.1 OR .01)"
45 PRINT"           3). DESIRED ORBITAL HEIGHT (IN MILES)"
50 PRINT"           4). SHIP'S PAYLOAD WEIGHT (IN POUNDS)"
55 INPUTA5:XX=15561:POKE XX,32:XX=XX+64:POKE XX,42
60 INPUTA7:POKE XX,32:XX=XX+64:POKE XX,42:A6=A5+1
65 INPUTA8:POKE XX,32:XX=XX+64:POKE XX,42:INPUT A2(A6)
70 FOR XX=1TO16:PRINT:NEXT:AB$=STRING$(22,CHR$(176))
71 PRINT"VERIFICATION: ":PRINTAB$:PRINT:PRINT
72 PRINT"NUMBER OF STAGES: ";A5
73 PRINT"ITERATIONS:       ";A7
74 PRINT"DESIRED ORBIT:    ";A8
75 PRINT"SHIP'S PAYLOAD:   ";A2(A6):PRINT:PRINT:PRINT
76 PRINT:FOR XX=1TO1000:NEXT:CLS:FORXX=1TO250:NEXT
100 FOR A9=1 TO A5:B=A6-A9:B0=B+1:CLS:PRINTAA$
110 PRINTTAB(12);"STAGE NUMBER #";B:PRINT
115 PRINT"ENTER:   * 1). FUEL WEIGHT (IN LBS.)"
120 PRINT"           2). HULL (OR TANK) WEIGHT (IN LBS.)"
125 PRINT"           3). THRUST"
130 PRINT"           4). SPECIFIC IMPULSE FUEL (OR) OXIDIZER: "
135 PRINTTAB(15);"GASOLINE=252, PEROXIDE=300, HYDROGEN=500"
140 INPUT A1(B):XX=15561:POKE XX,32:XX=XX+64:POKE XX,42
145 INPUT A2(B):POKE XX,32:XX=XX+64:POKE XX,42:AY(B)=A2(B)
150 A2(B)=A2(B)+A2(B0)+A1(B0):B1=A2(B)+A1(B)
155 PRINT @ 342,"-------> AT LEAST ";B1;" POUNDS";
160 PRINT @ 630," "
165 INPUT A3(B):POKE XX,32:XX=XX+64:POKE XX,42:INPUT A4(B)
170 FOR XX=1 TO 16:PRINT:NEXT:PRINT"VERIFICATION:":PRINT
175 XY=A6-1:FOR XX=1 TO A9
180 PRINT"STAGE(";XY;"): FUEL=";STR$(A1(XY));
185 PRINT", HULL=";STR$(AY(XY));", THRUST=";STR$(A3(XY));
190 PRINT", IMPULSE=";STR$(A4(XY)):XY=XY-1:NEXT:PRINT
195 PRINT"PRESS ENTER TO CONTINUE ..."
200 AB$=INKEY$:IF LEN(AB$)=0 THEN 200 
201 IF ASC(AB$)<>13 THEN 200 ELSE NEXT A9
305 PRINT"INITIALIZING VARIABLES, PLEASE WAIT ..."
310 B2=10
320 B3=B2*A7
330 B4=360
340 B5=B3/100.0
350 B6=5280.0*.3048
360 B7=6.67E-11*5.983E24
370 B8=ATN(1.0)/45.
380 B9=90.
390 C=1.0
400 C0=SQR(B7/9.80665)
410 C1=C0
420 C2=SQR(B7/(C0+B6*A8))/.3048
430 C3=0.0:C4=0.0:C5=0.0:C6=0.0:C7=0.0:C8=0.0:C9=0.0
500 D=0.0:D0=0.0:D1=0.0:D2=0.0:D3=0.0
543 FOR XX=1 TO 16:PRINT:NEXT
550 PRINT "THE SHIP CAN SWIVEL ";B2;" DEG/SEC."
560 PRINT "EARTH'S GRAVITY IS 32.174 FT/SEC/SEC."
570 PRINT "FORWARD VELOCITY NEEDED FOR ORBIT ";C2;" FT/SEC"
571 FOR XX=1 TO 800:NEXT:FORXX=1TO7:PRINT:NEXT
572 PRINT @ 916,"ALL SYSTEMS GO":FOR XX=1 TO 250:NEXT
580 D=D+1
590 D4=A2(D)/2.2046
600 D5=A3(D)/A4(D)/2.2046
610 D6=A1(D)/2.2046
620 D7=D6
630 D8=A3(D)/2.2046*9.80665:PRINT:
640 PRINT"READY TO IGNITE STAGE";D;" ...  PRESS ENTER ";
641 PRINT"FOR IGNITION."
646 AB$=INKEY$:IF LEN(AB$)=0 THEN646 ELSEIF ASC(AB$)<>13 THEN646
648 CLS:PRINT @ 400,CHR$(23);"IGNITION":GOTO1090
660 PRINT"ENTER:   * 1).  THROTTLE SETTING IN PERCENT (0-100)."
661 PRINT"           2).  THRUST ANGLE IN DEGREES ((-";B4;") TO ";B4;")"
662 PRINT"           3).  BURN TIME IN SECONDS.":ZZ$=""
665 INPUT ZZ$:POKE 16073,32:IF LEN(ZZ$)=0 THEN 1460
666 D9=VAL(ZZ$):POKE 16137,42:INPUT E:POKE 16073,32
667 POKE 16137,42:INPUT E0:CLS
700 D9=ABS(D9/100.0)
710 E1=D9*D8
720 E2=D9*D5*A7
730 E3=E2/100.0
740 E4=E0-(A7/100)
750 E5=C5*C1
760 E6=0.0
770 IF E0=0.0 THEN 1080
780 IF C1<C0  THEN 1080
790 E6=E6+A7
791 IF X1=1 THEN800 ELSE CLS:PRINT @ 77, "*** FLIGHT LOG ***"
792 PRINT:X1=1:PRINT "ELAPSED FLIGHT TIME:";TAB(34);"SECONDS."
793 PRINT"FUEL LEFT:";TAB(34);"PERCENT"
794 PRINT "SHIP'S ANGLE:";TAB(34);"DEG."
795 PRINT "ALTITUDE:";TAB(34);"MILES"
796 PRINT "ASCENT RATE:";TAB(34);"MI/HR."
797 PRINT "ORBITAL VELOCITY:";TAB(34);"PERCENT"
798 PRINT "ORBITAL HEIGHT:";TAB(34);"PERCENT"
799 PRINT @ 916,"STAGE NUMBER";D
800 E7=D7-E2
810 E8=E1/(D4+(D7+E7)/2.0)
820 IF E7>=E3 THEN 850
830 E7=0.0:E8=0.0
850 IF ABS(E-B9)<B5 THEN 930
860 IF E<B9 THEN 890
870 B9=B9+B3
880 GOTO 900
890 B9=B9-B3
900 E9=B9*B8
910 C4=COS(E9)
920 C=SIN(E9)
930 F=E8*C4
940 F0=E8*C
950 F1=C5+F*A7
960 C6=(C5+F1)/2.0
970 C7=C7+C6*A7
980 F2=F0+C6[2/C1-B7/C1[2
990 F3=C8+F2*A7
1000 F4=C1+(C8+F3)/2.0*A7
1010 IF D9 <>0.0 THEN 1030
1020 F1=E5/F4
1030 D7=E7
1039 XX=((F4-C0)/.3048)/5280.:XY=XX*5280.
1040 C5=F1:PRINT @ 214," ";USING "####.###";C3+E6;
1041 PRINT @ 283, INT(100.*D7/D6);:PRINT @ 347,B9;
1042 PRINT @ 405," ";USING "#####.###";XX;
1043 PRINT @ 469," ";USING "#####.###";((F3/.3048)*15.)/22.;
1044 PRINT @ 533," ";USING "####.###";((C5/.3048)*100)/C2;
1045 PRINT @ 597," ";USING "####.###";100.*(XY/(A8*5280.));
1050 C8=F3
1060 C1=F4
1070 IF E6<E4 THEN 770
1080 C3=C3+E6
1090 D2=D2+1
1100 A(D2)=(C1-C0)/.3048
1110 IF C9>=A(D2) THEN 1130
1120 C9=A(D2)
1130 IF A(D2)>=0.0 THEN 1150
1140 A(D2)=0.0
1150 IF A(D2)<400000.0 THEN 1170
1160 D3=D3+1
1170 F5=A(D2)/5280.
1180 F6=C8/.3048
1190 F7=F6*15./22.
1200 F8=C5/.3048
1210 F9=F8*15./22
1220 A0(D2)=C7/B6
1230 G=100.*D7/D6
1240 G0=D7/D5
1250 G1=B7/C1[2-C6[2/C1
1260 G2=D8/(D4+D7)/.3048
1270 G3=G2*15./22.
1280 G4=G2-(G1/.3048)
1290 G5=G4*15./22.
1300 G6=G1/.3048/G2
1310 G7=100.*G6
1320 G8=90.
1330 IF G6>=1.0 THEN 1350
1340 G8=ATN(G6/SQR(1.0-G6[2))/B8
1350 G9=SQR(B7/C1)/.3048
1360 H=100.*F8/C2
1370 H0=100.*A(D2)/(A8*5280)
1380 H1=100.*F8/G9
1390 H2=(C2-F8)/G2
1400 H3=(G9-F8)/G2
1410 IF F6=0.0 THEN 1440
1420 H4=(A8*5280.-A(D2))/F6
1430 IF H4<>9999.99 THEN 1460
1440 H4=9999.99 
1460 CLS:IF X2 THEN 1464 ELSE X2=1
1462 PRINT"SECTION(2): LAUNCH AND ORBIT SPACECRAFT":PRINT
1464 IF D3<>1.0 THEN 1480
1470 PRINT "400K FT. ACHIEVED, YOU ARE IN VACUUM."
1480 PRINT "FLIGHT TIME","FUEL LEFT","@ FULL THROT.","SHIP ANG"
1490 PRINT C3;"SEC,",G;"%",G0;"SEC,",B9;"DEG."
1500 PRINT:X1=0
1510 PRINT "ALTITUDE","ASCENT RATE","FORWARD V.","RANGE"
1520 PRINT A(D2);"FT.",F6;"FT/SEC",F8;"FT/SEC",A0(D2);"MI."
1530 PRINT F5;"MI.",F7;"MI/HR.",F9;"MI/HR."
1540 FOR XX=1TO5:PRINT:NEXT
1541 PRINTTAB(35);"PRESS ENTER TO CONTINUE"
1542 AB$=INKEY$:IF LEN(AB$)=0 THEN1542 ELSEIF ASC(AB$)<>13 THEN      1542
1543 CLS:FORXX=1TO100:NEXT
1550 PRINT "MAX ACCEL","MAX VERT ACCEL","ANGLE(C.A.)",
1560 PRINT "THROT(C.A.)"
1570 '
1580 PRINT G2;"FT/S/S",G4;"FT/S/S","FULL THROT.","VERT. POS."
1590 PRINT G3;"MI/H/S",G5;"MI/H/S",G8;"DEG.",G7;"%"
1600 PRINT
1610 PRINT H;"% ORBITAL VELOCITY",H0;"% ORBITAL HEIGHT."
1620 PRINT H1;"% VELOCITY NEEDED FOR ORBIT AT CURRENT ALTITUDE"
1630 PRINT
1640 PRINT,,"TIME TO ACHIEVE:"
1650 PRINT "ORB.ALT.","ORB.VAL.","CUR.ALT. ORB.VEL."
1660 PRINT "AT CUR. RATE","AT FULL THROT.","AT FULL THROT."
1670 PRINT H4;"SEC.",H2;"SEC.",H3;"SEC."
1680 PRINT
1690 IF H<100. THEN 1760
1700 IF H0<100. THEN 1760
1710 D0=D0+1
1720 IF D0>1 THEN 1760
1730 PRINT "IN DESIRED ORBIT. ENTER (1)CONTINUE,(2)PLOT"
1740 INPUT H5
1750 IF H5=2 THEN 1920
1760 IF C3=0.0 THEN 660
1770 IF D7<=E3 THEN 1800
1780 IF A(D2)<=0.0 THEN 1800
1790 GOTO 660
1800 IF A(D2)=0.0 THEN 1890
1810 IF D<A5 THEN 580
1820 D1=D1+1
1830 IF D1<>1 THEN 1850
1840 PRINT "LAST STAGE SHUT-DOWN."
1850 IF D0<>0.0 THEN 1880
1860 IF A(D2)<=0.0 THEN 1880
1870 GOTO 660
1880 IF A(D2)>0.0  THEN 1920
1890 H6=INT(SQR(F6[2+F8[2)+.5)
1900 H7=INT(SQR(F7[2+F9[2)+.5)
1910 PRINT "YOU CRASHED AT ";H6;" FT/SEC, ";H7;" MI/HR."
1920 PRINT "AFTER ";D2;" PLOT POINTS: "
1930 FOR H8=1 TO D2
1940 '
1950 NEXT H8
1960 H9=25.0
1970 '
1980 I=C9*H9/100.0*1.0001
1990 I0=D2+1
2000 I0=I0-1
2010 IF A(I0)>I THEN 2000
2020 I1=100.0*A0(I0)/A0(D2)
2030 PRINT "LOWER ";H9;"% OR ";I;" MI. OF MAX ALT. ATTAINED."
2040 PRINT "FIRST ";I1;"% OR ";A0(I0);" MI. OF TOTAL RANGE."
2050 PRINT "WITH ";I0;" STEPS:"
2060 FOR I2=1 TO I0
2070 '
2080 NEXT I2
2090 END
