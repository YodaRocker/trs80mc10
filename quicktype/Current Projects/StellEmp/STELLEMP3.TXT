0 CLEAR900:DIMCC,ZZ,MC,M$,I$:MC=16384:CC=RND(-(PEEK(9)*256+PEEK(10))):GOTO110
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORZZ=CC-LEN(M$)+1TOCC:POKEZZ,PEEK(ZZ)-64:NEXT:RETURN
110 GOSUB140:CLS0:PRINT@233,:M$="STELLAR EMPIRES":GOSUB7:FORI=1TO2500:NEXT:CLS:GOTO160
140 Y1=0:Y2=31:X1=8:X2=58
150 CLS0:SET(X1,Y1,5):SET(X1,Y2,5):SET(X2,Y1,5):SET(X2,Y2,5):IFY1=15THENRETURN
151 Y1=Y1+1:Y2=Y2-1:X1=X1+2:X2=X2-2:GOTO150
160 CLS:PRINT:INPUT"HOW MANY PLAYERS (2-4)";X$:NP=VAL(X$):IFNP<2ORNP>4THEN160
170 PRINT
180 INPUT"FOR HOW LONG DO YOU WANT TO PLAY(IN TURNS)";X$:MT=VAL(X$):IFMT<1THEN180
190 NS=7+RND(4+NP)+2*NP:NP=NP-1:T=0:C=RND(3)-1:D=RND(4)
191 DIMS(NS,5+NP),N$(NS),E(NP,1),W(NP,9,2),F$(NP),B(NS,4+NP),M$(NP),D(NS),X(NP,NS)
200 PRINT:PRINT"THERE ARE"NS+1"STAR SYSTEMS."
210 FORI=0TONS:READN$(I):FORJ=0TONP+5:S(I,J)=0:NEXT:NEXT
220 FORI=0TONP:FORJ=0TO9:FORK=0TO2:W(I,J,K)=0:NEXT:NEXT:NEXT
230 FORP=0TONP:I=RND(4)+P*4:E(P,0)=I:E(P,1)=0:S(I,0)=4:S(I,3)=P:S(I,4)=5:S(I,5+P)=1
240 ONP+1GOSUB250,260,270,280:GOTO290
250 X=0:Y=0:RETURN
260 Y=7:IFNP=2THENX=3:RETURN
261 X=7:RETURN
270 X=7:IFNP=2THENY=1:RETURN
271 Y=0:RETURN
280 X=0:Y=7:RETURN
290 S(I,1)=X+RND(3):S(I,2)=Y+RND(3):CLS:PRINT"PLAYER"P+1
300 PRINT"PLAYER NAME = 'C' FOR COMPUTER"
310 INPUT"PLEASE ENTER NAME";F$(P):PRINT
320 IFF$(P)=""THENPRINT:GOTO310
330 IFF$(P)="C"THENCLS:NEXT:GOTO 400
340 IFLEN(F$(P))=INT(LEN(F$(P))/2)*2THENF$(P)=F$(P)+" "
350 M$="PRESS enter WHEN READY TO ENTER HOME STAR NAME.":GOSUB1
360 INPUT"OTHER PLAYERS MAY NOT WATCH!";X$:CLS
370 PRINT
380 PRINT"THE REGULAR NAME IS "N$(I):X$=LEFT$(N$(I),1):PRINT"THE NAME MUST START WITH THE    LETTER "X$"."
390 INPUT"ENTER NEW NAME";Y$:IFX$<>LEFT$(Y$,1)THENPRINT:GOTO380
391 N$(I)=Y$:CLS:NEXT
400 FORI=0TONS:IFS(I,0)=4THEN480
401 C=C+1:IFC=4THENC=1
410 D=D+1:IFD=5THEND=1
420 X=RND(5):Y=RND(5):OND-1GOSUB430,440,450:GOTO460
430 X=X+5:RETURN
440 Y=Y+5:RETURN
450 X=X+5:Y=Y+5:RETURN
460 FORJ=0TONS:IFX=S(J,1)ANDY=S(J,2)THEN420
461 NEXT
470 S(I,0)=C:S(I,1)=X:S(I,2)=Y:S(I,3)=-1:IFC=3THENS(I,4)=RND(3)
480 NEXT
490 PRINT"WORKING...":T=T+1:FORI=0TONS:FORJ=0TO4+NP:B(I,J)=0:NEXT:C=S(I,3):IFC=-2THENC=-1
500 B(I,4+C)=S(I,4):NEXT
510 FORP=0TONP:FORI=0TO9:IFW(P,I,2)<>T THEN540
511 C=W(P,I,0)
520 S(C,5+P)=1:IFB(C,4+P)=0THENB(C,0)=1
530 B(C,4+P)=B(C,4+P)+W(P,I,1):FOR J=0 TO 2:W(P,I,J)=0:NEXT
540 NEXT:NEXT
550 FORI=0TONS:C=0:FORJ=3TO4+NP:IFB(I,J)>0THENC=C+1:K=J
560 NEXT:IFC=0THEN630
561 IFC=1THENB(I,1)=K-4:S(I,3)=K-4:S(I,4)=B(I,K):GOTO630
570 B(I,0)=2:X=0:Y=0:X1=0:FORJ=3TO4+NP:IFB(I,J)=X THENY=Y+1:GOTO580
571 IFB(I,J)>X THENX=B(I,J):X1=J:Y=1
580 NEXT:IFY>1THEN620
590 B(I,1)=X1-4:Y=0:FORJ=3TO4+NP:IFJ=X1THEN600
591 IFB(I,J)>YTHENY=B(I,J)
600 NEXT:B(I,2)=INT(Y/2+0.5):S(I,3)=B(I,1)::S(I,4)=B(I,X1)-B(I,2):IFS(I,0)=3ANDS(I,3)<0THENS(I,3)=-2
610 GOTO630
620 B(I,1)=-2:S(I,3)=-1:S(I,4)=0:IFS(I,0)=3THENS(I,3)=-2:S(I,4)=RND(2)
630 NEXTI
640 FORP=0TONP:FORI=0TO9:IFW(P,I,2)=0THENW(P,I,2)=9999
650 NEXT
660 C=0:FORI=0TO8:IFW(P,I,2)<=W(P,I+1,2)THEN680
670 FORJ=0TO2:K=W(P,I,J):W(P,I,J)=W(P,I+1,J):W(P,I+1,J)=K:NEXT:C=C+1
680 NEXT:IFC>0THEN660
690 FORI=0TO9:IFW(P,I,2)=9999THENW(P,I,2)=0
700 NEXT:NEXT
710 FORP=0TONP:IF F$(P)="C"THENGOSUB1920:GOTO1590
720 IFE(P,1)=-1THEN1590
721 GOSUB140:CLS:PRINTTAB(11)"STARDATE"T:PRINT@64,"================================";:PRINT@140,"PLAYER"P+1
722 PRINT@386,"  PRESS ANY KEY WHEN READY":X$=INKEY$
730 SOUND200,1:SOUND10,3:PRINT@320,"================================";:PRINT@234,"EMPEROR "F$(P);
740 X$=INKEY$:IFX$=""THEN740
741 CLS:PRINT"PLAYER"P+1:GOSUB750:GOTO920
750 C=0:PRINT"BATTLE RESULTS FOR STARDATE"T:PRINT
760 FORI=0TONS:IFB(I,0)=0ORB(I,4+P)=0THEN900
761 C=C+1
770 M$="STAR SYSTEM: "+N$(I)+" ("+MID$(STR$(S(I,1)),2)+","+MID$(STR$(S(I,2)),2)+") CLASS:"+STR$(S(I,0))+".":GOSUB1:GOTO790
780 IFC>1THENX$="S":RETURN
781 X$="":RETURN
790 C=B(I,4+P):GOSUB780:IFB(I,0)=1THEN795
791 GOTO800
795 M$="YOUR"+STR$(C)+" STARSHIP"+X$+" ARRIVED AT THE STAR SYSTEM.":GOSUB1
796 M$="THE STAR SYSTEM OF "+N$(I)+" IS NOW PART OF YOUR EMPIRE.":GOSUB1:GOTO890
800 M$="YOUR"+STR$(C)+" STARSHIP"+X$+" WENT INTO BATTLE AGAINST:":GOSUB1
810 IFB(I,3)>0THENC=B(I,3):GOSUB780:M$=STR$(C)+" NATIVE WARSHIP"+X$:GOSUB1
820 FORJ=0TONP:IFJ=PORB(I,4+J)=0THEN840
830 C=B(I,4+J):GOSUB780:M$=STR$(C)+" STARSHIP"+X$+" FROM ENEMY PLAYER"+STR$(J+1):GOSUB1
840 NEXT
850 IFB(I,1)=-2THENM$="ALL STARSHIPS CONCERNED WERE DESTROYED.":GOSUB1:GOTO890
860 C=B(I,2):GOSUB780
861 IFB(I,1)=-1THENM$="THE NATIVE FORCE WAS VICTORIOUS, BUT THEY LOST"+STR$(C)+" WARSHIP"+X$:GOSUB1:GOTO890
870 IFB(I,1)<>PTHENM$="PLAYER"+STR$(B(I,1)+1)+" WAS THE VICTOR, BUT HE LOST"+STR$(C)+" STARSHIP"+X$:GOSUB1:GOTO890
880 M$="YOUR FORCE WAS VICTORIOUS. THE STAR SYSTEM "+N$(I)+" IS NOW PART OF YOUR EMPIRE.":GOSUB1
881 M$="YOU DID LOSE"+STR$(C)+" STARSHIP"+X$+".":GOSUB1
890 INPUT"PRESS enter TO CONTINUE";X$:PRINT
900 NEXT:IFC=0THENM$="THERE HAVE BEEN NO BATTLES OR NEW ARRIVALS ON STARDATE"+STR$(T)+".":GOSUB1:PRINT
910 RETURN
920 FORI=0TONS:IFS(I,0)=4THEN1760
921 IFS(I,0)<>3ORS(I,3)<>PTHEN1040
930 C=25-5*S(I,4):IFC<5THENC=5
940 X=RND(100):IFX>CTHEN1040
950 M$="THERE HAS BEEN A REBELLION ON "+N$(I)+"!!!":GOSUB1
960 C=20*S(I,4):X=RND(100):IFX>CTHEN1000
970 M$="WHICH WAS QUICKLY PUT DOWN BY YOUR LOYAL TROOPS.":GOSUB1
980 C=RND(3):IFC>S(I,4)THENC=S(I,4)-1:IFC=0THEN1030
990 GOSUB780:M$="BUT THE REBELS DID DESTROY"+STR$(C)+" STARSHIP"+X$:GOSUB1:S(I,4)=S(I,4)-C:GOTO 1030
1000 M$="WHICH COULD NOT BE CONTAINED BY YOUR FORCES!!!":GOSUB1
1010 C=S(I,4):GOSUB 780
1011 M$="YOU HAD"+STR$(C)+" STARSHIP"+X$+" STATIONED AT "+N$(I)+", ALL OF WHICH WERE DESTROYED.":GOSUB1
1020 PRINT"LONG LIVE THE REVOLUTION!!":S(I,3)=-2:S(I,4)=RND(2)
1030 INPUT"PRESS enter TO CONTINUE";X$:PRINT
1040 NEXTI
1050 IFS(E(P,0),3)<>PTHENPRINT"NO HOME STAR - NO INCOME!!":GOTO1180
1060 PRINT"INCOME CALCULATION":C=0
1070 PRINT"SYSTEM      CLASS      OUTPUT"
1080 FORI=0TONS:IFS(I,3)<>PTHEN1150
1090 ONS(I,0)GOSUB1100,1110,1120,1130:C=C+X:GOTO1140
1100 X=1:RETURN
1110 X=RND(3):RETURN
1120 X=2+RND(3):RETURN
1130 X=5+RND(5):RETURN
1140 PRINTLEFT$(N$(I),11)TAB(13)S(I,0)TAB(24)X"RU"
1150 NEXT:PRINT"TOTAL OUTPUT:"C"RU ":E(P,1)=E(P,1)+C
1160 PRINT"TOTAL RESOURCE UNITS:"E(P,1):C=INT(E(P,1)/5):GOSUB780
1170 E(P,1)=INT(E(P,1)-5*C):M$="PRODUCTION RATE:"+STR$(C)+" STARSHIP"+X$:GOSUB1:S(E(P,0),4)=S(E(P,0),4)+C
1171 INPUT"PRESS enter TO CONTINUE";X$:PRINT
1180 X$="":M$=LEFT$("DATE"+STR$(T)+" ENTER COMMAND (0=LIST)      ",32):GOSUB7:INPUTX$:Z=VAL(X$)
1200 CC=((PEEK(17024)AND1)*256+PEEK(17025)):FORZZ=CCTO480STEP32:PRINT@ZZ,"                               ";:NEXT
1210 PRINT@CC,:ONZ+1GOSUB1220,1250,1310,1350,750,1440,1490,1890:IFZ=8THEN1590
1211 IFZ=9THEN1580
1212 GOTO1180
1220 PRINT"1 DISPLAY YOUR STAR MAP":PRINT"2 GIVE DISTANCE BETWEEN 2 STARS":PRINT"3 LIST STARSHIPS IN HYPERSPACE"
1230 PRINT"4 REVIEW BATTLE DATA":PRINT"5 LIST DATA ON STAR SYSTEMS":PRINT"6 SEND STARSHIPS TO HYPERSPACE"
1240 PRINT"7 LEAVE/READ MESSAGE":PRINT"8 END TURN":PRINT"9 RESIGN FROM GAME":RETURN
1250 CLS:PRINT@416,"KEY: 1ST CHARACTER=STAR CLASS":PRINT"     2ND CHARACTER=SYSTEM NAME"
1251 PRINT"     3RD "CHR$(152)"=YOU, "CHR$(248)"=SOMEONE ELSE";
1260 PRINT@0,::PRINT@0,:FORI=1TO10:PRINTTAB(3*I-1)STR$(I);:NEXT
1261 FORI=1TO10:PRINTMID$(STR$(I)+" ",2,2)"응응응응응응응응응응응응응응응";:NEXT
1270 FORI=0TONS:IFS(I,5+P)=1THENX$=RIGHT$(STR$(S(I,0)),1):GOTO1280
1271 X$="?"
1280 X$=X$+CHR$(I+65):IFS(I,3)=PTHENC=152:GOTO1290
1281 C=248
1290 M$=X$:PRINT@(S(I,1)*32+S(I,2)*3)-1,;:GOSUB7:PRINTCHR$(C);:NEXT
1300 PRINT@352,:RETURN
1310 PRINT:INPUT"ENTER THE 1ST LETTER OF THE TWO STARS NAMES";X$,Y$:IFX$=""ORY$=""THENX$="Z":Y$="Z":GOTO1320
1311 X=ASC(X$)-65:Y=ASC(Y$)-65
1320 IFX<0ORX>NS ORY<0ORY>NS THENPRINT"YOU DON'T MAKE ANY SENSE!":RETURN
1330 GOSUB1340:GOSUB780:PRINT"THE DISTANCE IS"C"PARSEC"X$".":PRINT:RETURN
1340 C=INT(SQR((S(X,1)-S(Y,1))^2+(S(X,2)-S(Y,2))^2)+0.5):RETURN
1350 PRINT:PRINT"STARSHIPS IN HYPERSPACE:":PRINT" NUMBER/DESTINATION/DUE STARDATE"
1360 IFW(P,0,2)=0THENPRINT"THERE ARE NONE":PRINT:RETURN
1370 FORI=0TO9:IFW(P,I,2)=0THEN1430
1380 C=W(P,I,1):GOSUB780
1390 IFC<10THENPRINT"   ";:GOTO1400
1391 PRINT"    ";
1400 PRINTC;
1410 PRINT"  "N$(W(P,I,0));
1420 PRINTTAB(22)W(P,I,2)
1430 NEXT:PRINT:RETURN
1440 PRINT:PRINT"DATA ON STAR SYSTEMS":PRINT"SYSTEM    LOCATION CLASS  SHIPS"
1450 FORI=0TONS:IFS(I,3)<>PTHEN1480
1460 PRINTLEFT$(N$(I),11)TAB(12)MID$(STR$(S(I,1)),2)","MID$(STR$(S(I,2)),2)TAB(20)S(I,0)TAB(26);
1470 IFS(I,3)=PTHENPRINTS(I,4):GOTO1480
1471 PRINT"0"
1480 NEXT:PRINT:RETURN
1490 FORI=0TO9:IFW(P,I,2)=0THEN1500
1491 NEXT:PRINT"YOU ALREADY HAVE 10 FLEETS IN HYPERSPACE.":RETURN
1500 PRINT:INPUT"ENTER THE LETTER OF THE START   AND END STAR SYSTEMS";X$,Y$
1501 IFX$=""ORY$=""THENX$="Z":Y$="Z":GOTO1510
1502 X=ASC(X$)-65:Y=ASC(Y$)-65
1510 IFX<0ORX>NSORY<0ORY>NSTHEN1320
1520 IFX=YTHENPRINT"YOU DON'T MAKE ANY SENSE!":RETURN
1530 IFS(X,3)<>PTHENM$="YOU HAVE NO STARSHIPS AT "+N$(X)+".":GOSUB1:RETURN
1540 C=S(X,4):GOSUB780:M$="THERE ARE"+STR$(C)+" SHIP"+X$+" AT "+N$(X)+".":GOSUB1
1541 X$="":INPUT"HOW MANY ARE LEAVING";X$:K=VAL(X$):IFK<1THENPRINT"A GHOST FLEET, EH?":RETURN
1550 IFK>S(X,4)THENPRINT"TOO MANY!":RETURN
1560 S(X,4)=C-K:IF S(X,4)=0THENS(X,3)=-1:IFS(X,0)=3THENS(X,3)=-2
1570 W(P,I,0)=Y:W(P,I,1)=K:GOSUB1340:W(P,I,2)=T+C:M$="THE FLEET WILL ARRIVE AT STARDATE"+STR$(T+C)+".":GOSUB1:PRINT:RETURN
1580 E(P,1)=-1
1590 CLS:NEXTP
1600 FORI=0TONS:IFS(I,3)<>-2THEN1620
1610 IFS(I,0)=4THENS(I,4)=S(I,4)+1:GOTO1620
1611 IFRND(10)<7THENS(I,4)=S(I,4)+1
1620 NEXT:IFT=MTTHEN1680
1621 K=0
1630 FORP=0TONP:IFE(P,1)=-1THENK=K+1:GOTO1640
1631 C=P
1640 NEXT:IFK<>NPTHEN490
1641 GOSUB140
1650 PRINT"PLAYER"C+1"HAS WON!!  CONGRATS!"
1660 INPUT"WOULD YOU LIKE TO PLAY AGAIN    (Y/N)";X$
1670 IFLEFT$(X$,1)="Y"THENRUN
1671 CLS:END
1680 FORP=0TONP:W(P,0,0)=0:NEXT
1690 FORI=0TONS:IF S(I,3)>-1 THEN W(S(I,3),0,0)=W(S(I,3),0,0)+S(I,0)
1700 NEXT:FORP=0 TO NP:IFE(P,1)=-1 OR S(E(P,0),3)<>P THEN W(P,0,0)=0
1710 PRINT"PLAYER"P+1"HAS A SCORE OF"W(P,0,0)".":NEXT
1720 X=0:Y=0:C=0:FORP=0TONP:IFW(P,0,0)=X THENY=Y+1:GOTO1730
1721 IFW(P,0,0)>X THENX=W(P,0,0):Y=1:C=P
1730 NEXT:IFY>1THENPRINT"THERE IS NO DEFINITE WINNER.":GOTO1660
1740 PRINT:PRINT"PLAYER"C+1"HAS WON!":GOTO1660
1750 DATAALTAIR,BETELGEUSE,CAPELLA,DENEB,ERIDANI,FORNAX,GEMINI,HADAR,INDI,JACARA,KATENCOR,LAPERTAE,MIZAR
1751 DATANORYON,OPIUM,PROCYON,QUEATUS,RIGEL,SIRIUS,THORUS,UTOPIA,VENORA,WEREWORLD,XARNIA
1760 IFS(I,3)=PANDE(P,0)THEN1770
1761 GOTO1040
1770 C=50-S(I,4)*5:IFC>10THENC=10
1780 X=RND(100):IFX>CTHEN1040
1790 PRINT"THERE HAS BEEN A REVOLT ON THE  CLASS 4 SYSTEM OF "N$(I)
1800 C=S(I,4)*10:X=RND(100):IFX>CTHEN1840
1810 PRINT"WHICH WAS PUT DOWN WITH SOME    DIFFICULTY BY YOUR TROOPS"
1820 C=RND(5):IFC>=S(I,4)THENC=S(I,4)-1:IFC=0THEN1030
1830 GOSUB780:M$="THE WELL-EQUIPPED REBELS DESTROYED"+STR$(C)" STARSHIP"+X$:GOSUB1:S(I,4)=S(I,4)-C:GOTO1030
1840 C=S(I,4):GOSUB780:M$="YOUR FORCE OF"+STR$(C)+" STARSHIP"+X$
1841 M$=M$+" WAS VASTLY OVERWHELMED BY THE REBEL ARMY... LONG LIVE THE REVOLUTION!":GOSUB1
1850 S(I,3)=-2:S(I,4)=RND(3)
1860 FORC=0TONP:IFE(C,0)=ITHEN1870
1861 NEXTC
1870 IFE(C,1)<>-1THENS(I,3)=C
1880 GOTO1030
1890 CLS:PRINT"  INTERSTELLAR COMMUNICATIONS":FORM=0TONP:PRINTF$(M)" (PLAYER #"M+1")"
1891 PRINT"MESSAGE: "M$(M):PRINT:NEXTM
1900 INPUT"CHANGE YOUR MESSAGE (Y/N)";X$:IFLEFT$(X$,1)="Y"THENPRINT:INPUT"NEW MESSAGE: ";M$(P)
1910 RETURN
1920 CLS:PRINT"COMPUTER DECIDING MOVE":IFT=1THENGOSUB2060
1930 IFS(E(P,0),3)<>PTHEN1980
1940 C=0:FORI=0TONS:IFS(I,3)<>PTHEN1960
1950 ONS(I,0)GOSUB1100,1110,1120,1130:C=C+X
1960 NEXTI:E(P,1)=E(P,1)+C:C=INT(E(P,1)/5)
1970 E(P,1)=INT(E(P,1)-5*C):S(E(P,0),4)=S(E(P,0),4)+C
1980 IFS(E(P,0),4)<=TTHENRETURN
1990 FORK=0TO9:IFW(P,K,2)=0THEN2000
1991 NEXTK:RETURN
2000 FORI=1TONS:IFS(X(P,I),3)=PTHENNEXTI:RETURN
2010 Y=X(P,I):X=E(P,0):RA=INT(RND(T/2)):S(X,4)=S(X,4)-RA
2011 IFS(X,4)=0THENS(X,3)=-1:IFS(X,0)=3THENS(X,3)=-2
2020 IFRA=0THENRETURN
2030 W(P,K,0)=Y:W(P,K,1)=RA:GOSUB1340:W(P,K,2)=T+C:GOSUB2120
2040 IFS(E(P,0),4)<=T THENRETURN
2050 NEXTI:NEXTK:RETURN
2060 X=E(P,0):FORY=0TONS:GOSUB1340:D(Y)=C:NEXTY
2070 Z=0
2080 DM=99:FORY=0TONS
2090 IFD(Y)<=DMTHENDM=D(Y):C=Y
2100 NEXTY:X(P,Z)=C:Z=Z+1:D(C)=99
2110 IFZ>NSTHENRETURN
2111 GOTO2080
2120 RA=RND(4):ONRAGOTO2130,2140,2150,2160
2130 M$(P)="I'LL GET YOU YET!":RETURN
2140 M$(P)="JUST WAIT SIX TURNS!":RETURN
2150 M$(P)="I WILL RULE THE GALAXY!":RETURN
2160 M$(P)="I'M WAITING FOR YOU!":RETURN
3010 REM STELLAR EMPIRES GAME PROGRAM
3020 REM BY GRAHAM WILSON
3030 REM MODIFIED FOR THE TRS-80
3040 REM COLOR COMPUTER BY TED
3050 REM AND DREW SHORTER
3060 REM DOWNLOADED FROM FEB 1982
3070 REM CHROMASETTE MAGAZINE
3080 REM DISK AND OTHER MODIFICATIONS
3090 REM ADDED BY DONALD BARBER
3100 REM MC-10 VER. BY JIM GERRIE
