0 CLEAR500:DIMCC,ZZ,M,M$,I$:M=16384:CC=RND(-(PEEK(9)*256+PEEK(10))):GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
5 M$="PRESS ANY KEY.":GOSUB7
6 ON-(INKEY$="")GOTO6:CLS:RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+M:FORZZ=CC-LEN(M$)+1TOCC:POKEZZ,PEEK(ZZ)-64:NEXT:RETURN
10 REM..........SWORDSMAN......WALWYN
25 MZ=0
30 DIME$(4):E$(1)="YOU ARE UNHARMED AND RARING TO GO...!"
31 E$(2)="YOU ARE SLIGHTLY WOUNDED NOW. BE BRAVE!"
32 E$(3)="YOU HAVE RECEIVED MULTIPLE LACERATIONS."
33 E$(4)="IT'S YOUR LAST CHANCE NOW. GOOD LUCK..."
100 DIMV(9)
110 FORJ=1TO9:READV(J):NEXTJ
120 DATA 3,4,3,4,2,5,2,2,4
200 DIMV$(9)
210 FORK=1TO9:READV$(K):NEXT
220 DATA"GANGRENE GOAT","NORTH-SEA BOIL","VAMPIRE CAT"
230 DATA"POLITICAL WORM","MAD METER-MAID","SCHOOL SPINACH"
240 DATA"DANDRUFFED BEAR","RAMPANT BANKER","PICKLED ALBATROSS"
300 CLS:PRINTTAB(11)"swordsman"
310 M$="IN THIS GAME, YOU ARE TO BE PUT AGAINST A HORDE OF TERRIFYING MONSTERS. YOU ARE ARMED ONLY WITH A SWORD.":GOSUB1
340 PRINT"YOUR CONTROLS ARE:"
341 M$="A  MOVE TO THE LEFT ":GOSUB7:PRINT
350 M$="D  MOVE TO THE RIGHT":GOSUB7:PRINT
360 M$="S  LUNGE WITH SWORD ":GOSUB7:PRINT
370 M$="THERE ARE TWO SWORD POSITIONS. THE FIRST TIME YOU PRESS 'S', YOU PUT YOUR SWORD 'ON-GUARD'.":GOSUB1
400 M$="THE SECOND KEY-PRESS COMMITS YOU TO A SWORD THRUST.":GOSUB1:GOSUB5
420 M$="SOME MONSTERS ARE HARDER TO KILL THAN OTHERS. THE DREADED 'SCHOOL SPINACH' IS PARTICULARLY NASTY TO MEET.":GOSUB1
440 M$="YOU GET MORE POINTS IF YOU KILL THE MONSTER FURTHER TO THE RIGHT OF THE SCREEN.":GOSUB1
470 M$="EACH TIME YOU WIN, YOUR WOUNDS ARE SOOTHED JUST A LITTLE.":GOSUB1
480 M$="IF YOU ARE WOUNDED MORE THAN 4 TIMES THEN YOU ARE DEFINITELY   D E A D!":GOSUB1:GOSUB5
500 DIMD$(4,3),M$(9,4,3)
510 FORI=1TO4:FORJ=1TO3:READD$(I,J)
530 NEXT:NEXT
550 REM THESE DATA LINES HOLD VARIOUS STANCES OF THE SWORDSMAN
600 DATA " O!","'o+"," ))"
602 DATA " O!","'o+","/)"
604 DATA " O /","'o+"," X"
606 DATA " O","'o-+-","/ )"
650 FORK=1TO9:FORJ=1TO4:FORI=1TO3
660 READM$(K,J,I):IFRIGHT$(M$(K,J,I),1)="!"THENM$=M$(K,J,I):CC=LEN(M$):M$(K,J,I)=LEFT$(M$,CC-2)+CHR$(92)+CHR$(92)
661 IFRIGHT$(M$(K,J,I),1)="L"THENM$=M$(K,J,I):CC=LEN(M$):M$(K,J,I)=LEFT$(M$,CC-1)+CHR$(92)
680 NEXT:NEXT:NEXT
699 REM LINES 700-876 CONTAIN STANCES OF THE MONSTERS WHO OPPOSE THE SWORDSMAN
700 DATA " @"," gg-"," < >"
702 DATA " @"," gg-"," <!!"
704 DATA "","@gg-","/ !!"
706 DATA "@  /"," ggL"," ("
720 DATA " ***","*@+@*","*:::*"
722 DATA " ***","*@+@*","*'''*"
723 DATA ""," ***","*@+@*"
724 DATA " ***","*@+@*","*:::*"
740 DATA "","OŒ'"," X"
742 DATA "","OŒ'"," <<"
744 DATA "OŒ-"," !!",""
746 DATA "OŒ,","//",""
760 DATA "","@O  O ","  OO O"
762 DATA "","@  OO"," OO  O"
764 DATA "","  OO O","@O  O"
766 DATA "","   O","@OO OO"
780 DATA "$$$$$","$O.O$","$ + $"
782 DATA "$$$$$","$O.O$","$ = $"
784 DATA "$$$$$","$O.O$","$ - $"
786 DATA "$$$$$","$O.O$","$ * $"
800 DATA "## #","#O##"," ##"
802 DATA "####"," O###","  #"
804 DATA "###","###"," O##"
806 DATA "###","## ##"," #O"
820 DATA "*oo*.","-bb- "," <> "
822 DATA "*oo* ","-ee-."," << "
824 DATA "*oo* ","-aa- "," >< ."
825 DATA "*oo* ","-rr- "," >> "
840 DATA " O","-b-"," X"
842 DATA " O","/bL","( )"
844 DATA " O","(b)","< >"
846 DATA " O",")b(","/ L"
860 DATA "  // ","-Op-<",""
862 DATA "-Op-<","  !!",""
864 DATA "  // ","-Op-<",""
866 DATA "-Op-<","  !!",""
940 REM START OF GAME
950 REM VK=NUMBER OF DEAD MONSTERS
960 REM WK=WOUNDED LEVEL
970 REM PK=POINTS SCORED
980 VK=0:WK=1:MZ=0:MR=0:D8=0:D7=0:M6=0:M7=0:M8=0:D9=0:M9=0:P5=0:HS=PK*-(PK>HS)+HS*-(HS>=PK):PK=0:CLS
1000 GOSUB2800
1100 GOSUB4000
1900 GOTO1000
2750 FORZZ=1TO2000:NEXT:RETURN
2760 PRINT@352,;:M$="THE "+V$(MZ)+" CUT YOU UP!       ":GOSUB1
2765 FORI=1TO1500:NEXTI:RETURN
2799 REM CLEAR TOP SCREEN
2800 PRINT@0,"                                ";
2801 PRINT"Ÿßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß";
2802 FORX=128TO320STEP32:PRINT@X,"                                ";:PRINT@RND(96)+31,"Ï";:NEXT:PRINT@0,;
2820 RETURN
2950 MR=MZ
2960 MZ=RND(9):IFMZ=MRTHEN2960
2970 RETURN
4000 GOSUB2950:REM MONSTER BATTLE
4002 PRINT@288,"ÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿ";
4005 M$="IT'S THE "+V$(MZ)+"!":GOSUB1
4006 M$="(DEGREE OF DIFFICULTY="+STR$(V(MZ))+")":GOSUB1
4007 M$=E$(WK):GOSUB1
4010 M7=4:D7=2:M8=20+RND(5)
4011 D8=15:M6=4:D6=2:M9=M8:D9=D8
4015 PRINT@0,"POINTS";PK;TAB(16)"HIGH"HS
4020 ZZ=0
4021 K1=ASC(INKEY$+".")
4025 IFK1=65THEND8=D8-1:D7=1:IFD8<4THEND8=4
4030 IFK1=68THEND8=D8+1:D7=2:IFD8>27THEND8=27
4035 IFK1=83THEND7=D7+1:IFD7>3THEND7=4:D8=D8+1:IFD8>27THEND8=27
4036 IFK1=83ANDD7=2THEND7=3
4037 PRINT@191+D9,"     ";:PRINT@223+D9,"     ";:PRINT@255+D9,"     ";
4038 K3=192+D9:FORX=1TO3:PRINT@K3,D$(D7,X):K3=K3+32:NEXT
4039 D9=D8:D6=D7
4040 IFD7=4THEN4060
4050 GOTO4085
4060 K3=192+M9:FORX=1TO3:PRINT@K3,M$(MZ,M6,X)" ";:K3=K3+32:NEXT
4065 K3=225+M+D8+2:Z1=PEEK(K3):IFZ1<>96ANDZ1<>107ANDZ1<>109THEN4090
4070 PRINT@225+D8,"-+-";
4082 D7=2
4085 M7=M7+1:IFM7=5THENM7=1
4086 IFRND(0)>.35THENM8=M8-1
4088 K3=192+M8:FORX=1TO3:PRINT@K3,M$(MZ,M6,X)" ";:K3=K3+32:NEXT:IFM8<D8+2THEN4100
4089 M9=M8:M6=M7:GOTO4020
4090 PRINT@225+D8,"-+¿";:SOUND100,2:PRINTCHR$(8)"-";:PRINT@352,;:M$="WELL DONE! YOU GOT THE "+V$(MZ)+".":GOSUB1
4091 P5=INT(V(MZ)*D8):VK=VK+1:WK=WK-1:IFWK<1THENWK=1
4092 PRINT@0,"POINTS FOR KILL =";P5:PK=PK+P5:GOSUB2750
4093 GOSUB2800
4095 RETURN
4100 WK=WK+1
4102 IFWK<5THENPRINT@0,"TRY AGAIN...":GOSUB2760:GOSUB2800:GOTO4002
4105 PRINT@352,"":PRINT@352,;:FORI=1TO3:PRINT"OH DEAR...";:POKE49151,64
4107 FORJ=1TO750:NEXTJ:NEXTI:PRINT:SOUND1,1
4110 PRINT"AAAAAAAAAAARGH!"
4120 PRINT"ANOTHER HERO BITES THE DUST."
4125 M$="YOU KILLED"+STR$(VK)+" MONSTERS SCORING   ":GOSUB7
4126 PRINT@480,;:M$=MID$(STR$(PK),2)+" PTS. ANOTHER GO? (Y/N)":GOSUB7:FORZZ=CC+1TOM+511:POKEZZ,PEEK(ZZ)-64:NEXT
4130 A$=INKEY$:IFA$=""THEN4130
4133 IFA$="N"THENEND
4134 IFA$="Y"THEN980
4135 GOTO4130
5000 REM ******************************
