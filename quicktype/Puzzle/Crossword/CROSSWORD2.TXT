0 CLEAR1000:DIMCC,ZZ,M$,I$:GOTO49
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
5 PRINT@480,"                               ";:POKE16384+511,96:PRINT@480,;:RETURN
49 ZU=1:CU$=CHR$(8)+CHR$(128)+CHR$(8)+" ":GOTO101
50 REM INPUT ROUTINE
51 INPUTZ$
57 ZC=0:RETURN
59 REM"GET KEY ROUTINE
60 FORCC=1TO35:Z$=INKEY$:PRINTMID$(CU$,(ZU+1)*2-1,2);:IFZ$<>""THEN62
61 NEXTCC:ZU=-1*(ZU=0):IFZ$=""THEN60
62 IFZC$<>""THENFORZX=1TOLEN(ZC$):ZF=ZF+ZX*-(Z$=MID$(ZC$,ZX,1)):NEXT:IFZF=0THEN60
63 ZC$="":ZF=0:RETURN
101 REM MAIN LINE
105 REM GOTO130
110 GOSUB 10000
120 GOSUB 9000
130 GOSUB 1000
135 REM GOSUB4010
140 GOSUB 8000
200 :
210 GOSUB 11000
220 GOSUB 2500
230 GOSUB 3000
235 PRINT@0,""
238 IF NF<>0 THEN PRINT@480,MID$(STR$(NF),2);" DIDN'T FIT.";
250 FORCC=1TO50:PRINT@496,"PRESS a FOR NEW";:POKE16384+511,110:A$=INKEY$:IFA$<>""THENCC=50:NEXT:GOTO260
251 NEXT
252 FORCC=1TO50:PRINT@496,"HIT p TO PRINT.";:POKE16384+511,96:A$=INKEY$:IFA$<>""THENCC=50:NEXT:GOTO260
253 NEXT
260 IFA$<>"A"ANDA$<>"P"THEN250
270 IFA$="A"THEN200
280 IFA$<>"P"THEN250
400 :
410 REM PRINTING
420 FOR IO=0 TO 1
425 LPRINTCHR$(18)
430 GOSUB5
435 IF IO=0 THENGOSUB5:PRINT"ANSWERS";
437 IF IO=1 THENGOSUB5:PRINT"PUZZLE";
440 GOSUB5:PRINT"INSERT PAPER AND HIT enter";
450 G$=INKEY$
460 G$=INKEY$:IF G$<>CHR$(13) THEN 460
500 :
510 GOSUB5
520 PRINT"PRINTING";
530 GOSUB 13000 PRINT CROSSWOND GRID
531 LPRINTCHR$(32)
535 GOSUB5000
540 IF IO=0 THENGOSUB5:PRINT"ANSWERS COMPLETED";
550 NEXT IO
560 GOSUB5:PRINT"PUZZLE COMPLETED";
980 END
1000 :
1001 REM INPUT WORDS AND DEFINITIONS
1002 DIM W$(25),D$(25)
1005 I=0
1010 CLS
1015 PRINT"TITLE  ";:ZC=38:GOSUB50:TL$=Z$
1030 I=I+1
1035 IF I<1THENI=1
1040 PRINT:PRINT "WORD NO.";I" ";:GOSUB 50:W$(I)=Z$:B$=Z$
1045 IF B$="^" THEN I=I-1:GOTO 1035
1050 IF B$<>""THEN1055
1051 PRINT"DONE? (Y/N)?  ";
1052 ZC$="YN":GOSUB60:IFZ$="Y"THENN=I-1:FL=0:CLS:RETURN
1053 GOTO1035
1055 FOR J=1 TO LEN(W$(I))
1056 IFMID$(W$(I),J,1)<"A" OR MID$(W$(I),J,1)>"Z" THEN FA=1:J=LEN(W$(I))
1057 NEXT J
1058 IF FA=1 THEN FA=0:GOSUB 1150:GOTO 1040:REM REDO WORD
1060 PRINT : PRINT "DEFINITION NO.";I
1065 ZC=74:GOSUB50
1070 B$=Z$:IF B$="^"THEN I=I-1:GOTO1035
1075 IFB$=""THEN1051
1080 D$(I)=B$
1090 IF LEN(D$(I))>74THEN"DEFINITION IS TOO LONG":I=I-2
1100 IF I<20 THEN 1030
1110 N=20:RETURN
1150 PRINT:PRINT "LETTERS ONLY PLEASE!"
1160 RETURN
2500 :
2501 REM POKE FIRST WORD
2510 FOR K=0 TO N : DR(K)=0 : NEXT
2520 D(1)=1 : D(2)=32
2525 K=INT((N-1)*RND(0))+2
2530 W$=W$(1):W$(1)=W$(K):W$(K)=W$
2535 D$=D$(1):D$(1)=D$(K):D$(K)=D$
2540 T=1 : W$=W$(T) : LW=LEN(W$)
2550 K=INT(2*RND(0))+1:DR(T)=D(K)
2560 X=INT(14*RND(0))+5:IF K=1 AND X+LW>22 THEN 2560
2570 Y=INT(6*RND(0))+2:IF K=2 AND Y+LW>11 THEN 2570
2580 LC=16384+32*Y+X
2590 GOSUB 7000 POKE WORD
2599 RETURN
3000 :
3001 REM MAKE CROSSWORD
3010 T=T+1:U=0
3020 FOR C=1 TO N : IF DR(C)<>0 THEN U=U+1 : G(U)=C : NEXT C
3030 FOR I=1 TO U :S=1+INT(U*RND(1)):GG=G(I) :G(I)=G(S) :G(S)=GG :NEXT I
3100 :
3110 W$=W$(T) : LW=LEN(W$) : NF=NF+1
3130 PRINT@480,W$;"           ";
3170 FOR C=1TO U : E=G(C) : C$=W$(E)
3190 FOR L=1 TO LW: Z1$= MID$(W$,L,1)
3210 FORZQ=1 TO LEN(C$):Z2$= MID$(C$,ZQ,1)
3220 IF Z1$<>Z2$ THEN 3370
3240 IF DR(E)=32 THEN DR(T)=1
3250 IF DR(E)=1 THEN DR(T)=32
3280 LC=LO(E)+DR(E)*ZQ-DR(T)*L
3281 IFLC<16384 THEN 3370
3282 IF LC+LW*DR(T)>16384+480 THEN 3370
3284 WT=PEEK(LC)
3285 IF WT<27THEN3360
3286 IF PEEK(LC+(LW+1)*DR(T))<27 THEN3360
3290 POKELC,175
3301 :
3305 O=0
3310 FOR V=1 TO LW
3315 Q0=LC+V*DR(T):WI=PEEK(Q0):POKE Q0,175: POKEQ0,WI
3317 IF DR(T)=32 THEN 3322
3318 IF PEEK(Q0+32)<>96 AND FQ(Q0+32-AD)=DR(T) THEN 3345
3319 IF PEEK(Q0-32)<>96 AND FQ(Q0-32-AD)=DR(T) THEN 3345
3320 GOTO 3324
3322 IF PEEK(Q0+1)<>96 AND FQ(Q0+1-AD)=DR(T) THEN 3345
3323 IF PEEK(Q0-1)<>96 AND FQ(Q0-1-AD)=DR(T) THEN 3345
3324 FOR Y=1 TO U : IF DR(Y)=DR(T) THEN 3327
3325 IF Q0=LO(Y)THEN O=1:Y=U :GOTO3327
3326 IF Q0=LO(Y)+DR(Y)*(LEN(W$(Y))+1) THEN O=1:Y=U
3327 NEXT Y
3328 IF O=1 THEN 3345
3340 IF WI<>96 AND WI<>ASC(MID$(W$,V,1))-64 THEN 3345
3344 GOTO 3350
3345 V=LW:O=1
3350 NEXT V
3355 IF O=0 THEN GOSUB 7000:NF=0:ZQ=LEN(C$):L=LW:C=U
3360 POKE LC,WT
3370 NEXTZQ,L,C
3410 IF NF=0 THEN 3490
3420 IF N-U=NF THEN 3499
3430 W$=W$(U+1):D$=D$(U+1)
3440 FOR C=U+2 TO N:W$(C-1)=W$(C):D$(C-1)=D$(C):NEXT C
3475 W$(N)=W$:D$(N)=D$
3480 GOTO 3100
3490 IF T<N THEN 3000
3499 RETURN
4000 :
4010 LPRINTCHR$(18):RETURN
4500 Q$(SC)=Q$(SC)+LEFT$(L$,1)+CHR$(254)+" ":SC=SC+1
4530 FORI=1TOLEN(Q$(SC-1)):Q$(SC)=Q$(SC)+" ": NEXT I
4540 RETURN
5000 :
5001 REM  PRINT STUDENTS' COPY
5030 LPRINTCHR$(19)
5040 LPRINTTAB(34);CHR$(1);"ACROSS"
5060 LPRINTTAB(34);CHR$(1);CHR$(184);CHR$(184);CHR$(184);CHR$(184);CHR$(184);CHR$(184)
5080 FOR I=1 TO A
5090 LPRINTNA(I);" ";DA$(I)
5110 NEXT I
5111 LPRINT
5120 LPRINTTAB(36);CHR$(1);"DOWN"
5130 LPRINTTAB(36);CHR$(1);CHR$(184);CHR$(184);CHR$(184);CHR$(184)
5150 FOR I=1 TO D
5160 LPRINTND(I);" ";DD$(I)
5180 NEXT I
5200 RETURN
6000 :
6001 REM   INPUT DATA
6010 GOSUB50:B$=Z$
6020 IF B$="E" THEN FL=1
6030 RETURN
7000 :
7001 REM POKING THE WORD
7010 FOR I=1 TO LW:POKE LC+I*DR(T),ASC(MID$(W$,I,1))-64
7020 FQ=LC+I*DR(T)-16384:FQ(FQ)=DR(T)
7030 NEXTI
7040 CF=LC+DR(T):CL=LC+LW*DR(T)
7050 C0=32*INT(CF/32):CF=CF-C0
7060 C1=32*INT(CL/32):CL=CL-C1:C1=C1+32
7070 IF C0<UB THEN UB=C0
7080 IF C0>LB THEN LB=C0
7090 IF C1<UB THEN UB=C1
7100 IF C1>LB THEN LB=C1
7150 LO(T)=LC
7160 RETURN
8000 :
8001 REM SET UP
8020 DIM LC(25),DR(25),LO(25),G(25),FQ(1000),A$(19),DA$(25),DD$(25)
8025 DIM TB(25),Q$(25),NA(25),ND(25)
8030 QZ=RND(-(PEEK(9)*256+PEEK(10))):REM SEED RANDOM GENERATOR
8040 AD=16384
8400 :
8410 FOR J=0TO9
8420 FOR I=1 TO 8:READ A:A$(J)=A$(J)+CHR$(A):NEXT I
8425 A$(J+10)=CHR$(124)+CHR$(0)+LEFT$(A$(J),6)
8430 NEXT J
8480 UB=LC:LB=LC+DR(T)*(LEN(W$)+1)
8490 RETURN
8500 DATA 56,68,68,56,0,0,0,0
8510 DATA 124,0,0,0,0,0,0,0
8520 DATA 36,76,84,36,0,0,0,0
8530 DATA 0,68,84,104,0,0,0,0
8540 DATA 24,40,124,8,0,0,0,0
8550 DATA 116,84,84,72,0,0,0,0
8560 DATA 56,84,84,72,0,0,0,0
8570 DATA 64,76,80,96,0,0,0,0
8580 DATA 40,84,84,40,0,0,0,0
8590 DATA 36,84,84,56,0,0,0,0
9000 :
9001 REM INSTRUCTIONS
9010 CLS:PRINT"DO YOU WANT INSTRUCTIONS (Y/N)  ";:ZC$="YN":GOSUB60
9020 IF Z$="N"THEN 9310
9024 CLS:PRINTTAB(8)"CROSSWORD PUZZLE"
9030 PRINT:M$="CROSSWORD PUZZLE ALLOWS THE USER TO MAKE A CROSSWORD PUZZLE.":GOSUB1
9060 PRINT:M$="AS SOON AS YOU RUN THE PROGRAM THE COMPUTER WILL ASK YOU TO ENTER WORDS AND "
9080 M$=M$+"THEIR CORRESPONDING DEFINITIONS.":GOSUB1
9090 M$="ALWAYS END THE CURRENT LINE YOU'RE TYPING IN BY PRESSING THE enter KEY.":GOSUB1
9110 GOSUB9500
9120 CLS:M$="THERE ARE EDITING FEATURES IN THIS PROGRAM WHICH MAKE IT EASIER TO USE.":GOSUB1
9180 PRINT:M$="TO CORRECT THE PREVIOUS LINE ENTERED HIT enter BEFORE TYPING ANYTHING ELSE.":GOSUB1
9200 PRINT:M$="WHEN YOU HAVE FINISHED ENTERING ALL OF YOUR WORDS AND DEFINITIONS TYPE e AND enter.":GOSUB1
9230 GOSUB9500
9240 CLS:M$="WHEN YOU ENTER A WORD FOR THE PUZZLE YOU MUST USE LETTERS ONLY.":GOSUB1
9255 M$="DEFINITIONS MAY CONTAIN ANY CHARACTER FOUND ON THE KEYBOARD.":GOSUB1
9280 M$="THE DEFINITION MAY BE UP TO TWO LINES LONG.":GOSUB1 
9285 M$="THE PROGRAM CAN HANDLE UP TO 19 WORDS AND THEIR DEFINITIONS.":GOSUB1
9290 GOSUB9500
9310 RETURN
9500 PRINT:PRINT"PRESS enter TO CONTINUE  ";
9510 ZC$=CHR$(13):GOSUB60
9520 RETURN
10000 :
10001 REM TITLE
10020 CLS
10030 TT$="CROSSWORD PUZZLE"
10040 PRINTTAB(8)TT$
10100 RETURN
11000 :
11010 CLS
11070 RETURN
11090 END
13000 :
13001 REM PRINT CROSSWORD GRID
13020 LPRINTTAB(32-INT(LEN(TL$)/2));CHR$(1);TL$
13030 LPRINT:LPRINT
13040 A=0 : D=0 : CW=0 : EE=0
13100 :
13110 FOR R=4 TO 22
13120 FOR I=1 TO SC :Q$(I)="" :NEXT
13130 P$="" : R$="" : SC=1 : E=0
13140 GOSUB5:PRINT"PRINTING";R-3;
13200 :
13210 FOR C=7 TO 32
13220 XX=AD+32*R+C:X=PEEK(XX)
13230 IF X>26 THEN 13700:REM NOT A LETTER
13300 :
13310 P$=P$+CHR$(219)+CHR$(192)+CHR$(192) : FS=0 : E=1
13320 FOR I=1 TO N
13330 IF LO(I)+DR(I)<>XX THEN 13370
13340 IF DR(I)=1  THEN A=A+1 : NA(A)=CW+SC : DA$(A)=D$(I)
13350 IF DR(I)=32 THEN D=D+1 : ND(D)=CW+SC : DD$(D)=D$(I)
13360 FS=1
13370 NEXT I
13400 :
13410 IF FS=0 THEN Q$(SC)=Q$(SC)+CHR$(221)+"  " : GOTO 13500
13420 Q$(SC)=Q$(SC)+CHR$(221)+CHR$(254)+" "
13430 SC=SC+1 : TB(SC)=3*(C-6)
13500 :
13510 IF IO=1 THEN R$=R$+CHR$(221)+"  " : GOTO 13900
13520 R$=R$+CHR$(221)+" "+CHR$(X+64) : GOTO 13900
13700 :
13710 Y=PEEK(XX-1):ZQ=PEEK(XX-32):V=PEEK(XX-33)
13720 IFZQ<27 THEN P$=P$+CHR$(219)+CHR$(192)+CHR$(192) : GOTO 13800
13730 IF V>26 AND Y>26 THEN P$=P$+"   " : GOTO 13800
13740 P$=P$+CHR$(219)+"  "
13800 :
13810 IF Y<27 THEN Q$(SC)=Q$(SC)+CHR$(221)+"  " : R$=R$+CHR$(221)+"  " : GOTO 13900
13820 Q$(SC)=Q$(SC)+"   " : R$=R$+"   "
13900 :
13910 NEXT C
13915 IF E+EE=0 GOTO 13960
13920 EE=1 : LPRINTP$
13930 GOSUB 14000
13940 LPRINTR$
13950 CW=CW+SC-1
13955 IF E+EE=1 THEN R=22
13960 NEXT R
13990 RETURN
14000 :
14010 IF SC=1 THEN 14060
14020 FOR I=1 TO SC-1
14030 LPRINTA$(CW+I)
14040 LPRINTTAB(TB(I));Q$(I);CHR$(141);
14050 NEXT I
14060 LPRINTTAB(TB(SC));Q$(SC)
14070 RETURN

