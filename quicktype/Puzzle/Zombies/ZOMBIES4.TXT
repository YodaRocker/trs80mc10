15 DIMZ(15),Z1(15),W(15):MC=16384
20 CC=RND(8):ZZ=118+CC*16:C$=CHR$(ZZ):W$=CHR$(ZZ):B$=CHR$(127+CC*16):CLSCC
25 WC=0:FORI=1TO15:W(I)=0:Z(I)=0:Z1(I)=0:NEXT
30 FORI=0TO15
40 PRINT@I+1,C$;
45 PRINT@32*I,C$;
50 PRINT@(32*I+16),C$;
60 Z=RND(13)+1
70 IFI>1ANDI<15THENIFRND(0)<0.4THENWC=WC+1:W(WC)=32*Z+I:PRINT@32*Z+I,W$;
80 S=RND(10):S1=S
90 Z(INT(I/2)+1)=4+S
100 PRINT@480+I,C$;
110 NEXTI:IFWC=0THEN20
115 FORJ=1TO4
116 Z(J)=RND(10)+4:Z(J+4)=RND(10)+4:Z1(J)=Z(J):Z1(J+4)=Z(J+4):IFPEEK(32*Z(J)+Z(J+4)+MC)<>ASC(B$)THEN116
117 PRINT@32*Z(J)+Z(J+4),"z";:NEXTJ
120 K=RND(13)+1:K1=K:IFPEEK(32*S+K+MC)<>ASC(B$)THEN120
126 PRINT@53,"ZOMBIES";:PRINT@114,"TO MOVE: AWSZ";:GOTO200
130 FORI=1TO4
135 IFZ(I)=0THEN190
140 PRINT@32*Z(I)+Z(I+4),B$;
150 Z(I)=Z(I)-SGN(Z(I)-S)
160 Z(I+4)=Z(I+4)-SGN(Z(I+4)-K)
165 IFPEEK(MC+(32*Z(I)+Z(I+4)))=ZZTHENZ(I)=0:GOTO190
170 PRINT@32*Z(I)+Z(I+4),"z";
180 IFZ(I)=SANDZ(I+4)=KTHEN280
190 NEXTI
200 POKE32*S+K+MC,.
210 A$=INKEY$:IFA$="A"ORA$="S"ORA$="W"ORA$="Z"THEN220
215 GOTO210
220 PRINT@32*S+K,B$;
230 IFZ(1)+Z(2)+Z(3)+Z(4)=0THENPRINT@32*S+K,CHR$(96)"SAFE!";:SOUND230,15:V=V+1:G=G+1:GOTO300
235 K=K-2*((A$="S")-(A$="A"))
240 IFK<=0ORK>=16THEN280
250 S=S-2*((A$="Z")-(A$="W"))
260 IFS<=0ORS>=15THEN280
265 IFPEEK(32*S+K+MC)=ZZTHEN280
270 GOTO130
280 PRINT@213,"YOU'RE";
285 PRINT@245,"DEAD!!";:SOUND150,20:G=G+1
290 PRINT@308,"REDO? (R)";
295 PRINT@375,"OR";
300 PRINT@436,"NEW MAZE?";:PRINT@114,"GAME:        ";:FORI=1TO8:PRINTCHR$(8);:NEXT:PRINTG;
305 PRINT@470,"(Y/N)";
310 A$=INKEY$:IFA$=""THEN310
320 IFA$="Y"THEN20
330 IFA$="N"THEN350
335 IFA$="R"THENGOSUB400:GOTO126
340 GOTO310
350 CLS:PRINT"GAMES ="G"  VICTORIES ="V:PRINT"SURVIVAL RATE ="STR$(INT(V/G*100))"%":END
400 CLSCC:S=S1:K=K1
405 FORI=1TOWC:PRINT@W(I),W$;:NEXT:FORI=0TO15:PRINT@I+1,C$;:PRINT@32*I,C$;:PRINT@32*I+16,C$;:PRINT@480+I,C$;:NEXT
470 FORJ=1TO4:PRINT@32*Z1(J)+Z1(J+4),"z";:Z(J)=Z1(J):Z(J+4)=Z1(J+4):NEXT
480 RETURN
