' ROBOT MINEFIELD
' BY TIM HARTNELL (1983)
' TRANSCRIBED BY BOMBERPUNK (2008)

10 REM Robot Minefield
20 GOTO 250
30 REM Move Robots
40 TALLY = 0
50 FOR E = 1 TO 4
60 IF A(B(E), C(E)) = 42 THEN TALLY = TALLY + 1: GOTO 200
70 X = B(E): Y = C(E)
80 IF B(E) < D THEN B(E) = B(E) + 1
90 IF B(E) > D AND RND > .3 THEN B(E) = B(E) - 1
100 IF C(E) < F AND RND > .3 THEN C(E) = C(E) + 1
110 IF C(E) > F THEN C(E) = C(E) - 1
120 IF B(E) < 2 THEN B(E) = 2
130 IF B(E) > 14 THEN B(E) = 14
140 IF C(E) < 2 THEN C(E) = 2
150 IF C(E) > 14 THEN C(E) = 14
160 A(X, Y) = 46
170 IF A(B(E), C(E)) = 42 THEN TALLY = TALLY + 1: GOTO 200
180 IF A(B(E), C(E)) = 72 THEN A(B(E), C(E)) = 36: GOSUB 300: GOTO 910
190 A(B(E), C(E)) = 36
200 NEXT E
210 IF TALLY < CH THEN TALLY = CH
220 CH = TALLY
230 IF TALLY = 4 THEN GOSUB 300: GOTO 970
240 RETURN
250 GOSUB 530: REM INITIALISE
260 GOSUB 300: PRINT MINEFIELD
270 GOSUB 30: REM ROBOTS MOVE
280 GOSUB 420: REM HUMAN MOVE
290 GOTO 260
300 REM print minefield
310 CLS
320 IF TALLY > 0 THEN PRINT "Dead robot tally:"; TALLY: PRINT
330 IF TALLY = 0 THEN PRINT : PRINT
340 PRINT : PRINT : PRINT
350 FOR B = 1 TO 15
360 FOR C = 1 TO 15
370 PRINT CHR$(A(B, C)); " ";
380 NEXT C
390 PRINT
400 NEXT B
410 RETURN
420 REM player move
430 A(D, F) = 46
440 A$ = INKEY$
450 IF A$ = "" THEN 440
460 IF A$ = "N" AND D > 2 THEN D = D - 1
470 IF A$ = "S" AND D < 14 THEN D = D + 1
480 IF A$ = "E" AND F < 14 THEN F = F + 1
490 IF A$ = "W" AND F > 2 THEN F = F - 1
500 IF A(D, F) = 42 THEN GOSUB 300: GOTO 990
510 A(D, F) = 72
520 RETURN
530 REM initialise
540 DEFINT A-Z
550 DIM A(15, 15), B(4), C(4)
560 CLS
570 PRINT : PRINT "Please stand by for a moment..."
580 CH = 0
590 N = (VAL(RIGHT$(TIME$, 2)))
600 RANDOMIZE N
610 REM place walls
620 FOR B = 1 TO 15
630 FOR C = 1 TO 15
640 A(B, C) = 46
650 IF B = 1 OR B = 15 OR C = 1 OR C = 15 THEN A(B, C) = 88
660 NEXT C: NEXT B
670 REM place mines
680 FOR B = 1 TO 20
690 C = INT(RND * 13) + 1
700 D = INT(RND * 13) + 1
710 IF A(C, D) = 88 THEN 690
720 A(C, D) = 42
730 NEXT B
740 DATA 4,4,13,8,8,3,12,7
750 REM place robots
760 FOR E = 1 TO 4
770 D = INT(RND * 13) + 2
780 F = INT(RND * 13) + 2
790 IF A(D, F) <> 46 THEN 770
800 B(E) = D: C(E) = F
810 A(B(E), C(E)) = 36
820 NEXT E
830 REM place human
840 D = INT(RND * 13) + 2
850 F = INT(RND * 13) + 2
860 IF A(D, F) <> 46 THEN 830
870 A(D, F) = 72
880 RETURN
890 REM human at d,f
900 REM robots at b(e),c(e)
910 REM end of game
920 CLS : PRINT "A robot has got you!!"
930 CH = 0: TALLY = 0: GOSUB 340
940 A$ = ""
950 IF INKEY$ <> "" THEN 950
960 GOSUB 570: GOTO 260
970 CLS : PRINT "You've defeated the robots!!"
980 GOTO 930
990 CLS : PRINT "You've run into a mine!!"
1000 GOTO 930
