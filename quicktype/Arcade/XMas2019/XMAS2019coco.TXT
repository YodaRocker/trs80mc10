0 CLS:CLEAR2000:GOSUB10000:PRINT@232,"THE SANTA CLAUSE":PRINT:PRINTTAB(10)"BY JIM GERRIE":M=1024:GOTO1000
20 FORC=1TO4:FORT=1TO224:?@288,MID$(H$,T,32)MID$(I$,T,32)MID$(J$,T,32)MID$(K$,T,32)MID$(L$,T,32);:?@P,A$;:?@P+32,B$;
21 IFINKEY$=" "THENPRINT@R,CHR$(128);:R=P:PRINT@R,CHR$(207);
22 IFR THENH=PEEK(M+R+32):PRINT@R,CHR$(128);:R=R+32:PRINT@R,CHR$(207);:IFH>128THENR=.:IFH=255THENSOUND1,1:SOUND100,1:SC=SC+1:PRINT@486,SC;
23 NEXT:NEXT
50 CLS:M$="*-*-*-CHRISTMAS IS OVER!-*-*-*":GOSUB200:PRINT:PRINT
55 M$="YOUR FINAL SCORE:"+STR$(SC):GOSUB200:PRINT:PRINT
60 M$="YOU'RE A"+MID$(" BAD SANTA    N OKAY SANTA!  GOOD SANTA!   SUPER SANTA!  SONIC SANTA! ",1+INT(SC/6)*14,12):GOSUB200
75 IFSC>HS THENHS=SC
76 PRINT:PRINT:M$="BEST SANTA SO FAR:"+STR$(HS):GOSUB200
80 PRINT:PRINT:M$="TRY AGAIN NEXT YEAR? (Y/N)":GOSUB200
90 M$=INKEY$:IFM$=""THEN90
100 IFM$="N"THEN10003
110 IFM$="Y"THENSC=0:GOTO2050
120 GOTO90
200 T=LEN(M$):PRINTTAB((32-LEN(M$))/2)M$:RETURN
1000 DIMT,H$,I$,J$,K$,L$,R,H,C
1001 A$=CHR$(128)+CHR$(207)+CHR$(187)+CHR$(160)+CHR$(162)+CHR$(240)+CHR$(240)+CHR$(249)+CHR$(248)+CHR$(240)+CHR$(249)+CHR$(248)+CHR$(128)+CHR$(249)+CHR$(248)+CHR$(128)+CHR$(249)+CHR$(178)
1002 B$=CHR$(128)+STRING$(2,175)+CHR$(171)+CHR$(174)+CHR$(236)+CHR$(254)+CHR$(253)+CHR$(236)+CHR$(254)+CHR$(253)+CHR$(236)+CHR$(254)+CHR$(253)+CHR$(236)+CHR$(254)+CHR$(253)+CHR$(128)
1003 C$=STRING$(16,128)+CHR$(255)+STRING$(15,128)
1004 D$=STRING$(12,128)+CHR$(128)+CHR$(209)+CHR$(215)+CHR$(223)+CHR$(255)+CHR$(210)+STRING$(12,128)+CHR$(128)+CHR$(128)
1005 E$=STRING$(12,128)+CHR$(213)+CHR$(223)+CHR$(223)+CHR$(223)+CHR$(223)+CHR$(223)+CHR$(218)+STRING$(12,128)+CHR$(128)
1006 F$=STRING$(12,128)+CHR$(128)+CHR$(245)+CHR$(159)+CHR$(255)+CHR$(159)+CHR$(250)+STRING$(12,128)+CHR$(128)+CHR$(128)
1007 G$=STRING$(12,128)+CHR$(128)+CHR$(245)+CHR$(255)+CHR$(128)+CHR$(255)+CHR$(250)+STRING$(12,128)+CHR$(128)+CHR$(128)
2000 FORT=1TO7:R=RND(12):H$=H$+MID$(C$,R+1)+LEFT$(C$,R)
2010 I$=I$+MID$(D$,R+1)+LEFT$(D$,R)
2020 J$=J$+MID$(E$,R+1)+LEFT$(E$,R)
2030 K$=K$+MID$(F$,R+1)+LEFT$(F$,R)
2040 L$=L$+MID$(G$,R+1)+LEFT$(G$,R):NEXT
2041 H$=H$+LEFT$(H$,31):I$=I$+LEFT$(I$,31):J$=J$+LEFT$(J$,31):K$=K$+LEFT$(K$,31):L$=L$+LEFT$(L$,31)
2050 CLS0:PRINT@480,"":PRINT@480,"SCORE:";:T=0:P=32+8:R=0
2060 PRINT@448,STRING$(32,207);
2070 GOTO20
10000 IF PEEK(65535)=27 THEN POKE65497,0:GOTO10002 ELSE CLS:INPUT"CAN YOUR COMPUTER HANDLE DOUBLE SPEED (Y/N)";A$
10001 IF A$="Y" THEN POKE65495,0 ELSE IF A$<>"N" THEN10000
10002 CLS:RETURN
10003 IF PEEK(65535)=27 THEN POKE65496,0 ELSE POKE65494,0
10004 END
