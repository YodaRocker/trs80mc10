0 GOSUB85:CLEAR7500,36762:GOTO33
1 POKEVC,28:RETURN
2 R1=W:R2=3:RETURN
3 R1=3:R2=F:RETURN
4 R1=F:R2=U:R4=RN(C):RETURN
5 R1=U:R2=W:R3=R4:RETURN
6 R1=U:TC=C:C=RL:RETURN
7 IFQ=LFTHENRETURN
8 D=J:S=S-1:Q=Q-1:V3=VI+Q+1:V5=V3-1:RETURN
9 IFQ=RTTHENRETURN
10 D=U:S=S+1:Q=Q+1:V3=VI+Q+1:V5=V3+1:RETURN
11 R1=K:RETURN
12 HT=HT+U:PRINT@S,U$(W);:PRINT@Q,L$(W);:FORZ=UTOLF:NEXT:IFG=R8ORG2=R8THENR1=K:TK=U:RETURN
14 RETURN
15 IFLV<UTHEN81
16 LC=0:PRINT"GET READY FOR SLOPE #";11-LV:GOSUB69
17 CLS4:GOSUB1:D=0:S=80:Q=112:V3=VI+Q+1:V5=V3-1:TK=0:HT=0:TC=0
18 R1=1:R2=W:R3=RN(J):R4=R3:G=PEEK(V3):G2=PEEK(V5)
19 FORC=UTORL:?@S,BL$;:?@Q,BL$;:?@V4,MID$(B$(R1),R3,32)MID$(B$(R2),R4,31);:ONK(PEEK(PK)ANDPEEK(W))GOSUB7,9,11
20 G=PEEK(V3):G2=PEEK(V5):PRINT@S,U$(D);:PRINT@Q,L$(D);:IFG<>V2ORG2<>V2THENGOSUB12
21 ONR1GOSUB2,3,4,5,6:NEXT:IFTC<>0THEN120
23 CLS4:TC=C-1:FORT=1TO12:PRINT@S+(T*32),U$(D);:PRINT@Q+(T*32),L$(D);:FORZ=1TO10:NEXTZ
24 PRINT@S+(T*32),BL$;:PRINT@Q+(T*32),BL$;:NEXTT 
25 SOUND150,1:CLS:PRINT"YOU COMPLETED SLOPE #";11-LV;CHR$(8);"!":LC=1 
26 SC=TC-(HT*(INT(LV/2)*5)):L=11-LV
27 PRINT "SCORE =";SC
28 PRINT "BRANCHES HIT =";HT
29 PRINT "TRUNKS OR BOULDERS HIT =";TK
30 IFSC>=HS(L)THENINPUT"INPUT YOUR NAME";N$(L):N$(L)=LEFT$(N$(L)+" ",5):HS(L)=SC
31 PRINT "HIGH SCORE =";HS(L);"/";L;"BY ";N$(L)
32 GOTO96
33 DIMU$(6),L$(6),B$(4),N$(10),B$,BL$,T$,I$
34 DIMRN(1000),K(128),HS(10),Q,S,C,D,T,U,W,J,K,R1,R2,R3,R4,VI,V1,V2,V3,V4,V5,VC
35 DIMF,G,G2,GG,RL,TK,HT,SC,TC,CR,LC,PK,LF,RT,CH,Z,L,LV,R8
36 VI=16384:VC=49151:PK=17023:CH=16:LF=96:RT=125:R8=128:RL=1000
37 J=0:U=1:W=2:F=4:K=5:G=0:GG=218:V1=16895:V2=191:V4=480:CR=0
38 K(65)=1:K(83)=2:K(13)=3:FORT=1TO10:HS(T)=150:N$(T)="JIMG":NEXT
39 XC=36762:FORI=0TO103:POKEXC+I,PEEK(64022+I):NEXT:DATA49,49,60,55,54
40 FORI=0TO4:READY:POKEXC+I,Y:NEXT:POKE36855,V2
41 POKE17033,143:POKE17034,154:POKE17032,126
42 B$=CHR$(191):BL$=B$+B$+B$:FORT=1TO63:NEXTT
43 U$(0)=CHR$(155)+CHR$(145)+CHR$(189)
44 L$(0)=CHR$(184)+CHR$(152)+CHR$(191)
45 U$(1)=CHR$(190)+CHR$(162)+CHR$(167)
46 L$(1)=CHR$(191)+CHR$(164)+CHR$(180)
47 U$(2)=CHR$(255)+CHR$(255)+CHR$(255)
48 L$(2)=CHR$(255)+CHR$(255)+CHR$(255)
49 U$(3)=CHR$(191)+CHR$(191)+CHR$(096)+CHR$(191)+CHR$(191)
50 L$(3)=CHR$(191)+CHR$(096)+CHR$(096)+CHR$(096)+CHR$(191)
51 S3$(3)=CHR$(096)+CHR$(096)+CHR$(096)+CHR$(096)+CHR$(096)
52 S4$(3)=CHR$(191)+CHR$(191)+CHR$(128)+CHR$(191)+CHR$(191)
53 U$(4)=CHR$(191)+CHR$(085)+CHR$(085)+CHR$(085)+CHR$(191)
54 L$(4)=CHR$(085)+CHR$(085)+CHR$(085)+CHR$(085)+CHR$(085)
55 S3$(4)=CHR$(085)+CHR$(085)+CHR$(085)+CHR$(085)+CHR$(085)
56 S4$(4)=CHR$(191)+CHR$(191)+CHR$(128)+CHR$(191)+CHR$(191)
57 U$(5)=CHR$(191)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(191)
58 L$(5)=CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)
59 S3$(5)=CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)
60 S4$(5)=CHR$(191)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(191)
61 U$(6)=CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)
62 L$(6)=CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)
63 S3$(6)=CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)
64 S4$(6)=CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)
65 PRINT "PRESS # OF STARTING LEVEL (1-10)";
66 I$=INKEY$:Z=RND(10):IFI$=""THEN66
67 LV=11-INT(VAL(I$)):IFLV>10ORLV<1THENSOUND1,1:GOTO66
68 CLS:GOTO15
69 FORT=UTOF:B$(T)="":NEXT
70 FORT=UTO50
71 R1=RND(INT(LV/W)+W):IFR1=UTHEN73
72 R1=6:GOTO74
73 R1=RND(W)+W:IFRND(10)=UTHENR1=K
74 B$(U)=B$(U)+U$(R1)
75 B$(W)=B$(W)+L$(R1)
76 B$(3)=B$(3)+S3$(R1)
77 B$(F)=B$(F)+S4$(R1)
78 NEXT
79 FORT=3TORLSTEPF:RN(T)=RND(GG):NEXT:RN(J)=RND(GG):RN(RL)=RND(GG)
80 RETURN
81 IFCR=10THENPRINT"YOU COMPLETED ALL 10 RUNS!":SOUND200,5
82 PRINT"WOULD YOU LIKE TO TRY A"
83 PRINT"DIFFERENT MOUNTAIN (Y/N)?"
84 CR=0:GOTO105
85 CLS:PRINT@10,"SNOWBOARDER":PRINT
86 PRINT "  BY JIM GERRIE & GREG DIONNE":PRINT
87 PRINT "USE <A> TO MOVE LEFT,":PRINT"AND <S> TO MOVE RIGHT."
88 PRINT "HIT <ENTER> TO QUIT."
89 PRINT "TRY TO COMPLETE ALL 10 SLOPES."
90 PRINT "THE DIFFICULTY LEVEL INCREASES"
91 PRINT "FOR EACH SLOPE YOU TRY."
92 PRINT "HITTING BRANCHES = LOSE POINTS."
93 PRINT "IF YOU HIT A TREE STUMP, OR A"
94 PRINT "BOULDER THEN THE RUN IS OVER.":PRINT 
95 RETURN
96 PRINT "TRY AGAIN (Y), NEXT RUN (N),    OR QUIT (Q)?"
97 I$=INKEY$:IFI$=""THEN97
98 IFI$="Y" THEN 17
99 IFI$="N" THEN 102
100 IFI$="Q" THEN 82
101 GOTO97
102 IFLC=1THENCR=CR+1
103 LV=LV-U
104 GOTO15
105 I$=INKEY$:IFI$=""THEN105
106 IFI$="Y"THEN65
107 IFI$="N"THEN109
108 GOTO105
109 CLS:PRINT"HIGH SCORES:":FORT=1TO10:PRINTHS(T);"ON SLOPE";T;"BY ";N$(T):NEXT
110 END
120 PRINT@S,"¿†‡";:PRINT@Q,"¯¢¶";:FORZZ=1TO25:C1=PEEK(9)AND128:POKE49151,28+C1:NEXT
130 CLS0:SOUND1,1:PRINT"YOUR RUN IS OVER.":GOTO26
