0 CLS:CLEAR2000:GOTO100
1 IFI=-1ORI=1THENM$=S$(Y+J):S$(Y+J)=LEFT$(M$,X+(I*2))+"-"+MID$(M$,X+(I*2)+2):GOTO3
2 GOTO24
3 IFI=-1THENPRINT@P-2,"-"CHR$(92);:?@P+31,A$(6);:IFPEEK(P+64+M)<>96THEN5ELSEPRINT@P+63,B$(6);:GOTO5
4 PRINT@P+1,"/-";:?@P+31,A$(4);:IFPEEK(P+64+M)=96THENPRINT@P+63,B$(4);
5 SOUND83,1:?@P+31,A$(5+I);:IFPEEK(P+64+M)=96THENPRINT@P+63,B$(5+I);
6 H=H+1:SC=SC+N:PRINT@12,SC;:IFH<68THEN24
7 TL=T:T=.:GOTO24

20 FORT=E TO.STEP-1:POKE345,255:K=.:POKE344,255:POKE343,255:POKE342,255:POKE341,255:IFNOT((PEEK(345)ANDPEEK(344)ANDPEEK(343)ANDPEEK(342)ANDPEEK(341))=255)THENK=PEEK(135)
21 I=(K=8)-(K=9):J=(K=94)-(K=10):ONK(PEEK(X+I*2+(Y+J*2)*32+M))GOTO1,24:?@32*Y,S$(Y)S$(Y+1)S$(Y+2);:X=X+I:Y=Y+J:P=X+Y*32:?@P-1,P$(D);:?@P+31,A$(N+I);:IFPEEK(P+64+M)=96THEN?@P+63,B$(N+I);
24 A=A+C:D=1ANDT:IFRND(N)=1THENA=A-C:D=2:?@S-1,K$(D);:?@S+31,A$(D);:?@S+63,B$(D);:ONRND(2)GOTO28:C=-C:GOTO28
26 S=A+B*32:F=PEEK(S+M+96):?@64,S$(2)S$(3)S$(4);:?@S-1,K$(D);:?@S+31,A$(D);:IFPEEK(S+64+M)=96THEN?@S+63,B$(D);
27 IFS<67ORS>91THENA=A-C:C=-C
28 PRINT@P-1,P$(D);:IFA=X THENIFF=96ORF=79ORF=128THENTL=T:T=-T
29 PRINT@24,T:NEXT:IFT<-1THENPRINT@24,TL;:GOTO32
30 IFH=68THENPRINT@24,TL;:GOSUB800:SC=SC+(1000-E)+INT(180000000/((2*E-TL)^2)):PRINT@12,SC;:GOTO180
31 TL=0:GOTO34
32 FORB=B TOY:D=1AND T:S=A+B*32:?@(B-1)*32,S$(B-1)S$(B)S$(B+1);:?@S-1,K$(D);:?@S+31,A$(D);:?@S+63,B$(D);
33 FORT=1TO150:NEXT:NEXT:?@S-65,"got";:?@S-33,"you";
34 FORT=2TO10:D=1AND T:SOUND200,3:SOUND150,3:?@S-1,K$(D);:?@S+31,A$(D);:?@S+63,B$(D);:NEXT
40 IFSC>HS THENHS=SC
45 CLS:IFTL>0THENPRINT@2*32+11,"<GAME OVER>";:GOTO50
46 PRINT@2*32+11,"<TIME OVER>";
50 PRINT@4*32+8,"HIGH SCORE="HS
51 PRINT@6*32+13,"SCORE="SC
53 PRINT@14*32+8,"PUSH <SPACE> KEY"
54 Y=10:X=16:P=X+Y*32:GOSUB3000:?@P-1,P$(2);:?@P+31,A$(2);:?@P+63,B$(2);:FORT=1TO250:NEXT:M$=INKEY$:IFM$<>""THEN70
60 Y=9:X=16:P=X+Y*32:GOSUB3000:?@P-1,P$(3);:?@P+31,A$(3);:?@P+63,B$(3);:FORT=1TO250:NEXT:M$=INKEY$:IFM$<>""THEN70
61 GOTO54
70 IFM$="Q"THEN10003
71 IFM$<>" "THEN54
80 ST=0:SC=0:GOTO200
99 PRINT@64,S$(2)S$(3)S$(4)S$(5)S$(6)S$(7)S$(8)S$(9)S$(10)S$(11)S$(12)S$(13)S$(14)S$(15);:RETURN

100 DIMK(255),P$(3),K$(3),A$(6),B$(6),M,X,Y,A,B,T,I,J,C,D,E,F,S,P,M$,S$(15),H,K,N,SC,TL:M=1024:N=5:GOSUB1500
110 T=RND(-TIMER):GOSUB10000
120 K(100)=1:K(107)=2:K(109)=2:K(140)=2:K(138)=2:K(133)=2:K(79)=2:K(64)=2:K(131)=2:K(143)=2:K(91)=2:K(93)=2
121 
150 GOSUB4000:PRINT@490,"LEVEL (1-3)?";:SCREEN0,1
160 M$=INKEY$:IFM$=""THEN160
161 IFM$="3"THENE=800:GOTO200
162 IFM$="2"THENE=900:GOTO200
163 IFM$="1"THENE=1000:GOTO200
170 GOTO160
180 CLS:GOSUB900
200 CLS:SCREEN0,0:GOSUB1000:A=8:B=2:S=A+B*32:D=1:C=1:H=0:GOSUB99:PRINT@20,"TIME";:PRINT@7,"SCORE"SC;
205 ST=ST+1:PRINT@0,"<"ST">";
210 X=25:Y=10:P=X+Y*32:?@32*Y,S$(Y)S$(Y+1)S$(Y+2);:?@P-1,P$(D);:?@P+31,A$(D);:?@P+63,B$(D);
290 GOTO20

800 SOUND171,2:SOUND181,2:SOUND185,2:SOUND171,2:SOUND181,2:SOUND185,4:FORT=1TO250:NEXT
810 SOUND181,2:SOUND171,2:SOUND181,2:SOUND185,4:FORT=1TO250:NEXT
820 SOUND181,2:SOUND171,2:SOUND185,2:SOUND185,4:SOUND171,2:FORT=1TO25:NEXT:RETURN

900 ?"                   "CHR$(142)STRING$(10,140)"  ";
901 ?"                   "CHR$(138)" <7. 11> "CHR$(138)"  ";
902 ?"                   "CHR$(139)CHR$(130)+STRING$(7,131)CHR$(130)CHR$(131)"  ";
903 ?"                  "CHR$(138)STRING$(11,131)CHR$(130)+" ";
904 ?"                  "CHR$(138)" "CHR$(142)STRING$(3,140)STRING$(5,140)" "CHR$(138)" ";
905 ?"                  "CHR$(138)" "CHR$(138)STRING$(3,140)CHR$(136)STRING$(3,140)CHR$(136)" "CHR$(138)" ";
906 ?"                  "CHR$(138)" "CHR$(138)STRING$(3,140)CHR$(136)STRING$(3,140)CHR$(136)" "CHR$(138)" ";
907 ?"                  "CHR$(138)"           "CHR$(138)" ";
908 ?"                  "CHR$(138)" "CHR$(138)STRING$(3,131)CHR$(130)STRING$(3,131)CHR$(130)" "CHR$(138)" ";
909 ?"                  "CHR$(138)" "CHR$(138)STRING$(3,131)CHR$(130)STRING$(3,131)CHR$(130)" "CHR$(138)" ";
910 ?"                  "CHR$(138)" "CHR$(139)STRING$(8,131)" "CHR$(138)" ";
911 ?"                  "CHR$(138)" "CHR$(142)STRING$(8,140)" "CHR$(138)" ";
912 ?"                  "CHR$(138)" "CHR$(138)STRING$(3,140)CHR$(136)STRING$(3,140)CHR$(136)" "CHR$(138)" ";
913 ?"                  "CHR$(138)" "CHR$(138)STRING$(3,140)CHR$(136)STRING$(3,140)CHR$(136)" "CHR$(138)" ";
914 ?"                  "CHR$(138)"           "CHR$(138)" ";
915 ?STRING$(31,131);
916 D=1:Y=12:FORX=1TO15:P=X+Y*32:?@P-1," "P$(D);:?@P+31," "A$(D);:?@P+63," "B$(D);:FORT=1TO250:NEXT:NEXT
920 PRINT@10*32," WHEW! I MADE IT!";:FORT=1TO2500:NEXT
921 PRINT@10*32,"                 ";
950 PRINT@6*32," PUSH <SPACE> KEY";
951 FORT=1TO25:IFINKEY$=" "THENT=99
952 NEXT:IFT>99THENRETURN
953 PRINT@6*32,"                  ";
954 FORT=1TO25:IFINKEY$=" "THENT=99
955 NEXT:IFT>99THENRETURN
956 GOTO950
1000 S$(1)=CHR$(142)+STRING$(29,140)+CHR$(141)+CHR$(143)
1002 S$(2)=CHR$(138)+STRING$(29,143)+CHR$(133)+CHR$(143)
1003 S$(3)=CHR$(138)+"                             "+CHR$(133)+" "
1004 S$(4)=CHR$(138)+CHR$(93)+"$    $+$    $+$    $+$    $"+CHR$(91)+CHR$(133)+" "
1005 S$(5)=CHR$(138)+CHR$(93)+"$    $+$    $+$    $+$    $"+CHR$(91)+CHR$(133)+" "
1006 S$(6)=CHR$(138)+CHR$(93)+"$    $+$    $+$    $+$    $"+CHR$(91)+CHR$(133)+" "
1007 S$(7)=CHR$(138)+CHR$(93)+"$    $+$    $+$    $+$    $"+CHR$(91)+CHR$(133)+" "
1008 S$(08)=CHR$(138)+CHR$(93)+"$                         $"+CHR$(91)+CHR$(133)+" "
1009 S$(09)=CHR$(138)+CHR$(93)+"$    "+STRING$(3,143)+"    "+STRING$(3,143)+"    "+STRING$(3,143)+"    $"+CHR$(91)+CHR$(133)+" "
1010 S$(10)=CHR$(138)+CHR$(93)+"$    $+$    $+$    $+$    $"+CHR$(91)+CHR$(133)+" "
1011 S$(11)=CHR$(138)+CHR$(93)+"$    $+$    $+$    $+$    $"+CHR$(91)+CHR$(133)+" "
1012 S$(12)=CHR$(138)+CHR$(93)+"$    $+$    $+$    $+$    $"+CHR$(91)+CHR$(133)+" "
1013 S$(13)=CHR$(138)+CHR$(93)+"$    $+$    $+$    $+$    $"+CHR$(91)+CHR$(133)+" "
1014 S$(14)=CHR$(138)+"      ---    ---    ---      "+CHR$(133)+" "
1015 S$(15)=CHR$(139)+STRING$(29,131)+CHR$(135)
1016 CLS:PRINT@32,S$(1);:PRINT@15*32,S$(15);:RETURN
1500 K$(0)=CHR$(92)+"@>":K$(1)="<@/"
1501 K$(2)="<@>":K$(3)=CHR$(92)+"@/"
1502 P$(0)="<O>":P$(1)="<O>"
1503 P$(2)="<O>":P$(3)=CHR$(92)+"O/"
2000 A$(1)=CHR$(140)+CHR$(128)+CHR$(136):A$(0)=CHR$(132)+CHR$(128)+CHR$(140)
2010 B$(1)=CHR$(135)+"  ":B$(0)="  "+CHR$(139)
2020 A$(2)=CHR$(140)+CHR$(128)+CHR$(140):A$(3)=CHR$(132)+CHR$(128)+CHR$(136)
2030 B$(2)=CHR$(135)+" "+CHR$(139):B$(3)=""
2040 A$(4)=A$(0):A$(6)=A$(1):A$(5)=A$(2)
2050 B$(4)=B$(0):B$(6)=B$(1):B$(5)=B$(2):RETURN
3000 ?@7*32+8,CHR$(142)STRING$(15,140);
3001 ?@8*32+8,CHR$(138)" "CHR$(133)CHR$(138)" "CHR$(133)CHR$(138)" "CHR$(133)CHR$(138)" "CHR$(133)CHR$(138)" "CHR$(133)CHR$(138);
3002 ?@9*32+8,CHR$(138)" "CHR$(133)CHR$(138)" "CHR$(133)CHR$(138)" "CHR$(133)CHR$(138)" "CHR$(133)CHR$(138)" "CHR$(133)CHR$(138);
3003 ?@10*32+8,CHR$(138)" "CHR$(133)CHR$(138)" "CHR$(133)CHR$(138)" "CHR$(133)CHR$(138)" "CHR$(133)CHR$(138)" "CHR$(133)CHR$(138);
3004 ?@11*32+8,CHR$(138)" "CHR$(133)CHR$(138)" "CHR$(133)CHR$(138)" "CHR$(133)CHR$(138)" "CHR$(133)CHR$(138)" "CHR$(133)CHR$(138);
3005 ?@12*32+8,CHR$(138)" "CHR$(133)CHR$(138)" "CHR$(133)CHR$(138)" "CHR$(133)CHR$(138)" "CHR$(133)CHR$(138)" "CHR$(133)CHR$(138);
3006 ?@13*32+8,CHR$(139)STRING$(15,131);:RETURN
4000 PRINT"        shoplifting boy":POKEM+19,32
4010 PRINT"       BY HIROSHI SUZUKI"
4015 PRINT"              1979":PRINT
4020 PRINT"  'MANBIKI SHOUNEN' IS WIDELY"
4030 PRINT"  CREDITED AS THE FIRST STEALTH"
4035 PRINT"  GAME. AVOID THE SHOPKEEPER!"
4040 PRINT"  USE ARROW KEYS TO MOVE THE"
4050 PRINT"  SHOPLIFTER. COLLECT ALL THE"
4060 PRINT"  DOLLAR SIGNS TO COMPLETE A "
4070 PRINT"  LEVEL. BONUS FOR EACH LEVEL"
4080 PRINT"  COMPLETED UP TO: 222 LV1, "
4090 PRINT"  381 LV2, 567 LV3. POCKETING"
4100 PRINT"  SOMETHING = 5 POINTS.":PRINT:RETURN
10000 IF PEEK(65535)=27 THEN POKE65497,0:GOTO10002 ELSE CLS:INPUT"CAN YOUR COMPUTER HANDLE DOUBLE SPEED (Y/N)";M$
10001 IF M$="Y" THEN POKE65495,0 ELSE IF M$<>"N" THEN10000
10002 CLS:RETURN
10003 IF PEEK(65535)=27 THEN POKE65496,0 ELSE POKE65494,0
10004 END
