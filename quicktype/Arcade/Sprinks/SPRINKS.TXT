10 CLS:X=32768:GOSUB 3000:REM INSTALL COUNTER
20 X=0:Y=0:Z=0
100 REM * SPRINKS * TRS-80 COLOR BASIC 4K *
110 REM * ELMER'S ARCADE #4 * OCT. '83 * RICHARD RAMELLA
120 CLS(0)
130 V=500
140 FOR A=1 TO 32
150 M$=M$+CHR$(128)
160 NEXT A
170 FOR A=1 TO 32
180 A$=A$+CHR$(207)
190 NEXT A
200 B$=CHR$(207)
210 C$=CHR$(128)
220 FOR A=32 TO 416 STEP 128
230 PRINT@A,A$;
240 NEXT A
250 FOR A=32 TO 416 STEP 32
260 FOR B=0 TO 31 STEP 8
270 PRINT@A+B,B$;
280 NEXT B
290 PRINT@A+31,B$;
300 NEXT A
310 FOR A=4 TO 28 STEP 8
320 PRINT@160+A,C$;
330 PRINT@288+A,C$;
340 NEXT A
350 PRINT@232,LEFT$(M$,17);
360 A=32
370 B=14
380 RESET(X,Y)
390 SET(A,B,8)
400 X=RND(60)+2
410 Y=RND(23)+2
420 IFX=AANDY=BTHEN400
430 IFPOINT(X,Y)<>5THENSET(X,Y,7):GOTO440
431 GOTO 400
440 Z=USR(0)
450 Z=USR(1):PRINT@503,INT(Z/60)"/"INT(V/60);:IFZ=>VTHEN670
460 SET(A,B,8)
470 IFA=XANDB=YTHEN540
480 M=A
490 N=B
500 K=PEEK(2)ANDPEEK(17023)
501 IFK=87ANDPOINT(A,B-1)<>5THENB=B-1
502 IFK=90ANDPOINT(A,B+1)<>5THENB=B+1
503 IFK=65ANDPOINT(A-1,B)<>5THENA=A-1
504 IFK=83ANDPOINT(A+1,B)<>5THENA=A+1
510 RESET(M,N)
520 GOTO450
530 REM
540 PRINT@0,M$;
550 PRINT@0,"YOU GOT IT !";
560 J=J+(V-USR(1))
570 PRINT@480,"MEGASCORE:";STR$(J)+"000";
580 POKE8,0:FOR T=1 TO 2
590 SOUND 218,1
600 SOUND 213,1
610 SOUND 204,2
620 SOUND 193,2
630 SOUND 176,4
640 NEXT T:POKE8,8
650 P=P+1
660 GOTO 770
670 PRINT@0,"TIME'S UP. THE SPRINK ESCAPES";
680 S=S+1
690 POKE8,0:FOR T=1 TO 5
700 SOUND 204,1
710 FOR G=1 TO 100
720 NEXT G
730 SOUND 32,1
740 FOR G=1 TO 100
750 NEXT G
760 NEXT T:POKE8,8
770 PRINT@0,M$;
780 V=V-5
790 PRINT@0,"SCORE: YOU-";P;"SPRINKS-";S
800 FOR T=1 TO 1500
810 NEXT T:POKE8,0
820 IFS=5THENPRINT@0,M$;:PRINT@0,"SPRINKS PREVAIL...";:PRINT@480,"BUT NICE MEGASCORE:";STR$(J)+"000";:SOUNDRND(13)*8,1:GOTO820
821 POKE8,8:GOTO 380
830 END
3000 Z=16896:GOSUB 3001:Z=X:GOSUB 3001:EXEC X:RETURN
3001 READ U$:FOR Y=1 TO LEN(U$) STEP 2:POKE Z,(ASC(MID$(U$,Y,1))-65)*16+ASC(MID$(U$,Y+1,1))-65:Z=Z+1:NEXT:RETURN
3002 DATA HPAAAIDLDLDLPOECBGKGACEKCGPCCBDLGOAD
3003 DATA INCFCABAABNMALMDDKEDJFAINNALNOPNAINPPNDLLNOLMHCGAEEPFPNNPNNMPNNNMKMGJAANHOPCMHDIPPECBGIGAIJHAIAODJ
