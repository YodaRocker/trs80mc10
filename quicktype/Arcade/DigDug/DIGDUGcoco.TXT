0 CLS:CLEAR500:GOSUB10000
1 CLS:GOSUB3000:GOTO100
2 O=32*Y(C)+X(C):L=O+M:IFK(PEEK(L-2))>C ANDX(C)>0THENPRINT@O,STRING$(2,128);:X(C)=X(C)-2:PRINT@O-2,A$(C);:D(C)=1:RETURN
3 K=L-2:ONK(PEEK(K))GOTO15,18,18,18,18:GOTO18
4 O=32*Y(C)+X(C):L=O+M:IFK(PEEK(L+2))>C ANDX(C)<30THENPRINT@O,STRING$(2,128);:X(C)=X(C)+2:PRINT@O+2,B$(C);:D(C)=2:RETURN
5 K=L+2:ONK(PEEK(K))GOTO15,18,18,18,18:GOTO18
6 O=32*Y(C)+X(C):L=O+M:IFK(PEEK(L-32))>C ANDY(C)>0THENPRINT@O,STRING$(2,128);:Y(C)=Y(C)-1:PRINT@O-32,A$(C);:D(C)=3:RETURN
7 K=L-32:ONK(PEEK(K))GOTO15,18,18,18,18:GOTO18
8 O=32*Y(C)+X(C):L=O+M:IFK(PEEK(L+32))>C ANDY(C)<14THENPRINT@O,STRING$(2,128);:Y(C)=Y(C)+1:PRINT@O+32,A$(C);:D(C)=4:RETURN
9 K=L+32:ONK(PEEK(K))GOTO15,18,18,18,18:GOTO18
10 PRINT@R(C)-32,STRING$(2,128);:PRINT@R(C),A$(5);:R(C)=R(C)+32:GOTO21
11 PRINT@R(C)-32,STRING$(2,128);:PRINT@R(C),A$(5);:FORX=2TO4:IF32*Y(X)+X(X)=R(C)THEN13
12 NEXT:STOP
13 Y=X:X=4:NEXT:PRINT@R(C),"80";:SC=SC+80:PRINT@480,;:M$=STR$(SC):GOSUB99:SOUND100,5:PRINT@R(C),A$(5);:GOSUB30
14 R(C)=R(C)+32:GOTO21
15 X=PEEK(K):IFX=223ORX=221THENIFX(C)<30ANDX(C)>0THENGOSUB90:B=B+(B>0):PRINT@496,;:M$=STR$(B)+" ":GOSUB99:IFB<1THENJ=0
16 IFX=195ORX=202THENPRINT@K-M,STRING$(2,128);
17 IFX=175THENIFC>1THEN29
18 RETURN
19 K=K(PEEK(135)):RETURN
20 FORJ=3TO0STEP0:POKE345,255:POKE344,255:POKE343,255:POKE342,255:POKE341,255:K=0:ON-((PEEK(345)ANDPEEK(344)ANDPEEK(343)ANDPEEK(342)ANDPEEK(341))=255)GOSUB19:C=1:ONK GOSUB2,4,6,8,50:FORC=1TO3:ONK(PEEK(R(C)+M))GOTO21,21,11,21,10
21 NEXT:FORC=2TO4:IFRND(25)>GTHENON2+SGN(X(1)-X(C))GOSUB2,18,4:ON2+SGN(Y(1)-Y(C))GOSUB6,18,8:NEXT:NEXT:GOTO25
22 OND(C)GOSUB2,4,6,8:ONRND(9)GOSUB2,4,6,8,40:NEXT:NEXT
25 IFB>0THENGOSUB300:GOTO150
26 SOUND20,10:GOTO170
29 PRINT@O,STRING$(2,128);:FORX=K-MTO32STEP-2:PRINT@X,A$(C);:GOSUB90:GOSUB800:NEXT:Y=C
30 J=J-1:A$(Y)="":B$(Y)="":X(Y)=2:Y(Y)=0:RETURN
40 IFD(C)>2THENRETURN
50 X=0:Y=C*2+4:O=32*Y(C)+X(C):OND(C)GOSUB51,53,55,57:RETURN
51 L=O+M:ONK(PEEK(L-2))GOTO61,18,61,18:O=O-2:PRINT@O,A$(Y);:X=X+1:X=INT(X):PRINT@O,B$(Y);:IFX<3THEN51
52 RETURN
53 L=O+M:ONK(PEEK(L+2))GOTO62,18,62,18:O=O+2:PRINT@O,A$(Y);:X=X+1:X=INT(X):PRINT@O,B$(Y);:IFX<3THEN53
54 RETURN
55 L=O+M:ONK(PEEK(L-32))GOTO63,18,63,18:O=O-32:PRINT@O,A$(Y+1);:X=X+1:PRINT@O,B$(Y+1);:IFX<3THEN55
56 RETURN
57 L=O+M:ONK(PEEK(L+32))GOTO64,18,64,18:O=O+32:PRINT@O,A$(Y+1);:X=X+1:PRINT@O,B$(Y+1);:IFX<3THEN57
58 RETURN
61 O=O-2:GOTO65
62 O=O+2:GOTO65
63 O=O-32:GOTO65
64 O=O+32:GOTO65
65 FORX=1TO4:IF32*Y(X)+X(X)=O THEN67
66 NEXT:RETURN
67 IFC>1ANDX>1THENRETURN
68 IFX=1THEN80
72 Y=X:X=4:NEXT:PRINT@O,C$(Y);:SOUND100,5:PRINT@O,"20";:SOUND200,2:SC=SC+20:PRINT@480,;:M$=STR$(SC):GOSUB99:PRINT@O,STRING$(2,128);
73 GOTO30
80 FORX=1TO5:PRINT@O,A$(8);:GOSUB90:PRINT@O,A$(1);:GOSUB90:NEXT:IFD(1)=2THENPRINT@O,B$(1);
81 B=B+(B>0):PRINT@496,;:M$=STR$(B)+" ":GOSUB99:IFB<1THENJ=0
82 RETURN
90 PLAY"V1":RETURN
99 PRINTM$;:CC=((PEEK(136)AND1)*256+PEEK(137))-1+M:FORC1=CC-LEN(M$)+1TOCC:POKEC1,PEEK(C1)-64:NEXT:RETURN
100 DIMK(255),X(4),Y(4),C,O,K,R(3),L,G,A$(13),B$(13),D(4),M,X,Y,B,C$(4),C1,CC,N(21),SC:M=1024
105 C1=1:FORCC=1TO63STEP3:N(C1)=VAL(MID$("073093102119134141154166176180189196200206127148185148111127083",CC,3))
106 C1=C1+1:NEXT
110 K(1)=8:K(10)=2:K(94)=3:K(10)=4:K(65)=1:K(83)=2:K(87)=3:K(90)=4:K(32)=5:K(74)=1:K(76)=2:K(73)=3:K(75)=4:FORX=224TO229:K(X)=2:NEXT
120 K(159)=2:K(153)=2:K(255)=2:K(128)=5:K(249)=2:K(183)=1:K(240)=1
130 K(141)=3:K(139)=3:K(230)=3:K(221)=1:K(223)=1:K(195)=1:K(202)=1
135 K(175)=1:K(225)=2:K(241)=2:K(173)=2:K(209)=2:K(173)=2:GOSUB3025
140 G=25:SC=0:B=10:N2=500
150 GOSUB2000:G=G-1:IFG<15THENG=15
156 N3=SC-(N2*INT(SC/N2)):IFSC=N3 THEN160
157 N2=N2+500:B=10:PRINT@496,STR$(B);:SOUND200,10
160 GOSUB900:X(1)=16:Y(1)=2:PRINT@32*Y(1)+X(1),A$(1);:GOTO20
170 IFSC>HS THENHS=SC
200 PRINT@8,"PLAY AGAIN (Y/N)?";
210 M$=INKEY$:IFM$=""THEN210
220 IFM$="Y"THEN140
230 IFM$="N"THEN10003
240 GOTO210
300 SOUND193,4
302 SOUND181,2
303 SOUND185,4
304 SOUND171,2
305 SOUND181,4
306 SOUND161,2
307 SOUND171,4
308 SOUND156,2
309 SOUND161,2
310 FORC1=1TO25:NEXT
311 SOUND210,4:RETURN
800 PRINT@0,CHR$(225)+CHR$(173)CHR$(129)CHR$(173)CHR$(209)CHR$(173)STRING$(22,175)CHR$(241)CHR$(173)CHR$(209)CHR$(173)CHR$(170)CHR$(167)CHR$(170)CHR$(167)CHR$(170)CHR$(167)STRING$(22,175)CHR$(170)CHR$(167)CHR$(170)CHR$(167);:RETURN
900 CLS0:GOSUB800
1002 PRINTSTRING$(32,159);:PRINTSTRING$(64,159);
1005 PRINTSTRING$(32,153);:PRINTSTRING$(64,153);
1008 PRINTSTRING$(32,255);:PRINTSTRING$(64,255);
1011 PRINTSTRING$(32,249);:PRINTSTRING$(64,249);
1014 PRINTSTRING$(32,249);:PRINTSTRING$(32,240);
1015 PRINT@496,;:M$=STR$(B):GOSUB99
1020 PRINT@480,;:M$=STR$(SC):GOSUB99
1040 FORC=1TO3:X=2+RND(12)*2:Y=2+RND(6):C1=32*Y+X:PRINT@C1,A$(5);:R(C)=C1+32:NEXT
1050 FORC=2TO4:X=2+RND(12)*2:Y=5+RND(8):X(C)=X:Y(C)=Y:PRINT@32*Y(C)+X(C),A$(C)STRING$(4,128);:NEXT
1060 IFJ>0THENRETURN
1065 IFHS>0THENM$="HIGH"+STR$(INT(HS)):PRINT@(32-LEN(M$))/2,;M$;:FORX=1TO2500:NEXT:GOSUB800:RETURN
1070 PRINT@13,"DIG DUG";:PRINT@42,"BY JIM GERRIE";
1071 M$="P2G2G2G2G2G2G2G2G2G2G2G2G2G2E2D2E2D2E2D2E2D2O2":GOSUB1500
1072 M$="E2E2E2E2E2E2E2E2E2E2E2E2E2E2D2E2D2E2D2G2P2":GOSUB1500
1080 PRINT@40,"& CARLOS CAMACHO";
1083 M$="G2G2G2G2G2G2G2G2G2G2G2G2G2E2D2E2D2E2D2E2D2O2":GOSUB1500
1084 M$="E2E2E2E2E2E2E2E2E2E2E2E2E2E2D2E2D2E2D2G2":GOSUB1500
1200 GOSUB800:HS=.5:RETURN
1500 FORCC=1TOLEN(M$)STEP2:SOUNDN(ASC(MID$(M$,CC,1))-64),ASC(MID$(M$,CC+1,1))-48:NEXT:RETURN
2000 A$(0)=STRING$(2,128):A$(1)=CHR$(221)+CHR$(218):A$(2)=CHR$(141)+CHR$(135):A$(3)=CHR$(141)+CHR$(135):A$(4)=CHR$(230)+CHR$(233):A$(5)=CHR$(183)+CHR$(187)
2005 A$(6)=CHR$(195)+CHR$(195):A$(7)=CHR$(202)+CHR$(128):A$(8)=CHR$(150)+CHR$(150):A$(9)=CHR$(154)+CHR$(144):A$(10)=CHR$(150)+CHR$(150):A$(11)=CHR$(154)+CHR$(144):A$(12)=CHR$(147)+CHR$(147):A$(13)=CHR$(153)+CHR$(144)
2010 B$(0)=STRING$(2,128):B$(1)=CHR$(223)+CHR$(216):B$(2)=CHR$(139)+CHR$(142):B$(3)=CHR$(139)+CHR$(142):B$(4)=CHR$(230)+CHR$(233):B$(5)=CHR$(183)+CHR$(187)
2015 B$(6)=CHR$(195)+CHR$(195):B$(7)=CHR$(202)+CHR$(128):B$(8)=STRING$(2,128):B$(9)=STRING$(2,128):B$(10)=STRING$(2,128):B$(11)=STRING$(2,128):B$(12)=STRING$(2,128):B$(13)=STRING$(2,128)
2030 C$(0)=STRING$(2,128):C$(1)=CHR$(223)+CHR$(223):C$(2)="  ":C$(3)="  ":C$(4)=CHR$(239)+CHR$(239)
2040 RETURN
3000 CLS:PRINTTAB(13)"dig dug"
3010 PRINT"THE OBJECTIVE OF DIG DUG IS "
3011 PRINT"TO ELIMINATE ALL UNDERGROUND-"
3012 PRINT"DWELLING MONSTERS. YOU CAN"
3013 PRINT"INFLATE THEM WITH YOUR PUMP"
3014 PRINT"(space) TO MAKE THEM EXPLODE" 
3015 PRINT"(20 PTS) OR DROP ROCKS ON THEM"
3016 PRINT"(80 PTS). ENEMIES ARE PURPLE"
3017 PRINT"SPACE POOKAS AND GREEN FYGARS."
3018 PRINT"YOU ARE DRESSED IN LIGHT BLUE &"
3019 PRINT"ABLE TO DIG TUNNELS. DIG DUG"
3020 PRINT"CAN ONLY TAKE 10 HITS. ALSO,"
3021 PRINT"AVOID FYGAR'S FIRE & POOKA'S
3022 PRINT"LASER. USE ARROW OR jlik KEYS."
3023 PRINT"500 PTS = FULL REJUVENATION.":RETURN
3025 PRINTTAB(5)"press any key to begin";
3030 X=RND(999):IFINKEY$=""THEN3030
3040 RETURN
10000 IF PEEK(65535)=27 THEN POKE65497,0:GOTO10002 ELSE CLS:INPUT"CAN YOUR COMPUTER HANDLE DOUBLE SPEED (Y/N)";M$
10001 IF M$="Y" THEN POKE65495,0 ELSE IF M$<>"N" THEN10000
10002 CLS:RETURN
10003 IF PEEK(65535)=27 THEN POKE65496,0 ELSE POKE65494,0
10004 END
