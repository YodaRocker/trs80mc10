0 GOTO800
1 X(T)=X(T)+1:POKEQ*Y(T)+X(T)+M,T:NEXT:GOTO21
2 X(T)=X(T)-1:POKEQ*Y(T)+X(T)+M,T:NEXT:GOTO21
3 Y(T)=Y(T)+1:POKEQ*Y(T)+X(T)+M,T:NEXT:GOTO21
4 Y(T)=Y(T)-1:POKEQ*Y(T)+X(T)+M,T:NEXT:GOTO21
5 ONRND(V)GOTO1,3:ON-(X>X(T))GOTO1:GOTO3
6 ONRND(V)GOTO1,2,3:ON-(X>X(T))GOTO1:ON-(X<X(T))GOTO2:GOTO3
7 ONRND(V)GOTO2,3:ON-(X<X(T))GOTO2:GOTO3
8 ONRND(V)GOTO3,4:ON-(Y>Y(T))GOTO3:GOTO4
9 ONRND(V)GOTO1,3,4:ON-(X>X(T))GOTO1:ON-(Y>Y(T))GOTO3:GOTO4
10 ONRND(V)GOTO1,2,3,4:ON-(X>X(T))GOTO1:ON-(X<X(T))GOTO2:ON-(Y>Y(T))GOTO3:GOTO4
11 ONRND(V)GOTO2,3,4:ON-(X<X(T))GOTO2:ON-(Y>Y(T))GOTO3:GOTO4
12 ONRND(V)GOTO1,4:ON-(X>X(T))GOTO1:GOTO4
13 ONRND(V)GOTO1,2:ON-(X>X(T))GOTO1:GOTO2
14 ONRND(V)GOTO1,2,4:ON-(X>X(T))GOTO1:ON-(X<X(T))GOTO2:GOTO4
15 ONRND(V)GOTO2,4:ON-(X<X(T))GOTO2:GOTO4
16 POKEQ*Y+X+M,Q:X=X+(G(X-1,Y)>.):POKEQ*Y+X+M,.:U=-1:GOTO20
17 POKEQ*Y+X+M,Q:X=X-(G(X+1,Y)>.):POKEQ*Y+X+M,.:U=1:GOTO20
18 POKEQ*Y+X+M,Q:Y=Y+(G(X,Y-1)>.):POKEQ*Y+X+M,.:U=-Q:GOTO20
19 POKEQ*Y+X+M,Q:Y=Y-(G(X,Y+1)>.):POKEQ*Y+X+M,.:U=Q

20 FORT=.TOF:POKEQ*Y(T)+X(T)+M,Q:ONG(X(T),Y(T))GOTO5,6,7,8,9,10,11,12,13,14,15:POKEQ*Y(T)+X(T)+M,T:NEXT
21 IFPEEK(Q*Y+X+M)THEN60
22 ONK(PEEK(2)ANDPEEK(W))GOTO16,17,18,19,30:GOTO20

30 T=Q*Y+X+M:POKET,.:T=T+U:FORS=TTOT+U*4STEPU:IFPEEK(S)<>QTHENZ=S:S=T+U*4:NEXT:S=Z:T=PEEK(S):GOTO35
31 POKES,42:POKES,42:POKES,42:POKES,42:POKES,Q:NEXT:GOTO20
35 IFT>.THENIFT<5THEN40
36 IFT=.THEN45
37 GOTO20

40 POKEQ*Y(T)+X(T)+M,255:SOUND1,2:POKEQ*Y(T)+X(T)+M,Q:X(T)=26+T:Y(T)=1:POKEQ*Y(T)+X(T)+M,T:SOUND20,2:SC=SC+1:GOSUB66
41 IFT=FTHENF=F-1
42 E=E+1:IFE=4THEN50
43 GOTO20

45 POKEQ*Y(T)+X(T)+M,255:SOUND1,3:POKEQ*Y(T)+X(T)+M,Q:X(T)=1:Y(T)=1:
46 SH=SH-1:GOSUB65:IFSH=.THEN90
47 POKEQ*Y(T)+X(T)+M,T:SOUND100,2:GOTO20

50 E=0:GOTO935

60 FORT=1TO4:POKE49151,64:FORZ=1TO150:NEXT:SOUND1,1:NEXT:X=25:Y=13:U=-1
61 SH=SH-1:GOSUB65:IFSH=.THEN90
62 T=Q*Y+X+M:POKET,.:GOTO20
65 PRINT@123,"SHIP";:PRINT@155,MID$(STR$(SH)+"   ",2,4);:RETURN
66 PRINT@219,"SCOR";:PRINT@251,MID$(STR$(SC)+"   ",2,4);:RETURN
67 PRINT@315,"HIGH";:PRINT@347,MID$(STR$(HS)+"   ",2,4);:RETURN

90 IFSC>HSTHENHS=SC:GOSUB67:PRINT@315,"high";:FORZ=1TO10:SOUND2,1:NEXT:GOSUB67
100 PRINT@480,"ALPHA FORCE   PLAY AGAIN (Y/N)?";
110 M$=INKEY$:IFM$=""THEN110
120 IFM$="Y"THEN930
130 IFM$="N"THENEND
140 GOTO110

800 CLS0:PRINT@32*3+6,"MISSION: DEFEAT THE";:PRINT@32*5+5,"A L P H A   F O R C E";
810 PRINT@32*7+9,"BY JIM GERRIE";

850 DIMT,X,Y,M,G(31,14),X(4),Y(4),Q,K(255),V,W,U,F,S,Z,E:M=16384:Q=32:W=17023:GOTO900

900 K(65)=1:K(83)=2:K(87)=3:K(90)=4:K(32)=5
901 K(74)=1:K(76)=2:K(73)=3:K(75)=4:T=RND(-(PEEK(9)*256+PEEK(10)))

910 PRINT@32*10,"USE: wasz OR ijkl TO STEER"
911 PRINT@32*11,"     space TO FIRE":PRINT"BUT AVOID HITTING YOUR ALLY '@'"

915 FORY=.TO14:FORX=.TO31:READG(X,Y):NEXT:NEXT

920 PRINT@486,"HIT ANY KEY TO BEGIN";
921 IFINKEY$=""THEN921

930 CLS:PRINT@480,"ALPHA FORCE";:V=20:E=0:SH=3:LV=0:SC=0

935 V=V-1:IFV<5THENV=5
936 F=4:LV=LV+1:PRINT@496,"               ";:PRINT@496,"LEVEL"LV;

940 T=143+RND(6)*16:IFPEEK(M)=TTHEN940
941 FORY=.TO14:FORX=.TO31:IFG(X,Y)THENPOKEQ*Y+X+M,Q:NEXT:NEXT:GOTO945
942 POKEQ*Y+X+M,T:NEXT:NEXT
945 GOSUB65:GOSUB66:GOSUB67
946 IF(LV/10)-INT(LV/10)=0THENSH=SH+1:GOSUB65:PRINT@123,"ship";:FORZ=1TO10:SOUND9,1:NEXT:GOSUB65

950 FORT=.TO4:X(T)=(T+1)*4:Y(T)=1:NEXT
999 X=25:Y=13:U=-1:T=Q*Y+X+M:POKET,.:GOTO20

1000 DATA,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,
1001 DATA,1,9,2,9,2,9,2,9,2,9,2,9,2,9,2,9,2,9,2,9,2,9,2,9,3,,,,,,
1002 DATA,4,,4,,4,,4,,4,,4,,4,,4,,4,,4,,4,,4,,4,,,,,,
1003 DATA,5,9,6,9,6,9,6,9,6,9,6,9,6,9,6,9,6,9,6,9,6,9,6,9,7,,,,,,
1004 DATA,4,,4,,4,,4,,4,,4,,4,,4,,4,,4,,4,,4,,4,,,,,,
1005 DATA,5,9,6,9,6,9,6,9,6,9,6,9,6,9,6,9,6,9,6,9,6,9,6,9,7,,,,,,
1006 DATA,4,,4,,4,,4,,4,,4,,4,,4,,4,,4,,4,,4,,4,,,,,,
1007 DATA,5,9,6,9,6,9,6,9,6,9,6,9,6,9,6,9,6,9,6,9,6,9,6,9,7,,,,,,
1008 DATA,4,,4,,4,,4,,4,,4,,4,,4,,4,,4,,4,,4,,4,,,,,,
1009 DATA,5,9,6,9,6,9,6,9,6,9,6,9,6,9,6,9,6,9,6,9,6,9,6,9,7,,,,,,
1010 DATA,4,,4,,4,,4,,4,,4,,4,,4,,4,,4,,4,,4,,4,,,,,,
1011 DATA,5,9,6,9,6,9,6,9,6,9,6,9,6,9,6,9,6,9,6,9,6,9,6,9,7,,,,,,
1012 DATA,4,,4,,4,,4,,4,,4,,4,,4,,4,,4,,4,,4,,4,,,,,,
1013 DATA,8,9,10,9,10,9,10,9,10,9,10,9,10,9,10,9,10,9,10,9,10,9,10,9,11,,,,,,
1014 DATA,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,
