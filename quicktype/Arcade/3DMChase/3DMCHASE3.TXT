0 REM01234567890123456789012345678901234567890123456
1 CLS:GOSUB10000
2 DATA60,54,55,150,246,129,126,38,25,220,247,195,1,5,131,1,1,221,252,206,67,113,236,1
3 DATA221,246,236,3,221,248,236,5,221,250,51,50,56,57,240,129,58,37,1,57,126
10 M=16384:GOTO220
50 DATA ".... .... .... .... .... .... .... .... .... .... .... .... .... .... .... ...."
60 DATA ".... WN.. .NS. .NS. .N.. .NS. .NS. .NS. .N.. .NS. .NS. .NS. .NS. .NS. .N.E ...."
70 DATA ".... W..E .... .... W..E .... .... .... W..E .... .... .... .... .... W..E ...."
80 DATA ".... W..E .... WN.. ..S. .NS. .NS. .NS. ...E .... WN.. .NS. .NS. .NS. ...E ...."
90 DATA ".... W..E .... W..E .... .... .... .... W..E .... W..E .... .... .... W..E ...."
100 DATA ".... W... .NS. .... .NS. .NS. .NS. .NS. ...E .... W... .NS. .NS. .NS. ...E ...."
110 DATA ".... W..E .... W..E .... .... .... .... W..E .... W..E .... .... .... W..E ...."
120 DATA ".... W... .NS. ..S. .N.. .NS. .NS. .NS. ...E .... W..E .... WN.. .NS. ...E ...."
130 DATA ".... W..E .... .... W..E .... .... .... W..E .... W.S. .NS. ...E .... W..E ...."
140 DATA ".... W..E .... WN.. ..S. .N.E .... WN.. ..S. .N.E .... .... W..E .... W..E ...."
150 DATA ".... W..E .... W..E .... W..E .... W..E .... W.S. .N.E .... W..E .... W..E ...."
160 DATA ".... W..E .... W..E .... W..E .... W..E .... .... W..E .... W..E .... W..E ...."
170 DATA ".... W.S. .NS. ..S. .NS. ..S. .NS. ..S. .NS. .NS. ..S. .NS. ..S. .NS. ..SE ...."
180 DATA ".... .... .... .... .... .... .... .... .... .... .... .... .... .... .... ...."
190 DATA ""
200 REM FORMAT FOR EACH SPACE: WNSE FOR WALLS OR . IF NO WALL THAT DIRECTION
210 REM INITIALISATION
220 CLS0
230 DIMM(16,16),DI$(3),WA$(1),A(2,2),C,D,V
235 DI$(0)="NORTH":DI$(1)="EAST ":DI$(2)="SOUTH":DI$(3)="WEST ":WA$(1)="-WALL"
240 WA$(0)="     "
250 X=0:Y=0:D=0:XS=0:YS=0:WA=0:A=0:B=0
260 PRINT"SETTING UP MAP";:REM READ MAP
270 REM INPUT L$
280 READL$
290 IFLEN(L$)=0THEN410
300 K=0
310 FORI=1TOLEN(L$):C$=MID$(L$,I,1)
330 IFC$="N"THENK=KOR8
340 IFC$="E"THENK=KOR4
350 IFC$="S"THENK=KOR2
360 IFC$="W"THENK=KOR1
370 IFC$=" "THENM(X,Y)=K:PRINT@16+(Y+1)*32+X,CHR$(143-(K=0));:K=0:X=X+1
380 IFX>=XSTHENXS=X
390 NEXT:M(X,Y)=K:X=0:Y=Y+1:GOTO270
410 PRINT@16+6*32+3,"B";:PRINT@17,"SCORE"SC;
420 REM CLS
430 YS=Y:XS=XS+1
440 X=1:Y=1
450 REM LOOP
460 GOSUB500: REM STATUS
470 GOSUB660: REM RENDER
480 GOSUB1940: REM INPUT
490 GOTO450
500 REM DISPLAY CURRENT (X,Y) VALUE
510 A=17
520 B=11
530 SL$="    "
540 V=5
550 PRINT@B*32+A,;
560 K=M(X,Y)
570 A=17:SL$=".":IFKAND8THENSL$="N"
580 PRINT@B*32+A,;
590 A=18:SL$=".":IFKAND4THENSL$="E"
600 PRINT@B*32+A,;
610 A=19:SL$=".":IFKAND2THENSL$="S"
620 PRINT@B*32+A,;
630 A=20:SL$=".":IFKAND1THENSL$="W"
640 PRINT@B*32+A,;
650 RETURN  
660 REM RENDER DUNGEON
670 REM --------------
680 REM CLEAR AREA
690 FOR B=0 TO 15
700 PRINT@B*32,"€€€€€€€€€€€€€€€€";
710 NEXT
720 REM FIND CELLS / REORIENT SW
730 REM STORE IN MA - WE'RE AT (0,1)
740 A=X:B=Y
750 CO=D+X+Y AND 1
760 FOR TY=0 TO 2
770 GOSUB2440:REM LEFT/RIGHT A/B
780 A(1,TY)=0
790 IF A>=0 AND B>=0 AND A<XS AND B<YS THEN A(1,TY)=M(A,B)
800 A(0,TY)=RL
810 A(2,TY)=RR
820 GOSUB2550:REM ADVANCE
830 NEXT
840 REM DRAW BACK WALLS
850 L=A(1,2):GOSUB2330
860 IF F=0 GOTO930
870 C=143:V=5:IFCO=1THENV=6
880 FORB=5TO9
890 FORA=5TO10
900 POKEM+A+B*32,C+V*16
910 NEXT
920 NEXT
930 V=6:IFCO=1THENV=5
940 FORDE=0TO2STEP2 
950 L=A(DE,2):GOSUB2330
960 IFF=0THEN1020
970 FORA=DE*5.5TO4+DE*5.5
980 FORB=5TO 9
990 POKEM+A+B*32,C+V*16
1000 NEXT
1010 NEXT 
1020 NEXT
1030 REM 1,2 LEFT WALL
1040 L=A(1,2):GOSUB2330
1050 IF RL=0 GOTO1100
1060 A=4:C=143
1070 FOR B=5 TO 9:POKEM+A+B*32,C+V*16:NEXT
1080 B=4:C=139:POKEM+A+B*32,C+V*16
1090 B=10:C=142:POKEM+A+B*32,C+V*16
1100 REM 1,2 RIGHT WALL
1110 IF RR=0 GOTO1160
1120 A=11:C=143
1130 FOR B=5 TO 9:POKEM+A+B*32,C+V*16:NEXT
1140 B=4:C=135:POKEM+A+B*32,C+V*16
1150 B=10:C=141:POKEM+A+B*32,C+V*16
1160 REM 1,1 BACK WALL
1170 L=A(1,1):GOSUB2330
1180 C=143
1190 IF F=0 GOTO1250
1200 FOR B=4 TO 10
1210 FOR A=4 TO 11
1220 POKEM+A+B*32,C+V*16
1230 NEXT
1240 NEXT
1250 REM (0-2),1 BACK WALLS
1260 V=5:IF CO=1 THEN V=6
1270 FOR DE=0 TO 2 STEP 2
1280 L=A(DE,1):GOSUB2330
1290 IF F=0 GOTO1350
1300 FOR A=DE*6 TO 3+DE*6
1310 FOR B=4 TO 10
1320 POKEM+A+B*32,C+V*16
1330 NEXT
1340 NEXT
1350 NEXT
1360 REM 1,1 LEFT SIDE WALL
1370 V=5:IF CO=1 THEN V=6
1380 L=A(1,1):GOSUB2330
1390 IF RL=0 GOTO1460
1400 FOR A=1 TO 3
1410 B=A:C=139:POKEM+A+B*32,C+V*16
1420 B=14-A:C=142:POKEM+A+B*32,C+V*16
1430 C=143
1440 FOR B=1+A TO 13-A:POKEM+A+B*32,C+V*16:NEXT
1450 NEXT
1460 REM 1,1 RIGHT SIDE WALL
1470 IF RR=0 GOTO1550
1480 FOR QX=1 TO 3
1490 A=15-QX
1500 B=QX:C=135:POKEM+A+B*32,C+V*16
1510 B=14-QX:C=141:POKEM+A+B*32,C+V*16
1520 C=143
1530 FOR B=1+QX TO 13-QX:POKEM+A+B*32,C+V*16:NEXT
1540 NEXT
1550 REM 0,1 BACK WALL
1560 L=A(1,0):GOSUB2330
1570 IF F=0 GOTO1630
1580 FOR B=1 TO 13
1590 FOR A=1 TO 14
1600 POKEM+A+B*32,C+V*16
1610 NEXT
1620 NEXT
1630 REM (0,2)-0 BACK WALLS
1640 V=6:IF CO=1 THEN V=5
1650 FOR DE=0 TO 2 STEP 2
1660 L=A(DE,0):GOSUB2330
1670 IF F=0 GOTO1700
1680 A=DE*7.5
1690 FOR B=1 TO 13:POKEM+A+B*32,C+V*16:NEXT
1700 NEXT
1710 REM (1,0) LEFT SIDE WALL
1720 L=A(1,0):GOSUB2330
1730 IF RL=0 GOTO1780
1740 A=0:B=0:C=139:POKEM+A+B*32,C+V*16
1750 B=14:C=142:POKEM+A+B*32,C+V*16
1760 C=143
1770 FOR B=1 TO 13:POKEM+A+B*32,C+V*16:NEXT
1780 REM (1,0) RIGHT SIDE WALL
1790 IF RR=0 GOTO1840
1800 A=15:B=0:C=135:POKEM+A+B*32,C+V*16
1810 B=14:C=141:POKEM+A+B*32,C+V*16
1820 C=143
1830 FOR B=1 TO 13:POKEM+A+B*32,C+V*16:NEXT
1840 REM DONE
1850 RETURN
1860 PRINT@12*32+5,"‘““’";
1870 PRINT@13*32+5,"•CCš";
1880 PRINT@14*32+5,"•žžž’";
1890 PRINT@15*32+6,"œ””˜";:FORC1=1TO3500:NEXT
1900 PRINT@12*32+5,"ÁÃÃÂ";
1910 PRINT@13*32+5,"ÅMCÊ";
1920 PRINT@14*32+5,"ÅÎÎÎÂ";
1930 PRINT@15*32+6,"ÌÄÄÈ";:GOTO1980
1940 REM ASK FOR PROMPT & HANDLE INPUT
1950 PRINT@16+32*(Y+1)+X,"*";:PRINT@497,"FACING "DI$(D);
1960 REM PRINT@17+4*32,"FORWARD":PRINT@17+5*32,"BACKWARD":PRINT@17+6*32,"LEFT"
1970 REM PRINT@17+7*32,"RIGHT":PRINT@17+8*32,"QUIT":PRINT@17+9*32,;
1980 C$=INKEY$:IFC$=""THEN1980
1990 PRINT@16+32*(Y+1)+X,".";
2000 IFC$="W"THENDM=1:GOTO2060
2010 IFC$="Z"THENDM=-1:GOTO2060
2020 IFC$="A"THEND=(D-1)AND3
2030 IFC$="S"THEND=(D+1)AND3
2040 IFC$="Q"THEN1860
2050 RETURN
2060 GOTO2170:REM CHECK WALLS
2070 IFD=0THENY=Y-DM
2080 IFD=1THENX=X+DM
2090 IFD=2THENY=Y+DM
2100 IFD=3THENX=X-DM
2110 WA=0
2120 IFY>=YSTHENY=0
2130 IFY<0THENY=YS-1
2140 IFX>=XSTHENX=0
2150 IFX<0THENX=XS-1
2160 RETURN
2170 REM CHECK WALLS
2180 K=M(X,Y)
2190 IFDM=-1 GOTO2270
2200 IFD=0AND(KAND8)THEN2250
2210 IFD=1AND(KAND4)THEN2250
2220 IFD=2AND(KAND2)THEN2250
2230 IFD=3AND(KAND1)THEN2250
2240 GOTO2070
2250 WA=1
2260 RETURN : REM WALL IN THE WAY
2270 REM BACKWARD
2280 IFD=2AND(KAND8)THEN2250
2290 IFD=3AND(KAND4)THEN2250
2300 IFD=0AND(KAND2)THEN2250
2310 IFD=1AND(KAND1)THEN2250
2320 GOTO2070
2330 REM RETURN FRONT/BACK/LEFT/RIGHT
2340 REM GIVEN L AND D
2350 SN=0:IFLAND8THENSN=1
2360 SE=0:IFLAND4THENSE=1
2370 SS=0:IFLAND2THENSS=1
2380 ZW=0:IFLAND1THENZW=1
2390 IFD=0THENF=SN:RR=SE:RB=SS:RL=ZW
2400 IFD=1THENF=SE:RR=SS:RB=ZW:RL=SN
2410 IFD=2THENF=SS:RR=ZW:RB=SN:RL=SE
2420 IFD=3THENF=ZW:RR=SN:RB=SE:RL=SS
2430 RETURN
2440 REM RETURN LEFT/RIGHT FROM A/B/D
2450 IFD=0THENLY=B:RY=B:LX=A-1:RX=A+1
2460 IFD=1THENLX=A:RX=A:LY=B-1:RY=B+1
2470 IFD=2THENLY=B:RY=B:LX=A+1:RX=A-1
2480 IFD=3THENLX=A:RX=A:LY=B+1:RY=B-1
2490 RL=0:RR=0
2500 IF LX<0 OR LX>=XS OR LY<0 OR LY>=YSTHEN2520
2510 RL=M(LX,LY)
2520 IF RX<0 OR RX>=XS OR RY<0 OR RY>=YSTHEN2540
2530 RR=M(RX,RY)
2540 RETURN
2550 REM STEP FORWARD
2560 IFD=0THENB=B-1:REM N
2570 IFD=1THENA=A+1:REM E
2580 IFD=2THENB=B+1:REM S
2590 IFD=3THENA=A-1:REM W
2600 RETURN
10000 FORA=0TO44:READB:POKE17227+A,B:NEXT:EXEC17227:RETURN
