0 CLS:CLEAR500:GOSUB10000
1 CLS:GOSUB183:M=1024:GOTO107
2 IFC(T-1,U)>=.THENPOKEJ,Z:T=T-1:J=J-1:Z=PEEK(J):R=1:POKEJ,66:RETURN
3 RETURN
4 IFC(T+1,U)>=.THENPOKEJ,Z:T=T+1:J=J+1:Z=PEEK(J):R=2:POKEJ,66:RETURN
5 RETURN
6 IFC(T,U-1)>=.THENPOKEJ,Z:U=U-1:J=J-P:Z=PEEK(J):R=3:POKEJ,66:RETURN
7 RETURN
8 IFC(T,U+1)>=.THENPOKEJ,Z:U=U+1:J=J+P:Z=PEEK(J):R=4:POKEJ,66:RETURN
9 RETURN
10 OND GOTO11,12,13:B=B-1:RETURN
11 A=A+1:RETURN
12 B=B+1:RETURN
13 A=A-1:RETURN
14 OND GOTO15,16,17:E=B:W=B:N=A-1:S=A+1:GOTO18
15 N=A:S=A:E=B-1:W=B+1:GOTO18
16 E=B:W=B:N=A+1:S=A-1:GOTO18
17 N=A:S=A:E=B+1:W=B-1
18 IFN<.ORN>=XS ORE<.ORE>=YS THENQ=.:GOTO20
19 Q=M(N,E)
20 IFS<.ORS>=XS ORW<.ORW>=YS THENO=.:RETURN
21 O=M(S,W):RETURN
22 N=I AND8:E=I AND4:S=I AND2:W=I AND1:OND GOTO23,24,25:F=N:O=E:Q=W:RETURN
23 F=E:O=S:Q=N:RETURN
24 F=S:O=W:Q=E:RETURN
25 F=W:O=N:Q=S:RETURN
26 GOSUB27:GOSUB51:GOTO26
27 PRINT@.,B$;:PRINT@P,B$B$B$;:PRINT@96,B$;:PRINT@128,B$;:PRINT@160,B$;:PRINT@192,B$;:PRINT@224,B$;:PRINT@256,B$;
28 PRINT@288,B$;:PRINT@320,B$;:PRINT@352,B$;:PRINT@384,B$;:PRINT@416,B$;:PRINT@448,B$B$B$;:A=X:B=Y:H=D+X+Y AND1
29 FORI=.TO2:GOSUB14:A(1,I)=.:IFA>=.ANDB>=.ANDA<XS ANDB<YS THENA(1,I)=M(A,B):A(.,I)=Q:A(2,I)=O:GOSUB10:NEXT
30 I=A(1,2):GOSUB22:IFF THENM$=LEFT$(D$(5-(H=1)),6):FORA=165TO293STEPP:PRINT@A,M$;:NEXT
31 V=6+(H=1):M$=LEFT$(D$(V),5):FORG=.TO2STEP2:I=A(G,2):GOSUB22:IFF THENA=G*5.5+160:FORB=A TOA+128STEPP:PRINT@B,M$;:NEXT
32 NEXT:I=A(1,2):GOSUB22:IFQ THENC=V*16+143:FORA=M+164TOM+292STEPP:POKEA,C:NEXT:POKEM+132,C-4:POKEM+324,C-1
33 IFO THENC=V*16+143:FORA=M+171TOM+299STEPP:POKEA,C:NEXT:POKEM+139,C-8:POKEM+331,C-2
34 I=A(1,1):GOSUB22:IFF THENM$=LEFT$(D$(V),8):FORA=132TO324STEPP:PRINT@A,M$;:NEXT
35 M$=LEFT$(D$(5-(H=1)),4):FORG=.TO2STEP2:I=A(G,1):GOSUB22:IFF THENA=G*6+128:FORB=A TOA+192STEPP:PRINT@B,M$;:NEXT
36 NEXT:V=5-(H=1):I=A(1,1):GOSUB22:IFQ=.THEN39
37 IFV=6THENPRINT@33,CHR$(235);:PRINT@65,CHR$(239)CHR$(235);:PRINT@97,STRING$(2,239)CHR$(235);:FORA=129TO321STEPP:PRINT@A,STRING$(3,239);:NEXT:PRINT@353,STRING$(2,239)+CHR$(238);:PRINT@385,CHR$(239)CHR$(238);:PRINT@417,CHR$(238);:GOTO39
38 PRINT@33,CHR$(219);:PRINT@65,CHR$(223)CHR$(219);:PRINT@97,STRING$(2,223)CHR$(219);:FORA=129TO321STEPP:PRINT@A,STRING$(3,223);:NEXT:PRINT@353,STRING$(2,223)CHR$(222);:PRINT@385,CHR$(223)CHR$(222);:PRINT@417,CHR$(222);
39 IFO=.THEN42
40 IFV=6THENPRINT@46,CHR$(231);:PRINT@77,CHR$(231)CHR$(239);:PRINT@108,CHR$(231)STRING$(2,239);:FORA=140TO332STEPP:PRINT@A,STRING$(3,239);:NEXT:PRINT@364,CHR$(237)STRING$(2,239);:PRINT@397,CHR$(237)CHR$(239);:PRINT@430,CHR$(237);:GOTO42
41 PRINT@46,CHR$(215);:PRINT@77,CHR$(215)CHR$(223);:PRINT@108,CHR$(215)STRING$(2,223);:FORA=140TO332STEPP:PRINT@A,STRING$(3,223);:NEXT:PRINT@364,CHR$(221)STRING$(2,223);:PRINT@397,CHR$(221)CHR$(223);:PRINT@430,CHR$(221);
42 I=A(1,.):GOSUB22:IFF THENM$=D$(V):FORA=33TO417STEPP:PRINT@A,M$;:NEXT
43 C=(6+(H=1))*16+143:FORG=.TO2STEP2:I=A(G,.):GOSUB22:IFF THENA=M+G*7.5+P:FORB=A TOA+384STEPP:POKEB,C:NEXT
44 NEXT:I=A(1,.):GOSUB22:IFQ THENPOKEM,C-4:POKEM+448,C-1:FORB=M+P TOM+416STEPP:POKEB,C:NEXT
45 IFO THENPOKEM+15,C-8:POKEM+463,C-2:FORA=M+47TOM+431STEPP:POKEA,C:NEXT:RETURN
46 RETURN
47 PRINT@389,CHR$(145)CHR$(147)CHR$(147)CHR$(146);:PRINT@421,CHR$(149)"CC"CHR$(154);:PRINT@453,CHR$(149)CHR$(158)CHR$(158)CHR$(158)CHR$(146);
48 PRINT@486,CHR$(156)CHR$(148)CHR$(148)CHR$(152);:SOUND200,1:SOUND225,2:SOUND175,2:SOUND100,1:C(X,Y)=2:SC=SC+1:CC=CC+1:PRINT@17,"SCORE"SC;
49 PRINT@389,CHR$(193)CHR$(195)CHR$(195)CHR$(194);:PRINT@421,CHR$(197)"MC"CHR$(202);:PRINT@453,CHR$(197)CHR$(206)CHR$(206)CHR$(206)CHR$(194);:PRINT@486,CHR$(204)CHR$(196)CHR$(196)CHR$(200);:IFCC=10THEN85
50 GOTO52
51 POKEM+16+P*(Y+1)+X,106:IFC(X,Y)>0THENONC(X,Y)GOTO47,49
52 K=ASC(INKEY$+" "):IFRND(L)=1THENON2+SGN(X-T)GOSUB2,9,4:ON2+SGN(Y-U)GOSUB6,9,8
53 ONRND(L)GOSUB2,4,6,8:IFRND(3)=1THENONR GOSUB2,4,6,8
54 IFT=X ANDU=Y THEN104
55 IFK=P THEN52
56 POKEM+16+P*(Y+1)+X,110:IFK=94THEN61
57 IFK=10THEN69
58 IFK=8THEND=D-1AND3:PRINT@497,"FACING "D$(D);:RETURN
59 IFK=9THEND=D+1AND3:PRINT@497,"FACING "D$(D);:RETURN
60 GOTO52
61 I=M(X,Y):OND GOTO63,65,67:IFI AND8THENRETURN
62 Y=Y-1:RETURN
63 IFI AND4THENRETURN
64 X=X+1:RETURN
65 IFI AND2THENRETURN
66 Y=Y+1:RETURN
67 IFI AND1THENRETURN
68 X=X-1:RETURN
69 I=M(X,Y):OND GOTO71,73,75:IFI AND2THENRETURN
70 Y=Y+1:RETURN
71 IFI AND1THENRETURN
72 X=X-1:RETURN
73 IFI AND8THENRETURN
74 Y=Y-1:RETURN
75 IFI AND4THENRETURN
76 X=X+1:RETURN
77 POKEJ,66:PRINT@465,;:M$="HE GOT YOU!":GOSUB182:SOUND1,10:IFSC>HS THENHS=SC
78 PRINT@49,;:M$="HIGH"+STR$(HS):GOSUB182
79 PRINT@497,"PLAY AGAIN?"CHR$(128)+CHR$(128);
80 M$=INKEY$:IFM$=""THEN80
81 IFM$="Y"THEN84
82 IFM$="N"THEN10003
83 GOTO80
84 RESTORE:SC=0:L=26:LV=0:GA=0:GOTO86
85 FORX=1TO10:SOUND225,1:NEXT
86 GA=GA+1
87 IFGA=1THENGOSUB98:PRINT@16+6*P+3," ";:C(3,5)=0:GOTO26
88 IFGA<4THENGOSUB93:GOTO26
89 IFGA=4THENGOSUB98:PRINT@16+6*P+7," ";:C(7,5)=0:GOTO26
90 IFGA<7THENGOSUB93:GOTO26
91 IFGA=7THENGOSUB98:PRINT@16+8*P+5," ";:C(5,7)=0:GOTO26
92 GOSUB93:GOTO26
93 FORX=1TO14:FORY=1TO12:IFC(X,Y)>=.THENC(X,Y)=0:PRINT@16+(Y+1)*P+X," ";
94 NEXT:NEXT
95 LV=LV-1:IFLV<0THENLV=0
96 CC=0:GOSUB99:L=L-1:IFL<15THENL=15
97 RETURN
98 GOSUB111:GOSUB95:RETURN
99 PRINT@17,STRING$(12,128)"***";:PRINT@29,LEFT$("   ",LV);:PRINT@17,"SCORE"SC;
100 FORI=1TO10
101 X=RND(14):Y=RND(12):IFC(X,Y)THEN101
102 C(X,Y)=1:NEXT
103 X=1:Y=1:PRINT@16+P*(Y+1)+X,"*";:T=14:U=12:J=M+16+P*(U+1)+T:Z=96:POKEJ,66:D=0:PRINT@497,"FACING "D$(D);:RETURN
104 GOSUB121:SOUND1,2:LV=LV+1:PRINT@28+LV," ";:IFLV<3THENFORA=1TO2500:NEXT
105 POKEJ,110:IFLV=3THEN77
106 GOSUB103:RETURN
107 DIMM,A,B,M(16,14),A(2,2),V,X,Y,C,D,M$,B$,I,H,G,C(16,14),D$(6),O,Q,P,K,N,S,E,W,XS,YS
108 DIMR,T,U,F,L,J,Z:I=RND(-TIMER):P=32
109 D$(5)=STRING$(14,223):D$(6)=STRING$(14,239):D$(0)="NORTH":D$(1)="EAST ":D$(2)="SOUTH":D$(3)="WEST "
110 B$=STRING$(16,128):GOSUB198:XS=16:YS=14:L=25:GOTO86
111 CLS0:PRINT@224,"MYSTERY MESSAGE";:PRINT@224+32,"FIRST SOLVED BY";:PRINT@224+96,"    ???????    ";:X=0:Y=0:D=0:PRINT@17,;:M$="SETTING UP MAP":GOSUB182:A=0:B=0
112 READD$(4):IFD$(4)=""THENRETURN
113 K=0:FORI=1TOLEN(D$(4)):M$=MID$(D$(4),I,1)
114 IFM$="N"THENK=K OR8:NEXT:GOTO120
115 IFM$="E"THENK=K OR4:NEXT:GOTO120
116 IFM$="S"THENK=K OR2:NEXT:GOTO120
117 IFM$="W"THENK=K OR1:NEXT:GOTO120
118 IFM$=" "THENM(X,Y)=K:C(X,Y)=(K=.):PRINT@16+(Y+1)*P+X,CHR$(143-(K=.));:K=.:X=X+1
119 NEXT
120 M(X,Y)=K:X=.:Y=Y+1:GOTO112
121 PRINT@67,STRING$(8,175);
122 PRINT@99,STRING$(8,175);
123 PRINT@133,STRING$(2,175);:PRINT@139,STRING$(2,175);
124 PRINT@165,STRING$(2,175);:PRINT@171,STRING$(2,175);
125 PRINT@197,STRING$(2,175);:PRINT@203,STRING$(2,175);
126 PRINT@229,STRING$(2,175);:PRINT@235,STRING$(2,175);
127 PRINT@261,STRING$(6,175);
128 PRINT@293,STRING$(6,175);
129 PRINT@325,STRING$(2,175);:PRINT@331,STRING$(2,175);
130 PRINT@357,STRING$(2,175);:PRINT@363,STRING$(2,175);
131 PRINT@389,STRING$(2,175);:PRINT@395,STRING$(2,175);
132 PRINT@421,STRING$(2,175);:PRINT@427,STRING$(2,175);
133 PRINT@451,STRING$(8,175);
134 PRINT@483,STRING$(8,175);:RETURN
137 DATA".... .... .... .... .... .... .... .... .... .... .... .... .... .... .... .... "
138 DATA".... WN.. .NS. .NS. .N.. .NS. .NS. .NS. .N.. .NS. .NS. .NS. .NS. .NS. .N.E .... "
139 DATA".... W..E .... .... W..E .... .... .... W..E .... .... .... .... .... W..E .... "
140 DATA".... W..E .... WN.. ..S. .NS. .NS. .NS. ...E .... WN.. .NS. .NS. .NS. ...E .... "
141 DATA".... W..E .... W..E .... .... .... .... W..E .... W..E .... .... .... W..E .... "
142 DATA".... W... .NS. .... .NS. .NS. .NS. .NS. ...E .... W... .NS. .NS. .NS. ...E .... "
143 DATA".... W..E .... W..E .... .... .... .... W..E .... W..E .... .... .... W..E .... "
144 DATA".... W... .NS. ..S. .N.. .NS. .NS. .NS. ...E .... W..E .... WN.. .NS. ...E .... "
145 DATA".... W..E .... .... W..E .... .... .... W..E .... W.S. .NS. ...E .... W..E .... "
146 DATA".... W..E .... WN.. ..S. .N.E .... WN.. ..S. .N.E .... .... W..E .... W..E .... "
147 DATA".... W..E .... W..E .... W..E .... W..E .... W.S. .N.E .... W..E .... W..E .... "
148 DATA".... W..E .... W..E .... W..E .... W..E .... .... W..E .... W..E .... W..E .... "
149 DATA".... W.S. .NS. ..S. .NS. ..S. .NS. ..S. .NS. .NS. ..S. .NS. ..S. .NS. ..SE .... "
150 DATA".... .... .... .... .... .... .... .... .... .... .... .... .... .... .... .... "
151 DATA""
152 DATA".... .... .... .... .... .... .... .... .... .... .... .... .... .... .... .... "
153 DATA".... WN.E .... WN.. .NS. .N.E .... WN.. .NS. .N.. .NS. .NS. .NS. .NS. .N.E .... "
154 DATA".... W..E .... W..E .... W..E .... W..E .... W..E .... .... .... .... W..E .... "
155 DATA".... W... .NS. ..SE .... W.S. .NS. ...E .... W.S. .NS. .N.. .NS. .NS. ...E .... "
156 DATA".... W..E .... .... .... .... .... W..E .... .... .... W..E .... .... W..E .... "
157 DATA".... W... .NS. .NS. .NS. .NS. .NS. .... .NS. .NS. .N.. ..S. .NS. .NS. ...E .... "
158 DATA".... W..E .... .... .... .... .... W..E .... .... W..E .... .... .... W..E .... "
159 DATA".... W... .NS. .NS. .NS. .N.E .... W..E .... WN.. ..SE .... WN.E .... W..E .... "
160 DATA".... W..E .... .... .... W..E .... W..E .... W..E .... WN.. ..S. .N.. ..SE .... "
161 DATA".... W..E .... WN.E .... W..E .... W... .NS. ...E .... W..E .... W..E .... .... "
162 DATA".... W..E .... W.S. .NS. ..SE .... W..E .... W.S. .NS. ..SE .... W.S. .N.E .... "
163 DATA".... W..E .... .... .... .... .... W..E .... .... .... .... .... .... W..E .... "
164 DATA".... W.S. .NS. .NS. .NS. .NS. .NS. ..S. .NS. .NS. .NS. .NS. .NS. .NS. ..SE .... "
165 DATA".... .... .... .... .... .... .... .... .... .... .... .... .... .... .... .... "
166 DATA""
167 DATA".... .... .... .... .... .... .... .... .... .... .... .... .... .... .... .... "
168 DATA".... WN.. .NS. .N.. .NS. .N.. .NS. .NS. .N.. .NS. .N.. .NS. .NS. .NS. .N.E .... "
169 DATA".... W..E .... W.SE .... W..E .... .... W..E .... W..E .... .... .... W..E .... "
170 DATA".... W..E .... .... .... W..E .... WN.. ..SE .... W..E .... WN.E .... W..E .... "
171 DATA".... W..E .... .... .... W..E .... W..E .... .... W..E .... W..E .... W..E .... "
172 DATA".... W..E .... WN.E .... W..E .... W.S. .N.E .... W..E .... W.S. .NS. ...E .... "
173 DATA".... W..E .... W..E .... W..E .... .... W..E .... W..E .... .... .... W..E .... "
174 DATA".... W... .NS. ..S. .NS. .... .NS. .N.. ..S. .N.. ..S. .N.. .NS. .NS. ...E .... "
175 DATA".... W..E .... .... .... W..E .... W..E .... W..E .... W..E .... .... W..E .... "
176 DATA".... W..E .... WN.E .... W..E .... W..E .... W.E. .... W.E. .... WNS. ...E .... "
177 DATA".... W..E .... W... .NS. ...E .... W.SE .... W..E .... W.S. .NSE .... W..E .... "
178 DATA".... W..E .... W..E .... W..E .... .... .... W..E .... .... .... .... W..E .... "
179 DATA".... W.S. .NS. ..S. .NS. ..S. .NS. .NS. .NS. ..S. .NS. .NS. .NS. .NS. ..SE .... "
180 DATA".... .... .... .... .... .... .... .... .... .... .... .... .... .... .... .... "
181 DATA""
182 PRINTM$;:B=((PEEK(136)AND1)*256+PEEK(137))-1+M:FORA=B-LEN(M$)+1TOB:POKEA,PEEK(A)-64:NEXT:RETURN
183 PRINTTAB(12)"mc-chase":REM HEY CHARLIE! AT-ONE-MENT
184 PRINT"  YOU ARE AT THE HERON POINT"
185 PRINT"CONVENTION CENTRE. IT IS"
186 PRINT"COCOFEST. THE CONVENTION HAS"
187 PRINT"BEEN TAKEN OVER BY RETROBRITE-"
188 PRINT"NEEDING COCOS. YOU MUST SWAP"
189 PRINT"OUT 10 YELLOWING COCOS FOR 10"
190 PRINT"BRIGHT AND SHINY MC-10S."
191 PRINT"  HOWEVER, YOU ARE BEING CHASED"
192 PRINT"BY THE 'B' WHO IS FILLED WITH"
193 PRINT"RIGHTEOUS INDIGNATION AT THE"
194 PRINT"MC-10 (FOR RUINING THE CHANCES"
195 PRINT"OF THE DELUXE COCO). HE WILL"
196 PRINT"TRY TO STOP YOU. YOU REGAIN A"
197 PRINT"LIFE EACH LEVEL. TO MOVE: ARROWS";:RETURN
198 PRINTTAB(6);:M$="HIT ANY KEY TO BEGIN":GOSUB182
199 M$=INKEY$:IFM$=""THEN199
200 RETURN
10000 IF PEEK(65535)=27 THEN POKE65497,0:GOTO10002 ELSE INPUT"CAN YOUR COMPUTER HANDLE DOUBLE SPEED (Y/N)";B$:IFB$="Y"THENPOKE65495,0
10002 RETURN
10003 IF PEEK(65535)=27 THEN POKE65496,0 ELSE POKE65494,0
10004 END
10005 ' IF YOU CAN MAKE IT
10006 ' THROUGH  6 SCREENS AND
10007 ' DECIPHER THE SECRET
10008 ' MYSTERY MESSAGE OF
10009 ' MAP 3, AND CAPTURE IT
10010 ' SEND THE IMAGE TO
10020 ' JIMGERRIE@NS.SYMPATICO.CA
10030 ' AND YOUR NAME WILL 
10040 ' APPEAR ON THE OPENING
10050 ' SCREEN OF THE OFFICIAL
10060 ' VERSION OF THE GAME!
10070 ' WIN ETERNAL COCO GLORY!
