0 CLS0:CLEAR5000:GOTO1000
1 RETURN
2 ON-(D<>2)GOTO5:ONK(PEEK(P-3+M))GOTO30,50,90,82:P=P-3:GOTO30
3 ON-(D>.)GOTO5:ONK(PEEK(P+3+M))GOTO30,50,93,82:P=P+3:GOTO30
4 D=-(D=1)*3-(D=.)-(D=2)*3-(D=3):GOTO30
5 D=-(D=.)*2-(D=1)*.-(D=2)*.-(D=3)*2:GOTO30
6 SET(I,13,4):I=I+J*2:J=.:D=-(D<2)-(D>1)*3:GOTO48
7 SET(X,13,4):X=T:GOSUB44:RETURN
8 I=I+J*2:J=.:RETURN
9 T=B(F+1):V=A(F+1):O=C(F+1):PRINT@.,V$F$(T)W$B$(V)G$(T)B$(O)X$C$(V)H$(T)C$(O)X$D$(V)I$(T)D$(O)Y$J$(T)Z$;
10 T=B(F):V=A(F):O=C(F):?V$F$(T)W$B$(V)G$(T)B$(O)X$C$(V)H$(T)C$(O)X$D$(V)I$(T)D$(O)Y$J$(T)Z$;
11 T=B(F-1):V=A(F-1):O=C(F-1):?V$F$(T)W$B$(V)G$(T)B$(O)X$C$(V)H$(T)C$(O)X$D$(V)I$(T)D$(O)Y$J$(T);:RETURN
12 ONK(PEEK(135))GOTO2,3,70,4,87:GOTO30
13 ONK(PEEK(135))GOTO64,65,60,55:GOTO51

20 FORN=.TO5STEP.:GOSUB9:PRINT@P,M$(D);:PRINT@P+C,N$(D);:PRINT@P+K,O$(D);
21 POKE345,255:POKE344,255:POKE343,255:POKE342,255:POKE341,255:POKE340,255:IFNOT((PEEK(345)ANDPEEK(344)ANDPEEK(343)ANDPEEK(342)ANDPEEK(341)ANDPEEK(340))=255)THEN12

30 IFG THEN?@G,M$(H);:?@G+C,N$(H);:?@G+K,O$(H);:IFJ=.THENIFRND(Q)=1THENJ=SGN(P-G):H=5+J:J=J*2:I=L+J*2:GOTO40
31 W=W+1:IFW=3THENW=.:T=2-RND(3):IFABS(E+T-F)<2THENIFE+T>1THENIFE+T<31THENE=E+T:B(E+1)=1:B(E)=2:B(E-1)=.
34 IFJ THENFORI=I TOI+J*2STEPJ:ONPOINT(I,13)GOSUB1,6,1,1,8:RESET(I,13):NEXT
36 NEXT:GOTO200

40 IFH=5THENONRND(3)GOSUB42,43,44
41 NEXT:GOTO200
42 H=4:D=1:GOSUB47:GOTO48
43 H=6:D=1:GOSUB47:GOTO48
44 SOUND1,1:H=H+1:Z=Z+1:PRINT@G,M$(H);:PRINT@G+C,N$(H);:PRINT@G+K,O$(H);:IFZ=3THENG=.:SC=SC+10:GOSUB120:SOUND38,1
45 RETURN
47 PRINT@P,M$(D);:PRINT@P+C,N$(D);:PRINT@P+K,O$(D);:RETURN
48 N=N+1:POKEM+511,117-N:SOUND1,4:RETURN

50 P=207:J=.:W=.
51 GOSUB9:PRINT@P,M$(D);:PRINT@P+C,N$(D);:PRINT@P+K,O$(D);
52 POKE345,255:POKE344,255:POKE343,255:POKE342,255:POKE341,255:POKE340,255:IFNOT((PEEK(345)ANDPEEK(344)ANDPEEK(343)ANDPEEK(342)ANDPEEK(341)ANDPEEK(340))=255)THEN13ELSE51
53 GOTO51

55 IFF=2THENIFU-S=.THEN250
56 IFF>2THENF=F-1:E=E-1:B(E+1)=1:B(E)=2:B(E-1)=.:PRINT@493,"FLOOR"F;:SOUND200,1
57 GOSUB99:GOTO51
60 IFF<29THENF=F+1:E=E+1:B(E+1)=1:B(E)=2:B(E-1)=.:PRINT@493,"FLOOR"F;:SOUND200,1
61 GOSUB99:GOTO51
64 P=P-4:D=2:NEXT:GOTO200
65 P=P+4:D=.:NEXT:GOTO200

66 IFK(PEEK(J(O+V)-C+M))<>1THENP=J(O+V)-C:GOSUB75:GOSUB47:P=P+C:IFK(PEEK(J(O+V+V)+M))<>1THENP=J(O+V+V):GOSUB75:GOSUB47
67 GOSUB75:IFG THENPRINT@G,M$(H);:PRINT@G+C,N$(H);:PRINT@G+K,O$(H);
68 T=J(O+V):ONK(PEEK(P+M+1))GOTO30,50,91,85:GOTO30

70 IFD=1THEND=.:GOSUB47:GOTO30
71 IFD=3THEND=2:GOSUB47:GOTO30
72 IFD=.THENV=1:GOSUB78:GOTO66
73 V=-1:GOSUB78:GOTO66
75 PRINT@160,V$F$(B(F))W$B$(A(F))G$(B(F))B$(C(F))X$C$(A(F))H$(B(F))C$(C(F))X$D$(A(F))I$(B(F))D$(C(F))Y$J$(B(F))Z$;:RETURN
78 FORT=1TO7:IFP=J(T)THENO=T
79 NEXT:RETURN

82 A=P:B=D:D=.:FORP=207TO335STEPK:GOSUB9:GOSUB47:SOUNDP/2,1:D=D+1:NEXT
83 IFE=F-1THENGOSUB9:D=.:P=P-C:GOSUB47:F=E:PRINT@493,"FLOOR"F;:GOSUB99:GOTO50
84 GOSUB9:GOSUB47:SOUNDP/2,1:D=D+1:GOSUB48:P=A:D=B:GOTO30
85 P=T:GOTO82

86 X=T:RETURN

87 IFG THENPRINT@G,M$(H);:PRINT@G+C,N$(H);:PRINT@G+K,O$(H);
88 Y=(D=2)-(D=.):X=((P/C)-INT(P/C))*K+2+Y*3:IFY=.THEN30
89 T=-(Y=-1)+63*-(Y=1):FORX=X TOT STEP Y*2:ONPOINT(X,13)GOSUB1,1,1,1,86,1,1,7:RESET(X,13):NEXT:GOTO30

90 P=P-3
91 IFP>207THENC(F)=C(F)+2:GOSUB75:C(F)=1:GOTO97
92 A(F)=A(F)+2:GOSUB75:A(F)=1:GOTO97

93 P=P+3
94 IFP>207THENC(F)=C(F)+2:GOSUB75:C(F)=1:GOTO97
96 A(F)=A(F)+2:GOSUB75:A(F)=1:GOTO97

97 S=S+1:SC=SC+1:GOSUB120:SOUND100,1:SOUND200,1:IFG=.THENIFRND(2)=1THENGOSUB99
98 GOTO30

99 G=.:IFRND(2)=1THENRETURN
100 IFRND(2)=1THENT=RND(2):G=211+T*3:I=40+T*6:L=I:H=6:GOTO110
101 T=RND(2):G=194+T*3:I=6+T*6:L=I:H=4:GOTO110
110 J=.:Z=.:IFPEEK(G+M+1)=159THEN100
111 RETURN

120 PRINT@480,"SCORE"SC;:IFSC>NL THENNL=NL+50:N=N+(N>0):POKEM+511,53-N
121 RETURN

200 PRINT@480,"SCORE"SC"           ";:PRINT@494,"PLAY AGAIN (Y/N)?";:POKEM+511,96
210 M$=INKEY$:IFM$=""THEN210
220 IFM$="Y"THEN1070
230 IFM$="N"THEN10003
240 GOTO210

250 P=367:D=.:E=1:B(E+1)=1:B(E)=2:B(E-1)=.:PRINT@493,"FLOOR"F+1;:GOSUB9:GOSUB47
280 PRINT@480,"MISSION COMPLETE. 100 PT BONUS.";:POKEM+511,96:SC=SC+100:NL=SC+50
281 GOSUB5000:GOSUB2000:FORT=1TO1500:NEXT:GOSUB1080:NEXT:GOTO200

1000 DIMA(31):GOSUB3000:DIMB(31),C(31),K(255),T,E,F,D,I,J,X,Y,X,Y,P,H,M,W,M,N,L,G
1001 M=RND(-TIMER):M=1024
1002 V$=STRING$(4,128)+CHR$(207)+STRING$(6,223)+STRING$(3,223):W$=STRING$(9,223)+CHR$(207)+STRING$(8,128)+CHR$(207):X$=CHR$(207)+STRING$(8,128)+CHR$(207):Y$=CHR$(207)+STRING$(8,128)+STRING$(10,207):Z$=STRING$(10,207)+STRING$(3,128)
1003 DIMB$(7),C$(7),D$(7),F$(2),G$(2),H$(2),I$(2),J$(2),M$(7),N$(7),O$(7),V,Q,U,O,Z,S,J(8),C,K,A,B,SC,HS,NL
1004 C=32:K=64:GOSUB10000

1010 B$(1)=CHR$(223)+STRING$(3,175)+CHR$(223)+STRING$(3,175)+CHR$(223):B$(2)=CHR$(223)+STRING$(3,191)+CHR$(223)+STRING$(3,175)+CHR$(223)
1011 B$(3)=CHR$(223)+STRING$(3,175)+CHR$(223)+STRING$(3,191)+CHR$(223):B$(4)=CHR$(223)+STRING$(3,208)+CHR$(223)+STRING$(3,175)+CHR$(223)
1012 B$(5)=CHR$(223)+STRING$(3,175)+CHR$(223)+STRING$(3,208)+CHR$(223):C$(1)=CHR$(223)+CHR$(174)+STRING$(2,175)+CHR$(223)+CHR$(174)+STRING$(2,175)+CHR$(223)
1013 C$(2)=CHR$(223)+CHR$(190)+CHR$(191)+CHR$(191)+CHR$(223)+CHR$(174)+STRING$(2,175)+CHR$(223):C$(3)=CHR$(223)+CHR$(174)+STRING$(2,175)+CHR$(223)+CHR$(190)+CHR$(191)+CHR$(191)+CHR$(223)
1014 C$(4)=CHR$(223)+STRING$(3,208)+CHR$(223)+CHR$(174)+STRING$(2,175)+CHR$(223):C$(5)=CHR$(223)+CHR$(174)+STRING$(2,175)+CHR$(223)+STRING$(3,208)+CHR$(223)
1015 D$(1)=CHR$(223)+STRING$(3,175)+CHR$(223)+STRING$(3,175)+CHR$(223):D$(2)=CHR$(223)+STRING$(3,191)+CHR$(223)+STRING$(3,175)+CHR$(223)
1016 D$(3)=CHR$(223)+STRING$(3,175)+CHR$(223)+STRING$(3,191)+CHR$(223):D$(4)=CHR$(223)+STRING$(3,208)+CHR$(223)+STRING$(3,175)+CHR$(223)
1017 D$(5)=CHR$(223)+STRING$(3,175)+CHR$(223)+STRING$(3,208)+CHR$(223):B$(6)=CHR$(223)+STRING$(3,208)+"roof"+CHR$(223):B$(7)=CHR$(223)+STRING$(3,208)+"exit"+CHR$(223)
1018 C$(6)=CHR$(223)+STRING$(2,208)+CHR$(207)+STRING$(7,236):C$(7)=CHR$(223)+STRING$(3,208)+STRING$(7,236):D$(6)=CHR$(223)+CHR$(208)+STRING$(2,207)+STRING$(7,236):D$(7)=CHR$(223)+STRING$(3,208)+STRING$(7,236)

1020 F$(0)=STRING$(5,208):F$(1)=CHR$(208)+CHR$(229)+CHR$(208)+CHR$(234)+CHR$(208):F$(2)=CHR$(239)+STRING$(3,223)+CHR$(239)
1021 G$(0)=STRING$(5,208):G$(1)=CHR$(208)+CHR$(229)+CHR$(208)+CHR$(234)+CHR$(208):G$(2)=CHR$(239)+CHR$(223)+CHR$(239)+CHR$(223)+CHR$(239)
1022 H$(0)=STRING$(5,208):H$(1)=CHR$(208)+CHR$(229)+CHR$(208)+CHR$(234)+CHR$(208):H$(2)=CHR$(239)+STRING$(3,223)+CHR$(239)
1023 I$(0)=STRING$(5,208):I$(1)=CHR$(208)+CHR$(229)+CHR$(208)+CHR$(234)+CHR$(208):I$(2)=CHR$(239)+STRING$(3,223)+CHR$(239)
1024 J$(0)=STRING$(5,208):J$(1)=CHR$(208)+CHR$(229)+CHR$(208)+CHR$(234)+CHR$(208):J$(2)=STRING$(5,236)

1030 M$(0)=CHR$(223)+CHR$(159)+CHR$(223)
1031 N$(0)=CHR$(223)+CHR$(212)+CHR$(209):N$(1)=STRING$(3,223)
1032 O$(0)=CHR$(222)+CHR$(214)+CHR$(223):O$(1)=CHR$(216)+CHR$(210)+CHR$(157)

1033 M$(2)=CHR$(223)+CHR$(159)+CHR$(223)
1034 N$(2)=CHR$(210)+CHR$(216)+CHR$(223):N$(3)=STRING$(3,223)
1035 O$(2)=CHR$(223)+CHR$(217)+CHR$(221):O$(3)=CHR$(158)+CHR$(209)+CHR$(212)

1040 M$(4)=CHR$(223)+CHR$(255)+CHR$(223)
1041 N$(4)=CHR$(210)+CHR$(216)+CHR$(223):N$(5)=STRING$(3,223)
1042 O$(4)=CHR$(223)+CHR$(217)+CHR$(221):O$(5)=CHR$(216)+CHR$(210)+CHR$(253)

1043 M$(6)=CHR$(223)+CHR$(255)+CHR$(223)
1044 N$(6)=CHR$(223)+CHR$(212)+CHR$(209):N$(7)=STRING$(3,223)
1045 O$(6)=CHR$(222)+CHR$(214)+CHR$(223):O$(7)=CHR$(254)+CHR$(209)+CHR$(212)

1050 J(0)=194:J(1)=197:J(2)=200:J(3)=203:J(4)=207:J(5)=211:J(6)=214:J(7)=217:J(8)=220
1060 K(65)=1:K(68)=2:K(87)=3:K(83)=4:K(32)=5:K(8)=1:K(9)=2:K(94)=3:K(10)=4
1061 K(128)=1:K(207)=1:K(239)=2:K(191)=3:K(208)=4:K(229)=4:K(234)=4

1065 GOSUB3150

1070 J=0:Q=18:GOSUB2000:SC=0:Q=18-A(.):Z=0:N=0:NL=50:GOSUB1080:GOSUB9:PRINTZ$;:GOTO20

1080 GOSUB2100:U=.:FORT=2TO29:E=RND(3):A(T)=E:IFE>1THENU=U+1
1081 E=RND(3):C(T)=E:IFE>1THENU=U+1
1082 B(T)=.:NEXT
1083 A(30)=6:C(30)=6:A(1)=7:C(1)=7
1084 F=29:E=29:B(E+1)=1:B(E)=2:B(E-1)=.:P=211:D=.:S=.:Z=.:Q=Q-2:IFQ<2THENQ=2
1087 GOSUB99:POKEM+511,117-N
1090 RETURN

2000 CLS0:PRINT@32*2+12,"elevator";:PRINT@32*4+13,"action";:PRINT@256,;
2010 PRINTSTRING$(12,208)+CHR$(208)+STRING$(2,207)+STRING$(2,207)+STRING$(2,207)+CHR$(207)+STRING$(12,208);
2020 PRINTSTRING$(12,208)+CHR$(208)+CHR$(207)+STRING$(5,208)+CHR$(207)+STRING$(12,208);
2030 PRINTSTRING$(12,208)+CHR$(208)+CHR$(207)+STRING$(5,208)+CHR$(175)+STRING$(12,208);
2040 PRINTSTRING$(12,208)+CHR$(208)+CHR$(207)+STRING$(5,208)+CHR$(175)+STRING$(12,208);
2050 PRINTSTRING$(12,208)+CHR$(208)+CHR$(207)+STRING$(5,208)+CHR$(175)+STRING$(12,208);:IFQ<10THENGOSUB4000
2060 PRINTSTRING$(4,208)+STRING$(10,207)+STRING$(5,208)+STRING$(10,207)+STRING$(3,208);
2070 PRINTSTRING$(4,208)+STRING$(10,207)+STRING$(4,208)+STRING$(11,207)+STRING$(3,208);
2080 PRINTSTRING$(4,208)+STRING$(10,207)+STRING$(3,208)+STRING$(12,207)+STRING$(3,208);:IFSC>HS THENHS=SC
2090 RETURN

2100 Y=0:FORX=55TO40STEP-1:SET(X,Y,8):Y=Y+1:NEXT:PRINT@490,"BY JIM GERRIE";:FORT=1TO1500:NEXT
2120 PRINT@490,"HIGH SCORE:"HS"  ";
2200 D=2:FORP=59TO276STEP31:GOSUB47:SOUNDP/2,2:PRINT@P,STRING$(3,208);:PRINT@P+32,STRING$(3,208);:PRINT@P+64,STRING$(3,208);:NEXT
2210 GOSUB47:SOUNDP/2,2:SOUND200,2:FORT=1TO2500:NEXT:PRINT@480,"SCORE        FLOOR 29    LIVES";:?@493,"FLOOR 29";:RETURN
3000 CLS6:PRINT@8,"elevator"CHR$(223)"action";:PRINT@45,CHR$(218);:PRINT@51,CHR$(213);:PRINT@64,;
3005 PRINTSTRING$(6,223)"USE: awsz OR jikl TO"STRING$(6,223);
3010 PRINTSTRING$(6,223)"MOVE & space TO FIRE"STRING$(6,223);
3020 PRINTSTRING$(6,223)"DOWN= CROUCH-- HIT  "STRING$(6,223);
3030 PRINTSTRING$(6,223)"AGAIN TO CHANGE DIR."STRING$(6,223);
3040 PRINTSTRING$(6,223)"UP=STAND UP OR JUMP."STRING$(6,223);
3060 PRINTSTRING$(6,223)"NEW LIFE EVERY 50   "STRING$(6,223);
3070 PRINTSTRING$(6,223)"POINTS. 10 PTS FOR  "STRING$(6,223);
3080 PRINTSTRING$(6,223)"EVERY AGENT KILLED. "STRING$(6,223);
3090 PRINTSTRING$(6,223)"OPEN ALL RED DOORS, "STRING$(6,223);
3100 PRINTSTRING$(6,223)"THEN DESCEND TO 1ST "STRING$(6,223);
3110 PRINTSTRING$(6,223)"FLOOR FOR BONUS AND "STRING$(6,223);
3130 PRINTSTRING$(6,223)"YOUR NEXT MISSION..."STRING$(6,223);:RETURN
3150 PRINT@486,"level"CHR$(223)"of"CHR$(223)"play";:POKE16884-15360,40:POKE16885-15360,49:POKE16886-15360,45:POKE16887-15360,51:POKE16888-15360,41:POKE16889-15360,63
3160 A(.)=VAL(INKEY$):IFA(.)<1ORA(.)>4THEN3160
3170 A(.)=A(.)*2:RETURN
4000 PRINT@256+64,;
4030 PRINTSTRING$(6,208)+"COCO "+STRING$(2,208)+CHR$(207)+STRING$(5,208)+CHR$(175)+STRING$(12,208);
4040 PRINTSTRING$(6,208)+" FEST"+STRING$(2,208)+CHR$(207)+STRING$(5,208)+CHR$(175)+STRING$(12,208);
4050 PRINTSTRING$(6,208)+CHR$(208)+CHR$(138)+CHR$(208)+CHR$(133)+STRING$(3,208)+CHR$(207)+STRING$(5,208)+CHR$(175)+STRING$(12,208);:RETURN

5000 SOUND64,3
5011 SOUND64,3:FORT=1TO150:NEXT
5012 SOUND103,6
5013 SOUND129,6
5014 SOUND142,3
5015 SOUND149,3
5016 SOUND142,3
5017 SOUND129,3
5018 SOUND122,3
5019 SOUND129,10:FORT=1TO150:NEXT
5020 SOUND64,3
5021 SOUND64,3:FORT=1TO150:NEXT
5022 SOUND103,6
5023 SOUND129,6
5024 SOUND142,3
5025 SOUND149,3
5026 SOUND142,3
5027 SOUND129,3
5028 SOUND103,3
5029 SOUND64,10:RETURN

10000 IF PEEK(65535)=27 THEN POKE65497,0 ELSE POKE65495,0
10002 RETURN
10003 IF PEEK(65535)=27 THEN POKE65496,0 ELSE POKE65494,0
10004 END
