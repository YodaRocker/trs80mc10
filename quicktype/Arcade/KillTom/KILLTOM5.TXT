0 CLS:DIMC1,CC,M$,W:W=16384:GOTO200
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+W:FORC1=CC-LEN(M$)+1TOCC:POKEC1,PEEK(C1)-64:NEXT:RETURN
9 GOSUB60
10 IFCTHEN13
11 GOSUB90:IFSC<5000THEN13
12 IFN2<2THENGOSUB90
13 IFC=0THEN9
14 GOSUB120:IFK$="Y"THEN206
15 RUN
16 DIMS(783),N(13),D(13,4),P(14),V,B,J,K,K$,I
18 GOSUB2000:PRINT@490,"PLEASE WAIT";:S=0:H=0
19 FORI=0TO15:FORJ=0TO1:F=I*24+J*12+S
20 READA:FORK=11TO0STEP-1:S(F)=0
21 IFA>=INT(2^K)THENS(F)=1:A=A-INT(2^K)
22 F=F+1:NEXT:NEXT:NEXT
23 FORI=0TO383
24 IFS(S+I)<>0THENS(S+I+400)=0:GOTO36
25 F=-1:IFS(S+I+1)=0THENF=F+1:T(F)=2
26 IFS(S+I-1)=0THENF=F+1:T(F)=4
27 IFS(S+I+24)=0THENF=F+1:T(F)=3
28 IFS+I-24>=0THENIFS(S+I-24)=0THENF=F+1:T(F)=1
29 IFF=1THEN33
30 K=0:FORJ=0TOF:K=K+T(J):NEXT
31 IFF<>0THENK=K+3
32 S(S+I+400)=K:GOTO36
33 IFABS(T(0)-T(1))=2THENS(S+I+400)=0:GOTO36
34 K=(T(0)*T(1)/2)+4:IFK=10THENK=8
35 S(S+I+400)=INT(K)
36 NEXT
37 FORI=0TO13:READN(I)
38 FORJ=1TON(I):READD(I,J):NEXT:NEXT
39 FORI=0TO14:READP(I):NEXT
40 FORI=0TO4:READX(I),Y(I):NEXT
41 RETURN
42 CLS:SC=0:H1=H:M=3:MM$="XXXXX":N=1
43 FORI=0TO383:K=S(S+I)
44 IFK>1THENS(S+I)=0:K=0
45 J=INT(I/24)*8+I+W:POKEJ,P(K)
46 NEXT
47 PRINT@90,"SCORE":PRINT@122,S
48 PRINT@188,"MEN":PRINT@220,LEFT$(MM$,M)
49 PRINT@283,"HIGH":PRINT@314,H1:RETURN
50 U=RND(349)+24:K=S(U+S):IFK<>0THEN50
51 FORI=1TON:IFT(I)=UTHEN50
52 NEXT:V=0:J=INT(U/24)*8+U+W:POKEJ,P(2)
53 S(U+S)=2:C=0:RETURN
54 N=N+1:IFN>8THENN=8
55 N2=N:FORI=1TON
56 T(I)=RND(349)+24:K=S(T(I)+S):IFK<>0THEN56
57 K=S(T(I)+S+400):IFK=0THEN56
58 R(I)=D(K,RND(N(K))):J=INT(T(I)/24)*8+T(I)+W
59 POKEJ,P(14):NEXT:RETURN
60 K$=CHR$(PEEK(2)ANDPEEK(17023)):B=0:IFK$="W"THENV=1
61 IFK$="D"THENV=2
62 IFK$="S"THENV=3
63 IFK$="A"THENV=4
64 IFV=0THENRETURN
65 IFK$=","THEN72
66 IFK$="."THEN77
67 I=U+X(V)+Y(V):K=S(S+I)
68 IFK<>0THENRETURN
69 J=INT(U/24)*8+U+W:POKEJ,P(0):S(U+S)=0
70 J=INT(I/24)*8+I+W:POKEJ,P(2):S(I+S)=2
71 U=I:RETURN
72 I=U+X(V)+Y(V):K=S(S+I)
73 FORJ=1TON:IFT(J)=ITHENRETURN
74 NEXT:IFK<>0ANDK<>3ANDK<>4THENRETURN
75 IFK=0THENF=3:S(I+S)=F:J=INT(I/24)*8+I+W:POKEJ,P(F):RETURN
76 F=K+1:S(I+S)=F:J=INT(I/24)*8+I+W:POKEJ,P(F):RETURN
77 I=U+X(V)+Y(V):B=1:K=S(S+I):IFK<3THENRETURN
78 IFK>5THEN82
79 K=K-1:IFK=2THENK=0
80 J=INT(I/24)*8+I+W:POKEJ,P(K):S(I+S)=K
81 RETURN
82 IFK=13THEN85
83 K=K+1:S(I+S)=K:J=INT(I/24)*8+I+W
84 POKEJ,P(K):RETURN
85 S(I+S)=0:J=INT(I/24)*8+I+W:POKEJ,P(0)
86 FORJ=1TON:IFT(J)=ITHENT(J)=0:N2=N2-1
87 NEXT:SC=RND(5)*100+S
88 PRINT@122,S:IFH1<STHENH1=S
89 PRINT@314,H1:RETURN
90 FORI=1TON:IFT(I)=0THEN103
91 K=S(S+T(I)):IFK=2THEN105
92 IFK>5THEN99
93 F=T(I)+X(R(I))+Y(R(I)):K=S(S+F)
94 IFK>5THEN103
95 GOSUB115:IFK=5THEN98
96 IFK=2THEN105
97 GOTO103
98 POKEJ,P(10):S(T(I)+S)=10:GOTO103
99 IFU+X(V)+Y(V)=T(I)ANDB=1THEN103
100 K=K-1:J=INT(T(I)/24)*8+T(I)+W
101 IFK=5THENPOKEJ,P(14):K=0:S(T(I)+S)=K:GOTO103
102 POKEJ,P(K):S(T(I)+S)=K
103 NEXT:IFN2=0THENGOSUB54
104 RETURN
105 J=INT(U/24)*8+U+W:POKEJ,P(14)
106 S(U+S)=0:M=M-1:IFM=0THENC=1
107 SOUND1,4:PRINT@220,LEFT$(MM$,M);" "
108 FORI=1TO5:PRINT@378,;:M$="YOU ":GOSUB7:PRINT@410,"lose"
109 PRINT@442,;:M$="A   ":GOSUB7:PRINT@474,;:M$="MAN ":GOSUB7
110 FORJ=1TO400:NEXT
111 PRINT@378,"    ":PRINT@410,"    "
112 PRINT@442,"    ":PRINT@474,"    "
113 NEXT:IFC=0THENGOSUB50
114 RETURN
115 J=INT(T(I)/24)*8+T(I)+W:POKEJ,P(0)
116 J=INT(F/24)*8+F+W:POKEJ,P(14)
117 S(F+S)=0:T(I)=F:F=S(S+F+400)
118 IFF=0THENRETURN
119 R(I)=D(F,RND(N(F))):RETURN
120 CLS:PRINT"YOU HAVE JUST LOST YOUR LAST"
121 PRINT"MAN !!!!":FORI=1TO2500:NEXT
122 PRINT:PRINT:IFS<=HTHEN125
123 PRINT"BUT YOU ALSO GOT THE NEW HIGH"
124 PRINT"SCORE OF ";S;" POINTS !!!!!":H=S:GOTO122
125 PRINT"ANOTHER GAME ? ...";
126 K$=INKEY$:IFK$<>"Y"ANDK$<>"N"THEN126
127 RETURN
128 REM map
129 DATA4095,4095,2048,2049,3038,2725,2562,701,2811,3745,2562
130 DATA687,2810,2689,2050,2813,3064,2049,2563,3837,2746,649
131 DATA2746,2731,2178,2723,3066,2729,2048,2057,4095,4095
132 DATA1,0,1,1,1,2,1,3,1,4
133 DATA2,1,2,2,1,4,2,2,3,2,3,4
134 DATA3,1,2,3,3,1,2,4,3,1,3,4,3,2,3,4,4,1,2,3,4
135 DATA32,159,24,46,48,15,35
136 DATA35,35,35,35,42,46,46,191
137 DATA0,0,0,-24,1,0,0,24,-1,0
200 CLS:GOSUB16
206 GOSUB42
207 GOSUB50
208 GOSUB54
209 GOTO9
2000 PRINT"  ATTACK OF THE KILLER TOMATOES"
2020 PRINT"       BY APOLON IVANKOVIC"
2030 PRINT"           MC-10 EDITS"
2040 PRINT"          BY JIM GERRIE"
2050 PRINT
2060 PRINT"YOU ARE TRAPPED IN A MAZE WITH"
2070 PRINT"DEADLY KILLER TOMATOES! YOU CAN"
2080 PRINT"DESTROY THEM BY DIGGING HOLES"
2090 PRINT"FOR THEM TO FALL INTO. THEY ARE"
2100 PRINT"DUMB BUT big! YOU NEED A LARGE"
2110 PRINT"HOLE TO TRAP THEM AND YOU MUST"
2120 PRINT"BE QUICK TO FILL IT BACK IN."
2130 PRINT"USE wasd TO MOVE AND ";:M$=",":GOSUB7:PRINT" & ";:M$=".":GOSUB7:PRINT" TO"
2140 PRINT"DIG AND REFILL HOLES.":RETURN
3000 GOTO3000
