0 CLS:DIMQ,CC,M$,W:W=16384:GOTO200
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+W:FORQ=CC-LEN(M$)+1TOCC:POKEQ,PEEK(Q)-64:NEXT:RETURN
9 GOSUB60:IFCTHEN14
11 GOSUB90:IFSC<5000THEN13
12 IFN2<2THENGOSUB90
13 IFC=.THEN9
14 GOSUB120:IFM$="Y"THEN206
15 END
16 DIMS(783),N(13),D(13,4),P(14),I,J,V,B,K,U,X(4),Y(4),T(11),F,N,C,N2
18 GOSUB2000:PRINT@490,"PLEASE WAIT";:H=0
19 FORI=0TO15:FORJ=0TO1:F=I*24+J*12
20 READA:FORK=11TO0STEP-1:S(F)=.
21 IFA>=INT(2^K)THENS(F)=1:A=A-INT(2^K)
22 F=F+1:NEXT:NEXT:NEXT
23 FORI=0TO383:IFS(I)THENS(I+400)=.:NEXT:GOTO37
25 F=-1:IFS(I+1)=.THENF=F+1:T(F)=2
26 IFS(I-1)=.THENF=F+1:T(F)=4
27 IFS(I+24)=.THENF=F+1:T(F)=3
28 IFI-24>=.THENIFS(I-24)=.THENF=F+1:T(F)=1
29 IFF=1THEN33
30 K=.:FORJ=0TOF:K=K+T(J):NEXT
31 IFFTHENK=K+3
32 S(I+400)=K:NEXT:GOTO37
33 IFABS(T(.)-T(1))=2THENS(I+400)=.:NEXT:GOTO37
34 K=(T(.)*T(1)/2)+4:IFK=10THENK=8
35 S(I+400)=INT(K):NEXT
37 FORI=0TO13:READN(I):FORJ=1TON(I):READD(I,J):NEXT:NEXT:FORI=0TO14:READP(I):NEXT
40 FORI=0TO4:READX(I),Y(I):NEXT:RETURN
42 CLS:SC=0:Q=0:H1=H:M=3:MM$="XXXXX":N=1
43 FORI=0TO383:K=S(I):IFK>1THENS(I)=.:K=.
45 J=INT(I/24)*8+I+W:POKEJ,P(K):NEXT
47 PRINT@90,"SCORE":PRINT@122,MID$(STR$(SC),2);
48 PRINT@188,"MEN":PRINT@220,LEFT$(MM$,M)
49 PRINT@283,"HIGH":PRINT@314,MID$(STR$(H1),1);:RETURN
50 U=RND(349)+24:K=S(U):IFKTHEN50
51 FORI=1TON:IFT(I)=UTHEN50
52 NEXT:V=.:J=INT(U/24)*8+U+W:POKEJ,P(2):S(U)=2:C=.:RETURN
54 N=N+1:IFN>8THENN=8
55 N2=N:FORI=1TON
56 T(I)=RND(349)+24:K=S(T(I)):IFKTHEN56
57 K=S(T(I)+400):IFK=.THEN56
58 R(I)=D(K,RND(N(K))):J=INT(T(I)/24)*8+T(I)+W:POKEJ,P(14):NEXT:RETURN
60 M$=CHR$(PEEK(2)ANDPEEK(17023)):B=.:IFM$="W"THENV=1
61 IFM$="D"THENV=2
62 IFM$="S"THENV=3
63 IFM$="A"THENV=4
64 IFV=.THENRETURN
65 IFM$=","THEN72
66 IFM$="."THEN77
67 I=U+X(V)+Y(V):K=S(I):IFKTHENRETURN
70 J=INT(U/24)*8+U+W:POKEJ,P(.):S(U)=.:J=INT(I/24)*8+I+W:Q=Q+1:POKEJ,P(2)+(1ANDQ):S(I)=2:U=I:RETURN
72 I=U+X(V)+Y(V):K=S(I)
73 FORJ=1TON:IFT(J)=ITHENRETURN
74 NEXT:IFKTHENIFK<>3ANDK<>4THENRETURN
75 IFK=.THENF=3:S(I)=F:J=INT(I/24)*8+I+W:POKEJ,P(F):RETURN
76 F=K+1:S(I)=F:J=INT(I/24)*8+I+W:POKEJ,P(F):RETURN
77 I=U+X(V)+Y(V):B=1:K=S(I):IFK<3THENRETURN
78 IFK>5THEN82
79 K=K-1:IFK=2THENK=.
80 J=INT(I/24)*8+I+W:POKEJ,P(K):S(I)=K:RETURN
82 IFK=13THEN85
83 K=K+1:S(I)=K:J=INT(I/24)*8+I+W:POKEJ,P(K):RETURN
85 S(I)=.:J=INT(I/24)*8+I+W:POKEJ,P(.):FORJ=1TON:IFT(J)=ITHENT(J)=.:N2=N2-1
87 NEXT:SC=RND(5)*100+SC:PRINT@122,MID$(STR$(SC),2);:IFH1<SCTHENH1=SC
89 PRINT@314,MID$(STR$(H1),1);:RETURN
90 FORI=1TON:IFT(I)=.THEN103
91 K=S(T(I)):IFK=2THEN105
92 IFK>5THEN99
93 F=T(I)+X(R(I))+Y(R(I)):K=S(F):IFK>5THEN103
95 GOSUB115:IFK=5THEN98
96 IFK=2THEN105
97 GOTO103
98 POKEJ,P(10):S(T(I))=10:GOTO103
99 IFB=1THENIFU+X(V)+Y(V)=T(I)THEN103
100 K=K-1:J=INT(T(I)/24)*8+T(I)+W:IFK=5THENPOKEJ,P(14):K=0:S(T(I))=K:GOTO103
102 POKEJ,P(K):S(T(I))=K
103 NEXT:IFN2=0THENGOSUB54
104 RETURN
105 J=INT(U/24)*8+U+W:POKEJ,P(14):S(U)=0:M=M-1:IFM=.THENC=1
107 SOUND1,4:PRINT@220,LEFT$(MM$,M);" "
108 FORI=1TO5:PRINT@378,;:M$="YOU ":GOSUB7:PRINT@410,"lose":PRINT@442,;:M$="A   ":GOSUB7:PRINT@474,;:M$="MAN ":GOSUB7
110 FORJ=1TO400:NEXT:PRINT@378,"    ":PRINT@410,"    ":PRINT@442,"    ":PRINT@474,"    ":FORJ=1TO400:NEXT:NEXT:IFC=0THENGOSUB50
114 Q=.:RETURN
115 J=INT(T(I)/24)*8+T(I)+W:POKEJ,P(.):J=INT(F/24)*8+F+W:POKEJ,P(14):S(F)=.:T(I)=F:F=S(F+400):IFF=.THENRETURN
119 R(I)=D(F,RND(N(F))):RETURN
120 CLS:PRINT"YOU HAVE JUST LOST YOUR LAST"
121 PRINT"MAN !!!!":FORI=1TO2500:NEXT
122 PRINT:PRINT:IFSC<=HTHEN125
123 PRINT"BUT YOU ALSO GOT THE NEW HIGH"
124 PRINT"SCORE OF"SC"POINTS !!!!!":H=SC:GOTO122
125 PRINT"ANOTHER GAME ? ...";
126 M$=INKEY$:IFM$<>"Y"ANDM$<>"N"THEN126
127 RETURN
128 REM map
129 DATA4095,4095,2048,2049,3038,2725,2562,701,2811,3745,2562
130 DATA687,2810,2689,2050,2813,3064,2049,2563,3837,2746,649
131 DATA2746,2731,2178,2723,3066,2729,2048,2057,4095,4095
132 DATA1,0,1,1,1,2,1,3,1,4
133 DATA2,1,2,2,1,4,2,2,3,2,3,4
134 DATA3,1,2,3,3,1,2,4,3,1,3,4,3,2,3,4,4,1,2,3,4
135 DATA32,159,24,46,48,15,35
136 DATA35,35,35,35,42,46,46,191
137 DATA0,0,0,-24,1,0,0,24,-1,0
200 CLS:GOSUB16
206 GOSUB42
207 GOSUB50
208 GOSUB54
209 GOTO9
2000 PRINT"  ATTACK OF THE KILLER TOMATOES"
2020 PRINT"       BY APOLON IVANKOVIC"
2030 PRINT"           MC-10 EDITS"
2040 PRINT"          BY JIM GERRIE"
2050 PRINT
2060 PRINT"YOU ARE TRAPPED IN A MAZE WITH"
2070 PRINT"DEADLY KILLER TOMATOES! YOU CAN"
2080 PRINT"DESTROY THEM BY DIGGING HOLES"
2090 PRINT"FOR THEM TO FALL INTO. THEY ARE"
2100 PRINT"DUMB BUT big! YOU NEED A LARGE"
2110 PRINT"HOLE TO TRAP THEM AND YOU MUST"
2120 PRINT"BE QUICK TO FILL IT BACK IN."
2130 PRINT"USE wasd TO MOVE AND ";:M$=",":GOSUB7:PRINT" & ";:M$=".":GOSUB7:PRINT" TO"
2140 PRINT"DIG AND REFILL HOLES.":RETURN
3000 GOTO3000
